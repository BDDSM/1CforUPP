////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСтруктураПараметровОтбораНадбавок; 
Перем мВалютаТарифнойСтавки;
Перем мСведенияОВидахРасчетаОснНачислений;
перем мСписокПредопределенныхВР;

Перем мОбработкаПодбораПоСтроке;
Перем мТекстПодбораПоСтроке;
Перем мПоследнееЗначениеЭлементаПодбораПоСтроке;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы
Процедура ПриОткрытии()
	
	СтруктураКолонок = Новый Структура();
	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("Надбавка");

	// Установить ограничение - изменять видимость колонок табличной части
	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.ПерсональныеНадбавки.Колонки, СтруктураКолонок);
		
	мВалютаТарифнойСтавки = Константы.ВалютаРегламентированногоУчета.Получить();
	
	// Установим видимость реквизитов в зависимости от уч.политики по персоналу организаций
	Если глЗначениеПеременной("глЕстьВалютныеПоказателиОрганизаций") Тогда
		мМассивЭУ = Новый Массив();
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта1);
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта2);
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта3);
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта4);
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта5);
		мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта6);	
		РаботаСДиалогамиПереопределяемый.УстановитьВидимостьЭУПоУчетнойПолитикеПоПерсоналу(мМассивЭУ,глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"),Организация);
	Иначе
		Для Сч = 1 По 6 Цикл
			ЭлементыФормы.ПерсональныеНадбавки.Колонки["Валюта"+Сч].Видимость = Ложь;
		КонецЦикла;
	КонецЕсли;
	
	мСтруктураПараметровОтбораНадбавок   = РаботаСДиалогамиПереопределяемый.ПолучитьСтруктуруОтборовНадбавокСотрудника(ВидДоговора);
	
	Заголовок = СотрудникиОрганизацийПереопределяемый.ПолучитьЗаголовокФормы(Наименование);

КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы
Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьПерсональныеНадбавки");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ПерсональныеНадбавки

// Процедура - обработчик события "НачалоВыбора" поля ввода Надбавка
Процедура ПерсональныеНадбавкиНадбавкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	РаботаСДиалогамиЗК.ОткрытьФормуВыбораОсновныеНачисленияОрганизаций(Элемент, Ссылка, мСтруктураПараметровОтбораНадбавок, СтандартнаяОбработка, Элемент.Значение, мСписокПредопределенныхВР);

КонецПроцедуры

// Процедура - обработчик события "ПриВыводеСтроки" ТЧ
//
Процедура ПерсональныеНадбавкиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Надбавка) Тогда
		Для СчПоказателей = 1 По 6 Цикл
			ОформлениеСтроки.Ячейки["НаименованиеПоказатель" + СчПоказателей].Видимость		= Ложь;
			ОформлениеСтроки.Ячейки["Показатель" + СчПоказателей].Видимость					= Ложь;		
			ОформлениеСтроки.Ячейки["Валюта" + СчПоказателей].Видимость						= Ложь;
			ОформлениеСтроки.Ячейки["НаименованиеПоказательТР" + СчПоказателей].Видимость	= Ложь;	
			ОформлениеСтроки.Ячейки["ТарифныйРазряд" +СчПоказателей].Видимость				= Ложь;
		КонецЦикла;
	Иначе
		РаботаСДиалогамиЗК.ПриВыводеСтрокиПлановыхНачислений(Элемент, ОформлениеСтроки, ДанныеСтроки, мСведенияОВидахРасчетаОснНачислений);
	КонецЕсли;	

КонецПроцедуры //ПерсональныеНадбавкиПриВыводеСтроки

Процедура ПерсональныеНадбавкиПриАктивизацииСтроки(Элемент)
    ПодключитьОбработчикОжидания("ОбработчикОжиданияПерсональныеНадбавкиПриАктивизацииСтроки", 0.05, Истина);	 	
КонецПроцедуры

// Процедура - обработчик ожидания для события ПриАктивизацииСтроки
// табличного поля Начисления
//
Процедура ОбработчикОжиданияПерсональныеНадбавкиПриАктивизацииСтроки()
	
	РаботаСДиалогамиЗК.УстановитьФорматЗначенийПоказателей(ЭлементыФормы.ПерсональныеНадбавки, мСведенияОВидахРасчетаОснНачислений, "Надбавка");
	
КонецПроцедуры // ОбработчикОжиданияПерсональныеНадбавкиПриАктивизацииСтроки

Процедура ПерсональныеНадбавкиНадбавкаПриИзменении(Элемент)
	
	Надбавка = ЭлементыФормы.ПерсональныеНадбавки.ТекущиеДанные.Надбавка;

	СведенияОВидеРасчета = РаботаСДиалогамиЗК.ПолучитьСведенияОВидеРасчетаСхемыМотивации(мСведенияОВидахРасчетаОснНачислений, Надбавка);
	ДанныеСтроки = ЭлементыФормы.ПерсональныеНадбавки.ТекущиеДанные;
	
	РаботаСДиалогамиЗК.УстановитьФорматЗначенийПоказателей(ЭлементыФормы.ПерсональныеНадбавки, мСведенияОВидахРасчетаОснНачислений, "Надбавка");
	
	Если СведенияОВидеРасчета.НеТребуетВалюты Тогда
		ДанныеСтроки.Валюта1			= Справочники.Валюты.ПустаяСсылка();
		
	Иначе
		ИспользуютсяНачисленияВВалюте = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");
			
		Если Не ИспользуютсяНачисленияВВалюте И ДанныеСтроки.Валюта1.Пустая() Тогда
			ДанныеСтроки.Валюта1 = мВалютаТарифнойСтавки;
		КонецЕсли;
			
	КонецЕсли;
	
	Для СчПоказателей = 1 По 6 Цикл
		Если СчПоказателей = 1 Или СчПоказателей <= Надбавка.Показатели.Количество() Тогда
			Если НЕ ЗначениеЗаполнено(ДанныеСтроки["Валюта" + СчПоказателей]) И 
				СведенияОВидеРасчета["Валюта" + СчПоказателей + "Видимость"] Тогда
				ДанныеСтроки["Валюта" + СчПоказателей] = мВалютаТарифнойСтавки;
			КонецЕсли;
			Если НЕ СведенияОВидеРасчета["Валюта" + СчПоказателей + "Видимость"] Тогда
				Если СчПоказателей <= СведенияОВидеРасчета.ФактКоличествоПоказателей Тогда
					ДанныеСтроки["Валюта" + СчПоказателей] = СведенияОВидеРасчета["ВалютаПоказателя" + СчПоказателей];
				КонецЕсли;
			КонецЕсли;
			Если НЕ СведенияОВидеРасчета["Показатель" + СчПоказателей + "Видимость"] Тогда
				ДанныеСтроки["Показатель" + СчПоказателей] = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;		
	
КонецПроцедуры

Процедура ПерсональныеНадбавкиНадбавкаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, мСтруктураПараметровОтбораНадбавок, Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"));	
	
КонецПроцедуры

Процедура ПерсональныеНадбавкиНадбавкаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, мСтруктураПараметровОтбораНадбавок, ЭтаФорма, Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"), мОбработкаПодбораПоСтроке, мТекстПодбораПоСтроке, мПоследнееЗначениеЭлементаПодбораПоСтроке, Ложь);
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСтруктураПараметровОтбораНадбавок = Новый Структура;
мСведенияОВидахРасчетаОснНачислений = Новый Соответствие;
мСписокПредопределенныхВР = ПроведениеРасчетов.ПредопределенныеНачисленияОрганизации();

мОбработкаПодбораПоСтроке								= Ложь;
мТекстПодбораПоСтроке									= "";
мПоследнееЗначениеЭлементаПодбораПоСтроке				= Неопределено;