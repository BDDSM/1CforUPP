////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мЖирныйШрифт;
Перем ОтображенныеОрганизации;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПервоеПодразделение(Подразделение, Организация, НазваниеОрганизации)
	
	Если ОтображенныеОрганизации[Организация] <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодразделенияОрганизаций.Ссылка,
	|	ПодразделенияОрганизаций.Владелец.Наименование КАК НазваниеОрганизации
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделенияОрганизаций."+ПодразделенияОрганизаций.Порядок[1].Имя+" ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	НазваниеОрганизации = Выборка.НазваниеОрганизации;
	
	ОтображенныеОрганизации[Организация] = Истина;
	
	Возврат Выборка.Ссылка = Подразделение;
	
КонецФункции

Процедура ОбновитьКнопки()
	
	Подменю = ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки;
	
	Подменю.Организации.Кнопки.ДействиеИсключитьИзПодразделения.Доступность	= Ложь;
	Подменю.ДействиеИсключитьИзПодразделения.Доступность					= Ложь;
	
	Кнопка = ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки.Организации.Кнопки.ДействиеРедактировать;
	Кнопка.Текст = "Редактировать подразделение";
	
	ДанныеСтроки	= ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение
	|ИЗ
	|	РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|ГДЕ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации = &ПодразделениеОрганизации";
	
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",	ДанныеСтроки.Ссылка);
	
	ИсключитьИзПодразделения = Не Запрос.Выполнить().Пустой();
	
	Подменю.Организации.Кнопки.ДействиеИсключитьИзПодразделения.Доступность	= ИсключитьИзПодразделения;
	Подменю.ДействиеИсключитьИзПодразделения.Доступность					= ИсключитьИзПодразделения;
	
	Кнопка.Текст = "Редактировать подразделение " + ДанныеСтроки.Наименование;
	
КонецПроцедуры

Функция СпискиЭтойФормы()
	
	СпискиФормы = Новый Массив;
	СпискиФормы.Добавить(Подразделения);
	СпискиФормы.Добавить(ПодразделенияОрганизаций);
	
	Возврат СпискиФормы;
	
КонецФункции // СпискиЭтойФормы
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	РаботаСДиалогами.УдалитьКнопкуПрава(ЭлементыФормы.КоманднаяПанельПодразделения.Кнопки);  
	РаботаСДиалогами.УдалитьКнопкуПрава(ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки);
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ФормаСпискаПередОткрытиемДополнительно(ЭтаФорма, Новый Действие("ДополнительныеДействия"), СпискиЭтойФормы());
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Подразделения.Порядок.Установить("Порядок");
	
	ПодразделенияОрганизаций.Порядок.Установить("Владелец, Порядок");
	ПодразделенияОрганизаций.Колонки.Добавить("Владелец", Ложь);
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ФормаСпискаПриОткрытииДополнительно(ЭтаФорма, СпискиЭтойФормы());
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ФормаСпискаПриЗакрытииДополнительно(ЭтаФорма, СпискиЭтойФормы());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ВключитьВСтруктурноеПодразделение(Кнопка)
	
	ДанныеСтроки	= ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Справочники.Подразделения.ПолучитьФормуВыбора();
	Результат = Форма.ОткрытьМодально();
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СоответствиеПодразделенийИПодразделенийОрганизаций.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Организация.Установить(ДанныеСтроки.Владелец);
	НаборЗаписей.Отбор.ПодразделениеОрганизации.Установить(ДанныеСтроки.Ссылка);
	НаборЗаписей.Отбор.Подразделение.Установить(Результат);
	
	Строка = НаборЗаписей.Добавить();
	Строка.Подразделение			= Результат;
	Строка.Организация				= ДанныеСтроки.Владелец;
	Строка.ПодразделениеОрганизации	= ДанныеСтроки.Ссылка;
	
	НаборЗаписей.Записать();
	
	// Необходимо обновлять все строки, иначе табличное поле не изменит высоту ячейки
	ЭлементыФормы.Подразделения.ОбновитьСтроки();
	ЭлементыФормы.ПодразделенияОрганизаций.ОбновитьСтроки();
	
	ОбновитьКнопки();
	
КонецПроцедуры

Процедура ИсключитьИзСтруктурногоПодразделения(Кнопка)
	
	ДанныеСтроки	= ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СоответствиеПодразделенийИПодразделенийОрганизаций.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Организация.Установить(ДанныеСтроки.Владелец);
	НаборЗаписей.Отбор.ПодразделениеОрганизации.Установить(ДанныеСтроки.Ссылка);
	
	НаборЗаписей.Записать();
	
	// Необходимо обновлять все строки, иначе табличное поле не изменит высоту ячейки
	ЭлементыФормы.Подразделения.ОбновитьСтроки();
	ЭлементыФормы.ПодразделенияОрганизаций.ОбновитьСтроки();
	
	ОбновитьКнопки();
	
КонецПроцедуры

Процедура СписокОрганизаций(Кнопка)
	
	Справочники.Организации.ПолучитьФормуСписка().Открыть();
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКТекущемуЭлементуПодразделения(Кнопка)
	
	Если Не ЭлементыФормы.Подразделения.ТекущиеДанные = Неопределено Тогда
		НастройкаПравДоступа.РедактироватьПраваДоступа(ЭлементыФормы.Подразделения.ТекущиеДанные.Ссылка);
	Иначе
		Предупреждение("Не выбрано подразделение для настройки прав доступа!");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКоВсемуСправочникуПодразделения(Кнопка)
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(Справочники.Подразделения.ПустаяСсылка());
	
КонецПроцедуры


Процедура ДействияФормыПраваДоступаПользователейКТекущемуЭлементуПодразделенияОрганизаций(Кнопка)
	
	Если Не ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные = Неопределено и Не ЭлементыФормы.ПодразделенияОрганизаций.ТекущаяКолонка.Имя = "Организация" Тогда
		НастройкаПравДоступа.РедактироватьПраваДоступа(ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные.Ссылка);
	Иначе
		Предупреждение("Не выбрано подразделение организации для настройки прав доступа!");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКоВсемуСправочникуПодразделенияОрганизаций(Кнопка)
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	
КонецПроцедуры


Процедура КоманднаяПанельПодразделенияПереместитьВверх(Кнопка)
	
	Если Подразделения.Порядок.Найти("Порядок") = Неопределено Тогда
		Предупреждение("Изменять порядок элементов можно только если установлена сортировка по порядку!");
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ЭлементыФормы.Подразделения.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ИзменитьПорядок(ДанныеСтроки.Ссылка, "Вверх");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияПереместитьВниз(Кнопка)
	
	Если Подразделения.Порядок.Найти("Порядок") = Неопределено Тогда
		Предупреждение("Изменять порядок элементов можно только если установлена сортировка по порядку!");
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ЭлементыФормы.Подразделения.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ИзменитьПорядок(ДанныеСтроки.Ссылка, "Вниз");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияОрганизацийПереместитьВверх(Кнопка)
	
	Если ПодразделенияОрганизаций.Порядок.Найти("Порядок") = Неопределено Тогда
		Предупреждение("Изменять порядок элементов можно только если установлена сортировка по порядку!");
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ИзменитьПорядок(ДанныеСтроки.Ссылка, "Вверх");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияОрганизацийПереместитьВниз(Кнопка)
	
	Если ПодразделенияОрганизаций.Порядок.Найти("Порядок") = Неопределено Тогда
		Предупреждение("Изменять порядок элементов можно только если установлена сортировка по порядку!");
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ИзменитьПорядок(ДанныеСтроки.Ссылка, "Вниз");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияОрганизацийУпорядочитьПоКод(Кнопка)
	
	Кнопки = ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки.Организации.Кнопки.Сортировка.Кнопки;
	
	Для Каждого Кнопка Из Кнопки Цикл
		Кнопка.Пометка = Кнопка = Кнопки.УпорядочитьПоКод;
	КонецЦикла;
	
	ПодразделенияОрганизаций.Порядок.Установить("Владелец, Код");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияОрганизацийУпорядочитьПоНаименование(Кнопка)
	
	Кнопки = ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки.Организации.Кнопки.Сортировка.Кнопки;
	
	Для Каждого Кнопка Из Кнопки Цикл
		Кнопка.Пометка = Кнопка = Кнопки.УпорядочитьПоНаименование;
	КонецЦикла;
	
	ПодразделенияОрганизаций.Порядок.Установить("Владелец, Наименование");
	
КонецПроцедуры

Процедура КоманднаяПанельПодразделенияОрганизацийУпорядочитьПоПорядок(Кнопка)
	
	Кнопки = ЭлементыФормы.КоманднаяПанельПодразделенияОрганизаций.Кнопки.Организации.Кнопки.Сортировка.Кнопки;
	
	Для Каждого Кнопка Из Кнопки Цикл
		Кнопка.Пометка = Кнопка = Кнопки.УпорядочитьПоПорядок;
	КонецЦикла;
	
	ПодразделенияОрганизаций.Порядок.Установить("Владелец, Порядок");
	
КонецПроцедуры

Процедура ДополнительныеДействия(Элемент)
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ВыполнитьДополнительныеДействияФормыСписка(Элемент, ЭтаФорма, СпискиЭтойФормы());
	
КонецПроцедуры // ДополнительныеДействия

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ Подразделения

Процедура ПодразделенияПриАктивизацииСтроки(Элемент)
	
	Кнопка = ЭлементыФормы.КоманднаяПанельПодразделения.Кнопки.Предприятие.Кнопки.ДействиеРедактировать;
	Кнопка.Текст = "Редактировать";
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Кнопка.Текст = "Редактировать " + ДанныеСтроки.Наименование;
	
КонецПроцедуры

Процедура ПодразделенияПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ПодразделенияМассив = Новый Массив;
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки <> НеОпределено Тогда
			ПодразделенияМассив.Добавить(Оформление.ДанныеСтроки.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации.Наименование КАК Наименование,
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|ГДЕ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение В(&Подразделения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение";
	
	Запрос.УстановитьПараметр("Подразделения", ПодразделенияМассив);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПоиска = Новый Структура("Подразделение");
	
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки = НеОпределено Тогда
			Оформление.Ячейки.Наименование.УстановитьТекст("Структура предприятия");
			Оформление.Ячейки.Наименование.ОтображатьКартинку = Ложь;
			Оформление.Ячейки.Наименование.Шрифт = мЖирныйШрифт;
			Оформление.Ячейки.ПодразделенияОрганизаций.Видимость = Ложь;
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска.Подразделение = Оформление.ДанныеСтроки.Ссылка;
		
		Выборка.Сбросить();
		Выборка.НайтиСледующий(СтруктураПоиска);
		
		ПерваяСтрока	= Истина;
		Представление	= "";
		
		Пока Выборка.Подразделение = Оформление.ДанныеСтроки.Ссылка Цикл
			Если Не ПерваяСтрока Тогда
				Представление = Представление + Символы.ВК;
			КонецЕсли;
			ПерваяСтрока = Ложь;
			Представление = Представление + Выборка.Наименование;
			Если Не Выборка.Следующий() Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ПустаяСтрока(Представление) Тогда
			Представление = "";
			Оформление.Ячейки.ПодразделенияОрганизаций.ЦветТекста = ЦветаСтиля.ЦветРамки;
		КонецЕсли;
		
		Оформление.Ячейки.ПодразделенияОрганизаций.УстановитьТекст(Представление);
	КонецЦикла;
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ФормаСпискаОформлениеСпискаДополнительно(ЭтаФорма, Элемент, ОформленияСтрок);
	
КонецПроцедуры

Процедура ПодразделенияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	СтандартнаяОбработка = ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("СправочникСсылка.ПодразделенияОрганизаций");
	
КонецПроцедуры

Процедура ПодразделенияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	НаборЗаписей = РегистрыСведений.СоответствиеПодразделенийИПодразделенийОрганизаций.СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Владелец КАК Организация
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Ссылка = &ПодразделениеОрганизации";
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",	ПараметрыПеретаскивания.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
	НаборЗаписей.Отбор.ПодразделениеОрганизации.Установить(ПараметрыПеретаскивания.Значение);
	НаборЗаписей.Отбор.Подразделение.Установить(Строка);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Организация				= Выборка.Организация;
	Запись.ПодразделениеОрганизации	= ПараметрыПеретаскивания.Значение;
	Запись.Подразделение			= Строка;
	
	НаборЗаписей.Записать();
	
	// Необходимо обновлять все строки, иначе табличное поле не изменит высоту ячейки
	ЭлементыФормы.Подразделения.ОбновитьСтроки();
	ЭлементыФормы.ПодразделенияОрганизаций.ОбновитьСтроки(ПараметрыПеретаскивания.Значение);
	
	ОбновитьКнопки();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ПодразделенияОрганизаций

Процедура ПодразделенияОрганизацийПриАктивизацииСтроки(Элемент)
	
	ОбновитьКнопки();
	
КонецПроцедуры

Процедура ПодразделенияОрганизацийПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если Не ПустаяСтрока(ОформлениеСтроки.Ячейки.НесколькоПодразделений.Текст) Тогда
		ОформлениеСтроки.Ячейки.Подразделения.ЦветТекста	= WebЦвета.Красный;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодразделенияОрганизацийПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ОтображенныеОрганизации.Очистить();
	
	ПодразделенияМассив = Новый Массив;
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки <> НеОпределено Тогда
			ПодразделенияМассив.Добавить(Оформление.ДанныеСтроки.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение.Наименование КАК Наименование,
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|ГДЕ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации В(&Подразделения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение";
	
	Запрос.УстановитьПараметр("Подразделения", ПодразделенияМассив);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПоиска = Новый Структура("Подразделение");
	
	ПервыеПодразделения = Новый Массив;
	
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки = НеОпределено Тогда
			Оформление.Ячейки.Наименование.УстановитьТекст("Все организации");
			Оформление.Ячейки.Наименование.ОтображатьКартинку	= Ложь;
			Оформление.Ячейки.Наименование.Шрифт				= мЖирныйШрифт;
			Оформление.Ячейки.Подразделения.Видимость			= Ложь;
			Оформление.Ячейки.Организация.Видимость				= Ложь;
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска.Подразделение = Оформление.ДанныеСтроки.Ссылка;
		
		Выборка.Сбросить();
		Выборка.НайтиСледующий(СтруктураПоиска);
		
		ПерваяСтрока		= Истина;
		Представление		= "";
		НазваниеОрганизации	= "";
		
		Если ПервоеПодразделение(Оформление.ДанныеСтроки.Ссылка, Оформление.ДанныеСтроки.Владелец, НазваниеОрганизации) Тогда
			ПервыеПодразделения.Добавить(Оформление.ДанныеСтроки.Ссылка);
			Оформление.Ячейки.Организация.УстановитьТекст("      " + НазваниеОрганизации);
			Оформление.Ячейки.Организация.Шрифт = мЖирныйШрифт;
		Иначе
			Оформление.Ячейки.Организация.Видимость = Ложь;
		КонецЕсли;
		
		Пока Выборка.Подразделение = Оформление.ДанныеСтроки.Ссылка Цикл
			Если Не ПустаяСтрока(Представление) Тогда
				ПерваяСтрока = Ложь;
			КонецЕсли;
			Если Не ПерваяСтрока Тогда
				Оформление.Ячейки.НесколькоПодразделений.УстановитьТекст("Да");
				Представление = Представление + ", ";
			КонецЕсли;
			Представление = Представление + Выборка.Наименование;
			Если Не Выборка.Следующий() Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ПерваяСтрока Тогда
			Представление = "Включено в несколько структурных подразделений: " + Представление;
		КонецЕсли;
		
		Если ПустаяСтрока(Представление) Тогда
			Представление = "";
			Оформление.Ячейки.Подразделения.ЦветТекста = ЦветаСтиля.ЦветРамки;
		КонецЕсли;
		
		Оформление.Ячейки.Подразделения.УстановитьТекст(Представление);
	КонецЦикла;
	
	ИзменениеАктуальностиЭлементовПереопределяемый.ФормаСпискаОформлениеСпискаДополнительно(ЭтаФорма, Элемент, ОформленияСтрок, ПервыеПодразделения);
	
КонецПроцедуры

Процедура ПодразделенияОрганизацийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Отказ = Истина;
	
	ДанныеСтроки = ЭлементыФормы.ПодразделенияОрганизаций.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Справочники.ПодразделенияОрганизаций.ПолучитьФормуНовогоЭлемента();
	Форма.Владелец = ДанныеСтроки.Владелец;
	Форма.Открыть();
	
КонецПроцедуры

Процедура ПодразделенияОрганизацийНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Не ПараметрыПеретаскивания.Значение.Пустая();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мЖирныйШрифт = Новый Шрифт(ШрифтыСтиля.ШрифтТекста,,, Истина);

ОтображенныеОрганизации = Новый Соответствие;