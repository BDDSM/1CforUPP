// Процедура - обработчик события ПриЗакрытии
//
Процедура ПриЗакрытии()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЗапрашиватьКоличество", ЗапрашиватьКоличество);
	
	СохранитьЗначение("СпецификацииНоменклатуры_ФормаПодбора_Параметры", Параметры);
	
КонецПроцедуры // ПриЗакрытии()

// Процедура - обработчик события ПриОткрытии
//
Процедура ПриОткрытии()
	
	Параметры = ВосстановитьЗначение("СпецификацииНоменклатуры_ФормаПодбора_Параметры");
	Если Не Параметры = Неопределено Тогда
		Если Параметры.Свойство("ЗапрашиватьКоличество") Тогда
			ЗапрашиватьКоличество = Параметры.ЗапрашиватьКоличество;
		Иначе
			ЗапрашиватьКоличество = Истина;
		КонецЕсли;
	Иначе
		ЗапрашиватьКоличество = Истина;
	КонецЕсли;
	
	Отбор.Активная.Установить(Истина);
	
	ИспользоватьВерсииСпецификацийНоменклатуры = Константы.ИспользоватьВерсииСпецификацийНоменклатуры.Получить();
	ИспользоватьТолькоСборочныеСпецификации = глЗначениеПеременной("ИспользоватьТолькоСборочныеСпецификации");
	
	ЭлементыФормы.СправочникСписок.Колонки.КодВерсии.Видимость 		   = ИспользоватьВерсииСпецификацийНоменклатуры;
	ЭлементыФормы.СправочникСписок.Колонки.КодВерсии.ИзменятьВидимость = ИспользоватьВерсииСпецификацийНоменклатуры;
	
	ЭлементыФормы.СправочникСписок.Колонки.ВидСпецификации.Видимость = УправлениеПроизводством.ИспользоватьВидСпецификации(ИспользоватьТолькоСборочныеСпецификации);
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события ВыборЗначения
//
Процедура СправочникСписокВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	
	Если Значение[0].ЭтоГруппа ИЛИ Значение[0].Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Спецификация", Значение[0]);
	
	Количество = 1;
	Если ЗапрашиватьКоличество Тогда
		
		ИспользоватьТолькоСборочныеСпецификации = глЗначениеПеременной("ИспользоватьТолькоСборочныеСпецификации");
		Если Не УправлениеПроизводством.ИспользоватьВидСпецификации(ИспользоватьТолькоСборочныеСпецификации)
		 ИЛИ  Значение[0].ВидСпецификации = Перечисления.ВидыСпецификаций.Сборочная Тогда
			ТекстВвестиЧисло = "Укажите количество изделий";
		Иначе
			ТекстВвестиЧисло = "Укажите количество спецификаций";
		КонецЕсли;
		
		Если Не ВвестиЧисло(Количество, ТекстВвестиЧисло, 15, 3) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Параметры.Вставить("Количество", Количество);
	ОповеститьОВыборе(Параметры);
	
КонецПроцедуры // СправочникСписокВыборЗначения()