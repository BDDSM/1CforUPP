Перем мНовыйКлючСвязи;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура управляет видимостью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	ЭлементыФормы.АвтоподборХарактеристики.ОтборСтрок.КлючСвязи.Установить(КлючСвязи);
	ЭлементыФормы.АвтоподборХарактеристики.НастройкаОтбораСтрок.КлючСвязи.Доступность = Ложь;
	
	ЭлементыФормы.АвтоподборХарактеристикиОтходы.ОтборСтрок.КлючСвязи.Установить(КлючСвязи);
	ЭлементыФормы.АвтоподборХарактеристикиОтходы.НастройкаОтбораСтрок.КлючСвязи.Доступность = Ложь;
	
КонецПроцедуры // УправлениеДиалогом()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Проверим тип реквизита формы СтруктураПараметровФормы. Должен быть "Структура".
	// При неверном типе не будем запускать подбор.
	Если ТипЗнч(СтруктураИсходныхПараметров) <> Тип("Структура") Тогда
		
		Отказ = Истина;
		Возврат;

	КонецЕсли;
	
	СтруктураИсходныхПараметров.Свойство("НомерСтрокиТабличнойЧасти", 	НомерСтрокиТабличнойЧасти);
	СтруктураИсходныхПараметров.Свойство("ИмяТабличнойЧасти", 			ИмяТабличнойЧасти);
	
	Если НомерСтрокиТабличнойЧасти = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ИмяТабличнойЧасти = Неопределено Тогда
		ИмяТабличнойЧасти = "ИсходныеКомплектующие";
	КонецЕсли;
	
	СтрокаТабличнойЧасти = СправочникОбъект[ИмяТабличнойЧасти].Получить(НомерСтрокиТабличнойЧасти - 1);
	КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
	Если КлючСвязи = 0 Тогда
		КлючСвязи = УчетСерийныхНомеров.ПолучитьНовыйКлючСвязи(мПараметрыСвязиСтрокТЧ, СправочникОбъект, ИмяТабличнойЧасти, Истина);
		мНовыйКлючСвязи = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	УправлениеДиалогом();
	
	Если ИмяТабличнойЧасти = "ИсходныеКомплектующие" Тогда
		ЭлементыФормы.ПанельСвойств.ТекущаяСтраница = ЭлементыФормы.ПанельСвойств.Страницы.ИсходныеКомплектующие;
	Иначе
		ЭлементыФормы.ПанельСвойств.ТекущаяСтраница = ЭлементыФормы.ПанельСвойств.Страницы.ВозвратныеОтходы;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПриПовторномОткрытии" формы.
//
Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	СтруктураИсходныхПараметров.Свойство("НомерСтрокиТабличнойЧасти", 	НомерСтрокиТабличнойЧасти);
	
	Если НомерСтрокиТабличнойЧасти = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИмяТабличнойЧасти = "ИсходныеКомплектующие";
	
	СтрокаТабличнойЧасти = СправочникОбъект[ИмяТабличнойЧасти].Получить(НомерСтрокиТабличнойЧасти - 1);
	КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
	Если КлючСвязи = 0 Тогда
		КлючСвязи = УчетСерийныхНомеров.ПолучитьНовыйКлючСвязи(мПараметрыСвязиСтрокТЧ, СправочникОбъект, ИмяТабличнойЧасти, Истина);
		мНовыйКлючСвязи = Истина;
	КонецЕсли;
	
	УправлениеДиалогом();
	
КонецПроцедуры // ПриПовторномОткрытии()

// Процедура - обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // ПередЗакрытием()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "ОК" командной панели формы.
//
Процедура ОсновныеДействияФормыОк(Кнопка)
	
	Закрыть();
	
	СтрокаТабличнойЧасти = СправочникОбъект[ИмяТабличнойЧасти].Получить(НомерСтрокиТабличнойЧасти - 1);
	Если мНовыйКлючСвязи Тогда
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
	КонецЕсли;
	ТекстПредставления = "";
	
	Если ИмяТабличнойЧасти = "ИсходныеКомплектующие" Тогда
		ТабличнаяЧасть = АвтоподборХарактеристики;
	Иначе
		ТабличнаяЧасть = АвтоподборХарактеристикиОтходы;
	КонецЕсли;
	
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		Если Строка.КлючСвязи <> КлючСвязи Тогда
			Продолжить;
		КонецЕсли;
		ТекстПредставления = ТекстПредставления + ?(ПустаяСтрока(ТекстПредставления), "", ", ") + Строка(Строка.Свойство);
	КонецЦикла;
	СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = ТекстПредставления;
	
КонецПроцедуры // ОсновныеДействияФормыОк()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "НачалоВыбора" поля "Свойство" табличной части "АвтоподборХарактеристики".
//
Процедура АвтоподборХарактеристикиСвойствоНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Отбор.НазначениеСвойства.Установить(ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
	
	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		ФормаВыбора.ПараметрТекущаяСтрока = Элемент.Значение;
	КонецЕсли;
	
	ФормаВыбора.Открыть();
	
КонецПроцедуры // АвтоподборХарактеристикиСвойствоНачалоВыбора()

// Процедура - обработчик события "ПриНачалеРедактирования" табличной части "АвтоподборХарактеристики".
//
Процедура АвтоподборХарактеристикиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
	КонецЕсли;
	
КонецПроцедуры // АвтоподборНоменклатурыПриНачалеРедактирования()

// Процедура - обработчик события "НачалоВыбора" поля "Свойство" табличной части "АвтоподборХарактеристикиОтходы".
//
Процедура АвтоподборХарактеристикиОтходыСвойствоНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Отбор.НазначениеСвойства.Установить(ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
	
	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		ФормаВыбора.ПараметрТекущаяСтрока = Элемент.Значение;
	КонецЕсли;
	
	ФормаВыбора.Открыть();
	
КонецПроцедуры // АвтоподборХарактеристикиОтходыСвойствоНачалоВыбора()

// Процедура - обработчик события "ПриНачалеРедактирования" табличной части "АвтоподборХарактеристикиОтходы".
//
Процедура АвтоподборХарактеристикиОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
	КонецЕсли;
	
КонецПроцедуры // АвтоподборХарактеристикиОтходыПриНачалеРедактирования()

мНовыйКлючСвязи = Ложь;