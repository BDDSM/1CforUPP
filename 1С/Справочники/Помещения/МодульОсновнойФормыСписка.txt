////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мЖирныйШрифт;

Перем ОтображенныеТерритории;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПерваяТерритория(Помещение, Территория, НазваниеТерритории)
	
	Если ОтображенныеТерритории[Территория] <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Помещения.Ссылка,
	|	Помещения.Владелец.Наименование КАК НазваниеТерритории
	|ИЗ
	|	Справочник.Помещения КАК Помещения
	|ГДЕ
	|	Помещения.Владелец = &Территория
	|
	|УПОРЯДОЧИТЬ ПО
	|	Помещения.Наименование";
	Запрос.УстановитьПараметр("Территория", Территория);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	НазваниеТерритории = Выборка.НазваниеТерритории;
	
	ОтображенныеТерритории[Территория] = Истина;
	
	Возврат Выборка.Ссылка = Помещение;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.Помещения, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыФайлы(Кнопка)

	ДанныеСтроки = ЭлементыФормы.СправочникСписок.ТекущаяСтрока;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДляСпискаИзображений = Новый Структура("ОтборОбъектИспользование, ОтборОбъектЗначение, ДоступностьОтбораОбъекта, ВидимостьКолонкиОбъекта", Истина, ДанныеСтроки, Ложь, Ложь);
	СтруктураДляСпискаДополнительныхФайлов = Новый Структура("ОтборОбъектИспользование, ОтборОбъектЗначение, ДоступностьОтбораОбъекта, ВидимостьКолонкиОбъекта", Истина, ДанныеСтроки, Ложь, Ложь);
	ОбязательныеОтборы = Новый Структура("Объект", ДанныеСтроки);
	
	РаботаСФайлами.ОткрытьФормуСпискаФайловИИзображений(СтруктураДляСпискаИзображений, СтруктураДляСпискаДополнительныхФайлов, ОбязательныеОтборы, ЭтаФорма);

КонецПроцедуры

Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.Помещения, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
	
КонецПроцедуры

Процедура КоманднаяПанельТерриторииТерритории(Кнопка)
	
	Справочники.Территории.ПолучитьФормуСписка().Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура СправочникСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ОтображенныеТерритории.Очистить();
	
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки = Неопределено Тогда
			Оформление.Ячейки.Наименование.УстановитьТекст("Все территории");
			Оформление.Ячейки.Наименование.ОтображатьКартинку	= Ложь;
			Оформление.Ячейки.Наименование.Шрифт				= мЖирныйШрифт;
			Оформление.Ячейки.Подразделения.Видимость			= Ложь;
			Оформление.Ячейки.Организация.Видимость				= Ложь;
			Продолжить;
		КонецЕсли;
		
		НазваниеТерритории	= "";
		
		Если ПерваяТерритория(Оформление.ДанныеСтроки.Ссылка, Оформление.ДанныеСтроки.Владелец, НазваниеТерритории) Тогда
			Оформление.Ячейки.Территория.УстановитьТекст(НазваниеТерритории);
			Оформление.Ячейки.Территория.Шрифт = мЖирныйШрифт;
		Иначе
			Оформление.Ячейки.Территория.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мЖирныйШрифт = Новый Шрифт(ШрифтыСтиля.ШрифтТекста,,, Истина);

ОтображенныеТерритории = Новый Соответствие;

СправочникСписок.Порядок.Установить("Владелец, Наименование");