////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мЖирныйШрифт;

Перем ОтображенныеТерритории;

Перем мСвободныеПомещения Экспорт;

Перем СписокСвободныхПомещений;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПерваяТерритория(Помещение, Территория, НазваниеТерритории)
	
	Если ОтображенныеТерритории[Территория] <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Помещения.Ссылка,
	|	Помещения.Владелец.Наименование КАК НазваниеТерритории
	|ИЗ
	|	Справочник.Помещения КАК Помещения
	|ГДЕ
	|	Помещения.Владелец = &Территория
	|	" + ?(СписокСвободныхПомещений = Неопределено, "", "И Помещения.Ссылка В(&СписокСвободныхПомещений)") + "
	|	" + ?(Отбор.ДляВстреч.Использование, "И Помещения.ДляВстреч = &ДляВстреч", "") + "
	|	" + ?(Отбор.Наименование.Использование, "И Помещения.Наименование ПОДОБНО """ + Отбор.Наименование.Значение + """", "") + "
	|	" + ?(Отбор.Код.Использование, "И Помещения.Код ПОДОБНО """ + Отбор.Код.Значение + """", "") + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Помещения.Наименование";
	Запрос.УстановитьПараметр("Территория",					Территория);
	Запрос.УстановитьПараметр("СписокСвободныхПомещений",	СписокСвободныхПомещений);
	Запрос.УстановитьПараметр("ДляВстреч",					Отбор.ДляВстреч.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	НазваниеТерритории = Выборка.НазваниеТерритории;
	
	ОтображенныеТерритории[Территория] = Истина;
	
	Возврат Выборка.Ссылка = Помещение;
	
КонецФункции

Процедура ВыбратьПомещение(Значение, СтандартнаяОбработка = Истина)
	
	Если мСвободныеПомещения <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		
		ВыбраннаяСтрока = мСвободныеПомещения.Найти(Значение, "Помещение");
		
		ОповеститьОВыборе(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ЭлементыФормы.СправочникСписок.Колонки.СвободноС.Видимость = мСвободныеПомещения <> Неопределено;
	
	Если мСвободныеПомещения <> Неопределено Тогда
		СписокСвободныхПомещений = Новый СписокЗначений;
		
		Для Каждого СвободноеПомещение Из мСвободныеПомещения Цикл
			СписокСвободныхПомещений.Добавить(СвободноеПомещение.Помещение);
		КонецЦикла;

		Отбор.Ссылка.ВидСравнения	= ВидСравнения.ВСписке;
		Отбор.Ссылка.Значение		= СписокСвободныхПомещений;
		Отбор.Ссылка.Использование	= Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура СправочникСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ОтображенныеТерритории.Очистить();
	
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки = Неопределено Тогда
			Оформление.Ячейки.Наименование.УстановитьТекст("Все территории");
			Оформление.Ячейки.Наименование.ОтображатьКартинку	= Ложь;
			Оформление.Ячейки.Наименование.Шрифт				= мЖирныйШрифт;
			Оформление.Ячейки.Подразделения.Видимость			= Ложь;
			Оформление.Ячейки.Организация.Видимость				= Ложь;
			Продолжить;
		КонецЕсли;
		
		НазваниеТерритории	= "";
		
		Если ПерваяТерритория(Оформление.ДанныеСтроки.Ссылка, Оформление.ДанныеСтроки.Владелец, НазваниеТерритории) Тогда
			Оформление.Ячейки.Территория.УстановитьТекст(НазваниеТерритории);
			Оформление.Ячейки.Территория.Шрифт = мЖирныйШрифт;
		Иначе
			Оформление.Ячейки.Территория.Видимость = Ложь;
		КонецЕсли;
		
		Если мСвободныеПомещения <> Неопределено Тогда
			СвободноеПомещение = мСвободныеПомещения.Найти(Оформление.ДанныеСтроки.Ссылка, "Помещение");
			Если СвободноеПомещение <> Неопределено Тогда
				Оформление.Ячейки.СвободноС.УстановитьТекст("свободно с " + Формат(СвободноеПомещение.ДатаНачала, "ДФ=Ч:мм"));
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СправочникСписокВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	
	ВыбратьПомещение(Значение, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ДействияФормыВыбрать(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбратьПомещение(ДанныеСтроки.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мЖирныйШрифт = Новый Шрифт(ШрифтыСтиля.ШрифтТекста,,, Истина);

ОтображенныеТерритории = Новый Соответствие;

СправочникСписок.Порядок.Установить("Владелец, Наименование");