////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыЗаписатьНаДиск(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение("Выберите конвертацию, которую требуется сохранить на диск.");
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла				= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок		= "Укажите каталог для записи файлов конвертации";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		Попытка
			Если Не ПустаяСтрока(Конвертация.КонвертацияПомощник) Тогда
				КонвертацияПомощник				= Конвертация.ХранилищеПомощник.Получить();
				КонвертацияОписаниеПомощника	= Конвертация.ХранилищеОписаниеПомощника.Получить();
				
				Если	КонвертацияПомощник = Неопределено
					ИЛИ	КонвертацияОписаниеПомощника = Неопределено Тогда
					КонвертацияПомощник				= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПомощник + "_ert");
					КонвертацияОписаниеПомощника	= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПомощник + "_efd");
				КонецЕсли;
				
				КонвертацияПомощник.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".ert");
				КонвертацияОписаниеПомощника.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".efd");
			КонецЕсли;
			
			Если Не ПустаяСтрока(Конвертация.КонвертацияОбработка) Тогда
				КонвертацияОбработка			= Конвертация.ХранилищеОбработка.Получить();
				Если	КонвертацияОбработка = Неопределено Тогда
					КонвертацияОбработка			= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияОбработка + "_ert");
				КонецЕсли;
				КонвертацияОбработка.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияОбработка + ".ert");
			КонецЕсли;
			
			КонвертацияПравила				= Конвертация.ХранилищеПравила.Получить();
			Если	КонвертацияПравила = Неопределено Тогда
				КонвертацияПравила				= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПравила + "_xml");
			КонецЕсли;
			КонвертацияПравила.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПравила + ".xml");
			
			Предупреждение("Файлы конвертации записаны в каталог: "+ ДиалогВыбораФайла.Каталог);
			
		Исключение
			Предупреждение("Файлы конвертации записать не удалось: " + ОписаниеОшибки());
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыЗаменитьФайлы(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение("Выберите конвертацию, для которой требуется заменить файлы конвертации.");
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла				= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок		= "Укажите каталог с файлами конвертации";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ФайлПомощника			= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".ert";
		ФайлОписанияПомощника	= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".efd";
		ФайлОбработки			= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияОбработка + ".ert";
		ФайлПравил				= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПравила + ".xml";
		
		ФайлыЗаменены = Ложь;
		Попытка
			ОбъектКонвертации	= Конвертация.Ссылка.ПолучитьОбъект();
			
			Файл = Новый Файл(ФайлПомощника);
			Если Файл.Существует() Тогда
				ОбъектКонвертации.ХранилищеПомощник				= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлПомощника));
				ФайлыЗаменены = Истина;
			КонецЕсли;
			Файл = Новый Файл(ФайлОписанияПомощника);
			Если Файл.Существует() Тогда
				ОбъектКонвертации.ХранилищеОписаниеПомощника	= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлОписанияПомощника));
				ФайлыЗаменены = Истина;
			КонецЕсли;
			Файл = Новый Файл(ФайлОбработки);
			Если Файл.Существует() Тогда
				ОбъектКонвертации.ХранилищеОбработка			= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлОбработки));
				ФайлыЗаменены = Истина;
			КонецЕсли;
			Файл = Новый Файл(ФайлПравил);
			Если Файл.Существует() Тогда
				ОбъектКонвертации.ХранилищеПравила				= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлПравил));
				ФайлыЗаменены = Истина;
			КонецЕсли;
			ОбъектКонвертации.Записать();
			
			Если ФайлыЗаменены Тогда
				Предупреждение("Файлы конвертации заменены.");
				
			Иначе
				Предупреждение("В указанном каталоге не обнаружены файлы конвертации для замены.");
				
			КонецЕсли;
			
		Исключение
			Предупреждение("Файлы конвертации заменить не удалось.");
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыВосставитьФайлы(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение("Выберите конвертацию, для которой требуется восстановить файлы конвертации.");
		Возврат;
	КонецЕсли;
	
	Если Конвертация.Ссылка = Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ЗарплатаКадры Тогда
		ПроцедурыОбновленияИнформационнойБазы.ЗаполнитьПредопределенныеЭлементы77();
	КонецЕсли;
	
	Попытка
		ОбъектКонвертации	= Конвертация.Ссылка.ПолучитьОбъект();
		ОбъектКонвертации.ХранилищеПомощник				= Неопределено;
		ОбъектКонвертации.ХранилищеОписаниеПомощника	= Неопределено;
		ОбъектКонвертации.ХранилищеОбработка			= Неопределено;
		ОбъектКонвертации.ХранилищеПравила				= Неопределено;
		ОбъектКонвертации.Записать();
		Предупреждение("Файлы конвертации заменены.");
	Исключение
		Предупреждение("Файлы конвертации заменить не удалось.");
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СправочникСписок.Колонки.Добавить("ХранилищеПомощник");
СправочникСписок.Колонки.Добавить("ХранилищеОписаниеПомощника");
СправочникСписок.Колонки.Добавить("ХранилищеОбработка");
СправочникСписок.Колонки.Добавить("ХранилищеПравила");