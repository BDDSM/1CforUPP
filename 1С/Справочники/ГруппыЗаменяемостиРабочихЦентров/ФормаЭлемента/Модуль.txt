// Функция предназначена для поиска дублей в составе группы рабочих центров
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//  Истина в случае обнаружения дублей, Ложь - в противном случае.
//
Функция НаличиеДублей()
	
	ТабСостава = Состав.Выгрузить();
	ТабСостава.Колонки.Добавить("КоличествоРЦ");
	ТабСостава.ЗаполнитьЗначения(1, "КоличествоРЦ");
	ТабСостава.Свернуть("РабочийЦентр", "КоличествоРЦ");
	
	Если ТабСостава.Количество() <> Состав.Количество() Тогда
		
		// есть дубли
		Для каждого СтрокаСостава Из ТабСостава Цикл
			
			Если СтрокаСостава.КоличествоРЦ > 1 Тогда
				
				ОбщегоНазначения.Сообщение("В составе группы дублируется рабочий центр " + СтрокаСостава.РабочийЦентр + "!");
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // НаличиеДублей()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
	
КонецПроцедуры // ПриОткрытии()

Процедура ПриЗаписи(Отказ)
	
	Отказ = НаличиеДублей();
	
КонецПроцедуры // ПриЗаписи()

Процедура ПослеЗаписи()
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ПослеЗаписи()

Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ДействияФормыРедактироватьКод()