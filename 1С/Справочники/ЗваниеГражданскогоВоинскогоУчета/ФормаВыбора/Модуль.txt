////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСписокПредопределенныхЗваний;
Перем мОбработкаПодбораПоСтроке;
Перем мТекстПодбораПоСтроке;
Перем мПоследнееЗначениеЭлементаПодбораПоСтроке;

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция формирует структуру параметров для ввода звания по подстроке
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Структура имен и значений параметров
//
Функция ПолучитьСтруктуруПараметровПодбораПоСтроке()

	СтруктураПараметров = Новый Структура("Предопределенный", Истина);
	
	Возврат СтруктураПараметров;

КонецФункции // ПолучитьСтруктуруПараметровПодбораПоСтроке()()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВоинскиеЗвания.Ссылка,
	|	ВоинскиеЗвания.Наименование
	|ИЗ
	|	Справочник.ЗваниеГражданскогоВоинскогоУчета КАК ВоинскиеЗвания
	|ГДЕ
	|	ВоинскиеЗвания.Предопределенный";
	Выборка = Запрос.Выполнить().Выбрать();
	мСписокПредопределенныхЗваний	=	Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		мСписокПредопределенныхЗваний.Добавить(Выборка.Ссылка,Выборка.Наименование);
	КонецЦикла;
	
	ЭлементУправленияОбщевойсковогоЗвания = ЭлементыФормы.СправочникСписок.Колонки.ОбщевойсковоеЗвание.ЭлементУправления;
	ЭлементУправленияОбщевойсковогоЗвания.СписокВыбора = мСписокПредопределенныхЗваний;
	ЭлементУправленияОбщевойсковогоЗвания.ВысотаСпискаВыбора = мСписокПредопределенныхЗваний.Количество(); 
	ЭлементУправленияОбщевойсковогоЗвания.ШиринаСпискаВыбора = 30; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ 

Процедура СправочникСписокПередНачаломИзменения(Элемент, Отказ)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Отказ = Элемент.ТекущиеДанные.Предопределенный
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ реквизитов ТАБЛИЧНОГО ПОЛЯ 

Процедура СправочникСписокОбщевойсковоеЗваниеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = Справочники.ЗваниеГражданскогоВоинскогоУчета.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Отбор.Ссылка.ВидСравнения	= ВидСравнения.ВСписке;
	ФормаВыбора.Отбор.Ссылка.Значение		= мСписокПредопределенныхЗваний;
	ФормаВыбора.Отбор.Ссылка.Использование	= Истина;
	ФормаВыбора.НачальноеЗначениеВыбора = Элемент.Значение;
    ФормаВыбора.Заголовок = "Общевойсковые звания";
	ФормаВыбора.ЭлементыФормы.СправочникСписок.Колонки.ОбщевойсковоеЗвание.Видимость = Ложь;
	ФормаВыбора.ЭлементыФормы.СправочникСписок.Колонки.ОбщевойсковоеЗвание.ИзменятьНастройку = Ложь;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура СправочникСписокОбщевойсковоеЗваниеАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке(), Тип("СправочникСсылка.ЗваниеГражданскогоВоинскогоУчета"));
	
КонецПроцедуры

Процедура СправочникСписокОбщевойсковоеЗваниеОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке(), ЭтаФорма, Тип("СправочникСсылка.ЗваниеГражданскогоВоинскогоУчета"), мОбработкаПодбораПоСтроке, мТекстПодбораПоСтроке, мПоследнееЗначениеЭлементаПодбораПоСтроке, Ложь);
	
КонецПроцедуры



мОбработкаПодбораПоСтроке                 = Ложь;
мТекстПодбораПоСтроке                     = "";
мПоследнееЗначениеЭлементаПодбораПоСтроке = Неопределено;