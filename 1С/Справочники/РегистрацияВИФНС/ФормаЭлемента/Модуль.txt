Процедура УправлениеЭУ()
	
	Если Метаданные.Справочники.Найти("ФизическиеЛица") = Неопределено Тогда
		Если Представитель.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Организация Тогда
			ТекСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительЮрЛицо;
		Иначе
			ТекСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительФизЛицо;
		КонецЕсли;
	Иначе
		Если ТипЗнч(Представитель) = Тип("СправочникСсылка.Контрагенты") Тогда
			ТекСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительЮрЛицо;
		Иначе
			ТекСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительФизЛицо;
		КонецЕсли;
	КонецЕсли;
	ЭлементыФормы.ПанельПредставитель.ТекущаяСтраница = ТекСтраница;
	
	ЭлементыФормы.ПанельКПП.Видимость = НЕ РегламентированнаяОтчетность.ЭтоПБОЮЛ(Владелец);
	
КонецПроцедуры

Процедура ДействияФормыЗаполнить(Кнопка)

	СведОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Владелец, ОбщегоНазначения.ПолучитьРабочуюДату(), "КодНО, НаимНО, КППЮЛ");
	Если ЗначениеЗаполнено(СведОбОрганизации) Тогда
		Если НЕ ПустаяСтрока(СведОбОрганизации.КодНО) Тогда
			Результат = Справочники.РегистрацияВИФНС.НайтиПоКоду(СведОбОрганизации.КодНО,,, Владелец);
			Если НЕ ЗначениеЗаполнено(Результат) Тогда
				Код = СведОбОрганизации.КодНО;
				НаименованиеИФНС = СведОбОрганизации.НаимНО;
				КПП = СведОбОрганизации.КППЮЛ;
			Иначе
				Сообщить("В справочнике уже есть необходимая запись об инспекции.", СтатусСообщения.Информация);
			КонецЕсли;
		Иначе
			Сообщить("Необходимо заполнить реквизит ""Код ИФНС"" в справочнике ""Организации"".", СтатусСообщения.Информация);
		КонецЕсли;
	Иначе
		Сообщить("Не удалось получить необходимые данные об организации.", СтатусСообщения.Важное);
	КонецЕсли;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		ОргПоУмолчанию = РегламентированнаяОтчетность.ПолучитьОрганизациюПоУмолчанию();
		Если ЗначениеЗаполнено(ОргПоУмолчанию) Тогда
			Владелец = ОргПоУмолчанию;
		КонецЕсли;
	КонецЕсли;

	УчетПоВсемОрганизациям = РегламентированнаяОтчетность.ПолучитьПризнакУчетаПоВсемОрганизациям();
	ЭлементыФормы.Владелец.ТолькоПросмотр = НЕ УчетПоВсемОрганизациям;
	
	УправлениеЭУ();
	
КонецПроцедуры

Процедура ПредставительПриИзменении(Элемент)
	
	УправлениеЭУ();
	
КонецПроцедуры

Процедура ВладелецПриИзменении(Элемент)
	
	УправлениеЭУ();
	
КонецПроцедуры