////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры
// Производит открывает форму подбора данных в табличные части документа
//
Процедура ДействиеПодбор()

	ФормаПодбора = Справочники.Номенклатура.ПолучитьФормуВыбора(,ЭтаФорма);
	ФормаПодбора.РазрешитьСоединятьОкно          = Истина;
	ФормаПодбора.СоединяемоеОкно                 = Истина;
	ФормаПодбора.РазрешитьСостояниеПрикрепленное = Истина;
	ФормаПодбора.ПоложениеПрикрепленногоОкна     = ВариантПрикрепленияОкна.Право;
	ФормаПодбора.СостояниеОкна                   = ВариантСостоянияОкна.Прикрепленное;
	ФормаПодбора.РазрешитьСостояниеОбычное       = Ложь;
	ФормаПодбора.РазрешитьСостояниеПрячущееся    = Истина;
	
	ФормаПодбора.ЗакрыватьПриВыборе = Ложь;
	ФормаПодбора.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаПодбора.МножественныйВыбор = Истина;
	ФормаПодбора.Открыть();
	
КонецПроцедуры // ДействиеПодбор()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
//  ТабличнаяЧасть    - табличная часть, в которую надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(Номенклатура)

	// Ищем выбранную позицию в таблице подобранной номенклатуры.
	//  Если не найдем - добавим новую строку.
	СтруктураОтбора = Новый Структура();

	СтруктураОтбора.Вставить("Номенклатура", Номенклатура);

	ТабличнаяЧасть = Состав;
	СтрокаТабличнойЧасти = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(ТабличнаяЧасть, СтруктураОтбора);
	Если СтрокаТабличнойЧасти = Неопределено Тогда

		// Не нашли - добавляем новую строку.
		СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
		СтрокаТабличнойЧасти.Номенклатура = Номенклатура;

	КонецЕсли;

	ТабличнаяЧастьИмя = "Состав";

	ЭлементыФормы[ТабличнаяЧастьИмя].ТекущаяСтрока = СтрокаТабличнойЧасти;
	ЭлементыФормы[ТабличнаяЧастьИмя].ТекущаяКолонка = ЭлементыФормы[ТабличнаяЧастьИмя].Колонки["Номенклатура"];

КонецПроцедуры // ОбработкаПодбора()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
 	ОбработкаПодбора(ЗначениеВыбора);	
	
КонецПроцедуры // ОбработкаВыбора()

Процедура ПриОткрытии()
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ПослеЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Подбор" командной панели
// табличного поля.
//
Процедура КоманднаяПанельСоставПодбор(Кнопка)
	
	ДействиеПодбор();
	
КонецПроцедуры // КоманднаяПанельПодбор()

Процедура СоставПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	Выбор = ПараметрыПеретаскивания.Значение;
	Если НЕ ЗначениеЗаполнено(Выбор) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Выбор) = Тип("Массив") Тогда
		Для Каждого Номенклатура Из Выбор Цикл
			ОбработкаПодбора(Номенклатура);
		КонецЦикла;
	Иначе
		ОбработкаПодбора(Выбор);
	КонецЕсли;

КонецПроцедуры // СоставПроверкаПеретаскивания()

Процедура ПередЗаписью(Отказ)
	
	Для Каждого Строка Из Состав Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			Сообщить("Не указана номенклатура в строке № " + Строка.НомерСтроки, СтатусСообщения.Важное);
			Отказ = Истина;
		ИначеЕсли Строка.Номенклатура.Услуга Тогда
			Сообщить("В строке № " + Строка.НомерСтроки + " указана услуга. Услуг быть не должно!", СтатусСообщения.Важное);
			Отказ = Истина;
		ИначеЕсли Строка.Номенклатура.Набор Тогда
			Сообщить("В строке № " + Строка.НомерСтроки + " указан набор. Наборов быть не должно!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()