////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.ПолучитьМакет("СписокВредныхПрофессий"+НомерСписка);

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;

	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.

	ТабличныйДокумент   = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть      = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод          = ТабличныйДокумент.Области.Код;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		Код          		= СокрЛП(ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКод.Лево, ТекущаяОбласть.Низ, ОбластьКод.Право).Текст);
		Наименование 		= СокрЛП(ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст);
		
		//Позиции списков должны начинаться с цифры 1 или с цифры 2
		Если (Лев(Код,1)<>"1")и(Лев(Код,1)<>"2") тогда
			Возврат;
		КонецЕсли;	 
		
		// Проверка наличия выбранной позиции
		
		Ссылка = Справочники.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.НайтиПоКоду(Код);
		
		Если НЕ Ссылка.Пустая() Тогда
			Вопрос = "В справочнике ""Списки профессий должностей льготного пенсионного обеспечения"" уже существует элемент с кодом """ + Код + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;
				
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Создание новой позиции
		
		ФормаНовогоЭлемента = Справочники.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		
		ФормаНовогоЭлемента.Код                = Код;
		ФормаНовогоЭлемента.Наименование       = Наименование;
		
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтруктураКолонок = Новый Структура();
			СтруктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "Код", 20));
			СтруктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтруктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			Форма.ТипСправочника = "СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения";
			Форма.НастроитьФорму(СтруктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			Код          = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКод.         Лево, ТекущаяСтрока, ОбластьКод.         Право).Текст;
			Наименование = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименование.Лево, ТекущаяСтрока, ОбластьНаименование.Право).Текст;
			
			ЭтоЧисло = Истина;
			//Попытка
			//	КодКакЧисло = Число(Код);
			//Исключение
			//	ЭтоЧисло = Ложь;
			//КонецПопытки;
			
			Если ЭтоЧисло Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Код = Код;				
				СтрокаПодбора.Наименование = Наименование;
				СтрокаПодбора.Видимость = Справочники.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.НайтиПоКоду(Код).Пустая();
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();
	КонецЕсли;

КонецПроцедуры