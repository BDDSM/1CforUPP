Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если СтатусСоглашения <> Перечисления.СтатусыСоглашенийЭД.Действует Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторОрганизации");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаОрганизации");
		МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СертификатАбонента");
		МассивНепроверяемыхРеквизитов.Добавить("Ответственный");
		
	Иначе
		Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторОрганизации");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторКонтрагента");
			МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаОрганизации");
			МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаКонтрагента");
			МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("СертификатАбонента");
		КонецЕсли;
		
		Если ЭтоИнтеркампани Тогда
			МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаОрганизации");
			МассивНепроверяемыхРеквизитов.Добавить("АдресРесурсаКонтрагента");
			
			// Удалим реквизиты из автоматической проверки.
			МассивНепроверяемыхРеквизитов.Добавить("Организация");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторОрганизации");
			МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторКонтрагента");
			
			// Проверим заполненность реквизитов вручную.
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументы.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация-отправитель"),
					ЭтотОбъект, 
					"Организация");
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументы.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация-получатель"),
					ЭтотОбъект,
					"Контрагент");
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументы.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор отправителя"),
					ЭтотОбъект,
					"ИдентификаторОрганизации");
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ИдентификаторКонтрагента) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументы.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор получателя"),
					ЭтотОбъект,
					"ИдентификаторКонтрагента");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры