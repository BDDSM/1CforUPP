////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры

// Обработчик события ПриОткрытии Формы.
//
Процедура ПриОткрытии()

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);

	УстановитьДоступностьЭлементов();

КонецПроцедуры //ПриОткрытии

// Обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписью(Отказ)

	Отказ = Ложь;

	УстановитьДоступностьЭлементов(, Отказ);

	Если Не Отказ Тогда
		Если ФормироватьНефискальныеЧеки
		   И Не ЗначениеЗаполнено(ШиринаЛенты) Тогда
			Сообщить("Ошибка! Не указана ширина ленты используемой ККМ.
			|Укажите ширину ленты!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПослеЗаписи формы.
//
Процедура ПослеЗаписи()

	Оповестить("ИзмененЭлементКассыККМ", , Ссылка);

	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);

КонецПроцедуры

Процедура ВладелецПриИзменении(Элемент)

	УстановитьДоступностьЭлементов(Истина);

КонецПроцедуры

Процедура ФормироватьНефискальныеЧекиПриИзменении(Элемент)

	УстановитьДоступностьЭлементов(Истина);

КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		УстановитьДоступностьЭлементов();
	КонецЕсли;

КонецПроцедуры


Процедура УстановитьДоступностьЭлементов(РазрешитьИзменениеФлага = Ложь, Отказ = Ложь)

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.РозничнаяТорговляОблагаетсяЕНВД
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Дата, Организация = &Ссылка) КАК УчетнаяПолитикаОрганизацийСрезПоследних");
	Запрос.УстановитьПараметр("Ссылка", Владелец);
	Запрос.УстановитьПараметр("Дата"  , ТекущаяДата());

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Если Выборка.РозничнаяТорговляОблагаетсяЕНВД Тогда
			ЭлементыФормы.ФормироватьНефискальныеЧеки.Доступность = Истина;
			ЭлементыФормы.РучнойРежимФормированияЧека.Доступность = ФормироватьНефискальныеЧеки;
			ЭлементыФормы.НадписьШиринаЛенты.Доступность          = ФормироватьНефискальныеЧеки;
			ЭлементыФормы.ШиринаЛенты.Доступность                 = ФормироватьНефискальныеЧеки;

			ЭлементыФормы.НадписьЗапретНефискальныхЧеков.Заголовок =
			"Использование режима печати нефискальных чеков ККМ возможно,
			|только если организация облагается ЕНВД.
			|Организация """ + Владелец + """ облагается ЕНВД.
			|Формирование нефискальных чеков доступно.";
			ЭлементыФормы.НадписьЗапретНефискальныхЧеков.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
		Иначе
			Если РазрешитьИзменениеФлага Тогда
				ФормироватьНефискальныеЧеки = Ложь;
			КонецЕсли;

			ЭлементыФормы.ФормироватьНефискальныеЧеки.Доступность = ФормироватьНефискальныеЧеки;
			ЭлементыФормы.РучнойРежимФормированияЧека.Доступность = Ложь;
			ЭлементыФормы.НадписьШиринаЛенты.Доступность          = Ложь;
			ЭлементыФормы.ШиринаЛенты.Доступность                 = Ложь;

			Если ФормироватьНефискальныеЧеки Тогда
				ЭлементыФормы.НадписьЗапретНефискальныхЧеков.Заголовок =
				"Внимание!
				|Организация """ + Владелец + """ не облагается ЕНВД.
				|Формирование нефискальных чеков недопустимо.
				|Отключите режим ""Формировать нефискальные чеки""";
				ЭлементыФормы.НадписьЗапретНефискальныхЧеков.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;

				Отказ = Истина;
			Иначе
				ЭлементыФормы.НадписьЗапретНефискальныхЧеков.Заголовок =
				"Использование режима печати нефискальных чеков ККМ возможно,
				|только если организация облагается ЕНВД.
				|Организация """ + Владелец + """ не облагается ЕНВД.
				|Формирование нефискальных чеков недоступно.";
				ЭлементыФормы.НадписьЗапретНефискальныхЧеков.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры