////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НаборЗаписей.Количество() = 0 тогда
		НаборЗаписей.Добавить();
	КонецЕсли;
	ЗаписьИзНабора = НаборЗаписей[0];
	
	ФизЛицо								= ЗаписьИзНабора.ФизЛицо;
	Период								= ЗаписьИзНабора.Период;
	ПорядокНачисленияСеверныхНадбавок	= ЗаписьИзНабора.ПорядокНачисленияСеверныхНадбавок;
	НачальныйПроцентСевернойНадбавки	= ЗаписьИзНабора.НачальныйПроцентСевернойНадбавки;
	СеверныйСтажЛет						= Цел(ЗаписьИзНабора.СеверныйСтажМесяцев / 12);
	СеверныйСтажМесяцев					= ЗаписьИзНабора.СеверныйСтажМесяцев - СеверныйСтажЛет * 12;
	СеверныйСтажДней					= ЗаписьИзНабора.СеверныйСтажДней;

	ФизЛицоПриОткрытии								= ФизЛицо;
	ПериодПриОткрытии								= Период;
	ПорядокНачисленияСеверныхНадбавокПриОткрытии	= ПорядокНачисленияСеверныхНадбавок;
	НачальныйПроцентСевернойНадбавкиПриОткрытии		= НачальныйПроцентСевернойНадбавки;
	СеверныйСтажЛетПриОткрытии						= СеверныйСтажЛет;
	СеверныйСтажМесяцевПриОткрытии					= СеверныйСтажМесяцев;
	СеверныйСтажДнейПриОткрытии						= СеверныйСтажДней;
	
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Если ФизЛицо.Пустая() тогда
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
		
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.РазделительИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодтвердитьИзменения(Кнопка)
	
	ЗаписьИзНабора 			= НаборЗаписей[0];
	// Если что-то поменяли тогда запишем 
	Если ПорядокНачисленияСеверныхНадбавокПриОткрытии <> ПорядокНачисленияСеверныхНадбавок ИЛИ 
		 НачальныйПроцентСевернойНадбавкиПриОткрытии  <> НачальныйПроцентСевернойНадбавки ИЛИ 
		 СеверныйСтажЛетПриОткрытии <> СеверныйСтажЛет ИЛИ 
		 СеверныйСтажМесяцевПриОткрытии <> СеверныйСтажМесяцев ИЛИ 
		 СеверныйСтажДнейПриОткрытии 	<> СеверныйСтажДней ИЛИ 
		 ПериодПриОткрытии 				<> Период Тогда
		 
		ЗаписьИзНабора.Период   				= Период;
		ЗаписьИзНабора.ПорядокНачисленияСеверныхНадбавок = ПорядокНачисленияСеверныхНадбавок;
		ЗаписьИзНабора.НачальныйПроцентСевернойНадбавки  = НачальныйПроцентСевернойНадбавки;
		ЗаписьИзНабора.СеверныйСтажМесяцев	 	= СеверныйСтажЛет * 12 + СеверныйСтажМесяцев;
		ЗаписьИзНабора.СеверныйСтажДней			= СеверныйСтажДней;
	КонецЕсли; 
	
	// оповестим все формы, показывающие данные физлица, для которого 
	// редактируются связанные данные
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","СеверныйСтаж"), ОбщегоНазначенияЗК.ОпределитьОповещаемоеФизЛицо(ФизЛицо, ВладелецФормы));
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СведенияОСтажеРаботыНаСевере.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры