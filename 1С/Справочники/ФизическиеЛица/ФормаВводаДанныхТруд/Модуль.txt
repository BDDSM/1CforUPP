////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мНаборЗаписейСеверногоСтажа Экспорт;
Перем ОбработкаКомментариев;
Перем ДоступноСеверныйСтаж;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ПрочитатьДанные()
	
	ЗаписьИзНабора 			= мНаборЗаписейСеверногоСтажа[0];
	
	Лет		= Цел(ЗаписьИзНабора.СеверныйСтажМесяцев / 12);
	Месяцев	= ЗаписьИзНабора.СеверныйСтажМесяцев - Лет * 12;
	
	СеверныйСтажЛет		= ?(Лет = 0, "", "" + Лет + " " + ОбщегоНазначения.ФормаМножественногоЧисла("год","года","лет", Лет) + ", ");
	СеверныйСтажМесяцев	= "" + Месяцев + " " + ОбщегоНазначения.ФормаМножественногоЧисла("месяц","месяца","месяцев",Месяцев) + ", ";
	
	СеверныйСтаж = "Выслуженный % надбавки: " + ЗаписьИзНабора.НачальныйПроцентСевернойНадбавки 
					+ "; группа начисления очередной надбавки " + ЗаписьИзНабора.ПорядокНачисленияСеверныхНадбавок 
					+ "; выслуженный стаж - " + СеверныйСтажЛет + СеверныйСтажМесяцев + ЗаписьИзНабора.СеверныйСтажДней + " " + ОбщегоНазначения.ФормаМножественногоЧисла("день","дня","дней",ЗаписьИзНабора.СеверныйСтажДней);
	Если СеверныйСтаж = "Выслуженный % надбавки: 0; группа начисления очередной надбавки ; выслуженный стаж - 0 месяцев, 0 дней" Тогда
		СеверныйСтаж = ?(ДоступноСеверныйСтаж, "Ввести данные о стаже", "Данные о стаже не заданы");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ДоступноСеверныйСтаж = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СведенияОСтажеРаботыНаСевере);
	ЭлементыФормы.СеверныйСтаж.ТолькоПросмотр = Не ДоступноСеверныйСтаж;
	
	ПрочитатьДанные();
	Заголовок = "Трудовая деятельность: " + Ссылка.Наименование; 
	
	ДоступноСЗВК = ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЗаписиОСтажеДляСЗВК);
	ЭлементыФормы.ДействияФормы.Кнопки.ДействиеВызватьФормуСЗВК.Доступность = ДоступноСЗВК;
	ЭлементыФормы.ДействияФормы.Кнопки.Действие.Доступность = ДоступноСЗВК;
	
	Для Каждого Стаж Из Стажи Цикл
		Строка = СтажиСГодом.Добавить();
		Строка.ВидСтажа			= Стаж.ВидСтажа;
		Строка.ДатаОтсчета		= Стаж.ДатаОтсчета;
		Строка.РазмерЛет		= Цел(Стаж.РазмерМесяцев / 12);
		Строка.РазмерМесяцев	= Стаж.РазмерМесяцев - Строка.РазмерЛет * 12;
		Строка.РазмерДней		= Стаж.РазмерДней;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФорму" И (Источник = ЭтаФорма Или Источник = Ссылка) Тогда 
		
		Если Параметр.ИмяЭлемента = "СеверныйСтаж" Тогда
			
			ПрочитатьДанные();
			Модифицированность = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Стажи.Очистить();
	Для Каждого Стаж Из СтажиСГодом Цикл
		Строка = Стажи.Добавить();
		Строка.ВидСтажа			= Стаж.ВидСтажа;
		Строка.ДатаОтсчета		= Стаж.ДатаОтсчета;
		Строка.РазмерМесяцев	= Стаж.РазмерМесяцев + Стаж.РазмерЛет * 12;
		Строка.РазмерДней		= Стаж.РазмерДней;
	КонецЦикла;
	
	// проверим правильность заполнения строк стажа
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВидСтажа", Справочники.ВидыСтажа.ПустаяСсылка());
	СтрокиСОшибками = Стажи.НайтиСтроки(Отбор);
	КоличествоОшибок = СтрокиСОшибками.Количество();
	ТекстСообщенияНеЗаданВидСтажа = "";
	Если КоличествоОшибок > 0 Тогда
		
		ТекстСообщенияНеЗаданВидСтажа = "не указан Вид стажа в " + ?(КоличествоОшибок = 1 , "строке", "строках");
		НомераСтрок = " ";
		Для каждого СтрокаСтажа Из СтрокиСОшибками Цикл
			НомераСтрок = НомераСтрок + СтрокаСтажа.НомерСтроки + ",";
		КонецЦикла;
		НомераСтрок = Лев(НомераСтрок, СтрДлина(НомераСтрок) - 1);
		ТекстСообщенияНеЗаданВидСтажа = ТекстСообщенияНеЗаданВидСтажа + ":" + НомераСтрок;
		
		Отказ = Истина;
		
	КонецЕсли;
	
	// проверим заполнение даты отсчета, ошибка когда:
	// Дата отсчета меньше даты рождения и дата рождения заполнена
	// Дата рождения не заполнена, дата отсчета меньше 1900 года
	
	ЗаполненаДатаРождения = ЗначениеЗаполнено(ДатаРождения);
	ДатаПроверки = ?(ЗаполненаДатаРождения, ДатаРождения, '19000101');
	НомераСтрокПустаяДата = " "; // строки в которых ошибка
	НомераСтрокПлохаяДата = " "; // строки в которых ошибка
	КоличествоОшибок = 0;
	Для каждого СтрокаСтажа Из Стажи Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСтажа.ДатаОтсчета) Тогда
			НомераСтрокПустаяДата = НомераСтрокПустаяДата + СтрокаСтажа.НомерСтроки + ",";
			КоличествоОшибок = КоличествоОшибок + 1;
		ИначеЕсли СтрокаСтажа.ДатаОтсчета <= ДатаПроверки Тогда
			НомераСтрокПлохаяДата = НомераСтрокПлохаяДата + СтрокаСтажа.НомерСтроки + ",";
			КоличествоОшибок = КоличествоОшибок + 1;
		КонецЕсли;	
		
	КонецЦикла;
	
	ТекстСообщенияНеЗаданаДатаОтсчета = "";
	Если КоличествоОшибок > 0 Тогда
		
		Если Не ПустаяСтрока(НомераСтрокПустаяДата) Тогда
			НомераСтрокПустаяДата = Лев(НомераСтрокПустаяДата, СтрДлина(НомераСтрокПустаяДата) - 1);
			ТекстСообщенияНеЗаданаДатаОтсчета = " в " + ?(КоличествоОшибок = 1 , "строке ", "строках ")
			+ НомераСтрокПустаяДата
			+ "не указана Дата отсчета";
		КонецЕсли;	
		
		Если Не ПустаяСтрока(НомераСтрокПлохаяДата) Тогда
			Если Не ПустаяСтрока(ТекстСообщенияНеЗаданаДатаОтсчета) Тогда
				ТекстСообщенияНеЗаданаДатаОтсчета = ТекстСообщенияНеЗаданаДатаОтсчета + Символы.ПС;	
			КонецЕсли;	
			НомераСтрокПлохаяДата = Лев(НомераСтрокПлохаяДата, СтрДлина(НомераСтрокПлохаяДата) - 1);
			ТекстСообщенияНеЗаданаДатаОтсчета = " в " + ?(КоличествоОшибок = 1 , "строке: ", "строках: ")
			+ НомераСтрокПлохаяДата + " "
			+ ?(ЗаполненаДатаРождения, "Дата отсчета стажа должна быть позднее даты рождения - " + Формат(ДатаПроверки, "ДФ=dd.MM.yyyy") + ".", "Дата отсчета стажа должна быть позднее даты 01.01.1900 года.");
		КонецЕсли;
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Если Отказ Тогда
		ТекстСообщенияЗаголовок = "Ошибки в таблице ""Стажи общего характера""";
		ОбработкаКомментариев.ДобавитьСообщение(ТекстСообщенияЗаголовок, Перечисления.ВидыСообщений.Ошибка);
		Если Не ПустаяСтрока(ТекстСообщенияНеЗаданВидСтажа) Тогда
			ОбработкаКомментариев.ДобавитьСообщение(ТекстСообщенияНеЗаданВидСтажа, Перечисления.ВидыСообщений.Информация);
		КонецЕсли;
		Если Не ПустаяСтрока(ТекстСообщенияНеЗаданаДатаОтсчета) Тогда
			ОбработкаКомментариев.ДобавитьСообщение(ТекстСообщенияНеЗаданаДатаОтсчета, Перечисления.ВидыСообщений.Информация);
		КонецЕсли;
		ОбработкаКомментариев.ДобавитьСообщение("Данные не записаны!", Перечисления.ВидыСообщений.ВажнаяИнформация);
	КонецЕсли;		
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// запись в регистр 
	мНаборЗаписейСеверногоСтажа[0].Физлицо = Ссылка;
	Если ДоступноСеверныйСтаж и ЗначениеЗаполнено(мНаборЗаписейСеверногоСтажа[0].Период) тогда
		мНаборЗаписейСеверногоСтажа.Отбор.Физлицо.Использование 	= Истина;
		мНаборЗаписейСеверногоСтажа.Отбор.Физлицо.Значение 		= мНаборЗаписейСеверногоСтажа[0].Физлицо;
		мНаборЗаписейСеверногоСтажа.Отбор.Период.Использование 	= Истина;
		мНаборЗаписейСеверногоСтажа.Отбор.Период.Значение 		= мНаборЗаписейСеверногоСтажа[0].Период;
		мНаборЗаписейСеверногоСтажа.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьДанныеОФизлице", Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыДействиеВызватьФормуСЗВК(Кнопка)
	
	ФормаВводаДанныхСЗВК         = ПолучитьФорму("ФормаВводаДанныхСЗВК", ЭтаФорма, "ФизЛицо"+Код);
	ФормаВводаДанныхСЗВК.Открыть();
		
КонецПроцедуры

Процедура ДействияФормыДействие(Кнопка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизЛицо",СправочникОбъект.Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаписиОСтажеДляСЗВК.ДатаНачалаПериода,
	               |	ЗаписиОСтажеДляСЗВК.ДатаОкончанияПериода,
	               |	ЗаписиОСтажеДляСЗВК.Организация КАК НаименованиеОрганизации,
	               |	ЗаписиОСтажеДляСЗВК.Должность
	               |ИЗ
	               |	РегистрСведений.ЗаписиОСтажеДляСЗВК КАК ЗаписиОСтажеДляСЗВК
	               |
	               |ГДЕ
	               |	ЗаписиОСтажеДляСЗВК.ФизЛицо = &ФизЛицо И
	               |	ЗаписиОСтажеДляСЗВК.НомерДополнительнойЗаписи = 0";

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Количество() = 0 тогда
		Предупреждение("Форма СЗВ-К не содержит данных!");
		Возврат;
	КонецЕсли;	 

	Если ТрудоваяДеятельность.Количество() > 0 тогда
		Вопрос = "Существующие записи о периодах трудовой деятельноси будут удалены. Продолжить?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
    ТрудоваяДеятельность.Очистить();
	
	Пока Выборка.Следующий() цикл
		НоваяСтрока = ТрудоваяДеятельность.Добавить();
		НоваяСтрока.Организация 	= Выборка.НаименованиеОрганизации;
		НоваяСтрока.Должность 		= Выборка.Должность;
		НоваяСтрока.ДатаНачала	 	= Выборка.ДатаНачалаПериода;
		НоваяСтрока.ДатаОкончания	= Выборка.ДатаОкончанияПериода;
	КонецЦикла;	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура СеверныйСтажНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ФормаВвода	 			= ПолучитьФорму("ФормаЗаписиРаботаНаСевере", ЭтаФорма);
	ФормаВвода.НаборЗаписей = мНаборЗаписейСеверногоСтажа;
	ФормаВвода.Открыть();
	
КонецПроцедуры

Процедура СтажиВидСтажаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВидСтажа = ВыбранноеЗначение;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВидСтажа", ВидСтажа);
	СтрокиСДаннымСотрудником = Стажи.НайтиСтроки(Отбор);
	Если СтрокиСДаннымСотрудником.Количество() > 1 Или СтрокиСДаннымСотрудником.Количество() = 1 И СтрокиСДаннымСотрудником[0] <> ЭлементыФормы.Стажи.ТекущаяСтрока Тогда
		Предупреждение("Нельзя вводить один и тот же вид стажа дважды!");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мНаборЗаписейСеверногоСтажа = РегистрыСведений.СведенияОСтажеРаботыНаСевере.СоздатьНаборЗаписей();
СрезСеверныйСтаж    	  = РегистрыСведений.СведенияОСтажеРаботыНаСевере.СрезПоследних(,Новый Структура("ФизЛицо",Ссылка));
ЗаписьНабора 	 = мНаборЗаписейСеверногоСтажа.Добавить();
Если СрезСеверныйСтаж.Количество() > 0 тогда
	ЗаписьНабора.Период 							= СрезСеверныйСтаж[0].Период;
	ЗаписьНабора.Физлицо 							= СрезСеверныйСтаж[0].Физлицо;
	ЗаписьНабора.НачальныйПроцентСевернойНадбавки	= СрезСеверныйСтаж[0].НачальныйПроцентСевернойНадбавки;
	ЗаписьНабора.ПорядокНачисленияСеверныхНадбавок	= СрезСеверныйСтаж[0].ПорядокНачисленияСеверныхНадбавок;
	ЗаписьНабора.СеверныйСтажДней  					= СрезСеверныйСтаж[0].СеверныйСтажДней;
	ЗаписьНабора.СеверныйСтажМесяцев  				= СрезСеверныйСтаж[0].СеверныйСтажМесяцев;
КонецЕсли;