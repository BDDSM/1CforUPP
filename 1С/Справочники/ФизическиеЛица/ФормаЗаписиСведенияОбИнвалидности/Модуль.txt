Процедура ОбновитьДоступностьЭлементовУправления()

	ЭлементыФормы.ПолеВыбораГруппаИнвалидности.ТолькоПросмотр = Не Инвалидность;
	ЭлементыФормы.СерияСправки.ТолькоПросмотр = Не Инвалидность;
	ЭлементыФормы.НомерСправки.ТолькоПросмотр = Не Инвалидность;
	ЭлементыФормы.ДатаВыдачи.ТолькоПросмотр = Не Инвалидность;
	ЭлементыФормы.СрокДействияСправки.ТолькоПросмотр = Не Инвалидность;
	
	ЭлементыФормы.ПолеВыбораГруппаИнвалидности.АвтоОтметкаНезаполненного = Инвалидность;
	ЭлементыФормы.ПолеВыбораГруппаИнвалидности.ОтметкаНезаполненного = Инвалидность и НЕ ЗначениеЗаполнено(ГруппаИнвалидности);

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НаборЗаписей.Количество() = 0 тогда
		НаборЗаписей.Добавить();
		ЗаписьИзНабора 			= НаборЗаписей[0];
		ЗаписьИзНабора.Период  	= ПериодПриОткрытии;
	КонецЕсли;
	ЗаписьИзНабора 			= НаборЗаписей[0];
	ФизЛицо             	= ЗаписьИзНабора.ФизЛицо;
	Период 					= ?(ЗначениеЗаполнено(ЗаписьИзНабора.Период), ЗаписьИзНабора.Период, '19000101');
	ГруппаИнвалидности  	= ЗаписьИзНабора.ГруппаИнвалидности;
	ДатаВыдачи 		   		= ЗаписьИзНабора.ДатаВыдачи;
	Инвалидность  	   		= ЗаписьИзНабора.Инвалидность;
	НомерСправки		   	= ЗаписьИзНабора.НомерСправки;
	СерияСправки        	= ЗаписьИзНабора.СерияСправки;
	СрокДействияСправки 	= ЗаписьИзНабора.СрокДействияСправки;

	ФизЛицоПриОткрытии             = ФизЛицо;
	ПериодПриОткрытии              = Период;
	ГруппаИнвалидностиПриОткрытии  = ГруппаИнвалидности;
	ДатаВыдачиПриОткрытии 		   = ДатаВыдачи;
	ИнвалидностьПриОткрытии  	   = Инвалидность;
	НомерСправкиПриОткрытии		   = НомерСправки;
	СерияСправкиПриОткрытии        = СерияСправки;
	СрокДействияСправкиПриОткрытии = СрокДействияСправки;
	
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Если ФизЛицо.Пустая() тогда
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
		
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.РазделительИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
	КонецЕсли;
	
	ОбновитьДоступностьЭлементовУправления();
	
КонецПроцедуры

Процедура ПодтвердитьИзменения(Кнопка)
	
	Если Инвалидность и ПустаяСтрока(ГруппаИнвалидности) Тогда
		Предупреждение("Не указана группа инвалидности!");
		Возврат;
	КонецЕсли;	
	
	ЗаписьИзНабора 			= НаборЗаписей[0];
	// Если что-то поменяли тогда запишем 
	Если ГруппаИнвалидностиПриОткрытии 		<> ГруппаИнвалидности ИЛИ 
		 ДатаВыдачиПриОткрытии 				<> ДатаВыдачи ИЛИ 
		 ИнвалидностьПриОткрытии 			<> Инвалидность ИЛИ 
		 НомерСправкиПриОткрытии 			<> НомерСправки ИЛИ 
		 СерияСправкиПриОткрытии 			<> СерияСправки ИЛИ 
		 СрокДействияСправкиПриОткрытии 	<> СрокДействияСправки ИЛИ 
		 ПериодПриОткрытии 					<> Период Тогда
		ЗаписьИзНабора.Период   				= Период;
		ЗаписьИзНабора.ГруппаИнвалидности   	= ГруппаИнвалидности;
		ЗаписьИзНабора.ДатаВыдачи   			= ДатаВыдачи;
		ЗаписьИзНабора.Инвалидность			 	= Инвалидность;
		ЗаписьИзНабора.НомерСправки			 	= НомерСправки;
		ЗаписьИзНабора.СерияСправки				= СерияСправки;
		ЗаписьИзНабора.СрокДействияСправки   	= СрокДействияСправки;
	КонецЕсли; 
	
	// оповестим все формы, показывающие данные физлица, для которого 
	// редактируются связанные данные
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Инвалидность"), ОбщегоНазначенияЗК.ОпределитьОповещаемоеФизЛицо(ФизЛицо, ВладелецФормы));
	
	Закрыть();

	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СведенияОбИнвалидностиФизлиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

Процедура ИнвалидностьПриИзменении(Элемент)
	
	Если Не Инвалидность Тогда
		ГруппаИнвалидности = "";
		СерияСправки = "";
		НомерСправки = "";
		ДатаВыдачи = Дата('00010101');
		СрокДействияСправки = Дата('00010101');
	КонецЕсли;
	
	ОбновитьДоступностьЭлементовУправления();
	
КонецПроцедуры