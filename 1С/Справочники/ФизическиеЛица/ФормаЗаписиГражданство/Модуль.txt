////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// в переменной запоминаем страну которая задана до установки
// переключателя - Лицо без гражданства
Перем мСтрана;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НаборЗаписей.Количество() = 0 тогда
		НаборЗаписей.Добавить();
		ЗаписьИзНабора = НаборЗаписей[0];
	КонецЕсли;
	ЗаписьИзНабора 						= НаборЗаписей[0];
	Период 								= ?(ЗначениеЗаполнено(ЗаписьИзНабора.Период), ЗаписьИзНабора.Период, '19000101');
	ВидЗастрахованногоЛица 				= ЗаписьИзНабора.ВидЗастрахованногоЛица;
	Страна 								= ЗаписьИзНабора.Страна;
	ФизЛицо								= ЗаписьИзНабора.Физлицо;
	
	Если ФизЛицо.Пустая() тогда
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
		
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.РазделительИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
	КонецЕсли;
	
	// установим переключатель в форме в соответствии с выбранным гражданством
	ЛицоБезГражданства = Страна = Справочники.КлассификаторСтранМира.ЛицоБезГражданства;
	ЭлементыФормы.ГражданинСтраны.ТолькоПросмотр = ЛицоБезГражданства;
	Если ЛицоБезГражданства Тогда
		ЭлементыФормы.ГражданинСтраны.Значение = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	Иначе
		ЭлементыФормы.ГражданинСтраны.Значение = Страна;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	РасшифровкаПФР = "";
	Если ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ Или Не ЗначениеЗаполнено(ВидЗастрахованногоЛица) Тогда
		РасшифровкаПФР = "На граждан России распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме.";
	ИначеЕсли ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПостоянноПроживающиеИностранцы Тогда
		РасшифровкаПФР = "На постоянно проживающих на территории РФ иностранных граждан и лиц без гражданства распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме. Страховые взносы за постоянно проживающих уплачиваются в том же порядке, что и за граждан РФ";
	ИначеЕсли ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПроживающиеИностранцы Тогда
		РасшифровкаПФР = "На временно проживающих на территории РФ иностранных граждан и лиц без гражданства распространяется обязательное медицинское и социальное страхование в полном объеме. В части пенсионного страхования все уплачиваемые за временно проживающих взносы с 2012 года относятся к страховой части пенсии.";
	ИначеЕсли ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцы Тогда
		РасшифровкаПФР = "На временно пребывающих на территории РФ иностранных граждан и лиц без гражданства не распространяется медицинское и социальное страхование в РФ. Обязательное пенсионное страхование распространяется на таких лиц с 1 января 2012 года при условии заключения трудового договора на срок не менее 6 месяцев.";
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.ГражданствоФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение 					 = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения 				 = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование 			 = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

Процедура ПодтвердитьИзменения(Кнопка)
	
	ЗаписьИзНабора = НаборЗаписей[0];
	Если ВидЗастрахованногоЛицаПриОткрытии <> ВидЗастрахованногоЛица Или
		СтранаПриОткрытии <> Страна Или
		ПериодПриОткрытии <> Период Тогда
	
		ЗаписьИзНабора.Период   						= Период;
		ЗаписьИзНабора.ВидЗастрахованногоЛица  		   	= ВидЗастрахованногоЛица;
		ЗаписьИзНабора.Страна						   	= Страна;
		Если ЗаписьИзНабора.Страна = Справочники.КлассификаторСтранМира.Россия Тогда
			ЗаписьИзНабора.ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ
		КонецЕсли;
		ЗаписьИзНабора.НеИмеетПравоНаПенсию = ЗаписьИзНабора.ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцы;
	
	КонецЕсли;
	
	// оповестим все формы, показывающие данные физлица, для которого 
	// редактируются связанные данные
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Гражданство"), ОбщегоНазначенияЗК.ОпределитьОповещаемоеФизЛицо(ФизЛицо, ВладелецФормы));
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ПереключательГражданствоПриИзменении(Элемент)
	
	Если ЛицоБезГражданства Тогда
		мСтрана = ЭлементыФормы.ГражданинСтраны.Значение;
		Страна = Справочники.КлассификаторСтранМира.ЛицоБезГражданства;
		ЭлементыФормы.ГражданинСтраны.Значение = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	Иначе
		Страна = мСтрана;
		ЭлементыФормы.ГражданинСтраны.Значение = Страна;
	КонецЕсли;
	
	ЭлементыФормы.ГражданинСтраны.ТолькоПросмотр = ЛицоБезГражданства;
	Если Страна = Справочники.КлассификаторСтранМира.Россия Тогда
		ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ
	ИначеЕсли ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ Или Не ЗначениеЗаполнено(ВидЗастрахованногоЛица) Тогда	
		ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПостоянноПроживающиеИностранцы
	КонецЕсли;
	
КонецПроцедуры

Процедура ГражданинСтраныПриИзменении(Элемент)
	
	Страна = Элемент.Значение;
	Если Страна = Справочники.КлассификаторСтранМира.Россия Тогда
		ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ
	ИначеЕсли ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ Или Не ЗначениеЗаполнено(ВидЗастрахованногоЛица) Тогда	
		ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПостоянноПроживающиеИностранцы
	КонецЕсли;
	
КонецПроцедуры

Процедура ПринятыеПоДолгосрочнымДоговорамПериодЗавершенияПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		Элемент.Значение = КонецМесяца(Элемент.Значение)
	КонецЕсли;
КонецПроцедуры

Процедура ПринятыеПоДолгосрочнымДоговорамПериодПриИзменении(Элемент)
	Элемент.Значение = НачалоМесяца(Элемент.Значение)
КонецПроцедуры