Перем ЭталонныйСценарий;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует текст заголовка
//
// Параметры:
//	Нет.
//
Процедура СформироватьЗаголовокФормы()

	Заголовок =  "Сравнительный анализ оборотов по бюджету: "+Строка(Бюджет);

КонецПроцедуры // СформироватьЗаголовокФормы()

// Проверяет даты начала и конца ограничения оборотов и корректирует при необходимости
//
Процедура ПроверкаИнтервала(ТекущаяСтрока)
	
	Если (ТекущаяСтрока.ДатаКон='00010101') И (ТекущаяСтрока.ДатаНач<>'00010101') Тогда
		
		ТекущаяСтрока.ДатаКон=ОбщегоНазначения.ДатаКонцаПериода(ТекущаяСтрока.ДатаНач,Периодичность);
		
	ИначеЕсли (ТекущаяСтрока.ДатаКон<>'00010101') И (ТекущаяСтрока.ДатаНач='00010101') Тогда
		
		ТекущаяСтрока.ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(ТекущаяСтрока.ДатаКон,Периодичность);
		
	ИначеЕсли ТекущаяСтрока.ДатаКон<ТекущаяСтрока.ДатаНач Тогда
		
		Если ЭлементыФормы.ТаблицаСценарии.ТекущаяКолонка.Имя="ДатаКон" Тогда
			
			ТекущаяСтрока.ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(ТекущаяСтрока.ДатаКон,Периодичность);
			
		Иначе
			
			ТекущаяСтрока.ДатаКон=ОбщегоНазначения.ДатаКонцаПериода(ТекущаяСтрока.ДатаНач,Периодичность);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверкаИнтервала()

// Определяет максимальную из периодичностей сценариев, выбранных
// в ТЧ
//
// Параметры: нет
//
Процедура ОпределитьПериодичностьДанных()

	ТекущаяПериодичность=Перечисления.Периодичность.День;
	
	Для Каждого Строка Из ТаблицаСценарии Цикл
	
		Если НЕ Строка.Сценарий.Пустая() 
			И ОбщегоНазначения.ЧислоДнейВПериоде(Строка.Сценарий.Периодичность)>ОбщегоНазначения.ЧислоДнейВПериоде(ТекущаяПериодичность) Тогда
			ТекущаяПериодичность=Строка.Сценарий.Периодичность;
		КонецЕсли;
	
	КонецЦикла;
	
	Периодичность=ТекущаяПериодичность;
	
КонецПроцедуры // ОпределитьПериодичностьДанных()

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ДИАЛОГА

// Процедура - обработчик изменения бюджета
//
Процедура БюджетПриИзменении(Элемент)
	
	ЗаполнитьНачальныеНастройки();
	Оповестить("ИзмененыПараметрыНастройки",,ЭтаФорма);
	
КонецПроцедуры // БюджетПриИзменении()

// Процедура - обработчик нажатия кнопки ОК.
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Закрыть(Истина);
	
КонецПроцедуры // ОсновныеДействияФормыОК()

// Процедура - обработчик перед удалением строки отбора
//
Процедура ОтборПередУдалением(Элемент, Отказ)
	
	Если Не ПустаяСтрока(Элемент.ТекущаяСтрока.Имя) Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры // ОтборПередУдалением()

// Процедура - обработчик нажатия кнопки "Установить все" командной панели списка показателей
//
Процедура КоманднаяПанельСписокПоказателейУстановитьВсе(Кнопка)
	
	Для каждого Строка Из Показатели Цикл
		Строка.Использование = Истина;
	КонецЦикла;
		
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Снять все" командной панели списка показателей
//
Процедура КоманднаяПанельСписокПоказателейСнятьВсе(Кнопка)
	
	Для каждого Строка Из Показатели Цикл
		Строка.Использование = Ложь;
	КонецЦикла;
	
КонецПроцедуры

Процедура ТаблицаСценарииСценарийПриИзменении(Элемент)
	
	ОпределитьПериодичностьДанных();
	
	Для каждого Строка Из ТаблицаСценарии Цикл
	
		 Строка.ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(?(Строка.ДатаНач='00010101',РабочаяДата,Строка.ДатаНач),Периодичность);
		 Строка.ДатаКон=ОбщегоНазначения.ДатаКонцаПериода(?(Строка.ДатаКон='00010101',РабочаяДата,Строка.ДатаКон),Периодичность);
	
	 КонецЦикла; 
	 
				
КонецПроцедуры

Процедура ТаблицаСценарииДатаНачПриИзменении(Элемент)
	
	Элемент.Значение=ОбщегоНазначения.ДатаНачалаПериода(Элемент.Значение,Периодичность);
	ПроверкаИнтервала(ЭлементыФормы.ТаблицаСценарии.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ТаблицаСценарииДатаНачРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	Элемент.Значение=ОбщегоНазначения.ДобавитьИнтервал(Элемент.Значение,Периодичность,Направление);
	
	ПроверкаИнтервала(ЭлементыФормы.ТаблицаСценарии.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ТаблицаСценарииДатаКонПриИзменении(Элемент)
	
	Элемент.Значение=ОбщегоНазначения.ДатаКонцаПериода(Элемент.Значение,Периодичность);
	ПроверкаИнтервала(ЭлементыФормы.ТаблицаСценарии.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ТаблицаСценарииДатаКонРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	Элемент.Значение=ОбщегоНазначения.ДатаКонцаПериода(ОбщегоНазначения.ДобавитьИнтервал(Элемент.Значение,Периодичность,Направление),Периодичность);

	ПроверкаИнтервала(ЭлементыФормы.ТаблицаСценарии.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ТаблицаСценарииПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	// Первая строка выделяется жирным шрифтом
	Если ТаблицаСценарии.Индекс(ДанныеСтроки) = 0 Тогда
		ОформлениеСтроки.Шрифт = Новый Шрифт(ОформлениеСтроки.Шрифт, , , Истина, , , );
		
		Если НЕ ДанныеСтроки.Сценарий=ЭталонныйСценарий И ТаблицаСценарии.Количество()>1 Тогда
			// Изменен порядок строк
			Оповестить("ИзмененыПараметрыНастройки",,ЭтаФорма);
			ЭталонныйСценарий=ДанныеСтроки.Сценарий;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ДанныеСтроки.ВидДанных=Перечисления.ВидыДанныхДляОтчетовБюджетирования.Сценарий Тогда
		ОформлениеСтроки.Ячейки.Сценарий.ТолькоПросмотр=Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСценарииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования И ЭлементыФормы.ТаблицаСценарии.ТекущиеДанные <> Неопределено Тогда
	
		ЭталонныйСценарий=ЭлементыФормы.ТаблицаСценарии.ТекущиеДанные.Сценарий;
		ОпределитьПериодичностьДанных();
		ЗаполнитьНачальныеНастройки();
		Оповестить("ИзмененыПараметрыНастройки",,ЭтаФорма);
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПоказателиПриИзмененииФлажка(Элемент, Колонка)
	
	ЗаполнитьНачальныеНастройки();
	Оповестить("ИзмененыПараметрыНастройки",,ЭтаФорма);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СформироватьЗаголовокФормы();
	
	Если ТаблицаСценарии.Количество()>0 Тогда
		ЭталонныйСценарий=ТаблицаСценарии[0].Сценарий;
	Иначе
		ЭталонныйСценарий=Справочники.СценарииПланирования.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСценарииВидДанныхПриИзменении(Элемент)
	
	Если НЕ Элемент.Значение=Перечисления.ВидыДанныхДляОтчетовБюджетирования.Сценарий Тогда
		ЭлементыФормы.ТаблицаСценарии.ТекущиеДанные.Сценарий=Справочники.СценарииПланирования.ПустаяСсылка();
	КонецЕсли;		
		
КонецПроцедуры