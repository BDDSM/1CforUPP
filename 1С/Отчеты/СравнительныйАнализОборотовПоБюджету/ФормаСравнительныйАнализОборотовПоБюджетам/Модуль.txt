Перем ВысотаЗаголовка;
Перем ИдентификаторОкнаРасшифровки;
Перем ФормаНастройка;
Перем СтруктураРеквизитов;
Перем НеВосстанавливатьНастройку Экспорт;
Перем СтруктураСвязиЭлементовСДанными;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Управляет пометками кнопок ком. панели
//
// Параметры:
//	Нет.
//
Процедура УправлениеПометкамиКнопокКоманднойПанели()
	
	Если ПоказыватьЗаголовок Тогда
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Заголовок.Пометка = Истина;

	Иначе
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка = Ложь;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Заголовок.Пометка = Ложь;

	КонецЕсли;

	Если ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх Тогда
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Отбор.Пометка = Ложь;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Отбор.Пометка = Ложь;
	Иначе
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Отбор.Пометка = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Отбор.Пометка = Истина;
	КонецЕсли;
		
КонецПроцедуры // УправлениеПометкамиКнопокКоманднойПанели()

// Формирует текст заголовка
//
// Параметры:
//	Нет.
//
Процедура СформироватьЗаголовокФормы()

	Заголовок =  "Сравнительный анализ оборотов по бюджету: "+Строка(Бюджет);

КонецПроцедуры // СформироватьЗаголовокФормы()

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьТаблицуОтчета() Экспорт

	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка);

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ЭлементыФормы.ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	
	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ДокументРезультат;

	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры // ОбновитьТаблицуОтчета()

//  Управляет выводом заголовка
//
// Параметры:
//	Нет.
//
Процедура ВыводЗаголовка()

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ЭлементыФормы.ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры // ВыводЗаголовка()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек() Экспорт
	
	СтруктНастр = Новый Структура;
	СтруктНастр.Вставить("НастройкиПостроителя", ПостроительОтчета.ПолучитьНастройки());
	СтруктНастр.Вставить("ОтборПомечен", ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Отбор.Пометка);
	СтруктНастр.Вставить("ЗаголовокПомечен", ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка);
	СтруктНастр.Вставить("ТаблицаСценарии", ТаблицаСценарии.Выгрузить());
    СтруктНастр.Вставить("Бюджет",Бюджет);
	СтруктНастр.Вставить("Периодичность", Периодичность);
	СтруктНастр.Вставить("Показатели", Показатели.Выгрузить());
	СтруктНастр.Вставить("ВыводитьАбсолютныеОтклонения", ВыводитьАбсолютныеОтклонения);
	СтруктНастр.Вставить("ВыводитьОтносительныеОтклонения", ВыводитьОтносительныеОтклонения);
	
	
	Возврат СтруктНастр;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт

	ПостроительОтчета.УстановитьНастройки(СтруктураСНастройками.НастройкиПостроителя);
	ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка = СтруктураСНастройками.ЗаголовокПомечен;
	ТаблицаСценарии.Загрузить(СтруктураСНастройками.ТаблицаСценарии);
	Бюджет=СтруктураСНастройками.Бюджет;
	Периодичность=СтруктураСНастройками.Периодичность;
	Показатели.Загрузить(СтруктураСНастройками.Показатели);
    ВыводитьАбсолютныеОтклонения=СтруктураСНастройками.ВыводитьАбсолютныеОтклонения;
	ВыводитьОтносительныеОтклонения=СтруктураСНастройками.ВыводитьОтносительныеОтклонения;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНОЙ ПАНЕЛИ

// Процедура - обработчик нажатия кнопки "Настройка".
//
Процедура КоманднаяПанельФормыНастройка(Кнопка)
	
	//ВызовСтандартнойНастройки(ЭтаФорма);
	
	ФормаНастройка = ОтчетОбъект.ПолучитьФорму("ФормаНастройка", ЭтаФорма);

	СтруктураСНастройками = СформироватьСтруктуруДляСохраненияНастроек();
	
	РезультатОткрытия = ФормаНастройка.ОткрытьМодально();

	Если РезультатОткрытия = Истина Тогда

		// Если изменились показатели - надо переписывать текст построителя
		// Запоминаем текущую настройку
		Настройки = ПостроительОтчета.ПолучитьНастройки(Истина, Истина, Истина, Истина);

		// Перезаполнение объекта (с новым текстом запроса)
		ЗаполнитьНачальныеНастройки();

		// Восстанавливаем запомненную настройку
		ПостроительОтчета.УстановитьНастройки(Настройки, Истина, Истина, Истина, Истина);
	
		ОбновитьТаблицуОтчета();

	Иначе
		//форму закрыли эскейпом или по "Закрыть" - восстановим настройки, отчет формировать не будем!
		//восстанавливаем значения
		НеВосстанавливатьНастройку = Ложь;
		ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками);
	КонецЕсли;
	
	УправлениеОтчетами.УстановитьСвязьПолейБыстрогоОтбораНаФорме(ЭлементыФормы, ПостроительОтчета.Отбор, СтруктураСвязиЭлементовСДанными);
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Отбор".
//
Процедура КоманднаяПанельФормыОтбор(Кнопка)

	Если НЕ ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх Тогда
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх;
	Иначе
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Нет;
	КонецЕсли;

	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Обновить".
//
Процедура КоманднаяПанельФормыОбновить(Кнопка)

	ОбновитьТаблицуОтчета();

КонецПроцедуры // ВыполнитьНажатие()

// Процедура - обработчик нажатия кнопки "Заголовок".
//
Процедура КоманднаяПанельЗаголовок(Кнопка)
	ПоказыватьЗаголовок = Не ПоказыватьЗаголовок;
	ВыводЗаголовка();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события перед открытием формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если неВосстанавливатьНастройку<>Истина Тогда
		
		ЗаполнитьНачальныеНастройки();
		
		Если ТипЗнч(СохраненныеНастройки)=Тип("Структура") И ТипЗнч(СохраненныеНастройки.НастройкиПостроителя)=Тип("НастройкиПостроителяОтчета") Тогда
				ПостроительОтчета.УстановитьНастройки(СохраненныеНастройки.НастройкиПостроителя);
		КонецЕсли;

		ВысотаЗаголовка = 0;

		Заголовок=мНазваниеОтчета;
		
	Иначе
		
		СтруктураРеквизитов = Новый Структура;

		Для каждого Реквизит Из Метаданные().Реквизиты Цикл
			СтруктураРеквизитов.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
		КонецЦикла; 

		Для каждого ТЧ Из Метаданные().ТабличныеЧасти Цикл
			СтруктураРеквизитов.Вставить(ТЧ.Имя, ЭтотОбъект[ТЧ.Имя].Выгрузить());
		КонецЦикла; 
		
	КонецЕсли;
		
	УправлениеОтчетами.УстановитьСвязьПолейБыстрогоОтбораНаФорме(ЭлементыФормы, ПостроительОтчета.Отбор, СтруктураСвязиЭлементовСДанными);
	
КонецПроцедуры // ПередОткрытием()     ЭтотОбъект

// Процедура - обработчик события при открытии формы
//
Процедура ПриОткрытии()
	
	УстановилиСверткуИзНастройки = Ложь;
	
	Если НЕ (НеВосстанавливатьНастройку = Истина) Тогда
		//отработаем сворачивание панели "Отбор"...
		Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда

			УстановилиСверткуИзНастройки = истина;
			ЭлементыФормы.ПанельОтбор.Свертка = ?(СохраненныеНастройки.ОтборПомечен, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Верх);
			//Это, что выше, не работает при еще не открытой форме.
			УправлениеПометкамиКнопокКоманднойПанели();

		КонецЕсли;
	Иначе
		//в следующий раз настройку мы все-таки восстановим - форма уже открыта...
		НеВосстанавливатьНастройку = Ложь;
		
	КонецЕсли;
	Если НЕ УстановилиСверткуИзНастройки Тогда
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
	УправлениеПометкамиКнопокКоманднойПанели();
КонецПроцедуры

// Процедура - обработчик события перед сохранением значений формы
//
Процедура ПередСохранениемЗначений(Отказ)

	СохраненныеНастройки = СформироватьСтруктуруДляСохраненияНастроек();

КонецПроцедуры // ПередСохранениемЗначений()

// Процедура - обработчик события после восстановления значений формы
//
Процедура ПослеВосстановленияЗначений()

	Если (НеВосстанавливатьНастройку = Истина) Тогда
		
		Для каждого Реквизит Из Метаданные().Реквизиты Цикл
			ЭтотОбъект[Реквизит.Имя] = СтруктураРеквизитов[Реквизит.Имя];
		КонецЦикла; 
		
		Для каждого ТЧ Из Метаданные().ТабличныеЧасти Цикл
			ЭтотОбъект[ТЧ.Имя].Загрузить(СтруктураРеквизитов[ТЧ.Имя]);
		КонецЦикла; 
				
	Иначе
		
		Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
			
			ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки);
			
			ЗаполнитьНачальныеНастройки();
			
			Если ТипЗнч(СохраненныеНастройки.НастройкиПостроителя)=Тип("НастройкиПостроителяОтчета") Тогда
				ПостроительОтчета.УстановитьНастройки(СохраненныеНастройки.НастройкиПостроителя);
			КонецЕсли;
			
			Если Открыта() Тогда
				//Это, что выше, не работает при еще не открытой форме.
				УправлениеПометкамиКнопокКоманднойПанели();
			КонецЕсли;
			
			Заголовок=мНазваниеОтчета;
			
		КонецЕсли;
		
		ЭлементыФормы.ДокументРезультат.Очистить();
		
	КонецЕсли; 
	
	СформироватьЗаголовокФормы();

КонецПроцедуры // ПослеВосстановленияЗначений()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик изменения данных в поле значения отбора
//
Процедура ПолеНастройки1ПриИзменении(Элемент)

	УправлениеОтчетами.ПолеНастройкиПриИзменении(Элемент, ПостроительОтчета.Отбор);
	
КонецПроцедуры // ПолеНастройки1ПриИзменении()

// Процедура - обработчик изменения данных в поле выбора вида сравнения
//
Процедура ПолеВидаСравнения1ПриИзменении(Элемент)

	УправлениеОтчетами.ПолеВидаСравненияПриИзменении(Элемент, ЭлементыФормы);
	
КонецПроцедуры // ПолеВидаСравнения1ПриИзменении()

// Процедура - обработчик события "Обработка расшифровки" поля табличного документа ДокументРезультат
//
Процедура ДокументРезультатОбработкаРасшифровки(Элемент, РасшифровкаСтроки, СтандартнаяОбработка)
	
	Если Элемент.ТекущаяОбласть.Лево>2 Тогда
		
		РасшифровкаСтроки=Элемент.Область(Элемент.ТекущаяОбласть.Верх, 1).Расшифровка;
		
		Если ТипЗнч(РасшифровкаСтроки) = Тип("Структура") Тогда
			
			// Расшифровка колонки находится в заголовке колонки
			РасшифровкаКолонки = Элемент.Область(ВысотаЗаголовка+1, Элемент.ТекущаяОбласть.Лево).Расшифровка;
			
			Расшифровка = Новый Структура;
			
			Для каждого Элемент Из РасшифровкаСтроки Цикл
				
				Если Элемент.Ключ="СтрокаБюджета" Тогда
					Если ТипЗнч(Элемент.Значение)=Тип("СправочникСсылка.СтатьиБюджета") Тогда
						
						СписокСтатей=Новый СписокЗначений;
						
						Запрос=Новый Запрос;
						Запрос.Текст="ВЫБРАТЬ
						             |	СтатьиБюджета.СтатьяОборотов КАК СтатьяОборотов
						             |ИЗ
						             |	Справочник.СтатьиБюджета КАК СтатьиБюджета
						             |ГДЕ
						             |	СтатьиБюджета.Ссылка В ИЕРАРХИИ(&СтрокаБюджета)
									 |И НЕ СтатьиБюджета.СтатьяОборотов=&ПустаяСтатья";
									 
									 
						Запрос.УстановитьПараметр("СтрокаБюджета",Элемент.Значение);
						Запрос.УстановитьПараметр("ПустаяСтатья",Справочники.СтатьиОборотовПоБюджетам.ПустаяСсылка());

						СписокСтатей.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СтатьяОборотов"));
						
						Расшифровка.Вставить("СтатьяОборотов",СписокСтатей);
						Расшифровка.Вставить("СтрокаБюджета",Элемент.Значение);
						
					ИначеЕсли ТипЗнч(Элемент.Значение)=Тип("СправочникСсылка.СчетаБюджета") Тогда
						Расшифровка.Вставить("СтатьяБаланса",Элемент.Значение.СтатьяБаланса);
						Расшифровка.Вставить("СтрокаБюджета",Элемент.Значение);
					КонецЕсли;
				Иначе
					Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТипЗнч(РасшифровкаКолонки) = Тип("Структура") Тогда
				
				Для каждого Элемент Из РасшифровкаКолонки Цикл
					Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
				КонецЦикла;
				
			КонецЕсли;
			
			ОбработкаРасшифровкиОтчетаБюджетирования(Расшифровка, СтандартнаяОбработка, ОтчетОбъект);
			
		КонецЕсли;
		
	Иначе 
		
		Если ТипЗнч(РасшифровкаСтроки)=Тип("СправочникСсылка.СтатьиБюджета") Тогда
			
			ФормаСписка=Справочники.СтатьиБюджета.ПолучитьФормуСписка();
			ФормаСписка.ПараметрТекущаяСтрока=РасшифровкаСтроки;
			ФормаСписка.ПараметрОтборПоВладельцу=Бюджет;
			ФормаСписка.Открыть();
			
		ИначеЕсли ТипЗнч(РасшифровкаСтроки)=Тип("СправочникСсылка.СчетаБюджета") Тогда
			
			ФормаСписка=Справочники.СчетаБюджета.ПолучитьФормуСписка();
			
			ФормаСписка.ПараметрТекущаяСтрока=РасшифровкаСтроки;
			ФормаСписка.ПараметрОтборПоВладельцу=Бюджет;
			ФормаСписка.Открыть();
			
		КонецЕсли;
			
	КонецЕсли;

КонецПроцедуры // ДокументРезультатОбработкаРасшифровки()

Процедура БюджетПриИзменении(Элемент)
	
	СформироватьЗаголовокФормы();
	УправлениеОтчетами.УстановитьСвязьПолейБыстрогоОтбораНаФорме(ЭлементыФормы, ПостроительОтчета.Отбор, СтруктураСвязиЭлементовСДанными);
		
КонецПроцедуры