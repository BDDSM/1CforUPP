Перем мСтараяВерсияФормы Экспорт;

Перем мСтрукТЗДокументы Экспорт; // таблица значений с данными по товаро-транспортным документам
Перем мСтруктураРеквизитовВыгрузки; // данные об отправителе, подписантах и курьере

Перем мРаздел1, мРаздел2, мРаздел3, мПриложение, мСведения, мЗачет; // Поля табличных документов

Перем ТаблицаСообщений Экспорт;

Перем мИдГруппы1, мИдГруппы2, мИдГруппы3;

Перем мСтруктураМногостраничныхРазделов Экспорт;

Перем мСтруктураМногострочныхРазделов Экспорт;

Перем мСтруктураИсхКолвоСтрокРазделов Экспорт;

// Список выводимых на печать страниц.
Перем мДеревоВыбранныхСтраниц Экспорт;

// Форматная строка представления денежных показателей отчета.
Перем мСтрокаФормата Экспорт;

// Значение делителя при представлении чисел в назначенной размерности.
Перем мДелитель Экспорт;

// Код отчета по КНД.
Перем мКодОтчета;

// Хранит коллекцию цветов раскраски автоматически
// заполняемых ячеек табличного документа.
Перем мСтруктураЦветовРаскраски Экспорт;
Перем мОписаниеТиповЧислоЕ Экспорт;

Перем мРежимПечати Экспорт;

// Инициализируется при открытии формы
// используется в ЗаполнитьСведенияОбОрганизации.
Перем мВПрограммеИзмененаОрганизация;

Перем мИННЮЛ;
Перем мКППЮЛ;

Перем мБезОткрытияФормы Экспорт;

Перем мПечатныеФормы Экспорт;

Перем мСвойстваРазделовДекларации Экспорт;

Перем мСоставПоказателей Экспорт;

// Переменная для хранения координат масок и сами маски.
Перем мМаскиЯчейки;

Перем мДокументВосстановлен;

Перем СвойстваПоказателей Экспорт;

Перем мТЗСпецификацийРаздел1;
Перем мТЗСпецификацийРаздел1_07;
Перем мТЗСпецификацийРаздел3;
Перем мМассивТЗСпецификацийПрил;

Перем мДанныеОтчета Экспорт;

Перем ДополнительныеДанныеРазделов; // Дополнительные сведения одностраничных разделов, не отображаемые в табличных документах

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.ПередОткрытиемФормыРегламентированногоОтчета(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Инициализация();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	РегламентированнаяОтчетность.ВключитьОбработчикАвтосохранения(ЭтаФорма);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СохранитьДеревоНастройкиСтраниц();
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

// Раздел 1.

Процедура ПолеТабличногоДокументаРаздел1Выбор(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
	 ИЛИ ИмяОбласти = "КодСтраныОрг1" 
	 ИЛИ ИмяОбласти = "МестоНахождОрг_1" Тогда
		ВвестиМестонахождениеРаздел1(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел1");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов, Элемент);
		
	ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		КодСтраны = ?(ПустаяСтрока(Область.Значение), "643", "398");
		Элемент.Области.КодСтраныПокупатель.Значение = КодСтраны;
		
	ИначеЕсли ИмяОбласти = "КомиссионерБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресКомиссионераРаздел1");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныОрг1");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "МестоНахождОрг_1");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "КомиссионерБайконур");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов, Элемент);
		
	ИначеЕсли Найти(ИмяОбласти, "П000010000109") > 0 ИЛИ Найти(ИмяОбласти, "П000010000110") > 0 Тогда 
		СтандартнаяОбработка = Ложь;
		
		//необходимо вызывать отдельную форму, содержащую табличную часть из 3-х колонок
		//обращение к структуре за таблицей значений
		//опредилим номер строки 
		НомерСтроки = Сред(Область.Имя, 14);
		КлючСтруктуры = "К" + НомерСтроки;
		
		ТЗДокументы = Неопределено;
		мСтрукТЗДокументы.Свойство(КлючСтруктуры, ТЗДокументы);
		Если  ТЗДокументы = Неопределено Тогда
			//создание таблицы
			ТЗДокументы = Новый ТаблицаЗначений;
			ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
			ОписаниеТиповДата = Новый ОписаниеТипов("Дата", , Новый КвалификаторыДаты(ЧастиДаты.Дата));
			
			ТЗДокументы.Колонки.Добавить("Серия", ОписаниеТиповСтрока, "Серия");
			ТЗДокументы.Колонки.Добавить("Номер", ОписаниеТиповСтрока, "Номер");
			ТЗДокументы.Колонки.Добавить("Дата", ОписаниеТиповДата, "Дата");
		КонецЕсли;
				
		//открытие формы заполнения
		ФормаЗаполнения = ПолучитьФорму("ФормаЗаполнения");
		ФормаЗаполнения.Документы = ТЗДокументы.Скопировать();
		Результат = ФормаЗаполнения.ОткрытьМодально();
		
		//обработка результата
		Если Результат <> Неопределено Тогда
			ТЗДокументы = Результат;
			мСтрукТЗДокументы.Вставить(КлючСтруктуры, ТЗДокументы);
			
			СерияНомерТД = "";
			ДатаТД = "";
			Для каждого стрТЗ Из ТЗДокументы Цикл
				Серия = СокрЛП(стрТЗ.Серия);                   
				Номер = СокрЛП(стрТЗ.Номер);
				Дата = Формат(стрТЗ.Дата,"ДФ=""дд.ММ.гггг 'г.'""");
				
				Если СерияНомерТД <> "" Тогда
					СерияНомерТД = СерияНомерТД + Символы.ПС;
				КонецЕсли;
				Если ДатаТД <> "" Тогда
					ДатаТД = ДатаТД + Символы.ПС;
				КонецЕсли;
				
				СерияНомерТД = СерияНомерТД + Серия + ", " + Номер;
				
				ДатаТД = ДатаТД + Дата; 
			КонецЦикла; 
			
			Элемент.Области["П000010000109" + НомерСтроки].Значение = СерияНомерТД;
			Элемент.Области["П000010000110" + НомерСтроки].Значение = ДатаТД;
		КонецЕсли;
		
	ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВводаСпецификаций = ПолучитьФорму("ФормаВводаСпецификаций");
		ФормаВводаСпецификаций.ТЗСпецификаций = мТЗСпецификацийРаздел1;
		Результат = ФормаВводаСпецификаций.ОткрытьМодально();
		Если Результат <> Неопределено Тогда
			мТЗСпецификацийРаздел1 = Результат;
			Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел1);
			мРаздел1.Области.НомерСпецификации.Значение = Тексты.Номер;
			мРаздел1.Области.ДатаСпецификации.Значение = Тексты.Дата;
			Модифицированность = Истина;
		КонецЕсли
		
	ИначеЕсли ИмяОбласти = "НомерСпецификации_07" ИЛИ ИмяОбласти = "ДатаСпецификации_07" Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВводаСпецификаций = ПолучитьФорму("ФормаВводаСпецификаций");
		ФормаВводаСпецификаций.ТЗСпецификаций = мТЗСпецификацийРаздел1_07;
		Результат = ФормаВводаСпецификаций.ОткрытьМодально();
		Если Результат <> Неопределено Тогда
			мТЗСпецификацийРаздел1_07 = Результат;
			Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел1_07);
			мРаздел1.Области.НомерСпецификации_07.Значение = Тексты.Номер;
			мРаздел1.Области.ДатаСпецификации_07.Значение = Тексты.Дата;
			Модифицированность = Истина;
		КонецЕсли
		
	ИначеЕсли Найти(Область.Имя, "ПрЛизинг") > 0 Тогда
		СтандартнаяОбработка = Ложь;
		мРаздел1.Области.ПрЛизинг.Значение = ?(СокрЛП(мРаздел1.Области.ПрЛизинг.Значение) = "", "Х", "");
		
	ИначеЕсли Найти(Область.Имя, "ПрДавСырья") > 0 Тогда
		СтандартнаяОбработка = Ложь;
		мРаздел1.Области.ПрДавСырья.Значение = ?(СокрЛП(мРаздел1.Области.ПрДавСырья.Значение) = "", "Х", "");
		
	ИначеЕсли Найти(Область.Имя, "П000010000107") > 0 Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВыбора = Справочники.Валюты.ПолучитьФормуВыбора();
		ФормаВыбора.РежимВыбора = Истина;
		ФормаВыбора.Заголовок = "Выбор кода валюты";
		
		Если НЕ РегламентированнаяОтчетность.ПустоеЗначение(Область.Значение) Тогда
			НайденнаяСтрока = Справочники.Валюты.НайтиПоКоду(Область.Значение);
			Если ЗначениеЗаполнено(НайденнаяСтрока) Тогда
				ФормаВыбора.ПараметрТекущаяСтрока = НайденнаяСтрока;
			КонецЕсли;
		КонецЕсли;
		
		Если ФормаВыбора.ОткрытьМодально() <> Неопределено Тогда
			
			Если Область.Значение = ФормаВыбора.ТекущийЭлемент.ТекущиеДанные.Код Тогда
				Область.Значение = "";
			Иначе
				Область.Значение = ФормаВыбора.ТекущийЭлемент.ТекущиеДанные.Код;
				Если Область.Значение = "643" Тогда
					СуффиксНомераСтроки = Сред(Область.Имя, 14);
					Элемент.Области["П000010000108" + СуффиксНомераСтроки].Значение = 1;
				КонецЕсли;
			КонецЕсли;
			
			Модифицированность = Истина;
			
		КонецЕсли;
	ИначеЕсли Найти(Область.Имя, "П000010000116") > 0  
		  ИЛИ Найти(Область.Имя, "П000010000117") > 0
		  Или Найти(Область.Имя, "П000010000118") > 0 Тогда
		 СтандартнаяОбработка = Ложь;
		 
		 НомерДопСтр = Сред(Область.Имя,15);
		 
		 СтруктураСтавокТабл = Новый Структура;
		 СтруктураСтавокТабл.Вставить("Ст16", мРаздел1.Области["П000010000116_" + НомерДопСтр].Значение);
		 СтруктураСтавокТабл.Вставить("Ст17", мРаздел1.Области["П000010000117_" + НомерДопСтр].Значение);
		 СтруктураСтавокТабл.Вставить("Ст18", мРаздел1.Области["П000010000118_" + НомерДопСтр].Значение);
		 СтруктураСтавокТабл.Вставить("Ст21", мРаздел1.Области["П000010000121_" + НомерДопСтр].Значение);
		 
		 ФормаВводаСтавкиНалога = ПолучитьФорму("ФормаВводаСтавкиНалога");
		 СтруктураСтавокФорма = АнализироватьПоляСтавки(СтруктураСтавокТабл);
		 ФормаВводаСтавкиНалога.СтруктураСтавок = СтруктураСтавокФорма;
		 Результат = ФормаВводаСтавкиНалога.ОткрытьМодально();
		 
		Если Результат <> Неопределено Тогда
			НовСтруктура = Результат;
			Если НовСтруктура.ПредставлениеПрочерк Тогда
				мРаздел1.Области["П000010000114_" + НомерДопСтр].Значение = 0; 
				мРаздел1.Области["П000010000114_" + НомерДопСтр].Формат = "ЧН=-";
			Иначе
				мРаздел1.Области["П000010000114_" + НомерДопСтр].Формат = "ЧДЦ=6; ЧН=";
			КонецЕсли;
			
			мРаздел1.Области["П000010000116_" + НомерДопСтр].Значение = НовСтруктура.Ст16;
			мРаздел1.Области["П000010000117_" + НомерДопСтр].Значение = НовСтруктура.Ст17;
			мРаздел1.Области["П000010000118_" + НомерДопСтр].Значение = НовСтруктура.Ст18;
			мРаздел1.Области["П000010000121_" + НомерДопСтр].Значение = НовСтруктура.Ст21;
			
			Расчет();
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрВводаПоля = НайтиСвойстваПоказателя(Область.Имя);
	
	//создание динамического списка выбора
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		//обработка таблицы значений - удаление вариантов, не проходящих по условиям
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ТЗВыбора = ПараметрВводаПоля.ТаблицаЗначений.Скопировать();
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры // ПолеТабличногоДокументаРаздел1Выбор

Процедура ПолеТабличногоДокументаРаздел1ПриАктивизацииОбласти(Элемент)
	
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент, "Раздел1");
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаРаздел1ПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

// Раздел 3.

Процедура ПолеТабличногоДокументаРаздел3Выбор(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
	 ИЛИ ИмяОбласти = "КодСтраныПокупатель" 
	 ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		ВвестиМестонахождениеРаздел3(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел3");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов, Элемент);
		
	ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателяРаздел3");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов, Элемент);
		
	ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВводаСпецификаций = ПолучитьФорму("ФормаВводаСпецификаций");
		ФормаВводаСпецификаций.ТЗСпецификаций = мТЗСпецификацийРаздел3;
		Результат = ФормаВводаСпецификаций.ОткрытьМодально();
		Если Результат <> Неопределено Тогда
			мТЗСпецификацийРаздел3 = Результат;
			Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел3);
			мРаздел3.Области.НомерСпецификации.Значение = Тексты.Номер;
			мРаздел3.Области.ДатаСпецификации.Значение = Тексты.Дата;
			Модифицированность = Истина;
		КонецЕсли
	КонецЕсли;
	
	ПараметрВводаПоля = НайтиСвойстваПоказателя(Область.Имя);
	
	//создание динамического списка выбора
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		//обработка таблицы значений - удаление вариантов, не проходящих по условиям
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ТЗВыбора = ПараметрВводаПоля.ТаблицаЗначений.Скопировать();
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
	КонецЕсли;

	
КонецПроцедуры

Процедура ПолеТабличногоДокументаРаздел3ПриАктивизацииОбласти(Элемент)
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент, "Раздел3");
КонецПроцедуры

Процедура ПолеТабличногоДокументаРаздел3ПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

// Приложение.

Процедура ПолеТабличногоДокументаПриложениеВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Перем ТаблицаСтраницРаздела;
	
	ИмяОбласти = Область.Имя;
	
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
	 ИЛИ ИмяОбласти = "КодСтраныПокупатель" 
	 ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		ВвестиМестонахождениеПриложение(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавца");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
		СтраницыРазделаПриложение = мСтруктураМногостраничныхРазделов.Приложение;
		АктивнаяСтраница = СтраницыРазделаПриложение.Найти(Истина, "АктивнаяСтраница");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, АктивнаяСтраница.ДополнительныеДанные, Элемент);
		
	ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
		СтандартнаяОбработка = Ложь;
		Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
		
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателя");
		ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
		СтраницыРазделаПриложение = мСтруктураМногостраничныхРазделов.Приложение;
		АктивнаяСтраница = СтраницыРазделаПриложение.Найти(Истина, "АктивнаяСтраница");
		
		ОтобразитьКодСтраны(ПараметрыОбработки, АктивнаяСтраница.ДополнительныеДанные, Элемент);
		
	ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
		// получаем номер текущей доп страницы
		Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
			Возврат;
		КонецЕсли;
		
		СтрТаблицаСтраницы = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
		
		Если СтрТаблицаСтраницы = Неопределено Тогда
			РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, "Приложение");
			Возврат;
		КонецЕсли;
		
		НомерТекущейСтраницы   = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы);
		
		СтандартнаяОбработка = Ложь;
		
		ТЗСтраницы = мМассивТЗСпецификацийПрил[НомерТекущейСтраницы];
		
		ФормаВводаСпецификаций = ПолучитьФорму("ФормаВводаСпецификаций");
		ФормаВводаСпецификаций.ТЗСпецификаций = ТЗСтраницы;
		Результат = ФормаВводаСпецификаций.ОткрытьМодально();
		Если Результат <> Неопределено Тогда
			ТЗСтраницы = Результат;
			Тексты = ПолучитьТекстыИзТЗСпецификаций(ТЗСтраницы);
			мПриложение.Области.НомерСпецификации.Значение = Тексты.Номер;
			мПриложение.Области.ДатаСпецификации.Значение = Тексты.Дата;
			Модифицированность = Истина;
		КонецЕсли
	КонецЕсли;
	
	ПараметрВводаПоля = НайтиСвойстваПоказателя(Область.Имя);
	
	// Создание динамического списка выбора.
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		// Удаление вариантов, не проходящих по условиям.
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ТЗВыбора = ПараметрВводаПоля.ТаблицаЗначений.Скопировать();
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаПриложениеПриАктивизацииОбласти(Элемент)
	
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент, "Приложение");

КонецПроцедуры

Процедура ПолеТабличногоДокументаПриложениеПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

// Сведения о выписках банка.

Процедура ПолеТабличногоДокументаСведенияВыбор(Элемент, Область, СтандартнаяОбработка)
	ИмяОбласти = Область.Имя;
	
	ПараметрВводаПоля = НайтиСвойстваПоказателя(ИмяОбласти);
	
	//создание динамического списка выбора
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		//обработка таблицы значений - удаление вариантов, не проходящих по условиям
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ТЗВыбора = ПараметрВводаПоля.ТаблицаЗначений.Скопировать();
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

Процедура ПолеТабличногоДокументаСведенияПриАктивизацииОбласти(Элемент)
	
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент, "Сведения");
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаСведенияПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

// Сведения о зачетах.

Процедура ПолеТабличногоДокументаЗачетВыбор(Элемент, Область, СтандартнаяОбработка)
	ИмяОбласти = Область.Имя;
	
	ПараметрВводаПоля = НайтиСвойстваПоказателя(ИмяОбласти);
	
	//создание динамического списка выбора
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		//обработка таблицы значений - удаление вариантов, не проходящих по условиям
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ТЗВыбора = ПараметрВводаПоля.ТаблицаЗначений.Скопировать();
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

Процедура ПолеТабличногоДокументаЗачетПриАктивизацииОбласти(Элемент)
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент, "Зачет");
КонецПроцедуры

Процедура ПолеТабличногоДокументаЗачетПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Производит очистку отчета в целом, или текущего листа или текущего экземпляра многостраничного раздела.
//
Процедура КоманднаяПанельФормыВыбратьВариантОчисткиОтчета(Кнопка)
	
	МеханизмыОчисткиРегламентированныхОтчетов(Кнопка);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВыгрузить(Кнопка)
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
		Модифицированность = Истина;
	КонецЕсли;
	
	Если Модифицированность И Вопрос("Перед выгрузкой необходимо сохранить отчет. Продолжить?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Сохранить() Тогда
		Возврат;
	КонецЕсли;
	
	Выгрузить();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПроверить(Кнопка)
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
		Модифицированность = Истина;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если Вопрос("Перед выгрузкой отчета требуется сохранить введенную информацию." + Символы.ПС + "Продолжить?",РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да ИЛИ НЕ Сохранить() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НовыйДок = Документы.ВыгрузкаРегламентированныхОтчетов.СоздатьДокумент();
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(мСохраненныйДок);
	НовыйДок.ПолучитьФорму(, ЭтаФорма).Проверить(СпДокОсн);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПараметрыВыгрузки(Кнопка)
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
		Модифицированность = Истина;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если Вопрос("Перед выгрузкой отчета требуется сохранить введенную информацию." + Символы.ПС + "Продолжить?",РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да ИЛИ НЕ Сохранить() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ФормаВыгрузки = ПолучитьФорму("ФормаВыгрузки2012Кв2");
	ФормаВыгрузки.СтруктураРеквизитов = мСтруктураРеквизитовВыгрузки;
	
	Результат = ФормаВыгрузки.ОткрытьМодально();
	Если Результат <> Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ПолучитьКопиюСтруктуры(Результат);
		Сохранить();
	КонецЕсли;
	
КонецПроцедуры

// Вызывает диалог настройки вывода на печать, показа и выгрузки.страниц отчета,
// а также процедуру управления видимостью страниц.
//
Процедура КоманднаяПанельФормыКнопкаНастройкаСтраниц(Кнопка)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	
	// Управляем видимостью страниц основной панели формы.
	РегламентированнаяОтчетность.роПоказатьСтраницыОтчета(ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПоиск(Кнопка)
	
	РегламентированнаяОтчетность.роПоискВРегламентированномОтчете(ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаЗаполнитьСведенияОбОрганизацииНажатие(Элемент)
	
	мВПрограммеИзмененаОрганизация = Истина;
	ЗаполнитьСведенияОбОрганизации();
	мВПрограммеИзмененаОрганизация = Ложь;
	Модифицированность = Истина;
	
	мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыРасширитьПолеБланка(Кнопка)
	
	РегламентированнаяОтчетность.ПриНажатииНаКнопкуПереходаВРежимВводаДанных(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура СохранитьДеревоНастройкиСтраниц()
	
	Если мСохраненныйДок <> Неопределено Тогда

		Если Не мСохраненныйДок.ЭтоНовый() Тогда
			// Сохраняем выбранные листы для печати в хранилище
			ХранилищеДанных = Новый ХранилищеЗначения(мДеревоСтраницОтчета);
			мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
 			Попытка
				мСохраненныйДок.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;

	КонецЕсли;
	
Конецпроцедуры

Процедура ВвестиМестонахождениеРаздел1(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел1");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныОрг1" 
	      ИЛИ ИмяОбласти = "МестоНахождОрг_1" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресКомиссионераРаздел1");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныОрг1");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "МестоНахождОрг_1");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, ДополнительныеДанныеРазделов, Элемент);
	
КонецПроцедуры

Процедура ВвестиМестонахождениеРаздел3(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел3");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныПокупатель" 
	      ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателяРаздел3");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, ДополнительныеДанныеРазделов, Элемент);
	
КонецПроцедуры

Процедура ВвестиМестонахождениеПриложение(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	СтраницыРазделаПриложение = мСтруктураМногостраничныхРазделов.Приложение;
	
	Если СтраницыРазделаПриложение.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
		СтраницыРазделаПриложение.Колонки.Добавить("ДополнительныеДанные");
	КонецЕсли;
	
	АктивнаяСтраница = СтраницыРазделаПриложение.Найти(Истина, "АктивнаяСтраница");
	
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
	
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавца");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныПокупатель" 
	      ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателя");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, АктивнаяСтраница.ДополнительныеДанные, Элемент);
	
КонецПроцедуры

Процедура ВвестиМестонахождение(ПараметрыВвода, ХранилищеДополнительныхСведений, ПолеТабличногоДокумента)
	
	ИмяЭлементаСтруктуры = ПараметрыВвода.ИмяЭлементаСтруктуры;
	ИмяОбластиКодаСтраны = ПараметрыВвода.ИмяОбластиКодаСтраны;
	ИмяОбластиМестонахождения = ПараметрыВвода.ИмяОбластиМестонахождения;
	
	Если ТипЗнч(ХранилищеДополнительныхСведений) <> Тип("Структура") Тогда
		ХранилищеДополнительныхСведений = Новый Структура;
	КонецЕсли;
	
	ВводимыйАдрес = Неопределено;
	Если НЕ ХранилищеДополнительныхСведений.Свойство(ИмяЭлементаСтруктуры, ВводимыйАдрес) Тогда
		ВводимыйАдрес = Новый Структура;
		ВводимыйАдрес.Вставить("Страна",          "");
		ВводимыйАдрес.Вставить("Индекс",          "");
		ВводимыйАдрес.Вставить("Регион",          "");
		ВводимыйАдрес.Вставить("Район",           "");
		ВводимыйАдрес.Вставить("Город",           "");
		ВводимыйАдрес.Вставить("НаселенныйПункт", "");
		ВводимыйАдрес.Вставить("Улица",           "");
		ВводимыйАдрес.Вставить("Дом",             "");
		ВводимыйАдрес.Вставить("Корпус",          "");
		ВводимыйАдрес.Вставить("Квартира",        "");
		
		ВводимыйАдрес.Вставить("ТипДома",     Перечисления.ТипыДомов.Дом);
		ВводимыйАдрес.Вставить("ТипКорпуса",  Перечисления.ТипыКорпусов.Корпус);
		ВводимыйАдрес.Вставить("ТипКвартиры", Перечисления.ТипыКвартир.Квартира);
		
		ВводимыйАдрес.Вставить("Представление", "");
		ВводимыйАдрес.Вставить("ПредставлениеАдреса", "");
		
	КонецЕсли;
	
	ФормаВводаАдреса = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("ВводРоссийскогоАдреса");
	ФормаВводаАдреса.ДоступностьОбъекта = Ложь;
	ФормаВводаАдреса.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
	
	Если ПустаяСтрока(ВводимыйАдрес.Страна) Тогда
		
		ФормаВводаАдреса.Поле1 = ВводимыйАдрес.Индекс;
		ФормаВводаАдреса.Поле2 = ВводимыйАдрес.Регион;
		ФормаВводаАдреса.Поле3 = ВводимыйАдрес.Район;
		ФормаВводаАдреса.Поле4 = ВводимыйАдрес.Город;
		ФормаВводаАдреса.Поле5 = ВводимыйАдрес.НаселенныйПункт;
		ФормаВводаАдреса.Поле6 = ВводимыйАдрес.Улица;
		ФормаВводаАдреса.Поле7 = ВводимыйАдрес.Дом;
		ФормаВводаАдреса.Поле8 = ВводимыйАдрес.Корпус;
		ФормаВводаАдреса.Поле9 = ВводимыйАдрес.Квартира;
		
		ФормаВводаАдреса.ТипДома     = ВводимыйАдрес.ТипДома;
		ФормаВводаАдреса.ТипКорпуса  = ВводимыйАдрес.ТипКорпуса;
		ФормаВводаАдреса.ТипКвартиры = ВводимыйАдрес.ТипКвартиры;
		
		ФормаВводаАдреса.Представление = ВводимыйАдрес.Представление;
		
	Иначе
		
		ФормаВводаАдреса.Поле1 = ВводимыйАдрес.Страна;
		ФормаВводаАдреса.Представление = ВводимыйАдрес.Представление;
		
	КонецЕсли;
	
	РезультатВвода = ФормаВводаАдреса.ОткрытьМодально();
	
	Если РезультатВвода <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Если ФормаВводаАдреса.ПроизвольныйАдрес Тогда
			ВводимыйАдрес.Страна =          ФормаВводаАдреса.Поле1;
			ВводимыйАдрес.Индекс =          "";
			ВводимыйАдрес.Регион =          "";
			ВводимыйАдрес.Район =           "";
			ВводимыйАдрес.Город =           "";
			ВводимыйАдрес.НаселенныйПункт = "";
			ВводимыйАдрес.Улица =           "";
			ВводимыйАдрес.Дом =             "";
			ВводимыйАдрес.Корпус =          "";
			ВводимыйАдрес.Квартира =        "";
			
			ВводимыйАдрес.Представление = ФормаВводаАдреса.Представление;
			ВводимыйАдрес.ПредставлениеАдреса = ФормаВводаАдреса.ПредставлениеАдреса;
		Иначе
			ВводимыйАдрес.Страна =          "";
			ВводимыйАдрес.Индекс =          ФормаВводаАдреса.Поле1;
			ВводимыйАдрес.Регион =          ФормаВводаАдреса.Поле2;
			ВводимыйАдрес.Район =           ФормаВводаАдреса.Поле3;
			ВводимыйАдрес.Город =           ФормаВводаАдреса.Поле4;
			ВводимыйАдрес.НаселенныйПункт = ФормаВводаАдреса.Поле5;
			ВводимыйАдрес.Улица =           ФормаВводаАдреса.Поле6;
			ВводимыйАдрес.Дом =             ФормаВводаАдреса.Поле7;
			ВводимыйАдрес.Корпус =          ФормаВводаАдреса.Поле8;
			ВводимыйАдрес.Квартира =        ФормаВводаАдреса.Поле9;
			
			ВводимыйАдрес.ТипДома     = ФормаВводаАдреса.ТипДома;
			ВводимыйАдрес.ТипКорпуса  = ФормаВводаАдреса.ТипКорпуса;
			ВводимыйАдрес.ТипКвартиры = ФормаВводаАдреса.ТипКвартиры;
			
			ВводимыйАдрес.Представление = ФормаВводаАдреса.Представление;
			ВводимыйАдрес.ПредставлениеАдреса = "";
		КонецЕсли;
		
		ХранилищеДополнительныхСведений.Вставить(ИмяЭлементаСтруктуры, ВводимыйАдрес);
		
		Если ПустаяСтрока(ВводимыйАдрес.Страна) И АдресЗаполненПоКладр(ВводимыйАдрес) Тогда
			// Регламентированный адрес (Россия).
			ПолеТабличногоДокумента.Области[ИмяОбластиМестонахождения].Значение = ВводимыйАдрес.Представление;
		ИначеЕсли НЕ ПустаяСтрока(ВводимыйАдрес.Страна) Тогда
			// Произвольный адрес.
			Страна = Справочники.КлассификаторСтранМира.НайтиПоНаименованию(ВводимыйАдрес.Страна);
			Если ЗначениеЗаполнено(Страна) Тогда
				ПолеТабличногоДокумента.Области[ИмяОбластиМестонахождения].Значение = ВводимыйАдрес.ПредставлениеАдреса;
			КонецЕсли;
		Иначе
			// Адрес не задан.
			ПолеТабличногоДокумента.Области[ИмяОбластиМестонахождения].Значение = "";
		КонецЕсли;
		
		ОтобразитьКодСтраны(ПараметрыВвода, ХранилищеДополнительныхСведений, ПолеТабличногоДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтобразитьКодСтраны(ПараметрыВвода, ХранилищеДополнительныхСведений, ПолеТабличногоДокумента)
	
	ИмяЭлементаСтруктуры = ПараметрыВвода.ИмяЭлементаСтруктуры;
	ИмяОбластиКодаСтраны = ПараметрыВвода.ИмяОбластиКодаСтраны;
	ИмяОбластиМестонахождения = ПараметрыВвода.ИмяОбластиМестонахождения;
	ИмяОбластиМестонахождениеБайконур = ПараметрыВвода.ИмяОбластиМестонахождениеБайконур;
	
	Если ТипЗнч(ХранилищеДополнительныхСведений) <> Тип("Структура")
	 ИЛИ НЕ ХранилищеДополнительныхСведений.Свойство(ИмяЭлементаСтруктуры) Тогда
		Возврат; // Адрес не вводился
	КонецЕсли;
	
	АдресМестонахождения = ХранилищеДополнительныхСведений[ИмяЭлементаСтруктуры];
	НаБайконуре = НЕ ПустаяСтрока(ПолеТабличногоДокумента.Области[ИмяОбластиМестонахождениеБайконур].Значение);
	
	Если ПустаяСтрока(АдресМестонахождения.Страна) И АдресЗаполненПоКладр(АдресМестонахождения) Тогда
		// Регламентированный адрес (Россия).
		ПолеТабличногоДокумента.Области[ИмяОбластиКодаСтраны].Значение = ?(НаБайконуре, "398", "643");
	ИначеЕсли НЕ ПустаяСтрока(АдресМестонахождения.Страна) Тогда
		// Произвольный адрес.
		Страна = Справочники.КлассификаторСтранМира.НайтиПоНаименованию(АдресМестонахождения.Страна);
		Если ЗначениеЗаполнено(Страна) Тогда
			ПолеТабличногоДокумента.Области[ИмяОбластиКодаСтраны].Значение = ?(НаБайконуре, "398", Страна.Код);
		КонецЕсли;
	Иначе
		// Адрес не задан.
		ПолеТабличногоДокумента.Области[ИмяОбластиКодаСтраны].Значение = "";
	КонецЕсли;
	
	
КонецПроцедуры

Функция АдресЗаполненПоКладр(Адрес)
	
	Результат = НЕ ПустаяСтрока(Адрес.Индекс)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Регион)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Район)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Город)
	        ИЛИ НЕ ПустаяСтрока(Адрес.НаселенныйПункт)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Улица)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Дом)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Корпус)
	        ИЛИ НЕ ПустаяСтрока(Адрес.Квартира);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстыИзТЗСпецификаций(ТЗСпецификаций)
	Тексты = Новый Структура;
	ТекстНомер 	= "";
	ТекстДата 	= "";
	
	Для Сч = 1 По ТЗСпецификаций.Количество() Цикл
		Строка = ТЗСпецификаций[Сч - 1];
		
		ТекстНомер = ДобавитьРеквизит(ТекстНомер, Строка.Номер, ?(ТекстНомер = "", "", "; "));
		ТекстДата  = ДобавитьРеквизит(ТекстДата,  Формат(Строка.Дата, "ДФ=dd.MM.yyyy"),  ?(ТекстДата  = "", "", "; "));
		
	КонецЦикла; 
	
	Тексты.Вставить("Номер", ТекстНомер);
	Тексты.Вставить("Дата", ТекстДата);
	
	Возврат Тексты;
	
КонецФункции

Процедура ОбновитьТЗСпецификаций()
	// ТЗ спецификаций Раздел1
	мТЗСпецификацийРаздел1 = СоздатьТЗСпецификаций();
	
	// ТЗ спецификаций Раздел1_07
	мТЗСпецификацийРаздел1_07 = СоздатьТЗСпецификаций();
	
	// ТЗ спецификаций Раздел3
	мТЗСпецификацийРаздел3 = СоздатьТЗСпецификаций();
	
	// массив ТЗ спецификаций Приложение
	мМассивТЗСпецификацийПрил = Новый Массив;
	Элемент = СоздатьТЗСпецификаций(); 
	мМассивТЗСпецификацийПрил.Добавить(Элемент);
КонецПроцедуры

// ВставитьДополнительнуюСтраницу
// Создает дополнительную страницу многостраничного раздела формы.
//
// Параметры:
//  ИмяТекущейСтраницыПанели - строка - имя страницы основной панели формы,
//            в табличное поле которой вставляется дополнительная страница.
//
Процедура ВставитьДополнительнуюСтраницу( ИмяТекущейСтраницыПанели = Неопределено )

	РегламентированнаяОтчетность.роВставитьДополнительнуюСтраницу(ЭтаФорма, ИмяТекущейСтраницыПанели);

КонецПроцедуры // ВставитьДополнительнуюСтраницу()

// Очистить
// Процедура очищает содержимое редактируемых и вычисляемых
// ячеек табличного документа.
//
Процедура Очистить() Экспорт

	Если РегламентированнаяОтчетность.роОчистить(ЭтаФорма) Тогда

		ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница.Имя;
		Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ТекущаяСтраницаПанели, "РазделЯвляетсяАвтозаполняемым") Тогда
			УстановитьЦветФонаЯчейки(,, ТекущаяСтраницаПанели);
		КонецЕсли;

		ОбновитьТЗСпецификаций();
		
		РегламентированнаяОтчетность.ОтключитьОбработчикАвтосохранения(ЭтаФорма);
		РегламентированнаяОтчетность.ВключитьОбработчикАвтосохранения(ЭтаФорма);
	КонецЕсли;

КонецПроцедуры // Очистить()

// ПоказатьСтраницуМногостраничногоРаздела
// Вариант версия 10.05.2005
// Выводит в табличный документ данные страницы многостраничного раздела формы.
// Одновременно убирая признак у активной страницы и сохраняя в неё данные текущего табличного поля
// Параметры:
//  Шаг                      - число,  количество перелистываемых страниц;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы, содержащий многостраничный раздел;
//  ПоказатьПоИндексу        - булево, управляет выводом страницы по заданному индексу в коллекции страниц.
//
Процедура ПоказатьСтраницуМногостраничногоРаздела(Шаг, ИмяТекущейСтраницыПанели, ПоказатьПоИндексу = Ложь) Экспорт

	РегламентированнаяОтчетность.роПоказатьСтраницуМногостраничногоРаздела(ЭтаФорма, Шаг, ИмяТекущейСтраницыПанели, ПоказатьПоИндексу);

КонецПроцедуры // ПоказатьСтраницуМногостраничногоРаздела()

// ПоказатьСтраницуМногостраничногоРазделаПриВосстановлении
// Вывод необходимого номера страницы
// Выводит в табличный документ данные страницы многостраничного раздела формы.
//
// Параметры:
// Шаг                      - число,  номер страницы, на которую необходимо перейти;
// ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы, содержащий многостраничный раздел;
// УчестьАктивнуюСтраницу - Если истина, то учитывается страница, которая была ранее активной, иначе нет
//
Процедура ПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(Шаг, ИмяТекущейСтраницыПанели, УчестьАктивнуюСтраницу = Ложь)

	РегламентированнаяОтчетность.роПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(ЭтаФорма, Шаг, ИмяТекущейСтраницыПанели, УчестьАктивнуюСтраницу);

КонецПроцедуры // ПоказатьСтраницуМногостраничногоРаздела()

// ПолучитьСвойствоРаздела
// Получает из дерево св-в необходимый параметр
// Параметры
// ИмяСтраница - название страницы, для которой необходимо получить св-во
// ИмяСвойства - имя настройки
//
Функция ПолучитьСвойствоРаздела(Знач ИмяСтраницы, Знач ИмяСвойства)

	Возврат РегламентированнаяОтчетность.роПолучитьСвойствоРаздела( ЭтаФорма, ИмяСтраницы, ИмяСвойства);

КонецФункции // ПолучитьСвойствоРаздела

// УдалитьДополнительнуюСтраницу
// Удаляет текущую страницу многостраничного раздела формы.
// Программа стандартизации процедур 10.05.2005
// Параметры:
//  ИмяТекущейСтраницыПанели - строка - имя страницы основной панели формы,
//              в табличном поле которой удаляется дополнительная страница.
//
Процедура УдалитьДополнительнуюСтраницу(ИмяТекущейСтраницыПанели = Неопределено)
	Перем ТаблицаСтраницРаздела;
	
	// получаем номер текущей доп страницы
	Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	СтрТаблицаСтраницы = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
	
	Если СтрТаблицаСтраницы = Неопределено Тогда
		РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, "Приложение");
		Возврат;
	КонецЕсли;	
	НомерТекущейСтраницы   = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы);  
	
	// удаляем допстраницу
	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтраницу(ЭтаФорма, ИмяТекущейСтраницыПанели);

	// удаляем сообветствующий элемент в массиве структур Спецификаций
	
	Если ИмяТекущейСтраницыПанели = "Приложение" Тогда
		Если мМассивТЗСпецификацийПрил.Количество() > 1 Тогда 
			мМассивТЗСпецификацийПрил.Удалить(НомерТекущейСтраницы);
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры // УдалитьДополнительнуюСтраницу()

// Окончание блока стандартных процедур/функций

// Блок обязательных функций

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки показа страниц, вывода на печать и выгрузки в ИФНС.
//
//
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт

	МассивБулево = Новый Массив;
	МассивБулево.Добавить(Тип("Булево"));
	ОписаниеТиповБулево    = Новый ОписаниеТипов(МассивБулево);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповСтрока50 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	мСвойстваРазделовДекларации           = Новый ДеревоЗначений;
	мСвойстваРазделовДекларации.Колонки.Добавить("ИмяСтраницы",              ОписаниеТиповСтрока100);
	мСвойстваРазделовДекларации.Колонки.Добавить("МногострочностьВРазделе");
	// 0 - нет
	// 1 - есть одна многострочная часть
	// 2 - и более, точное кол-во число многострочных частей на листе

	// Позволяет записывать многострочность в многостраничных документ либо как ТаблицуЗначений, либо как Структуру
	// В случае, если как Структура, то необходима еще одна процедура СформироватьСтруктуруДанныхДопСтрокСтраницы
	// Только для Многостраничных документов !
	мСвойстваРазделовДекларации.Колонки.Добавить("СохранятьМногострКакТЗ",  ОписаниеТиповБулево);

	мСвойстваРазделовДекларации.Колонки.Добавить("МногостраничностьВРазделе",  ОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да

	// 3. Будет ли очищаться раздел по кнопки "Очистить"
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ОчищатьРаздел",  ОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да

	// 4. Если раздел многостраничный, параметр МногостраничностьВРазделе = Истина, тогда имя поля, по которому
	// будет осуществляться Представление доп. страницы
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ИмяПредставления",  ОписаниеТиповСтрока50);

	// 5. Есть ли в разделе значения, отображаемые в квадратном представлении
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ПредставлениеДанных",  ОписаниеТиповБулево);

	// 6. Обязательный раздел для выгрузки или нет
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("РазделОбязателенДляВыгрузки",  ОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да

	// 7. Обязательный раздел для выгрузки или нет
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("НазваниеПанелиТабличногоПоляРаздела",  ОписаниеТиповСтрока50);

	мСвойстваРазделовДекларации.Колонки.Добавить("РазделЯвляетсяАвтозаполняемым", ОписаниеТиповБулево);

	мСвойстваРазделовДекларации.Строки.Очистить();

	МногострочностьНастройка = Новый ТаблицаЗначений;
	МногострочностьНастройка.Колонки.Добавить("ИдГруппы");
	МногострочностьНастройка.Колонки.Добавить("Область");
	МногострочностьНастройка.Колонки.Добавить("ВысотаСтроки");
	МногострочностьНастройка.Колонки.Добавить("НезаполняемыеПоказателиЕсть", ОписаниеТиповБулево);

	МнгРаздел_1 = МногострочностьНастройка.Скопировать();
	НовСтр = МнгРаздел_1.Добавить();
	НовСтр.ИдГруппы = мИдГруппы1;
	НовСтр.Область = "МногострочнаяЧасть";
	НовСтр.ВысотаСтроки = 1;
	
	МнгРаздел_2 = МногострочностьНастройка.Скопировать();
	НовСтр = МнгРаздел_2.Добавить();
	НовСтр.ИдГруппы = мИдГруппы2;
	НовСтр.Область = "МногострочнаяЧасть";
	НовСтр.ВысотаСтроки = 1;
	
	МнгРаздел_3 = МногострочностьНастройка.Скопировать();
	НовСтр = МнгРаздел_3.Добавить();
	НовСтр.ИдГруппы = мИдГруппы3;
	НовСтр.Область = "МногострочнаяЧасть";
	НовСтр.ВысотаСтроки = 1;

	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва Раздела1
	СтрокаУровня1.ИмяСтраницы                          = "Раздел1";
	СтрокаУровня1.МногострочностьВРазделе              = МнгРаздел_1;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "ПродавецНаимОрг";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва Раздела2
	СтрокаУровня1.ИмяСтраницы                          = "Раздел2";
	СтрокаУровня1.МногострочностьВРазделе              = Ложь;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;

	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва Раздела3
	СтрокаУровня1.ИмяСтраницы                          = "Раздел3";
	СтрокаУровня1.МногострочностьВРазделе              = Ложь;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;

	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва Приложения
	СтрокаУровня1.ИмяСтраницы                          = "Приложение";
	СтрокаУровня1.МногострочностьВРазделе              = Ложь;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;

	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва листа сведений
	СтрокаУровня1.ИмяСтраницы                          = "Сведения";
	СтрокаУровня1.МногострочностьВРазделе              = МнгРаздел_2;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();

	// Добавим св-ва листа сведений
	СтрокаУровня1.ИмяСтраницы                          = "Зачет";
	СтрокаУровня1.МногострочностьВРазделе              = МнгРаздел_3;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;



КонецПроцедуры // ФормироватьСтруктуруСтраницОтчета()

// ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставления
// Процедура обнуляем переменные модуля, используемые для отображения в квадратиках
// в разделе 1
// запускается при добавлении новой страницы, а также при инициализации формы
//
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(Знач ИмяРаздела) Экспорт
	// Процедура заглушка
	Пусто = Неопределено;
	Возврат;
КонецПроцедуры // ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставления

// Окончание блока обязательных функций

// Функция собирает значения редактирукемых ячеек табличного документа
// в коллекцию значений типа "структура".
//
// Параметры:
//  ТабличноеПоле - поле табличного документа текущей формы.
//
// Возвращаемое значение:
//  Структура.
//
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле) Экспорт

	СтруктураДанныхПоля = Новый Структура;

	Для ПереборныйИнд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[ПереборныйИнд];

		
		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;

		ИмяПоказателя      = ТекущаяОбласть.Имя;
		ЗначениеПоказателя = ТекущаяОбласть.Значение;

		СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);

	КонецЦикла;

 	Возврат СтруктураДанныхПоля;

КонецФункции // СобратьДанныеТекущегоТаблПоля()

// Обновляет пользовательские настройки по вариантам заполнения
// показателей отчета в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьВариантыЗаполнения()
	Перем МассивВариантовЗаполненияСтраниц;

	ЕстьИзменения = Ложь;

	// Подлежат обновлению следующие структуры:
	// 1.Пользовательские настройки вариантов заполнения таблиц

	// Сначала создадим эталонные модели вариантов автозаполнения таблиц.
	// Их определяем по макету СоставПоказателей
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(мСоставПоказателей);

	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);

	ОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	ЕстьИзменения = Ложь;

	Для ПереборныйИнд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
		// организован Цикл по именованным областям макета,
		// содержащим состав показателей и варианты автозаполнения
		// таблиц на каждой странице основной панели формы.

		ТекОбласть  = МакетСоставаПоказателей.Области[ПереборныйИнд];
		ИмяСтраницы = ТекОбласть.Имя;

		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следующая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока50  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      мОписаниеТиповЧислоЕ   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            мОписаниеТиповЧислоЕ   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателяПоСтруктуре",     ОписаниеТиповСтрока50  );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);

			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
				НоваяСтрока.КодПоказателяПоСтруктуре  = чВариантЗаполнения;
			КонецЕсли;

		КонецЦикла;

		Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницы, МассивВариантовЗаполненияСтраниц) Тогда
			Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// не было предусмотрено автозаполнение по данным ИБ.
				// Добавим такую возможность в пользовательскую настройку

				// Для обеспечения назначения разных вариантов заполнения одного и того же показателя
				// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения
				// таблиц по каждой странице многостраничного раздела.
				МассивВариантовЗаполненияСтраниц = Новый Массив;
				// Первым элементом массива является "эталонная" таблица значений вариантов заполнения
				МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);

				// сохраняем варианты заполнения таблиц на данной странице
				мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);

				ЕстьИзменения = Истина;
			КонецЕсли;
		Иначе
			Если ТаблицаВариантыЗаполнения.Количество() = 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// было предусмотрено автозаполнение таблицы, а в текущей версии - отключено.
				// В этом случае из структуры вариантов автозаполнения удаляем элемент,
				// хранящий настройки вариантов автозаполнения текущей станицы.
				мСтруктураВариантыЗаполнения.Удалить(ИмяСтраницы);

				ЕстьИзменения = Истина;
			Иначе
				// Рассмотрим вариант, когда и в предыдущей, и в текущей версии формы было предусмотрено
				// автозаполнение текущей страницы. В этом случае обновляем таблицу значений, содержащей
				// пользовательские настройки вариантов автозаполнения текущей страницы.

				Для каждого ЭлементМассива Из МассивВариантовЗаполненияСтраниц Цикл
					Для каждого СтрокаТаблицы Из ТаблицаВариантыЗаполнения Цикл
						КодПоказателя     = СтрокаТаблицы.КодПоказателя;
						ВариантЗаполнения = СтрокаТаблицы.ВариантЗаполнения;

						Если ЭлементМассива.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотри вариант, когда в новой версии формы список реквизитов,
							// заполняемых автоматически по данным ИБ, дополнен новым реквизитом.
							НоваяСтрока = ЭлементМассива.Добавить();
							НоваяСтрока.КодПоказателя     = КодПоказателя;
							НоваяСтрока.ВариантЗаполнения = ВариантЗаполнения;

								ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЦикла;

					ТаблДляЦикла = ЭлементМассива.Скопировать();
					//Для каждого СтрокаТаблицы Из ЭлементМассива Цикл
					Для каждого СтрокаТаблицы Из ТаблДляЦикла Цикл
						КодПоказателя = СтрокаТаблицы.КодПоказателя;

						Если ТаблицаВариантыЗаполнения.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотрим вариант, когда в предыдущей версии отчета для данной страницы
							// было предусмотрено автозаполнение ячейки по коду КодПоказателя,
							// а в текущей версии - отключено. В этом случае удаляем эту ячейку
							// из таблицы пользовательских настроек вариантов автозаполнения.
							СтрокаВЭлементеМассива = ЭлементМассива.Найти(КодПоказателя, "КодПоказателя");
							Если  СтрокаВЭлементеМассива <> Неопределено Тогда
								ЭлементМассива.Удалить(СтрокаВЭлементеМассива);
								ЕстьИзменения = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	Возврат ЕстьИзменения;

КонецФункции // ОбновитьВариантыЗаполнения()

// Обновляет пользовательские настройки страниц отчета
// в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьНастройкиСтраниц()

	ЕстьИзменения = Ложь;

	// формируем дерево значений - пользовательских настройки страниц отчета
	мПользовательскиеНастройкиСтраниц = мДеревоСтраницОтчета.Скопировать();
                                     
	// формируем дерево значений - настройки страниц отчета обновленной формы
	ФормироватьСтруктуруСтраницОтчета();

	// Сначала проверяем, не изменилась ли структура настроек страниц (колонки дерева)
	Для каждого ИсхКолонка Из мДеревоСтраницОтчета.Колонки Цикл

		Если мПользовательскиеНастройкиСтраниц.Колонки.Найти(ИсхКолонка.Имя) = Неопределено Тогда
			// добавиляем новую колонку
			мПользовательскиеНастройкиСтраниц.Колонки.Добавить(ИсхКолонка.Имя, ИсхКолонка.ТипЗначения, ИсхКолонка.Заголовок, ИсхКолонка.Ширина);
			мПользовательскиеНастройкиСтраниц.ЗагрузитьКолонку(мДеревоСтраницОтчета.ВыгрузитьКолонку(ИсхКолонка.Имя), ИсхКолонка.Имя);

			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла;

	// Выполняем обратную задачу:
	// удаляем отсутствующие колонки
	Для каждого ТекКолонка Из мПользовательскиеНастройкиСтраниц.Колонки Цикл

		Если мДеревоСтраницОтчета.Колонки.Найти(ТекКолонка.Имя) = Неопределено Тогда
			// удаляем отсутствующую колонку
			мПользовательскиеНастройкиСтраниц.Колонки.Удалить(мПользовательскиеНастройкиСтраниц.Колонки.Индекс(ТекКолонка));

			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла;

	// Обновляем строки дерева значений - пользовательских настроек:
	//
	// добавляем новые строки и удаляем лишние:
	Если мДеревоСтраницОтчета.Строки.Количество() <> мПользовательскиеНастройкиСтраниц.Строки.Количество() Тогда

		КолвоКолонок     = мДеревоСтраницОтчета.Колонки.Количество();

		// добавляем новые строки
		Для каждого Строка Из мДеревоСтраницОтчета.Строки Цикл

			Если мПользовательскиеНастройкиСтраниц.Строки.Найти(Строка.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

				НоваяСтрока = мПользовательскиеНастройкиСтраниц.Строки.Добавить();
				Для НомКолонки = 0 По (КолвоКолонок - 1) Цикл
				
					НоваяСтрока[НомКолонки] = Строка[НомКолонки];
				
				КонецЦикла;

			КонецЕсли;

			// обновляем строки второго уровня
			Если Строка.Строки.Количество() > 0 Тогда

				Для каждого Строка2 Из Строка.Строки Цикл

					Если Строка.Строки.Найти(Строка2.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

						НоваяСтрока2 = НоваяСтрока.Строки.Добавить();
						Для НомКолонки = 0 По (КолвоКолонок - 1) Цикл

							НоваяСтрока2[НомКолонки] = Строка2[НомКолонки];

						КонецЦикла;

					КонецЕсли;

				КонецЦикла; 

			КонецЕсли; 

		КонецЦикла;

		// удаляем лишние строки
		Для каждого Строка Из мПользовательскиеНастройкиСтраниц.Строки Цикл

			Если мДеревоСтраницОтчета.Строки.Найти(Строка.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

				мПользовательскиеНастройкиСтраниц.Строки.Удалить(мПользовательскиеНастройкиСтраниц.Строки.Индекс(Строка));

			КонецЕсли;

		КонецЦикла;

		ЕстьИзменения = Истина;
	КонецЕсли;

	Для каждого ТекСтрока Из мДеревоСтраницОтчета.Строки Цикл

		НомСтр = мДеревоСтраницОтчета.Строки.Индекс(ТекСтрока);

		// устанавливаем признаки предопределенных установок выгружаемых страниц (значение флажка = 2);
		Если ТекСтрока.ВыгрузитьСтраницу = 2 Тогда
			
			Если мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ВыгрузитьСтраницу <> 2 Тогда
				мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ВыгрузитьСтраницу = 2;
				ЕстьИзменения = Истина;
			КонецЕсли;

		КонецЕсли;

		// обновляем представление
		Если ТекСтрока.Представление <> мПользовательскиеНастройкиСтраниц.Строки[НомСтр].Представление Тогда
			мПользовательскиеНастройкиСтраниц.Строки[НомСтр].Представление = ТекСтрока.Представление;
			ЕстьИзменения = Истина;
		КонецЕсли;

		// обновляем имя поля табличного документа
		Если ТекСтрока.ИмяПоляТабличногоДокум <> мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ИмяПоляТабличногоДокум Тогда
			мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ИмяПоляТабличногоДокум = ТекСтрока.ИмяПоляТабличногоДокум;
			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла; 

	мДеревоСтраницОтчета =  мПользовательскиеНастройкиСтраниц.Скопировать();

	Возврат ЕстьИзменения;

КонецФункции // ОбновитьНастройкиСтраниц()

// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
//
Процедура УстановитьЦветФонаЯчейки(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	РегламентированнаяОтчетность.роУстановитьЦветФонаЯчейки(ЭтаФорма, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
КонецПроцедуры // УстановитьЦветФонаЯчейки()

// ОбновитьПеременныеМодуля
// Обновляет переменные модуля из переменных структур
// Вызывается при показе и листании доп. страниц
//
Функция ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя) Экспорт

	// Процедура "заглушка", для других механизмов.
	ИзмененияПроизошли = Ложь;
	Возврат ИзмененияПроизошли;

КонецФункции // ОбновитьПеременныеМодуля

// Процедура управляет показом в форме периода построения отчета,
//
Процедура ПоказатьПериод()

	//НомерМесяца   = Формат(мДатаКонцаПериодаОтчета, "ДФ=М");
	//Если НомерМесяца = "12" Тогда
	//	НомерМесяца = "0";
	//КонецЕсли;

	СтрПериодОтчета = ПредставлениеПериода( НачалоДня(мДатаНачалаПериодаОтчета), КонецДня(мДатаКонцаПериодаОтчета), "ФП = Истина" );

	// Период в заголовке отчета
	Заголовок = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление() + " (за " + СтрПериодОтчета + ")";

КонецПроцедуры // ПоказатьПериод()

// Заполняет дату подписания отчета должностными лицами, в зависимости от
// наличия данных по этим лицам.
//
Процедура ЗаполнитьДатуВЯчейках() Экспорт
    Перем ТаблицаСтраниц;
	
	Если мВПрограммеИзмененаОрганизация Тогда
		Возврат;
	КонецЕсли;

	мРаздел1.Области.ДатаПодписиРук.Защита = Ложь;
	мРаздел1.Области.ДатаПодписиБух.Защита = Ложь;
	мРаздел3.Области.ДатаПодписиРук.Защита = Ложь;
	мРаздел3.Области.ДатаПодписиБух.Защита = Ложь;

	мРаздел1.Области.ДатаПодписиРук.Значение = Формат(ДатаПодписи, "ДФ=дд.ММ.гггг");
	мРаздел1.Области.ДатаПодписиБух.Значение = Формат(ДатаПодписи, "ДФ=дд.ММ.гггг");
	мРаздел3.Области.ДатаПодписиРук.Значение = Формат(ДатаПодписи, "ДФ=дд.ММ.гггг");
	мРаздел3.Области.ДатаПодписиБух.Значение = Формат(ДатаПодписи, "ДФ=дд.ММ.гггг");
	
	мРаздел1.Области.ДатаПодписиРук.Защита = Истина;
	мРаздел1.Области.ДатаПодписиБух.Защита = Истина;
	мРаздел3.Области.ДатаПодписиРук.Защита = Истина;
	мРаздел3.Области.ДатаПодписиБух.Защита = Истина;
			
	число = День(ДатаПодписи);
	месяц = Месяц(ДатаПодписи);
	год   = Год(ДатаПодписи);
	
	Для Каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		
		// Многостраничность - не записываем в Показатели отчета !!!
		//
		Если Не мСтруктураМногостраничныхРазделов.Свойство(СтраницаПанели.Имя, ТаблицаСтраниц) Тогда
			ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + СтраницаПанели.Имя;
			ТекТабличноеПоле        = ЭлементыФормы[ИмяТекТабличногоПоля];
			
			ТекТабличноеПоле.Области.Число.Защита = Ложь;
			ТекТабличноеПоле.Области.Месяц.Защита = Ложь;
			ТекТабличноеПоле.Области.Год.Защита   = Ложь;
			
			ТекТабличноеПоле.Области.Число.Значение = Число;
			ТекТабличноеПоле.Области.Месяц.Значение = Месяц;
			ТекТабличноеПоле.Области.Год.Значение   = Год;
			
			ТекТабличноеПоле.Области.Число.Защита = Истина;
			ТекТабличноеПоле.Области.Месяц.Защита = Истина;
			ТекТабличноеПоле.Области.Год.Защита   = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	РегламентированнаяОтчетность.роОбновитьСтруктуруМногостраничныхРазделов(ЭтаФорма, "Приложение");
	
	Для Каждого ДопСтр Из мСтруктураМногостраничныхРазделов.Приложение Цикл
		ДопСтр.Данные.Число = Число;
		ДопСтр.Данные.Месяц = Месяц;
		ДопСтр.Данные.Год   = Год;
	КонецЦикла;
	
	мПриложение.Области.Число.Защита = Ложь;
	мПриложение.Области.Месяц.Защита = Ложь;
	мПриложение.Области.Год.Защита   = Ложь;
	
	мПриложение.Области.Число.Значение = Число;
	мПриложение.Области.Месяц.Значение = Месяц;
	мПриложение.Области.Год.Значение   = Год;
	
	мПриложение.Области.Число.Защита = Истина;
	мПриложение.Области.Месяц.Защита = Истина;
	мПриложение.Области.Год.Защита   = Истина;                                               


КонецПроцедуры // ЗаполнитьДатуВЯчейках()

// ОпределитьИдМногострочногоРазделаПоАктивнойСтранице
//
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста = Неопределено) Экспорт

	Перем ИмяГруппы;

	Если ИмяЛиста = Неопределено Тогда

		ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
		ИмяТекущейСтраницыПанели = ТекущаяСтраницаПанели.Имя;
	Иначе
		ИмяТекущейСтраницыПанели = ИмяЛиста;

	КонецЕсли;

	Если ИмяТекущейСтраницыПанели = "Раздел1" Тогда
		ИмяГруппы = мИдГруппы1;
	ИначеЕсли ИмятекущейСтраницыПанели = "Сведения" Тогда
		ИмяГруппы = мИдГруппы2;
	ИначеЕсли ИмятекущейСтраницыПанели = "Зачет" Тогда
		ИмяГруппы = мИдГруппы3;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

	Возврат ИмяГруппы;

КонецФункции // ОпределитьИдМногострочногоРазделаПоАктивнойСтранице

// Процедура управляет выводом сведений о выбранной организации
// в соответствующие ячейки табличного документа.
//
// Параметры:
// ВПрограммеИзмененаДатаПодписи - Истина, если процедура запускается для обновления параметров завясящих
// от даты подписания документа, например ФИО должностных лиц
//
Процедура ЗаполнитьСведенияОбОрганизации(ВПрограммеИзмененаДатаПодписи = Ложь)

	Если (Организация  = Неопределено) Или (Организация = ОбщегоНазначения.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат
	КонецЕсли;

	ЧП = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Организация);

	// Составляем список данных, необходимых для вывода в декларацию
	Сведения = Новый СписокЗначений;
	
	Сведения.Добавить("", "КодНО"); // код ИФНС
	
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда

		Сведения.Добавить("", "ИННЮЛ"); // ИНН
		Сведения.Добавить("", "КППЮЛ"); // КПП
		Сведения.Добавить("", "НаимЮЛПол"); // Полное наименование 
		Сведения.Добавить("", ?(ЧП, "АдрМЖ", "АдрЮР"));

	КонецЕсли;

	Сведения.Добавить("", "ФИОРук"); // ФИО руководителя
	Сведения.Добавить("", "ФИОБух"); // ФИО бухгалтера
	Сведения.Добавить("", "ФИО"); // ФИО ПБОЮЛа

	// Теперь получаем данные из глобальной общей функции
	ОргСведения = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, Сведения);

	// Инициализируем переменные
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
		ИННЮЛ = "";
		КППЮЛ = "";
		НаимЮЛПол = "";
		Адр       = "";
	КонецЕсли;

	ФИОРук = "";
	ФИОБух = "";
	ФИО = "";

	// Получаем данные
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда

		ОргСведения.Свойство("ИННЮЛ",      ИННЮЛ    );
		ОргСведения.Свойство("КППЮЛ",      КППЮЛ    );
		ОргСведения.Свойство("НаимЮЛПол",  НаимЮЛПол);
		ОргСведения.Свойство(?(ЧП, "АдрМЖ", "АдрЮР"), Адр);

	КонецЕсли;

	ОргСведения.Свойство("ФИОРук",     ФИОРук   );
	ОргСведения.Свойство("ФИОБух",     ФИОБух   );
	ОргСведения.Свойство("ФИО",     ФИО   );
	Если ЧП Тогда
		НаимЮЛПол = ФИО;
		ФИОРук = ФИО;
		ФИОБух = "";
	КонецЕсли;

	Если ВПрограммеИзмененаДатаПодписи Тогда
		
		// Обновим только то, что зависит от даты подписи и выйдем
		мРаздел1.Области.ФИОРук.Значение  = ФИОРук;
		мРаздел3.Области.ФИОРук.Значение  = ФИОРук;
		
		мРаздел1.Области.ФИОБух.Значение = ФИОБух;
		мРаздел3.Области.ФИОБух.Значение = ФИОБух;
		
		Если ЧП Тогда
			мРаздел1.Области.ПокупательНаимОрг.Значение = ФИО;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	мИННЮЛ = ИННЮЛ;
	КППЮЛ = РегламентированнаяОтчетность.ДополнитьСтроку(КППЮЛ, 9, " ");
	
	мКППЮЛ = КППЮЛ;
	
	Если (мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(мРаздел1.Области.ПокупательМестоНахожд.Значение)) Тогда
		Адр = СтрЗаменить(Адр, ",,,,,,,,,", "");
		мРаздел1.Области.ПокупательМестоНахожд.Значение = Адр;
		
		Если РегламентированнаяОтчетность.ПустоеЗначение(СтрЗаменить(Адр, ",","")) Тогда
			ОбработанныйАдр = "";
			мРаздел1.Области.ПокупательМестоНахожд.Значение = ОбработанныйАдр;
		Иначе
			ОбработанныйАдр = РегламентированнаяОтчетность.ПредставлениеАдресаВФормате9Запятых(Адр);
			мРаздел1.Области.ПокупательМестоНахожд.Значение = ОбработанныйАдр;
		КонецЕсли;
		
		КодСтраны = ?(ПустаяСтрока(мРаздел1.Области.ПокупательБайконур.Значение), "643", "398");
		мРаздел1.Области.КодСтраныПокупатель.Значение = КодСтраны;
		
	КонецЕсли;
	
	Если (мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(мРаздел1.Области.ПокупательИНН_КПП.Значение)) Тогда
		мРаздел1.Области.ПокупательИНН_КПП.Значение = мИННЮЛ + ?(НЕ РегламентированнаяОтчетность.ПустоеЗначение(мКППЮЛ), "/","") + мКППЮЛ;
	КонецЕсли;
	
	// Работаем с названием ЮЛ
	Если (мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(мРаздел1.Области.ПокупательНаимОрг.Значение)) Тогда
		// Если пустая строка на Титульном листе, то запишем имеющееся у нас значение
		мРаздел1.Области.ПокупательНаимОрг.Значение = НаимЮЛПол;
	КонецЕсли;

	// Работаем с ФИО бухгалтера
	Если (мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(мРаздел1.Области.ФИОБух.Значение)) Тогда
		мРаздел1.Области.ФИОБух.Значение  = ФИОБух;
	КонецЕсли;

	// Работаем с ФИО руководителя	
	Если (мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(мРаздел1.Области.ФИОРук.Значение)) Тогда
		СведенияОПредставителе = ПолучитьСведенияОПредставителе(ОргСведения.КодНО);
		Если СведенияОПредставителе.ТипПредставителя = "1" тогда
			СведенияОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, "ФИОРук, ФИО");
			ФИОПодписанта = СокрЛП(?(ЗначениеЗаполнено(СведенияОбОрганизации.ФИОРук), СведенияОбОрганизации.ФИОРук, СведенияОбОрганизации.ФИО));
		Иначе
			ФИОПодписанта = СведенияОПредставителе.ФИОПредставителя;
		КонецЕсли;
		
		мРаздел1.Области.ФИОРук.Значение = ФИОПодписанта;
	КонецЕсли;

	// Даты проставятся всегда при открытии документа или при изменении организации
	Если (НЕ мДокументВосстановлен) ИЛИ (мВПрограммеИзмененаОрганизация) Тогда
	// Заполним даты формирования декларации (подписи)
		Модифицированность = Истина;
		ЗаполнитьДатуВЯчейках();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьСведенияОбОрганизации2

// Процедура контролирует корректность установленной точности указания
// суммовых (денежных) показателей при выборе единицы измерения.
//
Процедура ПроверитьТочность()
	
	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда

		Если ТочностьЕдиницыИзмерения > 2 Тогда
			Сообщить(ЭтаФорма.Заголовок+": при выводе сумм в рублях точность не может превышать 2 знака", СтатусСообщения.Внимание);
			ТочностьЕдиницыИзмерения = 2;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда

		Если ТочностьЕдиницыИзмерения > 3 Тогда
			Сообщить(ЭтаФорма.Заголовок+": при выводе сумм в тысячах рублей точность не может превышать 3 знака", СтатусСообщения.Внимание);
			ТочностьЕдиницыИзмерения = 3;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда

		Если ТочностьЕдиницыИзмерения > 6 Тогда
			Сообщить(ЭтаФорма.Заголовок+": при выводе сумм в миллионах рублей точность не может превышать 6 знаков", СтатусСообщения.Внимание);
			ТочностьЕдиницыИзмерения = 6;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПроверитьТочность()

// Процедура управляет представлением суммовых (денежных) показателей
// табличного документа при изменении размерности.
//
Процедура УстановитьФорматВывода()
	Перем СтруктураГруппы;
	Перем РазмерностьПрописьюТекст;

	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда
		мДелитель = 1;
		РазмерностьПрописьюТекст = "в рублях";
		мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда
		мДелитель = 1000;
		РазмерностьПрописьюТекст = "в тыс. рублей";
		мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда
		мДелитель = 1000000;
		РазмерностьПрописьюТекст = "в млн. рублей";
		мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6";
		
	КонецЕсли;

	//Группа1 = Неопределено;
	//мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, Группа1);

	//Для Инд = 0 По Группа1.Количество() - 1 Цикл

	//	Строка = "_" + Формат(Инд + 1, "ЧГ=0");

		//ФорматируемаяОбласть = мРаздел1.Области["П000010000106" + Строка];
		//ФорматируемаяОбласть.ТипЗначения = мОписаниеТиповЧислоЕ;
		//ФорматируемаяОбласть.Формат = мСтрокаФормата;

		//ФорматируемаяОбласть = мРаздел1.Области["П000010000115" + Строка];
		//ФорматируемаяОбласть.ТипЗначения = мОписаниеТиповЧислоЕ;
		//ФорматируемаяОбласть.Формат = мСтрокаФормата;

		//ФорматируемаяОбласть = мРаздел1.Области["П000010000119" + Строка];
		//ФорматируемаяОбласть.ТипЗначения = мОписаниеТиповЧислоЕ;
		//ФорматируемаяОбласть.Формат = мСтрокаФормата;

		//ФорматируемаяОбласть = мРаздел1.Области["П000010000120" + Строка];
		//ФорматируемаяОбласть.ТипЗначения = мОписаниеТиповЧислоЕ;
		//ФорматируемаяОбласть.Формат = мСтрокаФормата;

	// КонецЦикла;

	//мРаздел1.Области.П000010000115.Формат = мСтрокаФормата;

	//мРаздел1.Области.П000010000119.Формат = мСтрокаФормата;

	//мРаздел1.Области.П000010000120.Формат = мСтрокаФормата;
	
	//Группа2 = Неопределено;
	//мСтруктураМногострочныхРазделов.Свойство(мИдГруппы2, Группа2);

	//Для Инд = 0 По Группа2.Количество() - 1 Цикл

	//	Строка = "_" + Формат(Инд + 1, "ЧГ=0");

	//	ФорматируемаяОбласть = мСведения.Области["П000020000103" + Строка];
	//	ФорматируемаяОбласть.ТипЗначения = мОписаниеТиповЧислоЕ;
	//	ФорматируемаяОбласть.Формат = мСтрокаФормата;

	//КонецЦикла;

КонецПроцедуры // УстановитьФорматВывода()

// Обновляет пользовательские настройки при изменении версии формы.
//
Процедура ОбновитьПользовательскиеНастройки()

	Сообщить("Изменилась версия формы отчета.
				|Выполняется обновление пользовательских настроек.", СтатусСообщения.Информация);

	// Обновляем варианты заполнения показателей отчета
	ЕстьИзмененияВариантыЗаполнения = ОбновитьВариантыЗаполнения();

	// Обновляем настройки страниц отчета (содержание отчета)
	ЕстьИзмененияНастройкаСтраниц   = ОбновитьНастройкиСтраниц();

	Если ЕстьИзмененияВариантыЗаполнения ИЛИ ЕстьИзмененияНастройкаСтраниц Тогда
		// Если были изменения в пользовательских настройках,
		// то устанавливаем флаг модифицированности формы.
		Модифицированность = Истина;

		Сообщить("Обновление пользовательских настроек успешно завершено!");

	Иначе

		Сообщить("Изменения в структуре пользовательских настроек не обнаружены!");

	КонецЕсли;

КонецПроцедуры // ОбновитьПользовательскиеНастройки()

// Открывает форму документа ФормаНастройкиОтчета с целью выбора 
// выводимых на печать листов отчета.
//
// Параметры:
//   ВариантВыбора - строка, назначение выбора страниц.
//
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)

	ФормаНастройкиОтчета = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("НастройкиОтчета", ЭтаФорма, "дляВыбораСтраницОтчета");

	Если ФормаНастройкиОтчета.Открыта() Тогда
		ФормаНастройкиОтчета.Активизировать();
		Если Вопрос("Предыдущая операция выбора листов не завершена.
			|Завершить?",РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
			ФормаНастройкиОтчета.Закрыть();
		КонецЕсли;
	КонецЕсли;

	мДеревоВыбранныхСтраниц = "";

	ДеревоСтраницОтчета = мДеревоСтраницОтчета.Скопировать();

	ФормаНастройкиОтчета.НачальноеЗначениеВыбора = ДеревоСтраницОтчета;

	ФормаНастройкиОтчета.мПараметры.ЗначенияИзСписка = Истина;
	ФормаНастройкиОтчета.мПараметры.СчетчикСтраниц = Ложь;
	РезультатВыбора = ФормаНастройкиОтчета.ОткрытьМодально();
	Если РезультатВыбора <> Неопределено Тогда
		мДеревоВыбранныхСтраниц = РезультатВыбора;
		РегламентированнаяОтчетность.ПрорисоватьПодменюПечать(ЭтаФорма, Новый Действие("КоманднаяПанельДействийПоказатьБланк"), , Истина);
		Модифицированность = Истина;
	Иначе
		мДеревоВыбранныхСтраниц = ДеревоСтраницОтчета;
	КонецЕсли;

КонецПроцедуры // ОткрытьФормуВыбораСтраниц()

// Процедура управляет выводом отдельных страниц отчета на печать.
//
// Параметры:
//   Парам1 - структура, содержащая параметры печаты страницы:
//      - Имя выводимого на печать листа;
//      - ориентация страницы;
//      - имя табличного поля, который выводится на печать;
//      - имя области дабличного документа, выводимой на печать;
//      - строка, задающая способ печати.Принимает значения:
//           "ПечататьСразу" - непосредственный вывод на печать;
//           "ПечататьБланк" - с предварительным просмотром.
//
Процедура ВывестиНаПечатьЛист(СтруктураПараметровЛиста)
	
	Перем ИмяЛиста;           // имя выводимого на печать листа;
	Перем Ориентация;         // ориентация страницы;
	Перем ИмяТабличногоПоля;  // имя табличного поля, который выводится на печать;
	Перем ИмяОбластиТаблПоля; // имя области дабличного документа, выводимой на печать;
	Перем ВидПечати;          // строка, задающая способ печати
	
	Если СтруктураПараметровЛиста.ЧтоИскать <> Неопределено Тогда
		РегламентированнаяОтчетность.роНайтиЗначение(ЭтаФорма, СтруктураПараметровЛиста);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметровЛиста.Свойство("НаименованиеЛиста",  ИмяЛиста);
	СтруктураПараметровЛиста.Свойство("ОриентацияЛиста",    Ориентация);
	СтруктураПараметровЛиста.Свойство("ИмяТабличногоПоля",  ИмяТабличногоПоля);
	СтруктураПараметровЛиста.Свойство("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
	СтруктураПараметровЛиста.Свойство("ВидПечати",          ВидПечати);
	
	ПечатнаяФорма = Новый ТабличныйДокумент();
	
	ТабличноеПоле = ЭлементыФормы[ИмяТабличногоПоля];
	
	Если ПустаяСтрока(ИмяОбластиТаблПоля) Тогда
		ОбластьПечати = ТабличноеПоле.ПолучитьОбласть();
	Иначе
		ОбластьПечати = ТабличноеПоле.ПолучитьОбласть(ИмяОбластиТаблПоля);
	КонецЕсли;
	
	ПечатнаяФорма.Вывести(ОбластьПечати);
	
	ПечатнаяФорма.ОтображатьЗаголовки = Ложь;
	ПечатнаяФорма.ОтображатьСетку     = Ложь;
	ПечатнаяФорма.ЧерноБелыйПросмотр  = Истина;
	ПечатнаяФорма.ЧерноБелаяПечать    = Истина;
	ПечатнаяФорма.Автомасштаб         = Истина;
	ПечатнаяФорма.ОриентацияСтраницы  = Ориентация;
	
	УдалитьОбластиБайконура(ПечатнаяФорма);
	
	УдалитьПримечания(ПечатнаяФорма);
	
	мПечатныеФормы.Добавить(ПечатнаяФорма, ИмяЛиста);
	
КонецПроцедуры

Процедура УдалитьОбластиБайконура(ПечатнаяФорма)
	
	МассивИменУдаляемыхОбластей = Новый Массив;
	МассивИменУдаляемыхОбластей.Добавить("ПродавецБайконур");
	МассивИменУдаляемыхОбластей.Добавить("ПродавецБайконурНадпись");
	МассивИменУдаляемыхОбластей.Добавить("ПокупательБайконур");
	МассивИменУдаляемыхОбластей.Добавить("ПокупательБайконурНадпись");
	МассивИменУдаляемыхОбластей.Добавить("КомиссионерБайконур");
	МассивИменУдаляемыхОбластей.Добавить("КомиссионерБайконурНадпись");
	
	Для Каждого ИмяОбласти Из МассивИменУдаляемыхОбластей Цикл
		Если ПечатнаяФорма.Области.Найти(ИмяОбласти) <> Неопределено Тогда
			ПечатнаяФорма.Области[ИмяОбласти].Очистить(Истина, Истина, Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьПримечания(ПечатнаяФорма)
	
	ИмяОбласти = "ИННОрг_1";
	Если ПечатнаяФорма.Области.Найти(ИмяОбласти) <> Неопределено Тогда
		ПечатнаяФорма.Области[ИмяОбласти].Примечание.Текст = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Процедура управляет выводом всех страниц многостраничного раздела на печать.
//
// Параметры:
//   СтруктураПараметровСтраницы - структура, содержащая параметры печаты отчета:
//
//      - имя страницы основной панели формы (многостраничного раздела);
//      - имя выводимого на печать листа;
//      - ориентация страницы;
//      - имя табличного документа, который выводится на печать;
//      - имя области табличного документа, выводимой на печать (содержит не пустое значение,
//        когда выводится на печать не весь табличный документ, а часть его);
//      - строка, задающая способ печати.Принимает значения:
//           "ПечататьСразу" - непосредственный вывод на печать;
//           "ПечататьБланк" - с предварительным просмотром.
//
Процедура ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметровСтраницы)
	Перем ИмяСтраницы; // имя страницы основной панели формы;
	Перем ТаблицаСтраницРаздела;
	
	СтруктураПараметровСтраницы.Свойство("ИмяСтраницыПанели", ИмяСтраницы);

	Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницы, ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;

	АктивнаяСтраницаРаздела = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
	Если АктивнаяСтраницаРаздела = Неопределено Тогда
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы);
		Возврат;
	КонецЕсли;

	ИсходныйИндекс = ТаблицаСтраницРаздела.Индекс(АктивнаяСтраницаРаздела);

	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		// выводим на печать каждую страницу многостраничного раздела
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		ПоказатьСтраницуМногостраничногоРаздела(ТекущийИндекс, ИмяСтраницы, Истина);
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы);
	КонецЦикла;

	// В случае, если в многостраничном блоке находится 1 страница, то не следует ещё раз запускать ПоказатьСтраницуМногостраничногоРаздела
	// т.к. листов в разделе общее ко-во 1.

	Если мСтруктураМногостраничныхРазделов[ИмяСтраницы].Количество() <> 1 Тогда
		// вывод в форму данных исходной (активной) страницы
		ПоказатьСтраницуМногостраничногоРаздела(ИсходныйИндекс, ИмяСтраницы, Истина);
	КонецЕсли;

КонецПроцедуры // ВывестиНаПечатьДополнительныеСтраницы()

// Процедура очищает содержимое редактируемых и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
Процедура ОчиститьТабличноеПоле( ВыбТабличноеПоле ) Экспорт
	
	СписокПоказателейНеПодлежащихОчистке = Новый СписокЗначений;
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательИНН_КПП");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательНаимОрг");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательМестоНахожд");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОРук");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОБух");
	
	ПринудительнаяОчистка = Новый Массив;
	ПринудительнаяОчистка.Добавить("Раздел1.КодСтраныПродавец");
	ПринудительнаяОчистка.Добавить("Раздел1.КодСтраныОрг1");
	ПринудительнаяОчистка.Добавить("Раздел3.КодСтраныПродавец");
	ПринудительнаяОчистка.Добавить("Раздел3.КодСтраныПокупатель");
	ПринудительнаяОчистка.Добавить("Приложение.КодСтраныПродавец");
	ПринудительнаяОчистка.Добавить("Приложение.КодСтраныПокупатель");
	
	ИмяСтраницыПанели = СтрЗаменить(ВыбТабличноеПоле.Имя, "ПолеТабличногоДокумента", "");
	ИмяГруппыДопСтрок = ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяСтраницыПанели);
	
	Если ИмяГруппыДопСтрок <> Неопределено Тогда
		// Поле табличного документа содержит многострочный раздел.
		
		// Получим таблицу значений, содержащую данные дополнительных строк.
		ПромТаблицаДопСтрокРаздела = Новый ТаблицаЗначений;
		мСтруктураМногострочныхРазделов.Свойство(ИмяГруппыДопСтрок, ПромТаблицаДопСтрокРаздела);
		
		ИсходноеКоличествоСтрокГруппы = 0;
		мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппыДопСтрок, ИсходноеКоличествоСтрокГруппы);
		
		ТекущееКоличествоСтрок = ПромТаблицаДопСтрокРаздела.Количество();
		
		Если ТекущееКоличествоСтрок > ИсходноеКоличествоСтрокГруппы Тогда
			// Имеются дополнительные строки.
			// Удаляем их.
			
			НСтр = ПромТаблицаДопСтрокРаздела.Количество();
			Пока НСтр > ИсходноеКоличествоСтрокГруппы Цикл
				
				ТекСтрокаТаблицы = ПромТаблицаДопСтрокРаздела[НСтр - 1];
				
				ИмяПервойКолонки = ПромТаблицаДопСтрокРаздела.Колонки[0].Имя;
				ИмяПервойЯчейкиУдаляемойСтроки  = ИмяПервойКолонки + "_" + Формат(НСтр, "ЧГ=0");
				
				// область первой ячейки удаляемой строки многострочного раздела
				ОбластьЯчейки = ВыбТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
				
				// номера строк удаляемой области
				НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
				НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
				
				// Определим область удаляемой строки
				УдаляемаяОбласть = ВыбТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
				
				// непосредственно удаляем область строки табличного документа
				ВыбТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
				
				// удаляем из структуры многострочного раздела соответствующую строку
				ПромТаблицаДопСтрокРаздела.Удалить(ТекСтрокаТаблицы);
				
				НСтр = НСтр - 1;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	// Непосредственно очищаем ячейки табличного документа.
	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];
		
		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		// Ячейки не подлежащие очистки
		Если СписокПоказателейНеПодлежащихОчистке.НайтиПоЗначению(ТекущаяОбласть.Имя) <> Неопределено И ИмяСтраницыПанели = "Раздел1" Тогда
			Продолжить;
		КонецЕсли;
		
		ПолноеИмяПоказателя = ИмяСтраницыПанели + "." + ТекущаяОбласть.Имя;
		
		Если ПринудительнаяОчистка.Найти(ПолноеИмяПоказателя) = Неопределено И ТекущаяОбласть.Защита Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяОбласть.Очистить();
		
	КонецЦикла;
	
	ОчиститьСлужебныеДанныеРаздела(ИмяСтраницыПанели);
	
	Если ПолучитьСвойствоРаздела(ВыбТабличноеПоле.Имя, "ПредставлениеДанных") Тогда
		ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(ВыбТабличноеПоле.Имя);
	КонецЕсли;
	
	ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы1);
	
	Расчет(СтрЗаменить(ВыбТабличноеПоле.Имя, "ПолеТабличногоДокумента", ""));
	
КонецПроцедуры // ОчиститьТабличноеПоле()

Процедура ОчиститьСлужебныеДанныеРаздела(ИмяСтраницыПанели)
	
	Если ИмяСтраницыПанели = "Раздел1" Тогда
		Если ТипЗнч(ДополнительныеДанныеРазделов) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
			
		Если ДополнительныеДанныеРазделов.Свойство("АдресПродавцаРаздел1") Тогда
			ОчиститьСтруктуру(ДополнительныеДанныеРазделов.АдресПродавцаРаздел1);
		КонецЕсли;
		
		Если ДополнительныеДанныеРазделов.Свойство("АдресКомиссионераРаздел1") Тогда
			ОчиститьСтруктуру(ДополнительныеДанныеРазделов.АдресКомиссионераРаздел1);
		КонецЕсли;
		
	ИначеЕсли ИмяСтраницыПанели = "Раздел3" Тогда
		Если ТипЗнч(ДополнительныеДанныеРазделов) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеДанныеРазделов.Свойство("АдресПродавцаРаздел3") Тогда
			ОчиститьСтруктуру(ДополнительныеДанныеРазделов.АдресПродавцаРаздел3);
		КонецЕсли;
		
		Если ДополнительныеДанныеРазделов.Свойство("АдресПокупателяРаздел3") Тогда
			ОчиститьСтруктуру(ДополнительныеДанныеРазделов.АдресПокупателяРаздел3);
		КонецЕсли;
		
	ИначеЕсли ИмяСтраницыПанели = "Приложение" Тогда
		СтраницыРазделаПриложение = мСтруктураМногостраничныхРазделов.Приложение;
		
		Если СтраницыРазделаПриложение.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		АктивнаяСтраница = СтраницыРазделаПриложение.Найти(Истина, "АктивнаяСтраница");
		
		Если АктивнаяСтраница = Неопределено ИЛИ ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если АктивнаяСтраница.ДополнительныеДанные.Свойство("АдресПродавца") Тогда
			ОчиститьСтруктуру(АктивнаяСтраница.ДополнительныеДанные.АдресПродавца);
		КонецЕсли;
		
		Если АктивнаяСтраница.ДополнительныеДанные.Свойство("АдресПокупателя") Тогда
			ОчиститьСтруктуру(АктивнаяСтраница.ДополнительныеДанные.АдресПокупателя);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьСтруктуру(ОчищаемаяСтруктура)
	
	Если ТипЗнч(ОчищаемаяСтруктура) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементСтруктуры Из ОчищаемаяСтруктура Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Структура") Тогда
			ОчиститьСтруктуру(ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ]);
		Иначе
			ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ] = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает полученный из формы ФормаНастройкиОтчета дерево значений,
// определяет состав помеченных для вывода на печать листов.
//
// Параметры:
//  ВидПечати - строка, задающая способ печати:
//              "ПоказатьБланк" - непосредственный вывод на печать;
//              "ПечататьСразу" - с предварительным просмотром.
//
Процедура Печать(ВидПечати, ЧтоИскать = Неопределено) Экспорт

	Если ЧтоИскать = Неопределено Тогда
		Отказ = Ложь;
		РегламентированнаяОтчетность.ПередПечатьюРегламентированногоОтчета(ЭтаФорма, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ЧтоИскать = Неопределено И НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		Предупреждение("Перед формированием печатных форм необходимо определить в системе принтер и
						|задать его в качестве используемого по умолчанию!");
		Возврат;
	КонецЕсли;
	
	//ОткрытьФормуВыбораСтраниц("ВыбратьДляПечати");

	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДеревоЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	мПечатныеФормы.Очистить();

	СтруктураПараметров = Новый Структура;

	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.Строки Цикл
		Пометка = СтрокаУровня1.ВыводНаПечать;

		ИмяСтраницыПанели  = СтрокаУровня1.ИмяСтраницы;
		НаименованиеЛиста  = СтрокаУровня1.Представление;
		ОриентацияЛиста    = СтрокаУровня1.ОриентацияСтраницы;
		ИмяТабличногоПоля  = СтрокаУровня1.ИмяПоляТабличногоДокум;
		ИмяОбластиТаблПоля = СтрокаУровня1.ИмяОбластиТабличногоПоля;

		Если СтрокаУровня1.Строки.Количество() > 0 Тогда

			Для Каждого СтрокаУровня2 Из СтрокаУровня1.Строки Цикл
				Пометка = СтрокаУровня2.ВыводНаПечать;

				ИмяСтраницыПанели  = СтрокаУровня2.ИмяСтраницы;
				НаименованиеЛиста  = СтрокаУровня2.Представление;
				ОриентацияЛиста    = СтрокаУровня2.ОриентацияСтраницы;
				ИмяТабличногоПоля  = СтрокаУровня2.ИмяПоляТабличногоДокум;
				ИмяОбластиТаблПоля = СтрокаУровня2.ИмяОбластиТабличногоПоля;

				Если СтрокаУровня2.Строки.Количество() > 0 Тогда
					Для Каждого СтрокаУровня3 Из СтрокаУровня2.Строки Цикл

						Пометка = СтрокаУровня3.ВыводНаПечать;
						Если Пометка = 0 И ЧтоИскать = Неопределено Тогда
							Продолжить;
						КонецЕсли;

						ИмяСтраницыПанели  = СтрокаУровня3.ИмяСтраницы;
						НаименованиеЛиста  = СтрокаУровня3.Представление;
						ОриентацияЛиста    = СтрокаУровня3.ОриентацияСтраницы;
						ИмяТабличногоПоля  = СтрокаУровня3.ИмяПоляТабличногоДокум;
						ИмяОбластиТаблПоля = СтрокаУровня3.ИмяОбластиТабличногоПоля;

						СтруктураПараметров.Очистить();
						СтруктураПараметров.Вставить("НаименованиеЛиста",  НаименованиеЛиста);
						СтруктураПараметров.Вставить("ОриентацияЛиста",    ОриентацияЛиста);
						СтруктураПараметров.Вставить("ИмяТабличногоПоля",  ИмяТабличногоПоля);
						Структурапараметров.Вставить("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
						СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
						СтруктураПараметров.Вставить("ЧтоИскать",	       ЧтоИскать);

						Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели) Тогда
							ВывестиНаПечатьЛист(СтруктураПараметров);
						Иначе
							// выводим на печать все созданные страницы многостраничного раздела
							СтруктураПараметров.Вставить("ИмяСтраницыПанели", ИмяСтраницыПанели);
							ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров);
						КонецЕсли;
					КонецЦикла;

					Продолжить;

				КонецЕсли;

				Если Пометка = 0 И ЧтоИскать = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				СтруктураПараметров.Очистить();
				СтруктураПараметров.Вставить("НаименованиеЛиста",  НаименованиеЛиста);
				СтруктураПараметров.Вставить("ОриентацияЛиста",    ОриентацияЛиста);
				СтруктураПараметров.Вставить("ИмяТабличногоПоля",  ИмяТабличногоПоля);
				Структурапараметров.Вставить("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
				СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
				СтруктураПараметров.Вставить("ЧтоИскать",	       ЧтоИскать);

				Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели) Тогда
					ВывестиНаПечатьЛист(СтруктураПараметров);
				Иначе
					// выводим на печать все созданные страницы многостраничного раздела
					СтруктураПараметров.Вставить("ИмяСтраницыПанели", ИмяСтраницыПанели);
					ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров);
				КонецЕсли;
			КонецЦикла;

			Продолжить;

		КонецЕсли;

		Если Пометка = 0 И ЧтоИскать = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтруктураПараметров.Очистить();
		СтруктураПараметров.Вставить("НаименованиеЛиста",  НаименованиеЛиста);
		СтруктураПараметров.Вставить("ОриентацияЛиста",    ОриентацияЛиста);
		СтруктураПараметров.Вставить("ИмяТабличногоПоля",  ИмяТабличногоПоля);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
		СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
		СтруктураПараметров.Вставить("ЧтоИскать",	       ЧтоИскать);

		Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели) Тогда
			ВывестиНаПечатьЛист(СтруктураПараметров);
		Иначе
			// выводим на печать все созданные страницы многостраничного раздела
			СтруктураПараметров.Вставить("ИмяСтраницыПанели", ИмяСтраницыПанели);
			ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров);
		КонецЕсли;
	КонецЦикла;

	// обновляем исходное дерево значений 
	мДеревоСтраницОтчета = мДеревоВыбранныхСтраниц.Скопировать();
	
	Если ЧтоИскать = Неопределено Тогда
		РегламентированнаяОтчетность.ПроставитьНомераЛистов(ЭтаФорма, ВидПечати, Ложь);
	КонецЕсли;



КонецПроцедуры // Печать()

// ПолучитьКодИМНС
// Возвращает код ИМНС из справочника организации
//
Функция ПолучитьКодИМНС()

	Если (Организация  = Неопределено) Или (Организация = ОбщегоНазначения.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат "";
	КонецЕсли;

	Сведения = Новый СписокЗначений;

	Сведения.Добавить("", "КодНО");

	ОргСведения = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, Сведения);

	КодНО = "";

	ОргСведения.Свойство("КодНО", КодНО);

	Если КодНО = Неопределено Или РегламентированнаяОтчетность.ПустоеЗначение(КодНО) Тогда
		Результат = "";
	Иначе
		Результат = КодНО;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПолучитьКодИМНС

Функция ДобавитьРеквизит(Текст, ДобСтрока, Префикс)
	
	Если ЗначениеЗаполнено(ДобСтрока) Тогда
		Текст = Текст + Префикс + ДобСтрока;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
Функция Сохранить(Автосохранение = Ложь) Экспорт

	Перем ТаблицаСтраниц;

	Вариант = ВидДокумента * НомерКорректировки;

	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчета(ЭтаФорма, , Автосохранение, Вариант) Тогда
		Возврат Ложь;
	КонецЕсли;

	// установим текущие значения реквизитов документа
	мСохраненныйДок.ИсточникОтчета             = ЭтотОбъект.Метаданные().Имя;
	мСохраненныйДок.НаименованиеОтчета         = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление();
	мСохраненныйДок.ДатаНачала                 = мДатаНачалаПериодаОтчета;
	мСохраненныйДок.ДатаОкончания              = мДатаКонцаПериодаОтчета;
	мСохраненныйДок.Периодичность              = мПериодичность;
	мСохраненныйДок.ВыбраннаяФорма             = мВыбраннаяФорма;
	мСохраненныйДок.Организация                = Организация;
	мСохраненныйДок.ДатаПодписи                = ДатаПодписи;
	мСохраненныйДок.ЕдиницаИзмерения           = ЕдиницаИзмерения;
	мСохраненныйДок.ТочностьЕдиницыИзмерения   = ТочностьЕдиницыИзмерения;
	мСохраненныйДок.ВидОтчетности              = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	мСохраненныйДок.Комментарий                = Комментарий;
	мСохраненныйДок.КодИМНС                    = ПолучитьКодИМНС();

	мСохраненныйДок.Вид                        = Вариант;

	мВариант = Вариант;

	РегламентированнаяОтчетность.роОбновитьСтруктуруМногостраничныхРазделов(ЭтаФорма);
	ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы1);
	ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы2);
	ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы3);

	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();

	Для Каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл

		// Многостраничность - не записываем в Показатели отчета !!!
		//
		Если Не мСтруктураМногостраничныхРазделов.Свойство(СтраницаПанели.Имя, ТаблицаСтраниц) Тогда
			ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + СтраницаПанели.Имя;
			ТекТабличноеПоле        = ЭлементыФормы[ИмяТекТабличногоПоля];

			ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
			ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);
		КонецЕсли;

	КонецЦикла;

	// формируем список сохранения
	СписокСохранения = Новый Структура();

	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);

	// вставляем данные многострочных разделов
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", мСтруктураМногострочныхРазделов);

	// вставляем данные многостраничных разделов
	СписокСохранения.Вставить("ДанныеМногостраничныхРазделов", мСтруктураМногостраничныхРазделов);

	// вставляем данные вариантов автозаполнения ячеек
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", мСтруктураВариантыЗаполнения);

	// вставляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", мВерсияФормы);
	
	// вставляем флажок пересчета страниц
	СписокСохранения.Вставить("ФлПодсчетСтраниц", мСчетчикСтраниц);

	// вставляем флажок отключения
	СписокСохранения.Вставить("ФлОтклВыборЗначений", мАвтоВыборКодов);

	// вставляем флажок ручной ввод ВРЕМЕННАЯ ОТМЕНА
	СписокСохранения.Вставить("Автозаполнение", ФлажокОтклАвтоРасчет);
	
	// вставляем структуру, хранящую таблицы значений с данными по товаро-транспортным документам
	СписокСохранения.Вставить("мСтрукТЗДокументы", мСтрукТЗДокументы);
	
	// вставляем структуру, //структура, хранящая данные об отправителе, подписантах и курьере
	СписокСохранения.Вставить("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки);
	
	// вставляем спецификации раздел1
	СписокСохранения.Вставить("мТЗСпецификацийРаздел1", мТЗСпецификацийРаздел1); 
	
	// вставляем спецификации раздел1
	СписокСохранения.Вставить("мТЗСпецификацийРаздел1_07", мТЗСпецификацийРаздел1_07); 

	// вставляем спецификации раздел3
	СписокСохранения.Вставить("мТЗСпецификацийРаздел3", мТЗСпецификацийРаздел3); 
	
	// вставляем массив спецификации Приложения
	СписокСохранения.Вставить("мМассивТЗСпецификацийПрил", мМассивТЗСпецификацийПрил);
	
	СписокСохранения.Вставить("ПечатьБезШтрихкодаРазрешена", мПечатьБезШтрихкодаРазрешена);
	
	СписокСохранения.Вставить("ДополнительныеДанныеРазделов", ДополнительныеДанныеРазделов);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	Попытка
		мСохраненныйДок.Записать();
	Исключение
		Если НЕ Автосохранение Тогда
			Предупреждение(ОбщегоНазначения.СформироватьТекстСообщения(ОписаниеОшибки()));
		КонецЕсли;
		РегламентированнаяОтчетность.СкрытьКартинкуСохранение(ЭтаФорма);
		Возврат Ложь;
	КонецПопытки;
	
	РегламентированнаяОтчетность.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);

	СохранитьДеревоНастройкиСтраниц();

	КлючУникальности = мСохраненныйДок.Ссылка;

	Модифицированность = Ложь;

	Возврат Истина;

КонецФункции // Сохранить()

//вспомогательная процедура при восстановлении
Процедура ПеренестиДанныеСтруктураТЗ(СтруктураСпецификаций, ТЗСпецификаций)
	Для Сч = 1 По 5 Цикл
		НоваяСтрока = ТЗСпецификаций.Добавить();
        НоваяСтрока.Номер 	= СтруктураСпецификаций["НомерСП" + Сч];
		НоваяСтрока.Дата 	= СтруктураСпецификаций["ДатаСП" + Сч];
	КонецЦикла;
КонецПроцедуры

// мВерсияФормыЧисло
// функция возвращает число полученное из версии
// Например 01/01/2004 вернется как 20040101
//
Функция мВерсияФормыЧисло(Знач Версия)
	ЧисловойПараметр1 = Сред(Версия, 7, 4);
	ЧисловойПараметр2 = Сред(Версия, 4, 2);
	ЧисловойПараметр3 = Сред(Версия, 1, 2);
	Результат = ЧисловойПараметр1 + ЧисловойПараметр2 + ЧисловойПараметр3;

	Если РегламентированнаяОтчетность.роЯвляетсяЧислом(Результат) Тогда
		Возврат Число(Результат);
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции // мВерсияФормыЧисло

Функция ОбновитьДанныеРевизитовВыгрузки()
	
	ФИОПодп1Титул = ЭлементыФормы.ПолеТабличногоДокументаРаздел1.Области.ФИОРук.Значение;
	ФИОПодп2Титул = ЭлементыФормы.ПолеТабличногоДокументаРаздел1.Области.ФИОБух.Значение;
	
	// Сведения об организации.
	ЭтоПБОЮЛ = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Организация);
	
	СтрокаСведений = "ИННЮЛ, ИННФЛ, КППЮЛ, НаимЮЛПол, ФИО, КодНО";
	СведенияОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, СтрокаСведений);
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	Параметры.Вставить("НалогоплательщикИННЮЛ",  СведенияОбОрганизации.ИННЮЛ);
	Параметры.Вставить("НалогоплательщикКПП",    СведенияОбОрганизации.КППЮЛ);
	Параметры.Вставить("НалогоплательщикИННФЛ",  СведенияОбОрганизации.ИННФЛ);
	Параметры.Вставить("НалогоплательщикНаимЮЛ", СведенияОбОрганизации.НаимЮЛПол);
	Параметры.Вставить("НалогоплательщикФИО",    СведенияОбОрганизации.ФИО);
	
	Параметры.Вставить("КодИФНС", СведенияОбОрганизации.КодНО);
	
	ДанныеПредставителя = ПолучитьСведенияОПредставителе(СведенияОбОрганизации.КодНО); //возвращает ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка
	
	Параметры.Вставить("ПрОтпр", Число(ДанныеПредставителя.ТипПредставителя)); //1 - НП, 2 - представитель НП
	
	ОтправительЯвляетсяЮЛ = Истина;
	
	НаимОтпр        = "";
	ИННЮЛОтпр       = "";
	КППОтпр         = "";

	ИННФЛОтпр       = "";

	ФамилияФЛОтпр   = "";
	ИмяФЛОтпр       = "";
	ОтчествоФЛОтпр  = "";

	ПрПодп1         = 0;
	ИННФЛПодп1      = "";
	ДолжностьПодп1  = "";
	
	ФамилияПодп1    = "";
	ИмяПодп1        = "";
	ОтчествоПодп1   = "";

	НаимДовПодп1    = "";
	НомерДовПодп1   = "";
	ДатаДовПодп1    = "";
	
	ПрПодп2         = 0;
	ИННФЛПодп2      = "";
	ДолжностьПодп2  = "";
	
	ФамилияПодп2    = "";
	ИмяПодп2        = "";
	ОтчествоПодп2   = "";
	
	НаимДовПодп2    = "";
	НомерДовПодп2   = "";
	ДатаДовПодп2    = "";
	
	ПрКурьер        = 0;
	ИННФЛКурьер     = "";
	
	ФамилияКурьер   = "";
	ИмяКурьер       = "";
	ОтчествоКурьер  = "";
	
	НаимДовКурьер   = "";
	НомерДовКурьер  = "";
	ДатаДовКурьер   = "";

	Если ДанныеПредставителя.ТипПредставителя = "1" Тогда
		//нет представителя
		Если НЕ ЭтоПБОЮЛ Тогда
			//налогоплательщик - юр.лицо
			
			//отправитель
			ОтправительЯвляетсяЮЛ = Истина;
			
			НаимОтпр 		= СведенияОбОрганизации.НаимЮЛПол;
			ИННЮЛОтпр		= СведенияОбОрганизации.ИННЮЛ;
			КППОтпр         = СведенияОбОрганизации.КППЮЛ;
			
			//подписант 1
			ПрПодп1 = 2;
			
			ФИОПодп1 = РазложитьФИО(ФИОПодп1Титул);
			ФамилияПодп1    = ФИОПодп1.Фамилия;
			ИмяПодп1        = ФИОПодп1.Имя;
			ОтчествоПодп1   = ФИОПодп1.Отчество;
			
			//подписант 2
			ПрПодп2 = 2;
			
			ФИОПодп2 = РазложитьФИО(ФИОПодп2Титул);
			ФамилияПодп2    = ФИОПодп2.Фамилия;
			ИмяПодп2        = ФИОПодп2.Имя;
			ОтчествоПодп2   = ФИОПодп2.Отчество;
			
			//курьер
			ПрКурьер = 2;

			ФамилияКурьер   = ФИОПодп1.Фамилия;
			ИмяКурьер       = ФИОПодп1.Имя;
			ОтчествоКурьер  = ФИОПодп1.Отчество;
			
		Иначе
			//налогоплательщик - ИП
			
			//отправитель
			ОтправительЯвляетсяЮЛ = Ложь;
			
			ИННФЛОтпр       = СведенияОбОрганизации.ИННФЛ;
			
			ФИОотправителя = РазложитьФИО(ФИОПодп1Титул);
			ФамилияФЛОтпр    = ФИОотправителя.Фамилия;
			ИмяФЛОтпр        = ФИОотправителя.Имя;
			ОтчествоФЛОтпр   = ФИОотправителя.Отчество;

			//подписант 1
			ПрПодп1 = 1;
			ФИОПодп1 = РазложитьФИО(ФИОПодп1Титул);
			ФамилияПодп1    = ФИОПодп1.Фамилия;
			ИмяПодп1        = ФИОПодп1.Имя;
			ОтчествоПодп1   = ФИОПодп1.Отчество;
			
			//курьер
			ПрКурьер = 1;
			
			ФамилияКурьер   = ФИОотправителя.Фамилия;
			ИмяКурьер       = ФИОотправителя.Имя;
			ОтчествоКурьер  = ФИОотправителя.Отчество;
		КонецЕсли;
	Иначе
		//есть представитель
		Если ДанныеПредставителя.флПредставительЮрЛицо Тогда
			//представитель - юр.лицо

			//отправитель
			ОтправительЯвляетсяЮЛ = Истина;
			
			ПредставительСсылка = ДанныеПредставителя.ПредставительСсылка;
			НаимОтпр 		= СокрЛП(ПредставительСсылка);
			ИННЮЛОтпр		= ПредставительСсылка.ИНН;
			КППОтпр         = ПредставительСсылка.КПП;	
			
			//подписант 1
			ПрПодп1 = 3;
			ФИОПодп1 = РазложитьФИО(ФИОПодп1Титул);
			ФамилияПодп1    = ФИОПодп1.Фамилия;
			ИмяПодп1        = ФИОПодп1.Имя;
			ОтчествоПодп1   = ФИОПодп1.Отчество;

			//подписант 2
			ФИОПодп2 = РазложитьФИО(ФИОПодп2Титул);
			ФамилияПодп2    = ФИОПодп2.Фамилия;
			ИмяПодп2        = ФИОПодп2.Имя;
			ОтчествоПодп2   = ФИОПодп2.Отчество;

			//курьер
			ПрКурьер        = 3;

			ФамилияКурьер   = ФИОПодп1.Фамилия;
			ИмяКурьер       = ФИОПодп1.Имя;
			ОтчествоКурьер  = ФИОПодп1.Отчество;
		Иначе
			//представитель - физ.лицо
			
			//отправитель
			ОтправительЯвляетсяЮЛ = Ложь;
			
			ФИОотправителя = РазложитьФИО(ФИОПодп1Титул);
			ФамилияФЛОтпр    = ФИОотправителя.Фамилия;
			ИмяФЛОтпр        = ФИОотправителя.Имя;
			ОтчествоФЛОтпр   = ФИОотправителя.Отчество;
			
			//подписант 1
			ПрПодп1         = 3;
			ФамилияПодп1    = ФИОотправителя.Фамилия;
			ИмяПодп1        = ФИОотправителя.Имя;
			ОтчествоПодп1   = ФИОотправителя.Отчество;
			
			//подписант 2
			ФИОПодп2 = РазложитьФИО(ФИОПодп2Титул);
			ФамилияПодп2    = ФИОПодп2.Фамилия;
			ИмяПодп2        = ФИОПодп2.Имя;
			ОтчествоПодп2   = ФИОПодп2.Отчество;
			
			//курьер
			ПрКурьер = 3;

			ФамилияКурьер   = ФИОотправителя.Фамилия;
			ИмяКурьер       = ФИОотправителя.Имя;
			ОтчествоКурьер  = ФИОотправителя.Отчество;
		КонецЕсли;
	КонецЕсли;
	
	Параметры.Вставить("ОтправительЯвляетсяЮЛ", ОтправительЯвляетсяЮЛ);
	
	Параметры.Вставить("НаимОтпр",      НаимОтпр);
	Параметры.Вставить("ИННЮЛОтпр",     ИННЮЛОтпр);
	Параметры.Вставить("КППОтпр",       КППОтпр);

	Параметры.Вставить("ИННФЛОтпр",     ИННФЛОтпр);

	Параметры.Вставить("ФамилияФЛОтпр", ФамилияФЛОтпр);
	Параметры.Вставить("ИмяФЛОтпр",     ИмяФЛОтпр);
	Параметры.Вставить("ОтчествоФЛОтпр",ОтчествоФЛОтпр);

	Параметры.Вставить("ПрПодп1",       ПрПодп1);
	Параметры.Вставить("ИННФЛПодп1",    ИННФЛПодп1);
	Параметры.Вставить("ДолжностьПодп1",ДолжностьПодп1);
	
	Параметры.Вставить("ФамилияПодп1",  ФамилияПодп1);
	Параметры.Вставить("ИмяПодп1",      ИмяПодп1);
	Параметры.Вставить("ОтчествоПодп1", ОтчествоПодп1);
	
	Параметры.Вставить("НаимДовПодп1",  НаимДовПодп1);
	Параметры.Вставить("НомерДовПодп1", НомерДовПодп1);
	Параметры.Вставить("ДатаДовПодп1",  ДатаДовПодп1);
	
	Параметры.Вставить("ПрПодп2",       ПрПодп2);
	Параметры.Вставить("ИННФЛПодп2",    ИННФЛПодп2);
	Параметры.Вставить("ДолжностьПодп2",ДолжностьПодп2);
	
	Параметры.Вставить("ФамилияПодп2",  ФамилияПодп2);
	Параметры.Вставить("ИмяПодп2",      ИмяПодп2);
	Параметры.Вставить("ОтчествоПодп2", ОтчествоПодп2);
	
	Параметры.Вставить("НаимДовПодп2",  НаимДовПодп2);
	Параметры.Вставить("НомерДовПодп2", НомерДовПодп2);
	Параметры.Вставить("ДатаДовПодп2",  ДатаДовПодп2);
	
	Параметры.Вставить("ОтправленПочтой", Ложь);
	Параметры.Вставить("ПрКурьер",      ПрКурьер);
	Параметры.Вставить("ИННФЛКурьер",   ИННФЛКурьер);
	
	Параметры.Вставить("ФамилияКурьер", ФамилияКурьер);
	Параметры.Вставить("ИмяКурьер",     ИмяКурьер);
	Параметры.Вставить("ОтчествоКурьер",ОтчествоКурьер);
	
	Параметры.Вставить("НаимДовКурьер",  НаимДовКурьер);
	Параметры.Вставить("НомерДовКурьер", НомерДовКурьер);
	Параметры.Вставить("ДатаДовКурьер",  ДатаДовКурьер);
	
	Возврат Параметры;
	
КонецФункции

// Восстанавливает сохраненные данные отчета.
//
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеМногостраничныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;
	Перем Автозаполнение;
	Перем ТаблицаМногостраничногоДокумента;

	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если Организация <> мСохраненныйДок.Организация
	   И Организация <> ОбщегоНазначения.ПустоеЗначениеТипа("СправочникСсылка.Организации")
	   И Организация <> Неопределено Тогда
		// Меняем переменную, для того, чтобы запустилась процедура ЗаполнитьСведенияОбОрганизации
		мВПрограммеИзмененаОрганизация = Истина;
		мСохраненныйДок.Организация = Организация;
	Иначе
		// восстанавливаем реквизиты отчета
		Организация              = мСохраненныйДок.Организация;
	КонецЕсли;

	ДатаПодписи              = мСохраненныйДок.ДатаПодписи;
	ЕдиницаИзмерения         = мСохраненныйДок.ЕдиницаИзмерения;
	ТочностьЕдиницыИзмерения = мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий              = мСохраненныйДок.Комментарий;

	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();

	// список листов, выводимых на печать
	ДеревоНастройкиСтраниц = мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
	мДеревоСтраницОтчета   = ДеревоНастройкиСтраниц.Скопировать();

	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		мСтруктураМногострочныхРазделов = ДанныеМногострочныхРазделов;

		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы       = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;

			// Если лист многостраничный, то всегда выведется текущий на момент сохранения блок многострочности
			// а в процедуре вывести активную страницу, выведутся оставшиеся данные.
			ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);

		КонецЦикла;
	КонецЕсли;

	НомерСтроки = 1;
	Пока мРаздел1.Области.Найти("П000010000116_" + НомерСтроки) <> Неопределено Цикл
		Если мРаздел1.Области.Найти("П000010000116_" + НомерСтроки).Значение = "-" Тогда
			мРаздел1.Области["П000010000114_" + НомерСтроки].Формат = "ЧН=-";	
		Иначе
			мРаздел1.Области["П000010000114_" + НомерСтроки].Формат = "ЧДЦ=6; ЧН=";
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	//специфика данной версии отчета: для совместимости с сохраненными версиями отчета, в случае отсутствия зачетов - дополняем мСтруктураМногострочныхРазделов
	Если НЕ мСтруктураМногострочныхРазделов.Свойство(мИдГруппы3) Тогда
		мОписаниеТиповЧисло15_2  = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2);
		мОписаниеТиповСтрока254 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);	
		
		СтруктураГруппы3 = Новый ТаблицаЗначений;
		СтруктураГруппы3.Колонки.Добавить("П000030000101", мОписаниеТиповСтрока254);
		СтруктураГруппы3.Колонки.Добавить("П000030000102", мОписаниеТиповЧисло15_2);
		
		СтруктураГруппы3.Добавить();
		мСтруктураМногострочныхРазделов.Вставить(мИдГруппы3, СтруктураГруппы3);
	КонецЕсли;
	
	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		мСтруктураВариантыЗаполнения = ДанныеВариановАвтоЗаполнения;
	КонецЕсли;

	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета );

	// восстанавливаем версию формы
	Если СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы) Тогда
		Если ВерсияФормы <> мВерсияФормы Тогда
			мСтараяВерсияФормы = ВерсияФормы;
			// Версия формы изменилась.
			// Обновляем пользовательские настройки.
			ОбновитьПользовательскиеНастройки();
		КонецЕсли;
	КонецЕсли;

	// восстанавливаем сохраненные данные многостраничных разделов
	Если СписокСохранения.Свойство("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов) Тогда
		мСтруктураМногостраничныхРазделов = ДанныеМногостраничныхРазделов;
		
		// Отображаем данные, хранящиеся в структуре многостраничных разделов
		Для Каждого ЭлементСтруктуры Из мСтруктураМногостраничныхРазделов Цикл
			// 1 Нужно найти Активную страницу по индексу
			// и ей отобразить в табличном документе

			мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, ТаблицаМногостраничногоДокумента);
			АктивнаяСтраница = ТаблицаМногостраничногоДокумента.Найти(Истина, "АктивнаяСтраница");
			ИндАктивнаяСтраница = ТаблицаМногостраничногоДокумента.Индекс(АктивнаяСтраница);
			ПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(ИндАктивнаяСтраница, ЭлементСтруктуры.Ключ);
		КонецЦикла;

		//специфика данной версии отчета: для совместимости с сохраненными версиями отчета, в случае отсутствия зачетов - дополняем мСтруктураМногостраничныхРазделов
		Если НЕ мСтруктураМногостраничныхРазделов.Свойство("Зачет") Тогда
			мОписаниеТиповСтрока254 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);
			
			МассивБулево = Новый Массив;
			МассивБулево.Добавить(Тип("Булево"));
			мОписаниеТиповБулево    = Новый ОписаниеТипов(МассивБулево);
			
			ТаблицаСтраницыРазделаДляМногострочныхРазделов            = Новый ТаблицаЗначений;
			ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Представление",    мОписаниеТиповСтрока254, "Наименование");
			ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АктивнаяСтраница", мОписаниеТиповБулево);
			ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Данные");
			ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("ДанныеДопСтрок");
			ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АвтоматическоеПредставление", мОписаниеТиповБулево);
			
			ТаблицаСтраницыЗачет = ТаблицаСтраницыРазделаДляМногострочныхРазделов.Скопировать();
			СтрПерваяСтраница = ТаблицаСтраницыЗачет.Добавить();
			СтрПерваяСтраница.Представление      = "Новая страница";
			СтрПерваяСтраница.ДанныеДопСтрок = Неопределено; // В разделе есть многострочность, поэтому поле объявлено
			СтрПерваяСтраница.АктивнаяСтраница   = Истина;
			СтрПерваяСтраница.АвтоматическоеПредставление = Истина;
			
			
			ТаблицаСтраницыРаздела = РегламентированнаяОтчетность.ПолучитьТаблицуМногостраничногоРаздела();
			
			// Структура содержит данные дополнительных страниц зачета
			мСтруктураМногостраничныхРазделов.Вставить("Зачет", ТаблицаСтраницызачет);
		КонецЕсли;
	КонецЕсли;
	
	//восстанавливаем структуру, хранящую таблицы значений с данными по товаро-транспортным документам
	СписокСохранения.Свойство("мСтрукТЗДокументы", мСтрукТЗДокументы);
	Если мСтрукТЗДокументы = Неопределено Тогда
		мСтрукТЗДокументы = Новый Структура;
	КонецЕсли;
	
	// восстанавливаем флажок ПодсчетаСтраниц
	СписокСохранения.Свойство("ФлПодсчетСтраниц", мСчетчикСтраниц);
	
	СписокСохранения.Свойство("ФлОтклВыборЗначений", мАвтоВыборКодов);
	Если мАвтоВыборКодов = Неопределено Тогда
		мАвтоВыборКодов = Ложь;
	КонецЕсли;

	// восстанавливаем  ТЗ спецификаций Раздел1;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел1", мТЗСпецификацийРаздел1);
	Если мТЗСпецификацийРаздел1 = Неопределено Тогда
		мТЗСпецификацийРаздел1 = СоздатьТЗСпецификаций();
	КонецЕсли;
	
	// восстанавливаем ТЗ спецификаций Раздел1 стр 07;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел1_07", мТЗСпецификацийРаздел1_07);
	Если мТЗСпецификацийРаздел1_07 = Неопределено Тогда
		мТЗСпецификацийРаздел1_07 = СоздатьТЗСпецификаций();
	КонецЕсли;
	
	// восстанавливаем ТЗ спецификаций Раздел3;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел3", мТЗСпецификацийРаздел3);
	Если мТЗСпецификацийРаздел3 = Неопределено Тогда
		мТЗСпецификацийРаздел3 = СоздатьТЗСпецификаций();
	КонецЕсли;

	// восстанавливаем  массив ТЗ спецификаций Приложения;
	СписокСохранения.Свойство("мМассивТЗСпецификацийПрил", мМассивТЗСпецификацийПрил);
	Если мМассивТЗСпецификацийПрил = Неопределено Тогда
		мМассивТЗСпецификацийПрил = Новый Массив;
		Элемент = СоздатьТЗСпецификаций(); 
		мМассивТЗСпецификацийПрил.Добавить(Элемент);
	КонецЕсли;

	СписокСохранения.Свойство("Автозаполнение", Автозаполнение);
	ФлажокОтклАвтоРасчет = Автозаполнение;
	СнятьЗащитуСЯчеек(НЕ ФлажокОтклАвтоРасчет);
	
	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭлементыФормы[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;

		ПредставлениеДанных = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекТабличноеПоле, "ПредставлениеДанных");

		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;
			ИмяСтраницыПанели   = СтрЗаменить(ИмяТекТабличноеПоле, "ПолеТабличногоДокумента", "");

			Если мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели, ТаблицаМногостраничногоДокумента) Тогда
				Продолжить;
			КонецЕсли;

			Если ПредставлениеДанных Тогда
				Если ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			Попытка
				ТекТабличноеПоле.Области[ИмяПоказателя].Значение = ЗначениеПоказателя;
			Исключение
			КонецПопытки;

		КонецЦикла;

	КонецЦикла;

	Если ВерсияФормы = "16/08/2010" Тогда
		//восстанавливаем структуры спецификаций, достаем данные и укладываем в таблицы значений спецификаций
		СтруктураСпецификаций = Неопределено;
		СписокСохранения.Свойство("мСтруктураСпецификацийРаздел1", СтруктураСпецификаций);
	    ПеренестиДанныеСтруктураТЗ(СтруктураСпецификаций, мТЗСпецификацийРаздел1);
		
		СписокСохранения.Свойство("мСтруктураСпецификацийРаздел1_07", СтруктураСпецификаций);
	    ПеренестиДанныеСтруктураТЗ(СтруктураСпецификаций, мТЗСпецификацийРаздел1_07);
		
		СписокСохранения.Свойство("мСтруктураСпецификацийРаздел3", СтруктураСпецификаций);
	    ПеренестиДанныеСтруктураТЗ(СтруктураСпецификаций, мТЗСпецификацийРаздел3);
		
		МассивСтруктурСпецификаций = Неопределено;
		СписокСохранения.Свойство("мСтруктураСпецификацийПрил", МассивСтруктурСпецификаций);
		мМассивТЗСпецификацийПрил = Новый Массив;
		Для Сч = 1 По МассивСтруктурСпецификаций.Количество() Цикл
			ТЗСпецификаций = СоздатьТЗСпецификаций(); 
			ПеренестиДанныеСтруктураТЗ(МассивСтруктурСпецификаций[Сч - 1], ТЗСпецификаций);
			мМассивТЗСпецификацийПрил.Добавить(ТЗСпецификаций);
		КонецЦикла; 
		
		//добавляем именованные поля к каждой строке мнострочной части раздела 1
		Если мРаздел1.Области.Найти("П000010000121_1") <> Неопределено Тогда
			мРаздел1.Области.Найти("П000010000121_1").Имя = "";
		КонецЕсли;
		
		НомерСтроки = 1;
		Пока мРаздел1.Области.Найти("П000010000120_" + Формат(НомерСтроки, "ЧГ=0")) <> Неопределено Цикл
			НомСтрокиТабличногоДокумента = мРаздел1.Области.Найти("П000010000120_" + Формат(НомерСтроки, "ЧГ=0")).Низ;
			мРаздел1.Область("R" + НомСтрокиТабличногоДокумента + "C81").Имя = "П000010000121_" + НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		//преобразуем таблицу значений многострочного раздела 1 (изменились типы колонок 2, 14, 18, добавилась колонка 21)
		ОбновленнаяТЗ = мСтруктураМногострочныхРазделов[мИдГруппы1].Скопировать();
		ОбновленнаяТЗ.Очистить();
		
		ОбновленнаяТЗ.Колонки.Удалить("П000010000102");
		ОбновленнаяТЗ.Колонки.Удалить("П000010000108");
		ОбновленнаяТЗ.Колонки.Удалить("П000010000114");
		
		ОписаниеТиповСтрока1   = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(1);
		ОписаниеТиповЧисло9_4  = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(9, 4);
		ОписаниеТиповЧисло20_6 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(20, 6);
		ОписаниеТиповСтрока500 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(500);
		
		ОбновленнаяТЗ.Колонки.Добавить("П000010000102", ОписаниеТиповСтрока500);
		ОбновленнаяТЗ.Колонки.Добавить("П000010000108", ОписаниеТиповЧисло9_4);
		ОбновленнаяТЗ.Колонки.Добавить("П000010000114", ОписаниеТиповЧисло20_6);
		ОбновленнаяТЗ.Колонки.Добавить("П000010000121", ОписаниеТиповСтрока1);
		
		Для каждого СтрокаТЗ Из мСтруктураМногострочныхРазделов[мИдГруппы1] Цикл 
			ЗаполнитьЗначенияСвойств(ОбновленнаяТЗ.Добавить(), СтрокаТЗ) 
		КонецЦикла; 
		
		мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, ОбновленнаяТЗ);
		
		ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы1);
	КонецЕсли;
	
	Если (мВерсияФормыЧисло(мСтараяВерсияФормы) <= 20110720) Тогда
		// добавляем именованные поля к каждой строке мнострочной части раздела 1
		// копируем туда значение из колонки 4
		Если мРаздел1.Области.Найти("П000010000122_1") <> Неопределено Тогда
			мРаздел1.Области.Найти("П000010000122_1").Имя = "";
		КонецЕсли;
		
		НомерСтроки = 1;
		Пока мРаздел1.Области.Найти("П000010000120_" + Формат(НомерСтроки, "ЧГ=0")) <> Неопределено Цикл
			ИмяПоляЕдИзм = "П000010000104_" + Формат(НомерСтроки, "ЧГ=0");
			ИмяСлужебногоПоля = "П000010000122_" + Формат(НомерСтроки, "ЧГ=0");
			
			НомСтрокиТабличногоДокумента = мРаздел1.Области.Найти(ИмяПоляЕдИзм).Низ;
			
			мРаздел1.Область("R" + НомСтрокиТабличногоДокумента + "C82").Имя = ИмяСлужебногоПоля;
			мРаздел1.Область(ИмяСлужебногоПоля).Значение = мРаздел1.Область(ИмяПоляЕдИзм).Значение;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		//преобразуем таблицу значений многострочного раздела 1 (добавилась колонка 22)
		ОбновленнаяТЗ = мСтруктураМногострочныхРазделов[мИдГруппы1].Скопировать();
		ОбновленнаяТЗ.Очистить();
		
		ОписаниеТиповСтрока3   = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(3);
		
		ОбновленнаяТЗ.Колонки.Добавить("П000010000122", ОписаниеТиповСтрока3);
		
		Для каждого СтрокаТЗ Из мСтруктураМногострочныхРазделов[мИдГруппы1] Цикл 
			ЗаполнитьЗначенияСвойств(ОбновленнаяТЗ.Добавить(), СтрокаТЗ) 
		КонецЦикла; 
		
		мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, ОбновленнаяТЗ);
		
		ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы1);
		Модифицированность = Истина;
		
	КонецЕсли;
	
	СписокСохранения.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки);
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
	Иначе
		// В предыдущих редакция формы эти реквизиты могут отсутствовать.
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("НаимДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("НаимДовПодп2", "");
		КонецЕсли;
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("НомерДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("НомерДовПодп2", "");
		КонецЕсли;
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("ДатаДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("ДатаДовПодп2", "");
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ СписокСохранения.Свойство("ПечатьБезШтрихкодаРазрешена", мПечатьБезШтрихкодаРазрешена) Тогда
		мПечатьБезШтрихкодаРазрешена = Ложь;
	КонецЕсли;
	
	СписокСохранения.Свойство("ДополнительныеДанныеРазделов", ДополнительныеДанныеРазделов);
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);
	
	мДеревоВыбранныхСтраниц = мДеревоСтраницОтчета.Скопировать();
	
	УстановитьФорматВывода();
	
	Модифицированность = Ложь;
	
	мДокументВосстановлен = Истина;
	
КонецПроцедуры // ВосстановитьСохраненныеДанные()

// Процедура выводит строки многострочного раздела в табличный документ.
// При отсутствии в табличном документе необходимого количества строк
// добавляет отсутствующие строки.
//
// Параметры:
//  ИмяГруппы       - строка, имя многострочного раздела;
//  СтруктураГруппы - таблица значений, содержащая значения показателей многострочного раздела.
//
Процедура ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы) Экспорт
	
	РегламентированнаяОтчетность.роВывестиРазделВТабличныйДокумент(ЭтаФорма, ИмяГруппы, СтруктураГруппы);
	
	//Перем ИсходноеКоличествоСтрокГруппы;

	//// определим исходное колво строк многострочного раздела,
	//// соответствующее бумажной форме отчета
	//мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

	//// реальное кол-во строк (колво заполненных строк)
	//ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();

	//ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);

	//// При восстановлении сохраненных данных отчета сравниваем исходное кол-во
	//// отображаемых в форме строк многострочного раздела с реальным количеством
	//// созданных строк в сохраненном отчете.
	//Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда

	//	// При превышении реального кол-ва строк над исходным кол-вом (по шаблону),
	//	// автоматически добавляем необходимое колво строк.
	//	Для НомНовойСтроки = ИсходноеКоличествоСтрокГруппы + 1 По ТекущееКоличествоСтрокГруппы Цикл

	//		НомИсходнойСтроки = НомНовойСтроки - 1;

	//		// определяем имя первой ячейки последней из исходных строк
	//		ИмяПервойЯчейкиПоследнейСтроки = СтруктураГруппы.Колонки[0].Имя + "_" + Формат(НомИсходнойСтроки, "ЧГ=0");

	//		// область первой ячейки последней из исходных строк
	//		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];

	//		// номера строк исходной области
	//		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	//		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;

	//		СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;

	//		// номера строк вставляемой области
	//		НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	//		НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;

	//		// определяем вставляемую область
	//		ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );

	//		// добавляем в таблицу новую строку
	//		ТекТабличноеПоле.ВставитьОбласть( ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали );

	//		// переопределяем имена вставленной области
	//		Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл

	//			ИмяКолонки          = Колонка.Имя;

	//			ИмяОбластиЯчейки    = ИмяКолонки + "_" + Формат(НомИсходнойСтроки, "ЧГ=0");
	//			ИмяНовОбластиЯчейки = ИмяКолонки + "_" + Формат(НомНовойСтроки, "ЧГ=0");

	//			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
	//			ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;

	//			// определяем границы ячейки исходной области
	//			НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
	//			НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
	//			НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
	//			НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;

	//			// Назначаем имя ячейки исходной области,
	//			// (т.к. в результате вставки новой области оно было очищено)
	//			ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
	//			ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;

	//			// очищаем значения ячеек вставленной области
	//			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
	//				ОбластьЯчейки.Очистить();
	//			Иначе
	//				// порядковый номер последней строки перед вставкой
	//				ИсходныйНомер        = Число("0"+ОбластьЯчейки.Текст);
	//				НовыйПорядковыйНомер = ИсходныйНомер + 1;

	//				ОбластьЯчейки.Текст  = Строка(НовыйПорядковыйНомер);
	//			КонецЕсли;

	//		КонецЦикла;
	//	КонецЦикла;
	//КонецЕсли;

	//// выводим значения строк
	//Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
	//	Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл

	//		НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
	//		НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
	//		ИмяЯчейки                = КолонкаТаблицы.Имя + "_" + Формат(НомерСтрокиВФормеТаблицы, "ЧГ=0");
	//		ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
	//		ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицы.Имя];

	//		Если ОбластьЯчейкиТаблицы.СодержитЗначение = Истина Тогда
	//			// задаем значение ячейки
	//			ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;

	//		Иначе
	//			// устанавливаем порядковый номер строки
	//			ОбластьЯчейкиТаблицы.Текст    = ЗначениеЯчейки;
	//		КонецЕсли;

	//	КонецЦикла;
	//КонецЦикла;

КонецПроцедуры // ВывестиРазделВТабличныйДокумент()

// Функция определяет поле табличного документа формы, содержащее 
// многострочный раздел.по имени ИдГруппы.
//
// Параметры:
//  ИдГруппы - строка, имя многострочного раздела.
//
// Возвращаемое значение:
//  Поле табличного документа.
//
Функция ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы) Экспорт
	Перем ТекТабличноеПоле;
	
	Если ИдГруппы = мИдГруппы1 Тогда
		ТекТабличноеПоле = мРаздел1;
	ИначеЕсли ИдГруппы = мИдГруппы2 Тогда
		ТекТабличноеПоле = мСведения;
	ИначеЕсли ИдГруппы = мИдГруппы3 Тогда
		ТекТабличноеПоле = мЗачет;
	КонецЕсли;

	Возврат ТекТабличноеПоле;

КонецФункции // ОпределитьТабличноеПолеПоИдМногострочногоРаздела()

// Выполняет расчет вычисляемых показателей на заданной странице отчета
// (ячеек, выделенных зеленым цветом). 
// В случае неуказания листа, ничего происходить не будет.
//
// Параметры:
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы.
//
Процедура Расчет(ИмяТекущейСтраницыПанели = "") Экспорт
	
	Если ФлажокОтклАвтоРасчет Или мРежимПечати Тогда
		Возврат; // отключен режим АвтоЗаполнения
	КонецЕсли;
	
	Группа1 = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, Группа1);
	
	СуммаПоКолонке14 = 0;
	СуммаПоКолонке15 = 0;
	СуммаПоКолонке19 = 0;
	СуммаПоКолонке20 = 0;
	
	РассчитыватьИтог14 = Истина;
	ПроверитьАдвалорные = (НЕ Группа1.Количество() = 1);
	
	Если ПроверитьАдвалорные Тогда
		ВсеСтавкиАдвалорные = Истина;
		Для Инд = 0 По Группа1.Количество() - 1 Цикл
			СуффиксНомера = "_" + Формат(Инд + 1, "ЧГ=0");
			
			НБАкцизов = мРаздел1.Области["П000010000114" + СуффиксНомера].Значение;
			Если НБАкцизов = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтАкцизТверд = СтрЗаменить(мРаздел1.Области["П000010000116" + СуффиксНомера].Значение, ",", ".");
			ЗаполненаСтАкцизТверд = (СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ (ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)) И Число(СтАкцизТверд) <> 0));
			
			Если ЗаполненаСтАкцизТверд Тогда
				РассчитыватьИтог14 = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Инд = 0 По Группа1.Количество() - 1 Цикл
		Строка = "_" + Формат(Инд + 1, "ЧГ=0");
		
		П000010000114 = мРаздел1.Области["П000010000114" + Строка].Значение; 
		СуммаПоКолонке14 = СуммаПоКолонке14 + П000010000114;
		
		П000010000115 = мРаздел1.Области["П000010000115" + Строка].Значение;
		СуммаПоКолонке15 = СуммаПоКолонке15 + П000010000115;
		
		П000010000119 = мРаздел1.Области["П000010000119" + Строка].Значение; 
		СуммаПоКолонке19 = СуммаПоКолонке19 + П000010000119;
		
		П000010000120 = мРаздел1.Области["П000010000120" + Строка].Значение;
		СуммаПоКолонке20 = СуммаПоКолонке20 + П000010000120;
		
	КонецЦикла;
	
	мРаздел1.Области.П000010000114.Значение = ?(РассчитыватьИтог14, СуммаПоКолонке14, 0);
	мРаздел1.Области.П000010000115.Значение = СуммаПоКолонке15;
	мРаздел1.Области.П000010000119.Значение = Окр(СуммаПоКолонке19);
	мРаздел1.Области.П000010000120.Значение = Окр(СуммаПоКолонке20);
	
КонецПроцедуры // Расчет()

// НачальныеОперацииСМногострочнымиБлоками
//
Процедура НачальныеОперацииСМногострочнымиБлоками()
	Перем ИсходноеКоличествоСтрокГруппы;

	// создаем исходную структуру многострочных разделов
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭлементСтруктуры.Значение;

		ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);

		мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

		Для НСтр = 1 По  ИсходноеКоличествоСтрокГруппы Цикл

			СтрокаГруппы = СтруктураГруппы.Добавить();

			НомерСтроки = Формат(НСтр, "ЧГ=0");
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл

				ИмяКолонки    = Колонка.Имя;
				КодПоказателя = ИмяКолонки + "_" + НомерСтроки;
				
				ОбластьЯчейки = ТекТабличноеПоле.Области[КодПоказателя];

				Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
					ЗначениеЯчейки = ОбластьЯчейки.Значение;

					МаскаДляКолонки = ВернутьМаскуДляКолонки(ИмяКолонки);
					Если МаскаДляКолонки <> Неопределено Тогда
						ОбластьЯчейки.ЭлементУправления.Маска = МаскаДляКолонки;
					КонецЕсли;

				Иначе
					ЗначениеЯчейки = ОбластьЯчейки.Текст;
				КонецЕсли;

				СтрокаГруппы[ИмяКолонки] = ЗначениеЯчейки;

			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры // НачальныеОперацииСМногострочнымиБлоками

// ВернутьМаскуДляКолонки
//
Функция ВернутьМаскуДляКолонки(ИмяКолонки) // ВернутьМаскуДляКолонки
	Возврат мМаскиЯчейки.Получить(ИмяКолонки);
КонецФункции // ВернутьМаскуДляКолонки

// Выполняет начальные настройки параметров отчета.
// По данным макета "СоствПоказателей" формирует:
//   - состав показателей отчета;
//   - варианты заполнения показателей отчета;
//   - дерево выводимых на печать листов отчета.
//
// Процедура используется только в механизме Контрольных соотношений. В данном отчете
// процедура не используется.
// По сравнению с аналогами в других отчетах, эта процедура только формирует таблицу
// состава показателей. мТаблицаСоставПоказателей.
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт

	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(мСоставПоказателей);

	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);
	ОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл

		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// признак многострочности определяется по третьей колонке макета
			ПризнМногострочность = СокрЛП(МакетСоставаПоказателей.Область(Ном, 3).Текст);
			// по четвертой колонке определяется тип данных реквизита
			ТипДанныхРеквизита   = СокрЛП(МакетСоставаПоказателей.Область(Ном, 4).Текст);
			// по колонке 5 определяется исходное количество строк многострочного раздела
			стрКолСтрокИсходный  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 5).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);

			// КодПоказателя по структуре , под 9-й колонке
			КодПоказателяПоСтруктуре = СокрЛП(МакетСоставаПоказателей.Область(Ном, 9).Текст);

			чКолСтрокИсходный    = ? (ПустаяСтрока(стрКолСтрокИсходный),  0, Число(стрКолСтрокИсходный));
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));

			// формируем таблицу значений, содержащей состав показателей отчета
			НоваяСтрока = мТаблицаСоставПоказателей.Добавить();
			НоваяСтрока.ИмяПоляТаблДокумента   = ИмяСтраницы;
			НоваяСтрока.КодПоказателяПоСоставу = КодПоказателя;
			НоваяСтрока.КодПоказателяПоФорме   = КодПоказателяПоФорме;
			НоваяСтрока.ПризнМногострочности   = ПризнМногострочность;
			НоваяСтрока.ТипДанныхПоказателя    = ТипДанныхРеквизита;
			НоваяСтрока.КодПоказателяПоСтруктуре    = КодПоказателяПоСтруктуре;

		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Процедура активизирует область поля табличного документа формы,
// соответствующей заданному показателю. При необходимости,
// процедура устанавливает в качестве активной ту страницу раздела, на которой
// находится показатель.
//
// Параметры:
//	Ячейка - СтрокаТаблицыЗначений с "координатами" показателя
//
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	РегламентированнаяОтчетность.роАктивизироватьЯчейку(ЭтаФорма, Ячейка);
	
КонецПроцедуры

Процедура КоманднаяПанельДействийПоказатьБланк(Кнопка)
	
	Печать(Кнопка.Имя);
	
КонецПроцедуры

// Инициализация
//
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	мСтараяВерсияФормы = мВерсияФормы;
	
	мБезОткрытияФормы = БезОткрытияФормы;
	
	ФормироватьСтруктуруСтраницОтчета();
	
	ФормироватьСтруктуруСвойствСтраниц();
	
	мРаздел1    = ЭлементыФормы.ПолеТабличногоДокументаРаздел1;
	мРаздел2    = ЭлементыФормы.ПолеТабличногоДокументаРаздел2;
	мРаздел3    = ЭлементыФормы.ПолеТабличногоДокументаРаздел3;
	мПриложение = ЭлементыФормы.ПолеТабличногоДокументаПриложение;
	мСведения   = ЭлементыФормы.ПолеТабличногоДокументаСведения;
	мЗачет      = ЭлементыФормы.ПолеТабличногоДокументаЗачет;
	
	ВидДокумента = 0;
	ЭлементыФормы.НомерКорректировки.Доступность = (ВидДокумента = 1);
	НомерКорректировки = 1;
	
	мАвтоВыборКодов = Ложь;
	мВПрограммеИзмененаОрганизация = Ложь;
	мДокументВосстановлен = Ложь;
	
	// Для обновления листа о ФЛ, если Истина, то обновлять перезаписывать данные на листе.
	мВПрограммеИзмененаОрганизация = Ложь;
	мРежимПечати = Ложь;
	Если НЕ БезОткрытияФормы Тогда
		СформироватьСпискиВыбора();
		РегламентированнаяОтчетность.СформироватьСоставКнопкиОчиститьДляРегОтчетов(ЭтаФорма);
	КонецЕсли;
	
	Если мСохраненныйДок = Неопределено Тогда
		
		НачальныеОперацииСМногострочнымиБлоками();
		
		Если мСкопированаФорма <> Неопределено Тогда
			// Документ скопирован.
			ВосстановитьСохраненныеДанные();
		Иначе
			// Это новый документ.
			// Установим реквизиты формы по умолчанию.
			ЕдиницаИзмерения         = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			ТочностьЕдиницыИзмерения = 2;
			
			мДеревоВыбранныхСтраниц = мДеревоСтраницОтчета.Скопировать();
			
			ОбновитьТЗСпецификаций();
			
			УстановитьФорматВывода();
			
			мОбновитьКомментарий = Истина;
			
		КонецЕсли;
		
	Иначе
		Если НЕ БезОткрытияФормы Тогда
			// Установим маски, чтобы при воостановлении не устанавливать, а копировать с уже установленных масок на строках.
			НачальныеОперацииСМногострочнымиБлоками();
		КонецЕсли;
		
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();
		
	КонецЕсли;
	
	ПоказатьПериод();
	
	Если мСохраненныйДок = Неопределено Или мВПрограммеИзмененаОрганизация Тогда
		ЗаполнитьСведенияОбОрганизации();
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
	КонецЕсли;
	
	Для Каждого Страница Из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		УстановитьЦветФонаЯчейки( , , Страница.Имя);
	КонецЦикла;
	
	РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, "Раздел1");
	
	Если НЕ БезОткрытияФормы Тогда
		РегламентированнаяОтчетность.ПрорисоватьПодменюПечать(ЭтаФорма, Новый Действие("КоманднаяПанельДействийПоказатьБланк"), , Истина);
	КонецЕсли;
	
	РегламентированнаяОтчетность.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры // Инициализация

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ АВТОЗАПОЛНЕНИЯ

Процедура МеханизмыОчисткиРегламентированныхОтчетов(Кнопка) Экспорт
    Перем ТаблицаСтраницРаздела;
	
	Если Кнопка = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КнопкаИмя = Кнопка.Имя;
	Если КнопкаИмя = "ОчиститьВесьОтчет" Тогда
		Очистить();
		Возврат;
	КонецЕсли;

	ОчисткаПроизошла = Ложь;
	ИмяСтраницы = СтрЗаменить(КнопкаИмя, "ОчиститьТекущийЭкземплярМногостраничногоРаздела", "");
	ИмяСтраницы = СтрЗаменить(ИмяСтраницы, "ОчиститьТекущийРаздел", "");
	ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяСтраницы, "ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста", Ложь);
	Многостраничность = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяСтраницы, "МногостраничностьВРазделе", Ложь);
	УдалятьсяВсеДопЭкземпляры = Ложь;
	ПредставлениеРазделаОтчета = НРег(ПолучитьПредставлениеРазделаРегОтчета(ИмяСтраницы));
	СведенияОФЛ = (Найти(НРег(ПредставлениеРазделаОтчета), "физ") > 0) И (Найти(НРег(ПредставлениеРазделаОтчета), "лиц") > 0);

	ОчищенИлиОчищено = "очищено"; // Очищено заявление ...

	Если Найти(КнопкаИмя, "ОчиститьТекущийРаздел") > 0 Тогда
		// Очистить текущий раздел. Может быть многостраничный.
		Если Многостраничность Тогда
			КолВоСтраницВРазделе = мСтруктураМногостраничныхРазделов[ИмяСтраницы].Количество();
			Если КолВоСтраницВРазделе > 1 Тогда
				ТекстВопроса = "Внимание! Будут удалены все доп. листы текущего раздела (листа)." + Символы.ПС
				  + "Продолжить операцию?";
			Иначе
				Если СведенияОФЛ Тогда 
					// Очищаем лист со сведениями о физических лицах.
					ТекстВопроса = "Внимание! Будут очищены сведения о физическом лице." + Символы.ПС
					  + "Продолжить операцию?";
				Иначе
					ТекстВопроса = "Внимание! Будет " + ОчищенИлиОчищено + " " + ПредставлениеРазделаОтчета + "." + Символы.ПС
					  + "Продолжить операцию?";
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекстВопроса = "Внимание! Будет " + ОчищенИлиОчищено + " " + ПредставлениеРазделаОтчета + "." + Символы.ПС
			  + "Продолжить операцию?";
		КонецЕсли;

		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОчисткаПроизошла = Истина;
			Если (Многостраничность) И (КолВоСтраницВРазделе > 1) Тогда
				РегламентированнаяОтчетность.ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(ЭтаФорма, ИмяСтраницы);
				УдалятьсяВсеДопЭкземпляры = Истина;  				
			Иначе
				ОчиститьТабличноеПоле(ЭлементыФормы["ПолеТабличногоДокумента" + ИмяСтраницы]);
			КонецЕсли;
			Элемент = СоздатьТЗСпецификаций();
			Если ИмяСтраницы = "Раздел1" Тогда
	            мТЗСпецификацийРаздел1.Очистить();  		
				мТЗСпецификацийРаздел1 = СоздатьТЗСпецификаций();
				мТЗСпецификацийРаздел1_07.Очистить();
				мТЗСпецификацийРаздел1_07 = СоздатьТЗСпецификаций();
			ИначеЕсли ИмяСтраницы = "Раздел3" Тогда   	
				мТЗСпецификацийРаздел3.Очистить();
				мТЗСпецификацийРаздел3 = СоздатьТЗСпецификаций();
			ИначеЕсли ИмяСтраницы = "Приложение" Тогда
				мМассивТЗСпецификацийПрил.Очистить();  		
				мМассивТЗСпецификацийПрил.Добавить(Элемент);				
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Найти(КнопкаИмя, "ОчиститьТекущийЭкземплярМногостраничногоРаздела") > 0 Тогда
		
		Ответ = Вопрос("Внимание! Будет очищен текущий лист." + Символы.ПС
		      + "Продолжить операцию?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			  
			  Если Ответ = КодВозвратаДиалога.Да Тогда
				  ОчиститьТабличноеПоле(ЭлементыФормы["ПолеТабличногоДокумента" + ИмяСтраницы]);
				  
				  Если ИмяСтраницы = "Приложение" Тогда
					  // получаем номер текущей доп страницы
					  Если Не мСтруктураМногостраничныхРазделов.Свойство(Имястраницы, ТаблицаСтраницРаздела) Тогда
						  Возврат;
					  КонецЕсли;
					  
					  СтрТаблицаСтраницы = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
					  
					  Если СтрТаблицаСтраницы = Неопределено Тогда
						  РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, ИмяСтраницы);
						  Возврат;
					  КонецЕсли;	
					  НомерТекущейСтраницы   = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы);  
					  
					  мМассивТЗСпецификацийПрил.Удалить(НомерТекущейСтраницы);
					  
					  Элемент = СоздатьТЗСпецификаций();
					  
					  мМассивТЗСпецификацийПрил.Вставить(НомерТекущейСтраницы, Элемент);
				  КонецЕсли;
				  ОчисткаПроизошла = Истина;
			  КонецЕсли;

	КонецЕсли;

	Если ОчисткаПроизошла Тогда
		Модифицированность = Истина;
		Расчет("");

		Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяСтраницы, "РазделЯвляетсяАвтозаполняемым") Тогда
			УстановитьЦветФонаЯчейки(,, ИмяСтраницы);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // МеханизмыОчисткиРегламентированныхОтчетов

// ПолучитьПредставлениеРазделаРегОтчета
// Функция получает из мДеревоСтраницОтчета представление раздела отчета
//
// Параметры:
//  Форма          - ссылка на форму отчета
//  ИмяСтраницы    - название страницы отчета
// Возвращаемое значение:
//  Представление раздела, строка.
//
Функция ПолучитьПредставлениеРазделаРегОтчета(ИмяСтраницы)
	Для Каждого СтрокаУровня1 Из мДеревоСтраницОтчета.Строки Цикл
		Если ИмяСтраницы = СтрокаУровня1.ИмяСтраницы Тогда
			Попытка
				Результат = СтрокаУровня1.Представление;
			Исключение
				Результат = ИмяСтраницы;
			КонецПопытки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции // ПолучитьПредставлениеРазделаРегОтчета

// Процедура вызывается нажатием кнопки "ОК" командной панели действий формы.
// Выполняет сохранение данных отчета и закрывает текущую форму.
//
Процедура КоманднаяПанельДействийСохранитьИЗакрыть(Кнопка)

	Если Сохранить() Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельДействийСохранитьИЗакрыть()

// Процедура вызывается нажатием кнопки "Записать" командной панели действий формы.
// Выполняет сохранение данных отчета.
//
Процедура КоманднаяПанельДействийЗаписать(Кнопка)

	Сохранить();

КонецПроцедуры // КоманднаяПанельДействийЗаписать()

// ПолеВыбораВидДокументаОчистка
// Процедура-обработчик события "Очистка"
//
Процедура ПолеВыбораВидДокументаОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ПолеВыбораВидДокументаОчистка

// ПолеВыбораВидДокументаПриИзменении
// Процедура-обработчик события "ПриИзменении"
//
Процедура ПолеВыбораВидДокументаПриИзменении(Элемент)

	РегламентированнаяОтчетность.роПриИзмененииВидаДокументаИКорректировки(ЭтаФорма);

КонецПроцедуры // ПолеВыбораВидДокументаПриИзменении

// Процедура - обработчик события при смене текущей страницы основной
// панели формы.
//
Процедура ОсновнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница) Экспорт
	Перем ТекущаяОбласть;

	РегламентированнаяОтчетность.СформироватьСоставКнопкиОчиститьДляРегОтчетов(ЭтаФорма);

	ИмяТекущейСтраницыПанели = ЭлементыФормы.ОсновнаяПанель.Страницы[ТекущаяСтраница].Имя;

	КолвоМнгБлоков = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "МногострочностьВРазделе");
	Если ТипЗнч(КолвоМнгБлоков) = Тип("ТаблицаЗначений") Тогда
		КолвоМнгБлоков = КолвоМнгБлоков.Количество();
	КонецЕсли;

	Если КолвоМнгБлоков > 0 Тогда
		ИдГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтранице();
		Если ИдГруппы <> Неопределено Тогда
			ТекТабличноеПоле = мРаздел1;
			ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
		КонецЕсли;
		// управляем доступностью кнопок "Добавить" и "Удалить"
		РегламентированнаяОтчетность.роДоступностьКнопокДобавитьУдалить(ЭтаФорма, ТекущаяОбласть, ИмяТекущейСтраницыПанели);
	КонецЕсли;

	Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "МногостраничностьВРазделе") Тогда

		// управляем показом статика, отображающего номер текущей страницы
		РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели);
	КонецЕсли;

	Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "РазделЯвляетсяАвтозаполняемым") Тогда
		// управляем доступностью и выводом значений в поля отображения варианта заполнения ячеек
		ТекущаяОбласть = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
		РегламентированнаяОтчетность.роВывестиЭлементыВариантаЗаполнения(ЭтаФорма, ТекущаяОбласть);
	КонецЕсли;

КонецПроцедуры // ОсновнаяПанельПриСменеСтраницы()

// КнопкаПредыдущаяСтраницаНажатие
// единая процедура во всех разделах
//
Процедура КнопкаПредыдущаяСтраницаСведенияНажатие(Элемент)

	ПоказатьСтраницуМногостраничногоРаздела(-1, "Сведения");

КонецПроцедуры // КнопкаПредыдущаяСтраницаНажатие

// КнопкаСледующаяСтраницаНажатие
// единая процедура для всех кнопок на разных разделах
//
Процедура КнопкаСледующаяСтраницаСведенияНажатие(Элемент)

	ПоказатьСтраницуМногостраничногоРаздела(1, "Сведения");

КонецПроцедуры // КнопкаСледующаяСтраницаНажатие

// КнопкаДобавитьДопСтраницуНажатие
// единая процедура добавить страницу в раздел 
//
Процедура КнопкаДобавитьДопСтраницуСведенияНажатие(Элемент)

	ВставитьДополнительнуюСтраницу("Сведения");

КонецПроцедуры // КнопкаДобавитьДопСтраницуНажатие

// Процедура КнопкаУдалитьДопСтраницуНажатие(Элемент)
// единая кнопка на всех разделах УдалитьДопСтраницуНажатие
//
Процедура КнопкаУдалитьДопСтраницуСведенияНажатие(Элемент)

	УдалитьДополнительнуюСтраницу("Сведения");

КонецПроцедуры // КнопкаУдалитьДопСтраницуНажатие

// Процедура КнопкаПерейтиНаСтраницу(Элемент)
// единая процедура перехода на страницы
//
Процедура КнопкаПерейтиНаСтраницуСведенияНажатие(Элемент)

	РегламентированнаяОтчетность.роОткрытьФормуСпискаДопСтраниц(ЭтаФорма, "Сведения");

КонецПроцедуры //  Процедура КнопкаПерейтиНаСтраницу(Элемент)

// Процедура - обработчик события при изменении значения единицы измерения.
//
Процедура ПолеВыбораЕдиницаИзмеренияПриИзменении(Элемент)

	ПроверитьТочность();
	УстановитьФорматВывода();
	// Итоговые поля надо пересчитывать, потому что при расчете они округлются
	// в соответствии с установленной единицей изменения и точностью.
	Расчет("Раздел1");
	Модифицированность = Истина;

КонецПроцедуры // ПолеВыбораЕдиницаИзмеренияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода точности
// единицы измерения.
//
Процедура ПолеВводаТочностьЕдиницыИзмеренияПриИзменении(Элемент)

	ПроверитьТочность();
	УстановитьФорматВывода();

	// Итоговые поля надо пересчитывать, потому что при расчете они округлются
	// в соответствии с установленной единицей изменения и точностью.
	Расчет("Раздел1");

	Модифицированность = Истина;

КонецПроцедуры // ПолеВводаТочностьЕдиницыИзмеренияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода даты
// представления отчета.
//
Процедура ПолеВводаДатаСдачиПриИзменении(Элемент)

	// Переменна изменяется, для простановки дат на всех листах многостраничных разделов

	ЗаполнитьДатуВЯчейках();

	// Заполнить ТОЛЬКО параметры по текущей организации зависящие от даты подписания документа
	ЗаполнитьСведенияОбОрганизации(Истина);

	// Возведем признак модифицированности
	Модифицированность = Истина;

КонецПроцедуры // ПолеВводаДатаСдачиПриИзменении()

Процедура ФормироватьСтруктуруСтраницОтчета() 

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	// Дерево содержит коллекцию страниц отчета.
	// В колонках дерева хранятся следующие данные:
	//    - представление листа;
	//    - имя страницы основной панели формы, содержащей выводимый на печать лист;
	//    - имя поля табличного документа;
	//    - имя области ячеек табличного документа (используется в случае вывода
	//      на печать отдельных страниц титульного листа);
	//    - ориентация страницы при печати;
	//    - число, метка вывода страницы на печать;
	//    - число, метка показа страницы на основной панели формы;
	//    - число, метка выгрузки данных страницы в ИМНС.
	//
	мДеревоСтраницОтчета               = Новый ДеревоЗначений;
	мДеревоСтраницОтчета.Колонки.Добавить("Представление",            ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяСтраницы",              ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяПоляТабличногоДокум",   ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяОбластиТабличногоПоля", ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ОриентацияСтраницы");
	мДеревоСтраницОтчета.Колонки.Добавить("ВыводНаПечать",            ОписаниеТиповЧисло1);
	мДеревоСтраницОтчета.Колонки.Добавить("ПоказатьСтраницу",         ОписаниеТиповЧисло1);
	мДеревоСтраницОтчета.Колонки.Добавить("ВыгрузитьСтраницу",        ОписаниеТиповЧисло1);

	мДеревоСтраницОтчета.Строки.Очистить();

	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел1";
	СтрокаУровня1.Представление            = "Раздел1";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаРаздел1";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Ландшафт;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел2";
	СтрокаУровня1.Представление            = "Раздел2";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаРаздел2";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Ландшафт;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел3";
	СтрокаУровня1.Представление            = "Раздел3";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаРаздел3";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Ландшафт;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;

    СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Приложение";
	СтрокаУровня1.Представление            = "Приложение";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаПриложение";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Ландшафт;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;

	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Сведения";
	СтрокаУровня1.Представление            = "Сведения о выписках банка";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаСведения";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Портрет;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Зачет";
	СтрокаУровня1.Представление            = "Сведения о зачетах";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаЗачет";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Портрет;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;

КонецПроцедуры // ФормироватьСтруктуруСтраницОтчета()

// Обновляет структуру многострочнго раздела, заданного
// уникальным идентификатором. Заполняет таблицу значений
// многострочного раздела значениями соответствующих ячеек.
//
// Параметры:
//  ИмяГруппы - строка - идентификатор многострочного раздела.
//
Процедура ОбновитьСтруктуруМногострочногоРаздела(ИмяГруппы) Экспорт
	РегламентированнаяОтчетность.роОбновитьСтруктуруМногострочногоРаздела(ЭтаФорма, ИмяГруппы);
КонецПроцедуры // ОбновитьСтруктуруМногострочногоРаздела()

// ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти
//
Процедура ОбработкаИзмененияСодержимогоОбласти(Элемент, Область)
	
	Модифицированность = Истина;
	
	ИмяОбласти = Область.Имя;
	
	Страница = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница.Имя;
	
	Если Страница = "Раздел1" Тогда
		
		Если Найти(ИмяОбласти, "П000010000104") > 0  И мАвтоВыборКодов Тогда
			НомерСтроки = Сред(ИмяОбласти, 15);
			ИмяСлужебногоПоля = "П000010000122_" + НомерСтроки;
			Если Элемент.Области.Найти(ИмяСлужебногоПоля) <> Неопределено Тогда
				Элемент.Области[ИмяСлужебногоПоля].Значение = ""; //очищаем служебное поле кода единицы измерения
				Сообщить("При вводе единицы измерения вручную в сформированный файл выгрузки попадет введенное значение!");
			КонецЕсли;
		КонецЕсли;
		
		Если Найти(ИмяОбласти, "П000010000123") = 1  И мАвтоВыборКодов Тогда
			НомерСтроки = Сред(ИмяОбласти, 15);
			ИмяСлужебногоПоля = "П000010000124_" + НомерСтроки;
			Если Элемент.Области.Найти(ИмяСлужебногоПоля) <> Неопределено Тогда
				Элемент.Области[ИмяСлужебногоПоля].Значение = ""; //очищаем служебное поле кода единицы измерения
				Сообщить("При вводе единицы измерения дополнительной величины вручную в сформированный файл выгрузки попадет введенное значение!");
			КонецЕсли;
		КонецЕсли;
		
		Расчет(Страница);
		
	КонецЕсли;
	
КонецПроцедуры // ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти

// КнопкаДобавитьДопСтрокиРаздел1Нажатие
//
Процедура КнопкаДобавитьДопСтрокиРаздел1Нажатие(Элемент)

	РегламентированнаяОтчетность.роВставитьДополнительнуюСтроку(ЭтаФорма, , Истина);

КонецПроцедуры

// КнопкаУдалитьДопСтрокиРаздел1Нажатие
//
Процедура КнопкаУдалитьДопСтрокиРаздел1Нажатие(Элемент)

	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтроку(ЭтаФорма, , Истина);
	Расчет();

КонецПроцедуры // КнопкаУдалитьДопСтрокиРаздел1Нажатие

Процедура КнопкаДобавитьДопСтрокиСведенияНажатие(Элемент)
	
	РегламентированнаяОтчетность.роВставитьДополнительнуюСтроку(ЭтаФорма, , Истина);
	//ВставитьДополнительнуюСтроку();
	
КонецПроцедуры

Процедура КнопкаДобавитьДопСтрокиЗачетНажатие(Элемент)
	
	РегламентированнаяОтчетность.роВставитьДополнительнуюСтроку(ЭтаФорма, , Истина);
	//ВставитьДополнительнуюСтроку();
	
КонецПроцедуры

Процедура СпециальногоДобавленияСтроки(ТекТабличноеПоле, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти) Экспорт
	// определяем вставляемую область
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти,);
	Если ВставляемаяОбласть.Имя = "МногострочнаяЧасть" Тогда
		ВосстановитьИмяОбласти = Истина;
		ВставляемаяОбласть.Имя = "";
	Иначе
		ВосстановитьИмяОбласти = Ложь;
	КонецЕсли;
	// добавляем в таблицу новую строку
	ТекТабличноеПоле.ВставитьОбласть(ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	Если ВосстановитьИмяОбласти Тогда
		ВставляемаяОбласть.Имя = "МногострочнаяЧасть";
	КонецЕсли;
КонецПроцедуры

Процедура СпециальногоУдаленияСтроки(ТекТабличноеПоле, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти) Экспорт
	
	// Определим область удаляемой строки
	УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
	
	Если УдаляемаяОбласть.Имя = "МногострочнаяЧасть" Тогда
		ВосстановитьИмяОбласти = Истина;
	Иначе
		ВосстановитьИмяОбласти = Ложь;
	КонецЕсли;
	
	// непосредственно удаляем область строки табличного документа
	ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
	
	Если ВосстановитьИмяОбласти Тогда
		ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, ).Имя = "МногострочнаяЧасть";
	КонецЕсли;
КонецПроцедуры

Процедура КнопкаУдалитьДопСтрокиСведенияНажатие(Элемент)
	
	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтроку(ЭтаФорма, Истина);
	
КонецПроцедуры

Процедура КнопкаУдалитьДопСтрокиЗачетНажатие(Элемент)
	
	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтроку(ЭтаФорма, Истина);
	
КонецПроцедуры

// ФлажокОтклАвтоРасчетПриИзменении
//
Процедура ФлажокОтклАвтоРасчетПриИзменении(Элемент)

	СнятьЗащитуСЯчеек(НЕ ФлажокОтклАвтоРасчет);

КонецПроцедуры // ФлажокОтклАвтоРасчетПриИзменении

// СнятьЗащитуСЯчеек
// Снимает защиту с защищаемых ячеек
// Параметры:
// ПоставитьЗащиту - Истина - ставит защиту, Ложь - убрать
//
Процедура СнятьЗащитуСЯчеек(ПоставитьЗащиту)

	Группа1 = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, Группа1);

	ЦветФонаРазБлокировки = Неопределено;
	мСтруктураЦветовРаскраски.Свойство("_0", ЦветФонаРазБлокировки);

	ЦветФонаБлокировки = Неопределено;
	мСтруктураЦветовРаскраски.Свойство("_4", ЦветФонаБлокировки);

	Цвет          = ?(ПоставитьЗащиту, ЦветФонаБлокировки, ЦветФонаРазБлокировки);

	мРаздел1.Области.П000010000114.Защита = ПоставитьЗащиту;
	мРаздел1.Области.П000010000114.ЦветФона = Цвет;

	мРаздел1.Области.П000010000115.Защита = ПоставитьЗащиту;
	мРаздел1.Области.П000010000115.ЦветФона = Цвет;

	мРаздел1.Области.П000010000119.Защита = ПоставитьЗащиту;
	мРаздел1.Области.П000010000119.ЦветФона = Цвет;

	мРаздел1.Области.П000010000120.Защита = ПоставитьЗащиту;
	мРаздел1.Области.П000010000120.ЦветФона = Цвет;

КонецПроцедуры // СнятьЗащитуСЯчеек

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА ВЫГРУЗКИ В XML
//
Процедура Выгрузить() Экспорт
	
	НовыйДок = Документы.ВыгрузкаРегламентированныхОтчетов.СоздатьДокумент();
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(мСохраненныйДок);
 	Если НовыйДок.ПолучитьФорму(, ЭтаФорма).СформироватьИЗаписать(СпДокОсн) Тогда
		// Сообщить("Отчет успешно выгружен!", СтатусСообщения.Информация);
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьПоказатели() Экспорт
	
	СформироватьСтруктуруПоказателей();
	Параметры = СформироватьСтруктуруПараметров();
	Если НЕ ПроверитьВозможностьВыгрузки(Параметры) Тогда
		Возврат Ложь;
	КонецЕсли;
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки, Параметры);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

Функция РазложитьФИО(Знач ФИОСтр)
	
	ФИОСтр = СокрЛП(ФИОСтр);
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ПервыйПробел = Найти(ФИОСтр, " ");
	Если ПервыйПробел = 0 Тогда
		ФИО.Фамилия = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Фамилия = СокрЛП(Лев(ФИОСтр, ПервыйПробел - 1));
	ФИОСтр = СокрЛП(Сред(ФИОСтр, ПервыйПробел + 1));
	
	ВторойПробел = Найти(ФИОСтр, " ");
	Если ВторойПробел = 0 Тогда
		ФИО.Имя = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Имя = СокрЛП(Лев(ФИОСтр, ВторойПробел - 1));
	
	ФИО.Отчество = СокрЛП(Сред(ФиоСтр, ВторойПробел + 1));
	
	Возврат ФИО;
	
КонецФункции

Функция ПолучитьКопиюСтруктуры(Знач СтруктураРезультата)
	Результат = Новый Структура;
	Для Каждого Стр Из СтруктураРезультата Цикл
		Результат.Вставить(Стр.Ключ, Стр.Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции // ПолучитьКопиюСтруктуры

Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина) Экспорт
	
	Отказ = Ложь;
	РегламентированнаяОтчетность.ПередВыгрузкойРегламентированногоОтчета(ЭтаФорма, Отказ, ПроверятьБлокировки);
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СформироватьСтруктуруПоказателей();
	Параметры = СформироватьСтруктуруПараметров();
	Если НЕ ПроверитьВозможностьВыгрузки(Параметры) Тогда
		Возврат Ложь;
	КонецЕсли;
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки, Параметры);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	ТекстВыгрузки = ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры);
	Если Открыта() Тогда
		СохранитьФайлВыгрузкиНаДиск(ТекстВыгрузки, Параметры);
	Иначе
		// Сообщить("" + РегламентированнаяОтчетность.ПредставлениеДокументаРеглОтч(мСохраненныйДок) + " успешно выгружен!");
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура СохранитьФайлВыгрузкиНаДиск(ТекстXML, Параметры)
	
	ПутьДляВыгрузки = РегламентированнаяОтчетность.ПолучитьПутьВыгрузки(); // получаем каталог для сохранения
	Если ПутьДляВыгрузки <> Ложь Тогда
		ФайлДляВыгрузки = ?(Прав(ПутьДляВыгрузки, 1) = "\", ПутьДляВыгрузки, ПутьДляВыгрузки + "\") + Параметры.ИдФайл + ".xml";
		ЗаписатьТесктНаДиск(ТекстXML, ФайлДляВыгрузки); // записываем XML в файл на диск
	КонецЕсли;
	
КонецПроцедуры

Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ПолучитьМакет("СтруктураXML505");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Функция СформироватьИдФайл(Параметры)
	
	ИдентОтпр = "";
	Если Параметры.Свойство("ИННФЛОтпр") И ЗначениеЗаполнено(Параметры.ИННФЛОтпр) Тогда
		ИдентОтпр = Параметры.ИННФЛОтпр;
	ИначеЕсли Параметры.Свойство("ИННЮЛОтпр") И ЗначениеЗаполнено(Параметры.ИННЮЛОтпр) Тогда
		ИдентОтпр = Параметры.ИННЮЛОтпр + Параметры.КППОтпр;
	КонецЕсли;
	
	Возврат Параметры.ПрефиксИмениФайлаВыгрузки
	            + "_" + Параметры.КодИФНС
				+ "_" + Параметры.КодИФНС
				+ "_" + ИдентОтпр
				+ "_" + Формат(РабочаяДата, "ДФ=ггггММдд")
				+ "_" + Строка(Новый УникальныйИдентификатор);
	
КонецФункции

Функция ПолучитьСведенияОПредставителе(КодИФНС)
	
	флПредставительЮрЛицо = Истина;
	НаименованиеОрганизацииПредставителя = "";
	ФИОПредставителя = "";
	ТипПредставителя = "1";
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	РегистрацияВИФНС.Код,
	|	РегистрацияВИФНС.КПП,
	|	РегистрацияВИФНС.Представитель,
	|	РегистрацияВИФНС.УполномоченноеЛицоПредставителя
	|ИЗ
	|	Справочник.РегистрацияВИФНС КАК РегистрацияВИФНС
	|ГДЕ
	|	РегистрацияВИФНС.Код = &Код
	|	И РегистрацияВИФНС.Владелец = &Владелец");
	Запрос.УстановитьПараметр("Код", КодИФНС);
	Запрос.УстановитьПараметр("Владелец", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Представитель) Тогда
		ТипПредставителя = "2";
		Если НЕ РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Выборка.Представитель) Тогда
			флПредставительЮрЛицо = Истина;
			ФИОПредставителя = СокрЛП(Выборка.УполномоченноеЛицоПредставителя);
			ПредставительСсылка = Выборка.Представитель;
		Иначе
			флПредставительЮрЛицо = Ложь;
			ПредставительСсылка = Выборка.Представитель;
			ФИОПредставителя = СокрЛП(ПредставительСсылка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка", 
							 ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка);
КонецФункции

Функция СформироватьСтруктуруПоказателей()
	
	мДанныеОтчета = Новый Структура;
	
	СохрСтруктура = мСохраненныйДок.ДанныеОтчета.Получить();
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		мДанныеОтчета.Вставить(Раздел.Ключ, РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;
	мСтруктураМногостраничныхРазделов = СохрСтруктура.ДанныеМногостраничныхРазделов;
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		ТаблицаЛистовРаздела = Раздел.Значение;
		Для Каждого Лист Из ТаблицаЛистовРаздела Цикл
			Лист.Данные = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Лист.Данные);
		КонецЦикла;
		мДанныеОтчета.Вставить(Раздел.Ключ, Раздел.Значение);
	КонецЦикла;
	
КонецФункции

Функция СформироватьСтруктуруПараметров()
	
	мСтруктураРеквизитовВыгрузки = Неопределено;
	ДанныеСохраненногоОтчета = мСохраненныйДок.ДанныеОтчета.Получить();
	Если НЕ ДанныеСохраненногоОтчета.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки) Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
	КонецЕсли;
	
	Параметры = Новый Структура;
	
	ЭтоПБОЮЛ = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Организация);
	
	Параметры.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	
	Параметры.Вставить("ВерсПрог", РегламентированнаяОтчетность.СформироватьВерсПрог());
	Параметры.Вставить("ДатаДок", Формат(РабочаяДата, "ДФ=дд.ММ.гггг"));
	Параметры.Вставить("ДатаДокДата", РабочаяДата); // Используется для проверки даты выгрузки документа
	
	Параметры.Вставить("КодИФНС", мСтруктураРеквизитовВыгрузки.КодИФНС);
	
	Параметры.Вставить("ПрОтпр", Строка(мСтруктураРеквизитовВыгрузки.ПрОтпр));
	
	Если мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ Тогда
		Параметры.Вставить("НаимОтпр",  мСтруктураРеквизитовВыгрузки.НаимОтпр);
		Параметры.Вставить("ИННЮЛОтпр", мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр);
		Параметры.Вставить("КППОтпр",   мСтруктураРеквизитовВыгрузки.КППОтпр);
	Иначе
		Параметры.Вставить("ИННФЛОтпр",      мСтруктураРеквизитовВыгрузки.ИННФЛОтпр);
		Параметры.Вставить("ФамилияФЛОтпр",  мСтруктураРеквизитовВыгрузки.ФамилияФЛОтпр);
		Параметры.Вставить("ИмяФЛОтпр",      мСтруктураРеквизитовВыгрузки.ИмяФЛОтпр);
		Параметры.Вставить("ОтчествоФЛОтпр", мСтруктураРеквизитовВыгрузки.ОтчествоФЛОтпр);
	КонецЕсли;
	
	Если мСтруктураРеквизитовВыгрузки.ПрОтпр = 1 Тогда
		Параметры.Вставить("ПрПодп1", Формат(мСтруктураРеквизитовВыгрузки.ПрПодп1, ""));
		
		Если НЕ ЭтоПБОЮЛ ИЛИ (ЭтоПБОЮЛ И НЕ ПустаяСтрока(мСтруктураРеквизитовВыгрузки.ФамилияПодп2 + мСтруктураРеквизитовВыгрузки.ИмяПодп2 + мСтруктураРеквизитовВыгрузки.ОтчествоПодп2)) Тогда
			Параметры.Вставить("ПрПодп2", "4");
			Параметры.Вставить("ИННФЛПодп2",    мСтруктураРеквизитовВыгрузки.ИННФЛПодп2);
			Параметры.Вставить("ДолжностьПодп2",мСтруктураРеквизитовВыгрузки.ДолжностьПодп2);
			
			Параметры.Вставить("ФамилияПодп2",  мСтруктураРеквизитовВыгрузки.ФамилияПодп2);
			Параметры.Вставить("ИмяПодп2",      мСтруктураРеквизитовВыгрузки.ИмяПодп2);
			Параметры.Вставить("ОтчествоПодп2", мСтруктураРеквизитовВыгрузки.ОтчествоПодп2);
			
			Если мСтруктураРеквизитовВыгрузки.Свойство("НаимДовПодп2") Тогда
				Параметры.Вставить("НаимДовПодп2",  мСтруктураРеквизитовВыгрузки.НаимДовПодп2);
				Параметры.Вставить("НомерДовПодп2", мСтруктураРеквизитовВыгрузки.НомерДовПодп2);
				Параметры.Вставить("ДатаДовПодп2",  Формат(мСтруктураРеквизитовВыгрузки.ДатаДовПодп2, "ДФ=дд.ММ.гггг"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если мСтруктураРеквизитовВыгрузки.ПрПодп1 = 3 Тогда
		Параметры.Вставить("НаимДовПодп1",  мСтруктураРеквизитовВыгрузки.НаимДовПодп1);
		Параметры.Вставить("НомерДовПодп1", мСтруктураРеквизитовВыгрузки.НомерДовПодп1);
		Параметры.Вставить("ДатаДовПодп1",  Формат(мСтруктураРеквизитовВыгрузки.ДатаДовПодп1, "ДФ=дд.ММ.гггг"));
	КонецЕсли;
	
	Параметры.Вставить("ИННФЛПодп1",    мСтруктураРеквизитовВыгрузки.ИННФЛПодп1);
	Параметры.Вставить("ДолжностьПодп1",мСтруктураРеквизитовВыгрузки.ДолжностьПодп1);
	
	Параметры.Вставить("ФамилияПодп1",  мСтруктураРеквизитовВыгрузки.ФамилияПодп1);
	Параметры.Вставить("ИмяПодп1",      мСтруктураРеквизитовВыгрузки.ИмяПодп1);
	Параметры.Вставить("ОтчествоПодп1", мСтруктураРеквизитовВыгрузки.ОтчествоПодп1);
	
	Если НЕ мСтруктураРеквизитовВыгрузки.ОтправленПочтой Тогда
		Параметры.Вставить("ПрКурьер",      Формат(мСтруктураРеквизитовВыгрузки.ПрКурьер, ""));
		Параметры.Вставить("ИННФЛКурьер",   мСтруктураРеквизитовВыгрузки.ИННФЛКурьер);
		
		Параметры.Вставить("ФамилияКурьер", мСтруктураРеквизитовВыгрузки.ФамилияКурьер);
		Параметры.Вставить("ИмяКурьер",     мСтруктураРеквизитовВыгрузки.ИмяКурьер);
		Параметры.Вставить("ОтчествоКурьер",мСтруктураРеквизитовВыгрузки.ОтчествоКурьер);
		
		Если мСтруктураРеквизитовВыгрузки.ПрКурьер = 3 Тогда
			Параметры.Вставить("НаимДовКурьер",  мСтруктураРеквизитовВыгрузки.НаимДовКурьер);
			Параметры.Вставить("НомерДовКурьер", мСтруктураРеквизитовВыгрузки.НомерДовКурьер);
			Параметры.Вставить("ДатаДовКурьер",  Формат(мСтруктураРеквизитовВыгрузки.ДатаДовКурьер, "ДФ=дд.ММ.гггг"));
		КонецЕсли;
	КонецЕсли;
	
	Параметры.Вставить("ИмяФайлаСхемы",             "ON_ZVLRPOK_2_430_14_05_05_02.xsd");
	Параметры.Вставить("ПрефиксИмениФайлаВыгрузки", "ON_ZVLRPOK");
	
	Параметры.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "Документ");
	Параметры.Вставить("ИдФайл", СформироватьИдФайл(Параметры));
	
	Возврат Параметры;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Функция ПроверитьВозможностьВыгрузки(Параметры)
	
	СтатусПроверки = Истина;
	
	// Проверка сведений о файле выгрузки.
	ДатаДок = Параметры.ДатаДокДата;
	Если ДатаДок < '20100701' Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Дата формирования файла выгрузки не может быть раньше 01.07.2010", "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Проверка сведений о заявлении.
	Раздел1 = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	Раздел3 = мДанныеОтчета.ПолеТабличногоДокументаРаздел3;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.НомерЗаяв) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер заявления!", "Раздел1", "НомерЗаяв");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	ИННКПП = Раздел1.ПродавецИНН_КПП;
	Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
		ПозицияРазделителя = Найти(ИННКПП, "/");
		ИННПродавца = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
		КПППродавца = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
	Иначе
		ИННПродавца = СокрЛП(ИННКПП);
		КПППродавца = "";
	КонецЕсли;
	
	ИННКПП = Раздел1.ПокупательИНН_КПП;
	Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
		ПозицияРазделителя = Найти(ИННКПП, "/");
		ИННПокупателя = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
		КПППокупателя = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
	Иначе
		ИННПокупателя = СокрЛП(ИННКПП);
		КПППокупателя = "";
	КонецЕсли;
	
	ИННКПП = Раздел1.ИННОрг_1;
	Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
		ПозицияРазделителя = Найти(ИННКПП, "/");
		ИННКомиссионера = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
		КППКомиссионера = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
	Иначе
		ИННКомиссионера = СокрЛП(ИННКПП);
		КППКомиссионера = "";
	КонецЕсли;
	
	// Раздел 1. Продавец.
	
	Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныПродавец) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны организации-продавца!", "Раздел1", "КодСтраныПродавец");
		СтатусПроверки = Ложь;
	ИначеЕсли ПустаяСтрока(Раздел3.КодСтраныПродавец) Тогда
		Если НЕ ЗначениеСодержитсяВСписке(Раздел1.КодСтраныПродавец, "112,398") Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 код страны организации-продавца должен быть 112 или 398!", "Раздел1", "КодСтраныПродавец");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Раздел1.КодСтраныПродавец = "112" ИЛИ Раздел1.КодСтраныПродавец = "398" Тогда
		Если НЕ ЗначениеЗаполнено(ИННПродавца) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан УНП|ИНН|РНН(БИН|ИИН) продавца!", "Раздел1", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Раздел1.КодСтраныПродавец = "643" ИЛИ НЕ ПустаяСтрока(Раздел1.ПродавецБайконур) Тогда
		ДлинаИНН = СтрДлина(ИННПродавца);
		
		Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан ИНН продавца!", "Раздел1", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ДлинаИНН = 10 Тогда
			ДлинаКПП = СтрДлина(КПППродавца);
			Если ДлинаКПП = 0 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан КПП продавца!", "Раздел1", "ПродавецИНН_КПП");
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаКПП <> 9 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан КПП продавца!", "Раздел1", "ПродавецИНН_КПП");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПродавецНаимОрг) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование организации-продавца (ФИО индивидуального предпринимателя)!", "Раздел1", "ПродавецНаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПродавецМестоНахожд) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано местонахождение (место жительства) продавца!", "Раздел1", "ПродавецМестоНахожд");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Раздел 1. Покупатель.
	
	Если Раздел1.КодСтраныПокупатель = "643" ИЛИ НЕ ПустаяСтрока(Раздел1.ПокупательБайконур) Тогда
		ДлинаИНН = СтрДлина(ИННПокупателя);
		
		Если ДлинаИНН = 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан ИНН покупателя!", "Раздел1", "ПокупательИНН_КПП");
			СтатусПроверки = Ложь;
		ИначеЕсли ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан ИНН покупателя!", "Раздел1", "ПокупательИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ДлинаИНН = 10 Тогда
			ДлинаКПП = СтрДлина(КПППокупателя);
			Если ДлинаКПП = 0 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан КПП продавца!", "Раздел1", "ПокупательИНН_КПП");
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаКПП <> 9 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан КПП продавца!", "Раздел1", "ПокупательИНН_КПП");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПокупательНаимОрг) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование организации-покупателя (ФИО индивидуального предпринимателя)!", "Раздел1", "ПокупательНаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныПокупатель) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны организации-покупателя!", "Раздел1", "КодСтраныПокупатель");
		СтатусПроверки = Ложь;
	ИначеЕсли НЕ(Раздел1.КодСтраныПокупатель = "643" ИЛИ Раздел1.КодСтраныПокупатель = "398") Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 код страны организации-покупателя должен быть 398 или 643!", "Раздел1", "КодСтраныПокупатель");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПокупательМестоНахожд) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано местонахождение (место жительства) покупателя!", "Раздел1", "ПокупательМестоНахожд");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Раздел 1. Сведения о контракте.
	
	Если НЕ ЗначениеЗаполнено(Раздел1.НомерДог) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер договора(контракта) строки 05!", "Раздел1", "НомерДог");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ДатаДог) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задана дата договора(контракта) строки 05!", "Раздел1", "ДатаДог");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	ЕстьДоговорЛизинга = НЕ ПустаяСтрока(Раздел1.ПрЛизинг);
	ЕстьДоговорПереработки = НЕ ПустаяСтрока(Раздел1.ПрДавСырья);
	
	Если ЕстьДоговорЛизинга и ЕстьДоговорПереработки Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не могут быть одновременно заданы признаки заключения договора лизинга и договора переработки давальческого сырья", "Раздел1", "ПрЛизинг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Раздел 1. Сведения о комиссионере (посреднике).
	
	ЕстьСведенияОКомиссионере = НЕ ПустаяСтрока(Раздел1.ИННОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.НаимОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.КодСтраныОрг1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.МестоНахождОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.НомКонтр_2)
	                        ИЛИ ЗначениеЗаполнено(Раздел1.ДатаКонтр_2);
	
	Если ЕстьСведенияОКомиссионере Тогда
		
		Если ЗначениеЗаполнено(Раздел1.КодСтраныОрг1) И Найти("112,398,643", Раздел1.КодСтраныОрг1) <> 0 Тогда
			Если ПустаяСтрока(ИННКомиссионера) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан УНП|ИНН|РНН(БИН|ИИН) организации строки 06!", "Раздел1", "ИННОрг_1");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если Раздел1.КодСтраныОрг1 = "643" ИЛИ НЕ ПустаяСтрока(Раздел1.КомиссионерБайконур) Тогда
			ДлинаИНН = СтрДлина(ИННКомиссионера);
			
			Если ДлинаИНН = 0 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан ИНН покупателя!", "Раздел1", "ИННОрг_1");
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан ИНН покупателя!", "Раздел1", "ИННОрг_1");
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если ДлинаИНН = 10 Тогда
				ДлинаКПП = СтрДлина(КППКомиссионера);
				Если ДлинаКПП = 0 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан КПП продавца!", "Раздел1", "ИННОрг_1");
					СтатусПроверки = Ложь;
				ИначеЕсли ДлинаКПП <> 9 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан КПП продавца!", "Раздел1", "ИННОрг_1");
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныОрг1) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны организации строки 06!", "Раздел1", "КодСтраныОрг1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1.МестоНахождОрг_1) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано местонахождение (место жительства) организации строки 06!", "Раздел1", "МестоНахождОрг_1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	
		Если НЕ ЗначениеЗаполнено(Раздел1.НомКонтр_2) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер договора(контракта) строки 07!", "Раздел1", "НомКонтр_2");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1.ДатаКонтр_2) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задана дата договора(контракта) строки 07!", "Раздел1", "ДатаКонтр_2");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ПроверитьВозможностьВыгрузки_Товары(Параметры, СтатусПроверки);
	
	// Проверка раздела 3.
	
	ИННКПП = Раздел3.ПродавецИНН_КПП;
	Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
		ПозицияРазделителя = Найти(ИННКПП, "/");
		ИННПродавца_Раздел3 = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
		КПППродавца_Раздел3 = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
	Иначе
		ИННПродавца_Раздел3 = СокрЛП(ИННКПП);
		КПППродавца_Раздел3 = "";
	КонецЕсли;
	
	ИННКПП = Раздел3.ПокупательИНН_КПП;
	Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
		ПозицияРазделителя = Найти(ИННКПП, "/");
		ИННПокупателя_Раздел3 = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
		КПППокупателя_Раздел3 = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
	Иначе
		ИННПокупателя_Раздел3 = СокрЛП(ИННКПП);
		КПППокупателя_Раздел3 = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Раздел3.ПродавецИНН_КПП)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательИНН_КПП)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ТипПрод_3)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ТипПок_3)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецБайконур)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательБайконур)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг)
	 ИЛИ ЗначениеЗаполнено(Раздел3.КодСтраныПродавец)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецМестоНахожд)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательНаимОрг)
	 ИЛИ ЗначениеЗаполнено(Раздел3.КодСтраныПокупатель)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательМестоНахожд)
	 ИЛИ ЗначениеЗаполнено(Раздел3.НомерДог)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ДатаДог)
	 ИЛИ ЗначениеЗаполнено(Раздел3.НомерСпецификации)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ДатаСпецификации) Тогда
		// Продавец.
		Если НЕ ЗначениеЗаполнено(Раздел3.ТипПрод_3) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан тип организации-продавца!", "Раздел3", "ТипПрод_3");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ИННПродавца_Раздел3) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан УНП|ИНН|РНН(БИН|ИИН) продавца!", "Раздел3", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		ИначеЕсли НЕ ПустаяСтрока(Раздел3.ПродавецБайконур) Тогда
			ДлинаИНН = СтрДлина(ИННПродавца_Раздел3);
			Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан ИНН продавца!", "Раздел3", "ПродавецИНН_КПП");
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаИНН = 10 Тогда
				ДлинаКПП = СтрДлина(КПППродавца_Раздел3);
				Если ДлинаКПП = 0 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан КПП продавца!", "Раздел3", "ПродавецИНН_КПП");
					СтатусПроверки = Ложь;
				ИначеЕсли ДлинаКПП <> 9 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан КПП продавца!", "Раздел3", "ПродавецИНН_КПП");
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли СтрДлина(ИННПродавца_Раздел3) < 9 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан УНП|ИНН|РНН(БИН|ИИН) продавца!", "Раздел3", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(Раздел3.КодСтраныПродавец) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан код страны организации-продавца!", "Раздел3", "КодСтраныПродавец");
			СтатусПроверки = Ложь;
		ИначеЕсли НЕ (Раздел3.КодСтраныПродавец = "112" ИЛИ Раздел3.КодСтраныПродавец = "398") Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 код страны организации-продавца должен быть 398 или 112!", "Раздел3", "КодСтраныПродавец");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано полное наименование организации-продавца (ФИО индивидуального предпринимателя)!", "Раздел3", "ПродавецНаимОрг");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецМестоНахожд) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано местонахождение (место жительства) продавца!", "Раздел3", "ПродавецМестоНахожд");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Покупатель.
		Если ПустаяСтрока(Раздел3.ТипПок_3) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан тип организации-покупателя!", "Раздел3", "ТипПок_3");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ИННПокупателя_Раздел3) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан УНП|ИНН|РНН(БИН|ИИН) покупателя!", "Раздел3", "ПокупательИНН_КПП");
			СтатусПроверки = Ложь;
		ИначеЕсли Раздел3.КодСтраныПокупатель = "643" ИЛИ НЕ ПустаяСтрока(Раздел3.ПокупательБайконур) Тогда
			ДлинаИНН = СтрДлина(ИННПокупателя_Раздел3);
			Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан ИНН покупателя!", "Раздел3", "ПокупательИНН_КПП");
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаИНН = 10 Тогда
				ДлинаКПП = СтрДлина(КПППокупателя_Раздел3);
				Если ДлинаКПП = 0 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан КПП покупателя!", "Раздел3", "ПокупательИНН_КПП");
					СтатусПроверки = Ложь;
				ИначеЕсли ДлинаКПП <> 9 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан КПП покупателя!", "Раздел3", "ПокупательИНН_КПП");
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли СтрДлина(КПППокупателя_Раздел3) < 9 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан УНП|ИНН|РНН(БИН|ИИН) покупателя!", "Раздел3", "ПокупательИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано полное наименование организации-покупателя (ФИО индивидуального предпринимателя)!", "Раздел3", "ПокупательНаимОрг");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(Раздел3.КодСтраныПокупатель) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан код страны организации-покупателя!", "Раздел3", "КодСтраныПокупатель");
			СтатусПроверки = Ложь;
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПокупательМестоНахожд) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано местонахождение (место жительства) покупателя!", "Раздел3", "ПокупательМестоНахожд");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Контракт.
		Если НЕ ЗначениеЗаполнено(Раздел3.НомерДог) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан номер договора(контракта) в строке 12!", "Раздел3", "НомерДог");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ДатаДог) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задана дата договора(контракта) в строке 12!", "Раздел3", "ДатаДог");
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЕсли;
	
	// Проверка приложений.
	
	Приложение = мДанныеОтчета.Приложение;
	
	Для Каждого Лист Из Приложение Цикл
		
		Если НЕ ЗаполненЛистРазделаПриложение(Лист) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеЛиста = Лист.Данные;
		НомерЛиста = Приложение.Индекс(Лист) + 1;
		
		ИННКПП = ДанныеЛиста.ПродавецИНН_КПП;
		Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
			ПозицияРазделителя = Найти(ИННКПП, "/");
			ИННПродавца_Приложение = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
			КПППродавца_Приложение = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
		Иначе
			ИННПродавца_Приложение = СокрЛП(ИННКПП);
			КПППродавца_Приложение = "";
		КонецЕсли;
		
		ИННКПП = ДанныеЛиста.ПокупательИНН_КПП;
		Если СтрЧислоВхождений(ИННКПП, "/") <> 0 Тогда
			ПозицияРазделителя = Найти(ИННКПП, "/");
			ИННПокупателя_Приложение = СокрЛП(Лев(ИННКПП, ПозицияРазделителя - 1));
			КПППокупателя_Приложение = СокрЛП(Сред(ИННКПП, ПозицияРазделителя + 1));
		Иначе
			ИННПокупателя_Приложение = СокрЛП(ИННКПП);
			КПППокупателя_Приложение = "";
		КонецЕсли;
		
		// Продавец.
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ТипПрод_Пр) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан тип организации-продавца!", "Приложение", "ТипПрод_Пр", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если (ДанныеЛиста.КодСтраныПродавец = "643" ИЛИ ДанныеЛиста.КодСтраныПродавец = "112" ИЛИ ДанныеЛиста.КодСтраныПродавец = "398") И ПустаяСтрока(ИННПродавца_Приложение) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе Приложение не задан УНП|ИНН|РНН(БИН|ИИН) продавца!", "Приложение", "ПродавецИНН_КПП", НомерЛиста);
			СтатусПроверки = Ложь;
		ИначеЕсли ДанныеЛиста.КодСтраныПродавец = "643" ИЛИ НЕ ПустаяСтрока(ДанныеЛиста.ПродавецБайконур) Тогда
			ДлинаИНН = СтрДлина(ИННПродавца_Приложение);
			Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение неверно задан ИНН продавца!", "Приложение", "ПродавецИНН_КПП", НомерЛиста);
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаИНН = 10 Тогда
				ДлинаКПП = СтрДлина(КПППродавца_Приложение);
				Если ДлинаКПП = 0 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан КПП продавца!", "Приложение", "ПродавецИНН_КПП", НомерЛиста);
					СтатусПроверки = Ложь;
				ИначеЕсли ДлинаКПП <> 9 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение неверно задан КПП продавца!", "Приложение", "ПродавецИНН_КПП", НомерЛиста);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустаяСтрока(ДанныеЛиста.КодСтраныПродавец) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан код страны организации-продавца!", "Приложение", "КодСтраныПродавец", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано полное наименование организации-продавца (ФИО индивидуального предпринимателя)!", "Приложение", "ПродавецНаимОрг", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецМестоНахожд) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано местонахождение (место жительства) продавца!", "Приложение", "ПродавецМестоНахожд", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Покупатель.
		Если ПустаяСтрока(ДанныеЛиста.ТипПок_Пр) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан тип организации-покупателя!", "Приложение", "ТипПок_Пр", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ИННПокупателя_Приложение) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан УНП|ИНН|РНН(БИН|ИИН) покупателя!", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
			СтатусПроверки = Ложь;
		ИначеЕсли ДанныеЛиста.КодСтраныПокупатель = "643" ИЛИ НЕ ПустаяСтрока(ДанныеЛиста.ПокупательБайконур) Тогда
			ДлинаИНН = СтрДлина(ИННПокупателя_Приложение);
			Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение неверно задан ИНН покупателя!", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
				СтатусПроверки = Ложь;
			ИначеЕсли ДлинаИНН = 10 Тогда
				ДлинаКПП = СтрДлина(КПППокупателя_Приложение);
				Если ДлинаКПП = 0 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан КПП покупателя!", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
					СтатусПроверки = Ложь;
				ИначеЕсли ДлинаКПП <> 9 Тогда
					РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение неверно задан КПП покупателя!", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли СтрДлина(КПППокупателя_Приложение) < 9 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение неверно задан УНП|ИНН|РНН(БИН|ИИН) покупателя!", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано полное наименование организации-покупателя (ФИО индивидуального предпринимателя)!", "Приложение", "ПокупательНаимОрг", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ДанныеЛиста.КодСтраныПокупатель) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан код страны организации-покупателя!", "Приложение", "КодСтраныПокупатель", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПокупательМестоНахожд) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано местонахождение (место жительства) покупателя!", "Приложение", "ПокупательМестоНахожд", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Контракт.
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.НомерДог) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан номер договора(контракта)!", "Приложение", "НомерДог", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ДатаДог) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задана дата договора(контракта)!", "Приложение", "ДатаДог", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЦикла;
	
	// Проверка сведений о банках и выписках.
	
	ЧислоЗаполненныхСтрокВсего = 0;
	ЧислоЗаполненныхСтрокВсегоЗачетов = 0;
	
	Сведения = мДанныеОтчета.Сведения;
	
	Для каждого Лист Из Сведения Цикл
		
		ДанныеЛиста = Лист.Данные;
		ИндексЛиста = Сведения.Индекс(Лист) + 1;
		
		ТипПлат = ДанныеЛиста.ТипПлат;
		БИКБанка = ВРЕГ(СокрЛП(ДанныеЛиста.БИК));
		НаименованиеБанка = ВРЕГ(СокрЛП(ДанныеЛиста.НаименованиеБанка));
		
		Если НЕ ЗначениеЗаполнено(БИКБанка) И НЕ ЗначениеЗаполнено(НаименованиеБанка) И НЕ ЗначениеЗаполнено(ТипПлат) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТипПлат) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе ""Сведения о выписках банка"" на странице " + ИндексЛиста + " не заполнен тип платежного документа!", "Сведения", "ТипПлат", ИндексЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если (ТипПлат = 1 ИЛИ ТипПлат = 2) И ПустаяСтрока(БИКБанка) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе ""Сведения о выписках банка"" на странице " + ИндексЛиста + " не заполнен БИК банка!", "Сведения", "БИК", ИндексЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если (ТипПлат = 1 ИЛИ ТипПлат = 2) И ПустаяСтрока(НаименованиеБанка) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В разделе ""Сведения о выписках банка"" на странице " + ИндексЛиста + " не заполнено наименование банка!", "Сведения", "НаименованиеБанка", ИндексЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Проверяем сведения о выписках банка.
		ЧислоЗаполненныхСтрокЛиста = 0;
		НомерСтроки = 1;
		Пока ДанныеЛиста.Свойство("П000020000102_" + Формат(НомерСтроки, "ЧГ=")) Цикл
			
			СтрНомерСтроки = Формат(НомерСтроки, "ЧГ=");
			
			П000020000101 = ДанныеЛиста["П000020000101_" + СтрНомерСтроки];
			П000020000102 = ДанныеЛиста["П000020000102_" + СтрНомерСтроки];
			П000020000103 = ДанныеЛиста["П000020000103_" + СтрНомерСтроки];
			
			НомерСтроки = НомерСтроки + 1;
			
			Если НЕ ЗначениеЗаполнено(П000020000101) И НЕ ЗначениеЗаполнено(П000020000102) И НЕ ЗначениеЗаполнено(П000020000103) Тогда
				Продолжить;
			Иначе
				ЧислоЗаполненныхСтрокЛиста = ЧислоЗаполненныхСтрокЛиста + 1;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(П000020000101) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + ИндексЛиста + " раздела ""Сведения о выписках банка"" в строке " + СтрНомерСтроки + " не указана дата выписки!", "Сведения", "П000020000101_" + СтрНомерСтроки, ИндексЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(П000020000102) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + ИндексЛиста + " раздела ""Сведения о выписках банка"" в строке " + СтрНомерСтроки + " не указана сумма уплаченного налога по выписке!", "Сведения", "П000020000102_" + СтрНомерСтроки, ИндексЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если ТипПлат = 1 И ПустаяСтрока(П000020000103) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + ИндексЛиста + " раздела ""Сведения о выписках банка"" в строке " + СтрНомерСтроки + " не указан номер счета по выписке!", "Сведения", "П000020000103_" + СтрНомерСтроки, ИндексЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если ЧислоЗаполненныхСтрокЛиста = 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + ИндексЛиста + " раздела ""Сведения о выписках банка"" не определена ни одна выписка!", "Сведения", "П000020000101_1", ИндексЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		ЧислоЗаполненныхСтрокВсего = ЧислоЗаполненныхСтрокВсего + ЧислоЗаполненныхСтрокЛиста;
		
		Если (ТипПлат = 1 ИЛИ ТипПлат = 2) И ЧислоЗаполненныхСтрокЛиста = 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Сведения о выписках банка не могут быть не заполнены при типе платежного документа 1 или 2!", "Сведения", "П000020000101_1", 1);
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЦикла;
	
	// Проверка сведений о зачетах.
	
	Зачет = мДанныеОтчета.Зачет;
	
	Для Каждого Лист Из Зачет Цикл
		
		ДанныеЛиста = Лист.Данные;
		НомерЛиста = Зачет.Индекс(Лист) + 1;
		
		ТипПлат = ДанныеЛиста.ТипПлатЗачет;
		
		ЧислоЗаполненныхСтрокЛиста = 0;
		НомерСтроки = 1;
		Пока ДанныеЛиста.Свойство("П000030000101_" + Формат(НомерСтроки, "ЧГ=")) Цикл
			
			П000030000101 = ДанныеЛиста["П000030000101_" + Формат(НомерСтроки, "ЧГ=")];
			П000030000102 = ДанныеЛиста["П000030000102_" + Формат(НомерСтроки, "ЧГ=")];
			
			НомерСтроки = НомерСтроки + 1;
			
			Если НЕ ЗначениеЗаполнено(П000030000101) И НЕ ЗначениеЗаполнено(П000030000102) Тогда
				Продолжить;
			КонецЕсли;
			ЧислоЗаполненныхСтрокЛиста = ЧислоЗаполненныхСтрокЛиста + 1;
			
			Если НЕ ЗначениеЗаполнено(П000030000101) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + НомерЛиста + " раздела ""Сведения о зачетах"" в строке " + СтрНомерСтроки + " не указана дата зачета!", "Зачет", "П000030000101_" + СтрНомерСтроки, НомерЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(П000030000102) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + НомерЛиста + " раздела ""Сведения о зачетах"" в строке " + СтрНомерСтроки + " не указана сумма зачета!", "Зачет", "П000030000102_" + СтрНомерСтроки, НомерЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ЧислоЗаполненныхСтрокВсегоЗачетов = ЧислоЗаполненныхСтрокВсегоЗачетов + 1;
			
		КонецЦикла;
		
		Если ЧислоЗаполненныхСтрокЛиста <> 0 И ТипПлат <> 3 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + НомерЛиста + " раздела ""Сведения о зачетах"" не указан тип платежного документа!", "Зачет", "ТипПлатЗачет", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ТипПлат = 3 И ЧислоЗаполненныхСтрокЛиста = 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "На странице " + НомерЛиста + " раздела ""Сведения о зачетах"" не указаны сведения о зачетах!", "Зачет", "ТипПлатЗачет", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	СуммаНалогаНенулевая = (ЗначениеЗаполнено(Раздел1["П000010000119"]) ИЛИ ЗначениеЗаполнено(Раздел1["П000010000120"]));
	ЕстьСведенияОбУплатеНалога = (ЧислоЗаполненныхСтрокВсего <> 0 ИЛИ ЧислоЗаполненныхСтрокВсегоЗачетов <> 0);
	
	Если СуммаНалогаНенулевая И НЕ ЕстьСведенияОбУплатеНалога Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Сведения о выписках банка и зачетах не могут быть не заполнены при ненулевой итоговой сумме налога!", "Сведения", "П000020000101_1", 1);
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Проверка заполненности параметров выгрузки, задаваемых на форме выгрузки.
	мСтруктураРеквизитовВыгрузки = Неопределено;
	ДанныеСохраненногоОтчета = мСохраненныйДок.ДанныеОтчета.Получить();
	Если НЕ ДанныеСохраненногоОтчета.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки) Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРевизитовВыгрузки();
	КонецЕсли;
	
	КодИФНС               = мСтруктураРеквизитовВыгрузки.КодИФНС;
	ОтправительЯвляетсяЮЛ = мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ;
	ИННЮЛОтпр             = мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр;
	ИННФЛОтпр             = мСтруктураРеквизитовВыгрузки.ИННФЛОтпр;
	ПрПодп1               = мСтруктураРеквизитовВыгрузки.ПрПодп1;
	НаимДовПодп1          = мСтруктураРеквизитовВыгрузки.НаимДовПодп1;
	НомерДовПодп1         = мСтруктураРеквизитовВыгрузки.НомерДовПодп1;
	ДатаДовПодп1          = мСтруктураРеквизитовВыгрузки.ДатаДовПодп1;
	ОтправленПочтой       = мСтруктураРеквизитовВыгрузки.ОтправленПочтой;
	ПрКурьер              = мСтруктураРеквизитовВыгрузки.ПрКурьер;
	НаимДовКурьер         = мСтруктураРеквизитовВыгрузки.НаимДовКурьер;
	НомерДовКурьер        = мСтруктураРеквизитовВыгрузки.НомерДовКурьер;
	ДатаДовКурьер         = мСтруктураРеквизитовВыгрузки.ДатаДовКурьер;
	
	Если НЕ ЗначениеЗаполнено(КодИФНС) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не заполнен код налогового органа! Требуется заполнить в форме ""Параметры выгрузки"", вызываемой из командного меню отчета.", "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если (ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННЮЛОтпр)) ИЛИ (НЕ ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННФЛОтпр)) Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не заполнен ИНН отправителя! Требуется заполнить в форме ""Параметры выгрузки"", вызываемой из командного меню отчета.", "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли; 

	Если ПрПодп1 = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(НомерДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовПодп1) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не заполнены сведения о доверенности подписанта 1! Требуется заполнить в форме ""Параметры выгрузки"", вызываемой из командного меню отчета.", "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтправленПочтой И ПрКурьер = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(НомерДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовКурьер) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не заполнены сведения о доверенности курьера! Требуется заполнить в форме ""Параметры выгрузки"", вызываемой из командного меню отчета.", "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтатусПроверки;
	
КонецФункции

Процедура ПроверитьВозможностьВыгрузки_Товары(Параметры, СтатусПроверки)
	
	Раздел1 = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	
	ЧислоВыгружаемыхТоваров = 0;
	НомСтроки = 1;
	
	ЕстьДоговорЛизинга = НЕ ПустаяСтрока(Раздел1.ПрЛизинг);
	ЕстьДоговорПереработки = НЕ ПустаяСтрока(Раздел1.ПрДавСырья);
	
	Пока Раздел1.Свойство("П000010000101_" + Формат(НомСтроки, "ЧГ=")) Цикл
		СтрНомСтроки = Формат(НомСтроки, "ЧГ=");
		
		НомСтроки = НомСтроки + 1; // для следующего прохождения цикла, в этом используется лишь СтрНомСтроки
		СтрокаЗаполнена = Ложь;
		Для Инд = 2 По 13 Цикл
			Если ЗначениеЗаполнено(Раздел1["П0000100001" + Формат(Инд, "ЧЦ=2; ЧВН=; ЧГ=") + "_" + СтрНомСтроки]) Тогда
				СтрокаЗаполнена = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СтрокаЗаполнена Тогда
			Продолжить;
		КонецЕсли;
		
		ЧислоВыгружаемыхТоваров = ЧислоВыгружаемыхТоваров + 1;
		
		СтруктураСтавок = Новый Структура;
		СтруктураСтавок.Вставить("Ст16", СтрЗаменить(Раздел1["П000010000116_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст17", СтрЗаменить(Раздел1["П000010000117_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст18", СтрЗаменить(Раздел1["П000010000118_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст21", Раздел1["П000010000121_" + СтрНомСтроки]);
		
		СтруктураСтавокМодиф = АнализироватьПоляСтавки(СтруктураСтавок);
		ПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
		ПрОсвНДС   = СтруктураСтавокМодиф.Переключатель1;
		
		СтАкцизТверд = СтрЗаменить(Раздел1["П000010000116_" + СтрНомСтроки], ",", ".");
		СтАкцизАдвал = СтрЗаменить(Раздел1["П000010000117_" + СтрНомСтроки], ",", ".");
		СтНДС        = СтрЗаменить(Раздел1["П000010000118_" + СтрНомСтроки], ",", ".");
		
		СтАкцизТверд = ?(СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)), 0, Число(СтАкцизТверд));
		СтАкцизАдвал = ?(СтАкцизАдвал = "Льгота" ИЛИ СтАкцизАдвал = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизАдвал)), 0, Число(СтАкцизАдвал));
		СтНДС = ?(СтНДС = "Льгота" ИЛИ СтНДС = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтНДС)), 0, Число(СтНДС));
		
		// Значение единицы измерения дополнительной величины налоговой базы.
		ЗначениеКоличТов = Раздел1["П000010000105_" + СтрНомСтроки];
		ЗначениеНБАкциз = Раздел1["П000010000114_" + СтрНомСтроки];
		ЗаполнятьЕдИзмТовНБАкц = (ЗначениеКоличТов <> ЗначениеНБАкциз)
		                       И (ЗначениеКоличТов <>0)
		                       И НЕ (ПрОсвАкциз = 3 ИЛИ ПрОсвАкциз = 4);
		ИмяСлужебногоПоля = "П000010000124_" + СтрНомСтроки;
		ИмяПоляПредставления = "П000010000123_" + СтрНомСтроки;
		Если Раздел1.Свойство(ИмяСлужебногоПоля) И НЕ ПустаяСтрока(Раздел1[ИмяСлужебногоПоля])Тогда
			ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
		Иначе
			ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000102_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнено наименование товара!", "Раздел1", "П000010000102_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		ТНВЭД = Раздел1["П000010000103_" + СтрНомСтроки];
		Если (ПрОсвАкциз <> 4)
		 ИЛИ (ПрОсвНДС = 2)
		 ИЛИ (ПрОсвНДС = 1 И СтНДС = 10) Тогда
			Если ПустаяСтрока(ТНВЭД) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнен код ТН ВЭД!", "Раздел1", "П000010000102_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Единица измерения.
		Если (НЕ ЕстьДоговорЛизинга И НЕ ЕстьДоговорПереработки)
		 ИЛИ (НЕ ПустаяСтрока(Раздел1["П000010000105_" + СтрНомСтроки])) Тогда
			Если ПустаяСтрока(Раздел1["П000010000104_" + СтрНомСтроки]) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена единица измерения товара!", "Раздел1", "П000010000104_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Количество товара.
		Если (НЕ ЕстьДоговорЛизинга И НЕ ЕстьДоговорПереработки)
		 ИЛИ (НЕ ПустаяСтрока(Раздел1["П000010000104_" + СтрНомСтроки])) Тогда
			Если НЕ ЗначениеЗаполнено(Раздел1["П000010000105_" + СтрНомСтроки]) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указано количество товара!", "Раздел1", "П000010000105_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		СтоимостьТовара = Раздел1["П000010000106_" + СтрНомСтроки];
		Если НЕ ЗначениеЗаполнено(СтоимостьТовара) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена стоимость товара!", "Раздел1", "П000010000106_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		КодВалюты = Раздел1["П000010000107_" + СтрНомСтроки];
		Если ПустаяСтрока(КодВалюты) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана валюта товара!", "Раздел1", "П000010000107_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		КурсВалюты = Раздел1["П000010000108_" + СтрНомСтроки];
		Если НЕ ЗначениеЗаполнено(КурсВалюты) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указан курс валюты товара!", "Раздел1", "П000010000108_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		ИначеЕсли КодВалюты = "643" И КурсВалюты <> 1 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " для валюты с кодом 643 (Российский рубль) курс валюты должен равняться 1!", "Раздел1", "П000010000108_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Транспортный (товаросопроводительный) документ.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000109_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указаны серия, номер транспортного документа!", "Раздел1", "П000010000109_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000110_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана дата транспортного документа!", "Раздел1", "П000010000110_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Счет-фактура.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000111_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указан номер счета-фактуры!", "Раздел1", "П000010000111_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000112_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана дата счета-фактуры!", "Раздел1", "П000010000112_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Дата принятия на учет товара.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000113_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена дата принятия товара на учет!", "Раздел1", "П000010000113_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Налоговая база.
		Если ЗаполнятьЕдИзмТовНБАкц Тогда
			Если НЕ ЗначениеЗаполнено(ЗначениеЕдИзмТовНБАкц) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена единица измерения дополнительной величины, используемой для исчисления налоговой базы (акциз)!", "Раздел1", "П000010000123_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ЗначениеЕдИзмТовНБАкц) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " ошибочно заполнена единица измерения дополнительной величины, используемой для исчисления налоговой базы (акциз)!", "Раздел1", "П000010000123_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		НалоговаяБазаНДС = Раздел1["П000010000115_" + СтрНомСтроки];
		Если НалоговаяБазаНДС <= 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена налоговая база (НДС) !", "Раздел1", "П000010000115_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		
		Если (ПрОсвАкциз = 1)
		   И НЕ ЗначениеЗаполнено(СтруктураСтавокМодиф.СтавкаАкциза) Тогда
			Если НЕ ЗначениеЗаполнено(СтАкцизТверд) И НЕ ЗначениеЗаполнено(СтАкцизАдвал) Тогда
				РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана ставка акцизов!", "Раздел1", "П000010000116_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если ПрОсвНДС = 1 И СтНДС <> 10 И СтНДС <> 18 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " неверно задана ставка НДС!", "Раздел1", "П000010000118_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Суммы налогов
		Если (ПрОсвАкциз = 1 ИЛИ ПрОсвАкциз = 3) И Раздел1["П000010000119_" + СтрНомСтроки] <= 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не задана сумма Акциза!", "Раздел1", "П000010000119_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПрОсвНДС = 1 И Раздел1["П000010000120_" + СтрНомСтроки] <= 0 Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не задана сумма НДС!", "Раздел1", "П000010000120_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
	Если ЧислоВыгружаемыхТоваров = 0 Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 не заполнена ни одна строка!", "Раздел1", "П000010000102_1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаполненЛистРазделаПриложение(ЛистРаздела)
	
	ДанныеЛиста = ЛистРаздела.Данные;
	
	Результат = ЗначениеЗаполнено(ДанныеЛиста.ТипПрод_Пр)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецИНН_КПП)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.КодСтраныПродавец)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецМестоНахожд)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецБайконур)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ТипПок_Пр)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательИНН_КПП)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательНаимОрг)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.КодСтраныПокупатель)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательМестоНахожд)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательБайконур)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.НомерДог)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ДатаДог)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.НомерСпецификации)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ДатаСпецификации);
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеСодержитсяВСписке(ПроверяемоеЗначение, КонтрольныйСписок)
	
	Результат = Ложь;
	
	Значения = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(КонтрольныйСписок, ",");
	Для Инд = 0 По Значения.ВГраница() Цикл
		Значения[Инд] = СокрЛП(Значения[Инд]);
	КонецЦикла;
	
	Результат = (Значения.Найти(ПроверяемоеЗначение) <> Неопределено);
	
	Возврат Результат
	
КонецФункции

Функция СкопироватьУзел(Родитель, Узел)
	
	РодительСтрока = ?(Родитель = Неопределено, Узел.Владелец(), Родитель);
	
	// находим узел в максимальным индексом с тем же кодом, что и у копируемого и добавляем его сразу после найденного элемента
	СтрокиСТемЖеКодом = РодительСтрока.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
	МаксИндекс = -1;
	Для Каждого СтрокаСТемЖеКодом Из СтрокиСТемЖеКодом Цикл
		ТекИндекс = РодительСтрока.Строки.Индекс(СтрокаСТемЖеКодом);
		Если ТекИндекс > МаксИндекс Тогда
			МаксИндекс = ТекИндекс;
		КонецЕсли;
	КонецЦикла;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= РодительСтрока.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда // если аналогичный узел не найден или найденный узел - последний
		НовыйУзел = РодительСтрока.Строки.Добавить();
	Иначе
		НовыйУзел = РодительСтрока.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

Процедура ЗадатьЗначениеЭлемента(Узел, КодЭлемента, Значение)
	
	ВывестиПоказательВXML(ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента), Значение);
	
КонецПроцедуры

Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Код = КодЭлемента Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Процедура ВыгрузитьСтрокуМногострочнойЧасти(Данные, Узел, НомерСтроки, ПоказателиИсключения = Неопределено, ВыгружатьПустую = Ложь)
	
	Если ПоказателиИсключения = Неопределено Тогда
		СтрИсключения = Новый Структура;
	Иначе
		СтрИсключения = Новый Структура(ПоказателиИсключения);
	КонецЕсли;
	
	СуффиксТекущейСтроки = Формат(НомерСтроки, "ЧГ=");
	
	СтрокаПустая = Истина;
	Для Каждого Стр Из Узел.Строки Цикл
		Если НЕ ЗначениеЗаполнено(Стр.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ СтрИсключения.Свойство(Стр.Ключ) И ЗначениеПоказателяЗаполнено(Данные[Стр.Ключ + СуффиксТекущейСтроки], Стр) Тогда
			СтрокаПустая = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СтрокаПустая ИЛИ ВыгружатьПустую Тогда
		Для Каждого Стр Из Узел.Строки Цикл
			Если ЗначениеЗаполнено(Стр.Ключ) Тогда
				ВывестиПоказательВXML(Стр, Данные[Стр.Ключ + СуффиксТекущейСтроки]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗначениеСуммыЗаполнено(ЗначениеСуммы)
	
	Возврат (Окр(ЗначениеСуммы) <> 0)
	
КонецФункции

Функция ЗначениеПоказателяЗаполнено(ЗначениеПоказателя, ЯвляетсяСуммой)
	
	Если ТипЗнч(ЯвляетсяСуммой) = Тип("Булево") Тогда
		ПоказательЯвляетсяСуммой = ЯвляетсяСуммой;
	Иначе
		ПоказательЯвляетсяСуммой = (ЯвляетсяСуммой.Формат = "N" И ЯвляетсяСуммой.МаксРазмерность >= 10 И ЯвляетсяСуммой.МинРазмерность = 99999);
	КонецЕсли;
	
	Возврат ?(ПоказательЯвляетсяСуммой, ЗначениеСуммыЗаполнено(ЗначениеПоказателя), ЗначениеЗаполнено(ЗначениеПоказателя));
	
КонецФункции

Функция РазделитьИННКПП(ИННКПП)
	
	ИНН = СокрЛП(?(СтрЧислоВхождений(ИННКПП, "/") <> 0, Лев(ИННКПП, Найти(ИННКПП, "/") - 1), ИННКПП));
	КПП = СокрЛП(?(СтрЧислоВхождений(ИННКПП, "/") <> 0, Сред(ИННКПП, Найти(ИННКПП, "/") + 1), ""));
	
	Возврат Новый Структура("ИНН, КПП", ИНН, КПП);
	
КонецФункции

Процедура ЗаполнитьСодержательнуюЧасть(УзелРодитель)
	
	Раздел1 	= мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	Раздел3 	= мДанныеОтчета.ПолеТабличногоДокументаРаздел3;
	Приложение 	= мДанныеОтчета.Приложение;
	Сведения 	= мДанныеОтчета.Сведения;
	Зачет 		= мДанныеОтчета.Зачет;
	
	ДанныеСохраненногоОтчета = мСохраненныйДок.ДанныеОтчета.Получить();
	мТЗСпецификацийРаздел1 = ДанныеСохраненногоОтчета.мТЗСпецификацийРаздел1;
	мТЗСпецификацийРаздел1_07 = ДанныеСохраненногоОтчета.мТЗСпецификацийРаздел1_07;
	мТЗСпецификацийРаздел3 = ДанныеСохраненногоОтчета.мТЗСпецификацийРаздел3;
	мМассивТЗСпецификацийПрил = ДанныеСохраненногоОтчета.мМассивТЗСпецификацийПрил;
	мСтрукТЗДокументы = ДанныеСохраненногоОтчета.мСтрукТЗДокументы;
	
	УзелСвЗвл = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвЗвл");
	ЗаполнитьДаннымиУзел(Раздел1, УзелСвЗвл);
	ЗадатьЗначениеЭлемента(УзелСвЗвл, "НомерДокНП", Формат(Раздел1.НомерЗаяв,"ЧН=; ЧГ=0"));
	ЗадатьЗначениеЭлемента(УзелСвЗвл, "ДатаДокНП", Формат(мСохраненныйДок.ДатаПодписи, "ДФ=дд.ММ.гггг"));
	ЗадатьЗначениеЭлемента(УзелСвЗвл, "ПрЛизинг", ?(СокрЛП(Раздел1.ПрЛизинг) = "", "0", "1"));
	ЗадатьЗначениеЭлемента(УзелСвЗвл, "ПрДавСырья", ?(СокрЛП(Раздел1.ПрДавСырья) = "", "0", "1"));
	
	УзелСвКонтракт1 = ПолучитьПодчиненныйЭлемент(УзелСвЗвл, "СвКонтракт1");
	
	Если НЕ ПустаяСтрока(Раздел1.ПродавецБайконур) Тогда
		ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "ПрБкнрПродР1", "1");
	КонецЕсли;
	ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "ИдНомПродР1", РазделитьИННКПП(Раздел1.ПродавецИНН_КПП).ИНН);
	ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "КПППродР1", РазделитьИННКПП(Раздел1.ПродавецИНН_КПП).КПП);
	
	Если НЕ ПустаяСтрока(Раздел1.ПокупательБайконур) Тогда
		ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "ПрБкнрПокР1", "1");
	КонецЕсли;
	ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "ИдНомПокР1", РазделитьИННКПП(Раздел1.ПокупательИНН_КПП).ИНН);
	ЗадатьЗначениеЭлемента(УзелСвКонтракт1, "КПППокР1", РазделитьИННКПП(Раздел1.ПокупательИНН_КПП).КПП);
	
	УзелСвКонтр1 = ПолучитьПодчиненныйЭлемент(УзелСвКонтракт1, "СвКонтр1");
	ШаблонУзелСвСпециф = ПолучитьПодчиненныйЭлемент(УзелСвКонтр1, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел1.Количество() Цикл
		Строка = мТЗСпецификацийРаздел1[Сч - 1];
		
		УзелСвСпециф = СкопироватьУзел(УзелСвКонтр1, ШаблонУзелСвСпециф);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомПСпециф", Сч);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	УзелСвКонтрКомисс = ПолучитьПодчиненныйЭлемент(УзелСвЗвл, "СвКонтрКомисс");
	Если НЕ ПустаяСтрока(Раздел1.КомиссионерБайконур) Тогда
		ЗадатьЗначениеЭлемента(УзелСвКонтрКомисс, "ПрБкнрОрг", "1");
	КонецЕсли;
	ЗадатьЗначениеЭлемента(УзелСвКонтрКомисс, "ИдНомОрг", РазделитьИННКПП(Раздел1.ИННОрг_1).ИНН);
	ЗадатьЗначениеЭлемента(УзелСвКонтрКомисс, "КППОрг", РазделитьИННКПП(Раздел1.ИННОрг_1).КПП);
	
	УзелСвКонтракт2 = ПолучитьПодчиненныйЭлемент(УзелСвКонтрКомисс, "СвКонтракт2");
	
	ШаблонУзелСвСпециф = ПолучитьПодчиненныйЭлемент(УзелСвКонтракт2, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел1_07.Количество() Цикл
		Строка = мТЗСпецификацийРаздел1_07[Сч - 1];
		
		УзелСвСпециф = СкопироватьУзел(УзелСвКонтракт2, ШаблонУзелСвСпециф);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомПСпециф", Сч);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	ШаблонУзелСвТовар = ПолучитьПодчиненныйЭлемент(УзелСвЗвл, "СвТовар");
	ЧислоВыгруженныхСтрок = 0;
	НомерСтроки = 1;
	Пока СвойствоОпределено(Раздел1, "П000010000101_" + Формат(НомерСтроки, "ЧГ=")) Цикл
		
		Если ЗначениеЗаполнено(Раздел1["П000010000102_" + Формат(НомерСтроки, "ЧГ=")]) Тогда
			ЧислоВыгруженныхСтрок = ЧислоВыгруженныхСтрок + 1;
			УзелСвТовар = СкопироватьУзел(УзелСвЗвл, ШаблонУзелСвТовар);
			ВыгрузитьСтрокуМногострочнойЧасти(Раздел1, УзелСвТовар, НомерСтроки);
			
			ЗадатьЗначениеЭлемента(УзелСвТовар, "НомТовПП", Формат(ЧислоВыгруженныхСтрок, "ЧГ="));
			
			// Если единица измерения указана не из списка, тогда графа 22 будет пустой,
			// в этом случае будем выгружать графу 4.
			
			ИмяСлужебногоПоля = "П000010000122_" + Формат(НомерСтроки, "ЧГ=");
			
			Если Раздел1.Свойство(ИмяСлужебногоПоля) И СокрЛП(Раздел1[ИмяСлужебногоПоля]) <> "" Тогда
				ИмяПоляЕдИзм = ИмяСлужебногоПоля;
			Иначе
				ИмяПоляЕдИзм = "П000010000104_" + Формат(НомерСтроки, "ЧГ=");
			КонецЕсли;
			
			ЗадатьЗначениеЭлемента(УзелСвТовар, "ЕдИзмТов", СокрЛП(Раздел1[ИмяПоляЕдИзм]));
			
			ЗадатьЗначениеЭлемента(УзелСвТовар, "БазаВал", "1");
			
			СтАкцизТверд 	= СтрЗаменить(Раздел1["П000010000116_" + Формат(НомерСтроки, "ЧГ=")], ",", ".");
			СтАкцизАдвал 	= СтрЗаменить(Раздел1["П000010000117_" + Формат(НомерСтроки, "ЧГ=")], ",", ".");
			СтНДС 			= СтрЗаменить(Раздел1["П000010000118_" + Формат(НомерСтроки, "ЧГ=")], ",", ".");
			
			СтруктураСтавок = Новый Структура;
			СтруктураСтавок.Вставить("Ст16", СтАкцизТверд);
			СтруктураСтавок.Вставить("Ст17", СтАкцизАдвал);
			СтруктураСтавок.Вставить("Ст18", СтНДС);
			СтруктураСтавок.Вставить("Ст21", Раздел1["П000010000121_" + Формат(НомерСтроки, "ЧГ=")]);
			
			СтруктураСтавокМодиф = АнализироватьПоляСтавки(СтруктураСтавок);
			
			ПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
			ПрОсвНДС   = СтруктураСтавокМодиф.Переключатель1;
			ЗадатьЗначениеЭлемента(УзелСвТовар, "ПрОсвАкциз", ПрОсвАкциз);
			ЗадатьЗначениеЭлемента(УзелСвТовар, "ПрОсвНДС",   ПрОсвНДС);
			
			// Значение единицы измерения дополнительной величины налоговой базы.
			ЗначениеКоличТов = Раздел1["П000010000105_" + Формат(НомерСтроки, "ЧГ=")];
			ЗначениеНБАкциз = Раздел1["П000010000114_" + Формат(НомерСтроки, "ЧГ=")];
			ЗначениеПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
			ЗаполнятьЕдИзмТовНБАкц = (ЗначениеКоличТов <> ЗначениеНБАкциз)
			                       И (ЗначениеКоличТов <>0)
			                       И (ЗначениеПрОсвАкциз <> 3 И ЗначениеПрОсвАкциз <> 4);
			Если ЗаполнятьЕдИзмТовНБАкц Тогда
				ИмяСлужебногоПоля = "П000010000124_" + Формат(НомерСтроки, "ЧГ=");
				ИмяПоляПредставления = "П000010000123_" + Формат(НомерСтроки, "ЧГ=");
				Если Раздел1.Свойство(ИмяСлужебногоПоля) И НЕ ПустаяСтрока(Раздел1[ИмяСлужебногоПоля])Тогда
					ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
				Иначе
					// При вводе значения не через подбор из списка используется значение введенное вручную.
					ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
				КонецЕсли;
				ЗадатьЗначениеЭлемента(УзелСвТовар, "ЕдИзмТовНБАкц", ЗначениеЕдИзмТовНБАкц);
			КонецЕсли;
			
			// Поскольку на форме эти показатели строковые - при выгрузке приводим к числовому виду.
			ЗаполненаТвердаяСтавкаАкцизов = (СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ (ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)) И Число(СтАкцизТверд) <> 0));
			СтАкцизТверд = ?(СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)), "0.00", СтАкцизТверд);
			СтАкцизАдвал = ?(СтАкцизАдвал = "Льгота" ИЛИ СтАкцизАдвал = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизАдвал)), "0.00", СтАкцизАдвал);
			СтНДС = ?(СтНДС = "Льгота" ИЛИ СтНДС = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтНДС)), "0.00", СтНДС);
			
			Если ЗаполненаТвердаяСтавкаАкцизов Тогда
				ЗадатьЗначениеЭлемента(УзелСвТовар, "СтАкцизТверд", ?(ПрОсвАкциз = 2, "-1", СтАкцизТверд));
			Иначе
				ЗадатьЗначениеЭлемента(УзелСвТовар, "СтАкцизАдвал", ?(ПрОсвАкциз = 2, "-1", СтАкцизАдвал));
			КонецЕсли;
			ЗадатьЗначениеЭлемента(УзелСвТовар, "СтНДС",        ?(ПрОсвНДС = 2,   "-1", СтНДС));
			
			// выгружаем сведения о товарно-транспортных документах
			ШаблонУзелСвТСД = ПолучитьПодчиненныйЭлемент(УзелСвТовар, "СвТСД");
			ТТДокументы = Неопределено;
			Если НЕ мСтрукТЗДокументы.Свойство("К_" + Формат(НомерСтроки, "ЧГ="), ТТДокументы) ИЛИ НЕ ЗначениеЗаполнено(ТТДокументы) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрТТДокумент Из ТТДокументы Цикл
				
				ТТДСерия = ВРЕГ(СокрЛП(СтрТТДокумент.Серия));
				ТТДНомер = ВРЕГ(СокрЛП(СтрТТДокумент.Номер));
				ТТДДата = СтрТТДокумент.Дата;
				
				Если НЕ ЗначениеЗаполнено(ТТДСерия) И НЕ ЗначениеЗаполнено(ТТДНомер) И НЕ ЗначениеЗаполнено(ТТДДата) Тогда
					Продолжить;
				КонецЕсли;
				
				УзелСвТСД = СкопироватьУзел(УзелСвТовар, ШаблонУзелСвТСД);
				ЗадатьЗначениеЭлемента(УзелСвТСД, "СерНомТСД", ТТДСерия + ", " + ТТДНомер);
				ЗадатьЗначениеЭлемента(УзелСвТСД, "ДатаТСД", Формат(ТТДДата, "ДФ=dd.MM.yyyy"));
			КонецЦикла;
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	// Выгрузка раздела 3.
	УзелСвКонтракт3 = ПолучитьПодчиненныйЭлемент(УзелСвЗвл, "СвКонтракт3");
	ЗаполнитьДаннымиУзел(Раздел3, УзелСвКонтракт3);
	
	// Тип продавца.
	стрТипПрод = Раздел3.ТипПрод_3;
	Если стрТипПрод = "(комитент)" Тогда
		ТипПрод_3 = 0;
	ИначеЕсли стрТипПрод = "(доверитель)" Тогда
		ТипПрод_3 = 1;
	ИначеЕсли стрТипПрод = "(принципал)" Тогда
		ТипПрод_3 = 2;
	ИначеЕсли стрТипПрод = "(продавец)" Тогда
		ТипПрод_3 = 6;
	Иначе
		ТипПрод_3 = "";
	КонецЕсли;
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ТипПродР3", ТипПрод_3);
	
	// Тип покупателя.
	стрТипПок = Раздел3.ТипПок_3;
	Если стрТипПок = "(комиссионер)" Тогда
		ТипПок_3 = 3;
	ИначеЕсли стрТипПок = "(поверенный)" Тогда
		ТипПок_3 = 4;
	ИначеЕсли стрТипПок = "(агент)" Тогда
		ТипПок_3 = 5;
	ИначеЕсли стрТипПок = "(покупатель)" Тогда
		ТипПок_3 = 7;
	Иначе
		ТипПок_3 = "";
	КонецЕсли;
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ТипПокР3", ТипПок_3);
	
	Если НЕ ПустаяСтрока(Раздел3.ПродавецБайконур) Тогда
		ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ПрБкнрПродР3", "1");
	КонецЕсли;
	Если НЕ ПустаяСтрока(Раздел3.ПокупательБайконур) Тогда
		ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ПрБкнрПокР3", "1");
	КонецЕсли;
	
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ИдНомПродР3", РазделитьИННКПП(Раздел3.ПродавецИНН_КПП).ИНН);
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "КПППродР3", РазделитьИННКПП(Раздел3.ПродавецИНН_КПП).КПП);
	
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "ИдНомПокР3", РазделитьИННКПП(Раздел3.ПокупательИНН_КПП).ИНН);
	ЗадатьЗначениеЭлемента(УзелСвКонтракт3, "КПППокР3", РазделитьИННКПП(Раздел3.ПокупательИНН_КПП).КПП);
	
	УзелСвКонтрР3 = ПолучитьПодчиненныйЭлемент(УзелСвКонтракт3, "СвКонтрР3");
	ШаблонУзелСвСпециф = ПолучитьПодчиненныйЭлемент(УзелСвКонтрР3, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел3.Количество() Цикл
		Строка = мТЗСпецификацийРаздел3[Сч - 1];
		
		УзелСвСпециф = СкопироватьУзел(УзелСвКонтрР3, ШаблонУзелСвСпециф);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомПСпециф", Сч);
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
		ЗадатьЗначениеЭлемента(УзелСвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	// выгружаем сведения о выписках раздела "Сведения"
	НомерБанкаПП = 0;
	ШаблонУзелСвПлДок = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвПлДок");
	Для Каждого ЛистСведений Из Сведения Цикл
		ДанныеЛиста = ЛистСведений.Данные;
		
		УзелСвПлДок = СкопироватьУзел(УзелРодитель, ШаблонУзелСвПлДок);
		ЗаполнитьДаннымиУзел(ДанныеЛиста,УзелСвПлДок);
		
		ШаблонУзелСвВыпБанк = ПолучитьПодчиненныйЭлемент(УзелСвПлДок, "СвВыпБанк");
		
		ЧислоВыгруженныхСтрок = 0;
		НомерСтроки = 1;
		Пока СвойствоОпределено(ДанныеЛиста, "П000020000101_" + Формат(НомерСтроки, "ЧГ=")) Цикл
			
			Если ЗначениеЗаполнено(ДанныеЛиста["П000020000102_" + Формат(НомерСтроки, "ЧГ=")]) Тогда
				
				ЧислоВыгруженныхСтрок = ЧислоВыгруженныхСтрок + 1;					
				УзелСвВыпБанк = СкопироватьУзел(УзелСвПлДок, ШаблонУзелСвВыпБанк);
				ВыгрузитьСтрокуМногострочнойЧасти(ДанныеЛиста, УзелСвВыпБанк, НомерСтроки);
				ЗадатьЗначениеЭлемента(УзелСвВыпБанк, "НомВыпПП", Формат(ЧислоВыгруженныхСтрок, "ЧГ="));
				
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		Если НЕ УзелПуст(УзелСвПлДок) Тогда
			НомерБанкаПП = НомерБанкаПП + 1;
			ЗадатьЗначениеЭлемента(УзелСвПлДок, "НомБПП", Формат(НомерБанкаПП, "ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
	// выгружаем сведения раздела "Зачет"
	//НомерБанкаПП = 0; //закомментировал - счетчик не нужно обнулять
	ШаблонУзелСвПлДок = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвПлДок");
	Для Каждого ЛистЗачет Из Зачет Цикл
		ДанныеЛиста = ЛистЗачет.Данные;
		
		УзелСвПлДок = СкопироватьУзел(УзелРодитель, ШаблонУзелСвПлДок);
		ЗаполнитьДаннымиУзел(ДанныеЛиста,УзелСвПлДок);
		ЗадатьЗначениеЭлемента(УзелСвПлДок, "ТипПлат", ДанныеЛиста.ТипПлатЗачет);
		
		ШаблонУзелСвЗачет = ПолучитьПодчиненныйЭлемент(УзелСвПлДок, "СвЗачет");
		
		ЧислоВыгруженныхСтрок = 0;
		НомерСтроки = 1;
		Пока СвойствоОпределено(ДанныеЛиста, "П000030000101_" + Формат(НомерСтроки, "ЧГ=")) Цикл
			
			Если ЗначениеЗаполнено(ДанныеЛиста["П000030000102_" + Формат(НомерСтроки, "ЧГ=")]) Тогда
				
				ЧислоВыгруженныхСтрок = ЧислоВыгруженныхСтрок + 1;					
				УзелСвЗачет = СкопироватьУзел(УзелСвПлДок, ШаблонУзелСвЗачет);
				ВыгрузитьСтрокуМногострочнойЧасти(ДанныеЛиста, УзелСвЗачет, НомерСтроки);
				ЗадатьЗначениеЭлемента(УзелСвЗачет, "НомПП", Формат(ЧислоВыгруженныхСтрок, "ЧГ="));
				
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		Если НЕ УзелПуст(УзелСвПлДок) Тогда
			НомерБанкаПП = НомерБанкаПП + 1;
			ЗадатьЗначениеЭлемента(УзелСвПлДок, "НомБПП", Формат(НомерБанкаПП, "ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
	
	// выгружаем приложения
	НомерПриложения = 0;
	ШаблонУзелСвКонтрПр = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвКонтрПр");
	Для Каждого ЛистПриложения Из Приложение Цикл
		
		Индекс = Приложение.Индекс(ЛистПриложения);
		
		ДанныеЛиста = ЛистПриложения.Данные;
		
		УзелСвКонтрПр = СкопироватьУзел(УзелРодитель, ШаблонУзелСвКонтрПр);
		ЗаполнитьДаннымиУзел(ДанныеЛиста,УзелСвКонтрПр);
		
		//тип продавеца
		стрТипПрод = ДанныеЛиста.ТипПрод_Пр;
		Если стрТипПрод = "(комитент)" Тогда
			ТипПрод_Пр = 0;
		ИначеЕсли стрТипПрод = "(доверитель)" Тогда
			ТипПрод_Пр = 1;
		ИначеЕсли стрТипПрод = "(принципал)" Тогда
			ТипПрод_Пр = 2;
		ИначеЕсли стрТипПрод = "(комиссионер)" Тогда
			ТипПрод_Пр = 3;
		ИначеЕсли стрТипПрод = "(поверенный)" Тогда
			ТипПрод_Пр = 4;
		ИначеЕсли стрТипПрод = "(агент)" Тогда
			ТипПрод_Пр = 5;
		ИначеЕсли стрТипПрод = "(продавец)" Тогда
			ТипПрод_Пр = 6;
		Иначе
			ТипПрод_Пр = "";
		КонецЕсли;
		
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ТипПродП", ТипПрод_Пр);
		
		//тип покупателя
		стрТипПок = ДанныеЛиста.ТипПок_Пр;
		Если стрТипПок = "(комитент)" Тогда
			ТипПок_Пр = 0;
		ИначеЕсли стрТипПок = "(доверитель)" Тогда
			ТипПок_Пр = 1;
		ИначеЕсли стрТипПок = "(принципал)" Тогда
			ТипПок_Пр = 2;
		ИначеЕсли стрТипПок = "(комиссионер)" Тогда
			ТипПок_Пр = 3;
		ИначеЕсли стрТипПок = "(поверенный)" Тогда
			ТипПок_Пр = 4;
		ИначеЕсли стрТипПок = "(агент)" Тогда
			ТипПок_Пр = 5;
		ИначеЕсли стрТипПок = "(покупатель)" Тогда
			ТипПок_Пр = 7;
		Иначе
			ТипПок_Пр = "";
		КонецЕсли;
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ТипПокП", ТипПок_Пр);
		
		Если НЕ ПустаяСтрока(ДанныеЛиста.ПродавецБайконур) Тогда
			ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ПрБкнрПродП", "1");
		КонецЕсли;
		Если НЕ ПустаяСтрока(ДанныеЛиста.ПокупательБайконур) Тогда
			ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ПрБкнрПокП", "1");
		КонецЕсли;
	
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ИдНомПродП", РазделитьИННКПП(ДанныеЛиста.ПродавецИНН_КПП).ИНН);
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "КПППродП",   РазделитьИННКПП(ДанныеЛиста.ПродавецИНН_КПП).КПП);
		
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "ИдНомПокП",  РазделитьИННКПП(ДанныеЛиста.ПокупательИНН_КПП).ИНН);
		ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "КПППокП",    РазделитьИННКПП(ДанныеЛиста.ПокупательИНН_КПП).КПП);
		
		УзелСвКонтрП = ПолучитьПодчиненныйЭлемент(УзелСвКонтрПр, "СвКонтрП");
		ШаблонУзелСвСпециф = ПолучитьПодчиненныйЭлемент(УзелСвКонтрП, "СвСпециф");
		
		Для Сч = 1 По мМассивТЗСпецификацийПрил[Индекс].Количество() Цикл
			Строка = мМассивТЗСпецификацийПрил[Индекс][Сч - 1];
			
			УзелСвСпециф = СкопироватьУзел(УзелСвКонтрП, ШаблонУзелСвСпециф);
			ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомПСпециф", Сч);
			ЗадатьЗначениеЭлемента(УзелСвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
			ЗадатьЗначениеЭлемента(УзелСвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
		КонецЦикла;
		
		Если НЕ УзелПуст(УзелСвКонтрПр) Тогда
			НомерПриложения = НомерПриложения + 1;
			ЗадатьЗначениеЭлемента(УзелСвКонтрПр, "НомКонтрПП", Формат(НомерПриложения, "ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанными(ДеревоВыгрузки, Параметры)
	
	ОбработатьУсловныеЭлементы(Параметры, ДеревоВыгрузки);
	ЗаполнитьДаннымиУзел(Параметры, ДеревоВыгрузки);
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

Функция ИмеютсяАналогичныеСоседниеУзлы(Стр)
	
	Возврат (Стр.Родитель.Строки.НайтиСтроки(Новый Структура("Ключ", Стр.Ключ), Ложь).Количество() > 1);
	
КонецФункции

Процедура УдалитьУзел(Узел)
	
	РодительУзла = ?(Узел.Родитель = Неопределено, Узел.Владелец(), Узел.Родитель);
	РодительУзла.Строки.Удалить(Узел);
	
КонецПроцедуры

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (Найти(Узел.Обязательность, "Н") <> 0 ИЛИ Найти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			УдалитьУзел(Узел);
		ИначеЕсли (Найти(Узел.Обязательность, "М") <> 0 ИЛИ Найти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(Найти(Узел.Обязательность, "О") <> 0 ИЛИ Найти(Узел.Обязательность, "O") <> 0, ИмеютсяАналогичныеСоседниеУзлы(Узел), Истина) Тогда
			УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция УзелПуст(Узел)
	
	ПустойУзел = ?(Узел.Формат = "N", Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение)), НЕ ЗначениеЗаполнено(Узел.Значение));
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат ПустойУзел;
	
КонецФункции

Функция УсловиеВыполнено(Параметры, Условие)
	
	Попытка
		РезультатВычисленияВыражения = Вычислить(СтрЗаменить(Условие, "&", "Параметры."));
		Если ТипЗнч(РезультатВычисленияВыражения) <> Тип("Булево") Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не удалось вычислить выражение-условие: """ + Условие + """!", "Титульный", "");
		КонецЕсли;
		Возврат НЕ (РезультатВычисленияВыражения = Ложь);
	Исключение
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не удалось вычислить выражение-условие: """ + Условие + """!", "Титульный", "");
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

Функция СвойствоОпределено(Объект, ИмяСвойства)
	
	ГУИД = Новый УникальныйИдентификатор;
	ВремСтрукт = Новый Структура(ИмяСвойства, ГУИД);
	ЗаполнитьЗначенияСвойств(ВремСтрукт, Объект);
	Возврат (ВремСтрукт[ИмяСвойства] <> ГУИД);
	
КонецФункции

Процедура ДополнитьСтруктуруПараметров(Параметры, Узел)
	
	Если НЕ ПустаяСтрока(Узел.Раздел) Тогда
		Если СвойствоОпределено(мДанныеОтчета, "ПолеТабличногоДокумента" + Узел.Раздел) Тогда
			ПоказателиРаздела = мДанныеОтчета["ПолеТабличногоДокумента" + Узел.Раздел];
			Для Каждого Эл Из ПоказателиРаздела Цикл
				Параметры.Вставить(Эл.Ключ, Эл.Значение);
			КонецЦикла;
		Иначе
			ПоказателиРаздела = мДанныеОтчета[Узел.Раздел];
			Для Каждого Эл Из ПоказателиРаздела Цикл
				Параметры.Вставить(Эл.Ключ, Эл.Значение);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьУсловныеЭлементы(Знач Параметры, Узел)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = Параметры.ИмяКлючевогоУзлаСодержательнойЧасти Тогда // !!!
			Возврат;
		КонецЕсли;
		ДополнитьСтруктуруПараметров(Параметры, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ УсловиеВыполнено(Параметры, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(Параметры, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
		Иначе
			Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
		КонецЕсли;
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДаннымиУзел(Параметры, Узел)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			Если СвойствоОпределено(Параметры, "ИмяКлючевогоУзлаСодержательнойЧасти") И Параметры.ИмяКлючевогоУзлаСодержательнойЧасти = Стр.Код Тогда // !!!
				ЗаполнитьДаннымиУзел(Параметры, Стр);
				ЗаполнитьСодержательнуюЧасть(Стр);
			Иначе
				ЗаполнитьДаннымиУзел(Параметры, Стр);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) И СвойствоОпределено(Параметры, Стр.Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Стр.Ключ]);
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				Ключ = Сред(Стр.ЗначениеПоУмолчанию, 2);
				Если СвойствоОпределено(Параметры, Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Ключ]);
				КонецЕсли;
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеСоответствуетФормату(Стр)
	
	СтатусПроверки = Истина;
	Если Стр.Формат = "Т" ИЛИ Стр.Формат = "T" Тогда // учтем оба варианта: кириллицу и латиницу
		ДлинаЗначения = СтрДлина(Стр.Значение);
		Если ДлинаЗначения < Стр.МинРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Длина значения показателя """ + ПредставлениеПоказателя(Стр) + """ (" + АдресПоказателя(Стр)+ ") меньше минимально возможной (" + ДлинаЗначения + " < " + Стр.МинРазмерность + ")", "Титульный", "");
		КонецЕсли;
		Если ДлинаЗначения > Стр.МаксРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Длина значения показателя """ + ПредставлениеПоказателя(Стр) + """  (" + АдресПоказателя(Стр)+ ") больше максимально возможной (" + ДлинаЗначения + " > " + Стр.МаксРазмерность + ")", "Титульный", "");
		КонецЕсли;
	КонецЕсли;
	Возврат СтатусПроверки;
	
КонецФункции

Функция ПредставлениеПоказателя(Стр)
	
	Возврат ?(ПустаяСтрока(Стр.Представление), Стр.Код, Стр.Представление);
	
КонецФункции

Функция АдресПоказателя(СтрокаПоказателя)
	
	Результат = СтрокаПоказателя.Код;
	
	СтрокаВерхнегоУровня = СтрокаПоказателя.Родитель;
	Пока ЗначениеЗаполнено(СтрокаВерхнегоУровня) Цикл
		Результат = СтрокаВерхнегоУровня.Код + "\" + Результат;
		СтрокаВерхнегоУровня = СтрокаВерхнегоУровня.Родитель;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьДанные(Узел)
	
	СтатусПроверки = Истина;
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если НЕ ЗначениеСоответствуетФормату(Узел) Тогда
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	Для Каждого Стр из Узел.Строки Цикл
		СтатусПроверки = (ПроверитьДанные(Стр) И СтатусПроверки); // именно в указанном порядке для выдачи всех ошибок
	КонецЦикла;
	Возврат СтатусПроверки;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры)
	
	ПотокXML = СоздатьНовыйПотокXML(); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, Параметры); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	ТекстДляЗаписи = "<?xml version=""1.0"" encoding=""windows-1251""?>" + Сред(ТекстДляЗаписи, Найти(ТекстДляЗаписи, Символы.ПС));
	Возврат ТекстДляЗаписи;
	
КонецФункции

Функция СоздатьНовыйПотокXML() Экспорт
	
	ПотокXML = Новый ЗаписьXML();
	ПотокXML.УстановитьСтроку("UTF-8");
	ПотокXML.ЗаписатьОбъявлениеXML();
	ПотокXML.Отступ = Истина;
	Возврат ПотокXML;
	
КонецФункции

Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, Параметры) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		ПотокXML.ЗаписатьАтрибут("xsi:noNamespaceSchemaLocation", Параметры.ИмяФайлаСхемы);
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, Параметры);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда 
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, Параметры);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;

КонецФункции

Функция ЗаписатьТесктНаДиск(ТекстДляЗаписи, ФайлДляЗаписи) Экспорт
	
	ПотокЗаписи = Новый ЗаписьТекста(ФайлДляЗаписи, КодировкаТекста.ANSI);
	ПотокЗаписи.Записать(ТекстДляЗаписи);
	ПотокЗаписи.Закрыть();
	Сообщить("Отчет успешно выгружен в файл """ + ФайлДляЗаписи + """.", СтатусСообщения.Информация);
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
//	ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА АВТОСОХРАНЕНИЯ
//

// Процедура является частью механизма автосохранения 
// и назначается динамически методом ПодключитьОбработчикОжидания()
// Обработчик может подключаться и отключаться из процедуры ПриОткрытии текущей формы и
// процедуры ОсновныеДействияФормыКнопкаВыбратьНажатие формы ФормаНастройки
Процедура ОбработчикСобытияПоТаймеру()
	
	Если РегламентированнаяОтчетность.АвтосохранениеВозможно(ЭтаФорма) Тогда
		Сохранить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура КомментарийОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	Если СокрЛП(Текст) <> СокрЛП(Комментарий) Тогда
		мОбновитьКомментарий = Ложь;	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		мОбновитьКомментарий = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//	ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА ВЫБОРА ИЗ СПИСКА
//

Функция НайтиСвойстваПоказателя(КодПоказателя)
	СвойстваПоказателя = СвойстваПоказателей.Найти(КодПоказателя, "Показатель");
	Если СвойстваПоказателя = Неопределено Тогда
		Для Каждого Стр из СвойстваПоказателей Цикл
			Если Лев(КодПоказателя, СтрДлина(Стр.Показатель)) = Стр.Показатель Тогда
				СвойстваПоказателя = Стр;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат СвойстваПоказателя;
КонецФункции // ()

Процедура СформироватьСпискиВыбора()
	
	МакетСоставаПоказателей = ПолучитьМакет("Списки2012Кв1");
	
	КоллекцияСписковВыбора = Новый Соответствие;
	Для Каждого Область Из МакетСоставаПоказателей.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти = Область.Низ;
			ТаблСписка = Новый ТаблицаЗначений;
			ТаблСписка.Колонки.Добавить("Код", , , МакетСоставаПоказателей.Область(ВерхОбласти, 1, ВерхОбласти, 1).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("Обозначение", , "Обозначение", МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("ПолноеНаименование", ,"Полное наименование", МакетСоставаПоказателей.Область(ВерхОбласти, 3, ВерхОбласти, 3).ШиринаКолонки);
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
				Если КодПоказателя <> "###" Тогда
					НовСтрока = ТаблСписка.Добавить();
					НовСтрока.Код = КодПоказателя;
					НовСтрока.Обозначение = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
					НовСтрока.ПолноеНаименование = СокрП(МакетСоставаПоказателей.Область(НомСтр, 3).Текст);
				КонецЕсли;
			КонецЦикла;
			КоллекцияСписковВыбора.Вставить(Область.Имя, ТаблСписка);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваПоказателей = Новый ТаблицаЗначений;
	СвойстваПоказателей.Колонки.Добавить("Показатель");
	СвойстваПоказателей.Колонки.Добавить("Длина");
	СвойстваПоказателей.Колонки.Добавить("Тип");
	СвойстваПоказателей.Колонки.Добавить("ТекстПриВыборе");
	СвойстваПоказателей.Колонки.Добавить("ТаблицаЗначений");
	
	СвойстваПоказателей.Индексы.Добавить("Показатель");
	
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000104", 1, , "Выбор единицы измерения подакцизного товара", КоллекцияСписковВыбора["ЕдИзмПодАкцТоваров"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000123", 1, , "Выбор единицы измерения дополнительной величины для исчисления налоговой базы", КоллекцияСписковВыбора["ЕдИзмНБ"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПлат", 1, , "Выбор типа платежного документа", КоллекцияСписковВыбора["ТипПлатДокСведения"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПлатЗачет", 1, , "Выбор типа платежного документа", КоллекцияСписковВыбора["ТипПлатДокЗачет"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПрод_3", 1, , "Выбор типа продавца", КоллекцияСписковВыбора["ТипПродавцаРаздел3"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПок_3", 1, , "Выбор типа покупателя", КоллекцияСписковВыбора["ТипПокупателяРаздел3"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПрод_Пр", 1, , "Выбор типа продавца", КоллекцияСписковВыбора["ТипПродавцаПрил"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПок_Пр", 1, , "Выбор типа покупателя", КоллекцияСписковВыбора["ТипПокупателяПрил"]);
КонецПроцедуры

Функция ДобавитьСтрокуОписанияВвода(ТаблицаПараметров, Показатель, Длина, Тип = Неопределено, ТекстПриВыборе = Неопределено, Значения = Неопределено)
	
	НовСтр = ТаблицаПараметров.Добавить();
	НовСтр.Показатель = Показатель;
	НовСтр.Тип = Тип;
	НовСтр.Длина = Длина;
	НовСтр.ТекстПриВыборе = ТекстПриВыборе;
	НовСтр.ТаблицаЗначений = Значения;
	
КонецФункции

Процедура ВвестиПоказатель(Элемент, Область, КодПоказателя, Знач ЭтоСумма, Знач ТекстВыбора, ТаблицаВыбора, СтандартнаяОбработка, КолонкаПоиска = "Код") Экспорт
	
	Если ТаблицаВыбора = Неопределено ИЛИ мАвтоВыборКодов Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	//если показатель входит в состав многострочной части, тогда имя области не будет совпадать с переданным кодом показателя
	//необходимо это учесть
	//считаем, что в многострочной части не может быть показателя, состоящего из нескольких ячеек
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда
		ИсходноеЗначение = ?(ТипЗнч(Область.Значение) = Тип("Строка"), СокрЛП(Область.Значение), Область.Значение);
	Иначе
		Разделитель = ?(Лев(Область.Имя, СтрДлина(КодПоказателя) + 1) = КодПоказателя + "_", "_", "");
		ИндексОбласти = 1;
		ИсходноеЗначение = "";
		Пока ТекущийЭлемент.Области.Найти(КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")) <> Неопределено Цикл
			ИсходноеЗначение = ИсходноеЗначение + Строка(ТекущийЭлемент.Области[КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")].Значение);
			ИндексОбласти = ИндексОбласти + 1;
		КонецЦикла;
		ИсходноеЗначение = СокрЛП(ИсходноеЗначение);
	КонецЕсли;
	ИсходнаяСтрока = ТаблицаВыбора.Найти(ИсходноеЗначение, КолонкаПоиска);
	
	Если ИсходнаяСтрока = Неопределено Тогда
		РезультатВыбора = ТаблицаВыбора.ВыбратьСтроку(ТекстВыбора);
	Иначе
		РезультатВыбора = ТаблицаВыбора.ВыбратьСтроку(ТекстВыбора, ИсходнаяСтрока);
	КонецЕсли;
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//если показатель входит в состав многострочной части, тогда имя области не будет совпадать с переданным кодом показателя
	//необходимо это учесть
	//считаем, что в многострочной части не может быть показателя, состоящего из нескольких ячеек
	
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда
		Область.Значение = РезультатВыбора[КолонкаПоиска];
	Иначе
		СтрокаРезультатВыбора = Строка(РезультатВыбора[КолонкаПоиска]);
		Для Инд = СтрДлина(СтрокаРезультатВыбора) + 1 По ИндексОбласти - 1 Цикл
			СтрокаРезультатВыбора = " " + РезультатВыбора[КолонкаПоиска];
		КонецЦикла;
		Для Инд = 1 По ИндексОбласти - 1 Цикл
			ТекущийЭлемент.Области[КодПоказателя + Разделитель + Формат(Инд, "ЧГ=")].Значение = СокрЛП(Сред(РезультатВыбора[КолонкаПоиска], Инд, 1));
		КонецЦикла;
	КонецЕсли;
	
	// Изменение связанных данных.
	Если КодПоказателя = "П000010000104" Тогда
		
		НомерСтроки = Сред(Область.Имя, 15);
		
		Область.Значение = РезультатВыбора.Обозначение;
		Элемент.Области["П000010000122_" + НомерСтроки].Значение = РезультатВыбора.Код;
		
		СтруктураГруппы1 = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, СтруктураГруппы1);
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000104 = РезультатВыбора.Обозначение;
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000122 = РезультатВыбора.Код;
		
	КонецЕсли;
	
	Если КодПоказателя = "П000010000123" Тогда
		
		НомерСтроки = Сред(Область.Имя, 15);
		
		Область.Значение = РезультатВыбора.Обозначение;
		Элемент.Области["П000010000124_" + НомерСтроки].Значение = РезультатВыбора.Код;
		
		СтруктураГруппы1 = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, СтруктураГруппы1);
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000104 = РезультатВыбора.Обозначение;
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000122 = РезультатВыбора.Код;
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ОбработкаИзмененияСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

// Создаёт структуру спецификаций
// Возвращаемое значение: структура
//
Функция СоздатьТЗСпецификаций()

	ТЗСпецификаций = Новый ТаблицаЗначений;		
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
    ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	ОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);
	                                                                                                	
	ТЗСпецификаций.Колонки.Добавить("Дата", ОписаниеТиповДата, "Дата спец.", 20);
	ТЗСпецификаций.Колонки.Добавить("Номер", ОписаниеТиповСтрока50, "Номер спец.", 20); 
	
	Возврат ТЗСпецификаций;	

КонецФункции // СоздатьТЗСпецификаций()

// Анализирует значения текущей строки в столбцах 16, 17, 18, 21(вспомогательный)
//
// Параметры
//  <СтруктураСтавок>  - структура содержащая значения в столбцах 16, 17, 18, 21 в текущей строке
//
Функция АнализироватьПоляСтавки(СтруктураСтавок)

	СтруктураСтавокМодиф = Новый Структура;
	
	Если СтруктураСтавок.Ст18 = "Льгота" Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаНДС", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель1", 2);
	Иначе 	
		СтруктураСтавокМодиф.Вставить("СтавкаНДС", ?(СтруктураСтавок.Ст18 = "", 0, Число(СтруктураСтавок.Ст18)));
		СтруктураСтавокМодиф.Вставить("Переключатель1", 1);
	КонецЕсли;
	
	Если СтруктураСтавок.Ст16 = "" И СтруктураСтавок.Ст17 = "" Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза",0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	ИначеЕсли СтруктураСтавок.Ст16 = "Льгота" И Число(СтруктураСтавок.Ст17) = 0 Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 2);
	ИначеЕсли СтруктураСтавок.Ст16 = "-" И СтруктураСтавок.Ст17 = "-" Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		
		Если СтруктураСтавок.Ст21 = " " Тогда
			СтруктураСтавокМодиф.Вставить("Переключатель3", 4);
		Иначе
			СтруктураСтавокМодиф.Вставить("Переключатель3", 3);
		КонецЕсли;
		
	ИначеЕсли СтруктураСтавок.Ст17 = "Льгота" И Число(СтруктураСтавок.Ст16) = 0 Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 2);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 2);
	ИначеЕсли ?(СтруктураСтавок.Ст17 = "", 0, Число(СтруктураСтавок.Ст17)) = 0  Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", ?(СтруктураСтавок.Ст16 = "", 0, Число(СтруктураСтавок.Ст16)));
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	Иначе 
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", ?(СтруктураСтавок.Ст17 = "", 0, Число(СтруктураСтавок.Ст17)));
		СтруктураСтавокМодиф.Вставить("Переключатель2", 2);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	КонецЕсли;
	
Возврат СтруктураСтавокМодиф

КонецФункции // АнализироватьПоляСтавки()

////////////////////////////////////////////////////////////////////////////////

Процедура СтатусНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.НачалоВыбораСтатусаВРегламентированномОтчете(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура СтатусПриИзменении(Элемент)
	
	РегламентированнаяОтчетность.ПриИзмененииСтатусаВРегламентированномОтчете(ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура СтатусОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РегламентированнаяОтчетность.ОбработкаОповещенияОбИзмененииСтатусаРегламентированногоОтчета(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

Процедура КнопкаПредыдущаяСтраницаПриложениеНажатие(Элемент)
	ПоказатьСтраницуМногостраничногоРаздела(-1, "Приложение");
КонецПроцедуры

Процедура КнопкаСледующаяСтраницаПриложениеНажатие(Элемент)
	
	ПоказатьСтраницуМногостраничногоРаздела(1, "Приложение");

КонецПроцедуры

Процедура КнопкаДобавитьДопСтраницуПриложениеНажатие(Элемент)
	
	ВставитьДополнительнуюСтраницу("Приложение");
	
	Элемент = СоздатьТЗСпецификаций(); 
    мМассивТЗСпецификацийПрил.Добавить(Элемент);

КонецПроцедуры

Процедура КнопкаУдалитьДопСтраницуПриложениеНажатие(Элемент)
	
	
	УдалитьДополнительнуюСтраницу("Приложение");
	
КонецПроцедуры

Процедура КнопкаПерейтиНаСтраницуПриложениеНажатие(Элемент)
	
	РегламентированнаяОтчетность.роОткрытьФормуСпискаДопСтраниц(ЭтаФорма, "Приложение");
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаРаздел1R10C52ПриИзменении(Элемент)
	Перем ТаблицаСтраниц;
		
	Номер = Элемент.Значение;
	
	Для Каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		
		// Многостраничность - не записываем в Показатели отчета !!!
		//
		Если Не мСтруктураМногостраничныхРазделов.Свойство(СтраницаПанели.Имя, ТаблицаСтраниц) Тогда
			Если СтраницаПанели.Имя = "Раздел1" Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + СтраницаПанели.Имя;
			ТекТабличноеПоле        = ЭлементыФормы[ИмяТекТабличногоПоля];
			
			ТекТабличноеПоле.Области.НомерЗаяв.Защита = Ложь;
			ТекТабличноеПоле.Области.НомерЗаяв.Значение = Номер;
			ТекТабличноеПоле.Области.НомерЗаяв.Защита = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	РегламентированнаяОтчетность.роОбновитьСтруктуруМногостраничныхРазделов(ЭтаФорма, "Приложение");
	
	Для Каждого ДопСтр Из мСтруктураМногостраничныхРазделов.Приложение Цикл
		 ДопСтр.Данные.НомерЗаяв = Номер;
	 КонецЦикла;	
	 мПриложение.Области.НомерЗаяв.Защита = Ложь;
	 мПриложение.Области.НомерЗаяв.Значение = Номер;
	 мПриложение.Области.НомерЗаяв.Защита = Истина;

	
КонецПроцедуры

Процедура КнопкаПредыдущаяСтраницаЗачетНажатие(Элемент)
	ПоказатьСтраницуМногостраничногоРаздела(-1, "Зачет");
КонецПроцедуры

Процедура КнопкаСледующаяСтраницаЗачетНажатие(Элемент)
	ПоказатьСтраницуМногостраничногоРаздела(1, "Зачет");
КонецПроцедуры

Процедура КнопкаДобавитьДопСтраницуЗачетНажатие(Элемент)
	ВставитьДополнительнуюСтраницу("Зачет");
КонецПроцедуры

Процедура КнопкаУдалитьДопСтраницуЗачетНажатие(Элемент)
	УдалитьДополнительнуюСтраницу("Зачет");
КонецПроцедуры

Процедура КнопкаПерейтиНаСтраницуЗачетНажатие(Элемент)
	РегламентированнаяОтчетность.роОткрытьФормуСпискаДопСтраниц(ЭтаФорма, "Зачет");
КонецПроцедуры

Процедура КоманднаяПанельФормыДействиеОтправка(Кнопка)
	
	РегламентированнаяОтчетность.ПриНажатииНаКнопкуОтправкиВРегламентированномОтчете(ЭтаФорма, Кнопка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСоставПоказателей = "СоставПоказателей2007Кв1";

мОписаниеТиповЧислоЕ = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2);

// Список выбора поля выбора "Единица измерения"
ЭлементыФормы.ПолеВыбораЕдиницаИзмерения.СписокВыбора.Добавить(Перечисления.ПорядкиОкругленияОтчетности.Окр1,       "в рублях");
ЭлементыФормы.ПолеВыбораЕдиницаИзмерения.СписокВыбора.Добавить(Перечисления.ПорядкиОкругленияОтчетности.Окр1000,    "в тысячах рублей");
ЭлементыФормы.ПолеВыбораЕдиницаИзмерения.СписокВыбора.Добавить(Перечисления.ПорядкиОкругленияОтчетности.Окр1000000, "в миллионах рублей");

мИдГруппы1  = "П0000100001";
мИдГруппы2  = "П0000200001";
мИдГруппы3  = "П0000300001";

мМаскиЯчейки = Новый Соответствие;
мМаскиЯчейки.Вставить("П000010000103", "9999999999");

// количество строк многострочных разделов по "бумажной" форме отчета
// (минимальное количество строк, которое должно присутствовать всегда)
КолИсхСтрокИдГруппы1 = 1;
КолИсхСтрокИдГруппы2 = 1;
КолИсхСтрокИдГруппы3 = 1;

мОписаниеТиповЧисло4    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(4, 0);
мОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1, 0);
мОписаниеТиповСтрока500 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(500);
мОписаниеТиповСтрока10  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(10);
мОписаниеТиповСтрока16  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(16);
мОписаниеТиповСтрока3   = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(3);
мОписаниеТиповСтрока1   = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(1);
мОписаниеТиповЧисло16_6 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(16, 6);
мОписаниеТиповЧисло9_4  = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(9, 4);
мОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);
мОписаниеТиповСтрока20  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(20);
мОписаниеТиповЧисло20_6 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(20, 6);
мОписаниеТиповЧисло15_2 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2);
мОписаниеТиповЧисло5_2  = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(5, 2);
мОписаниеТиповСтрока254 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);
мОписаниеТиповСтрокаНеогр = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(0);

МассивБулево = Новый Массив;
МассивБулево.Добавить(Тип("Булево"));
мОписаниеТиповБулево    = Новый ОписаниеТипов(МассивБулево);

МассивТипов = Новый Массив;
МассивТипов.Добавить(Тип("Дата"));
мОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));

// структура строк многострочного раздела на странице
СтруктураГруппы1 = Новый ТаблицаЗначений;
СтруктураГруппы1.Колонки.Добавить("П000010000101", мОписаниеТиповЧисло4);
СтруктураГруппы1.Колонки.Добавить("П000010000102", мОписаниеТиповСтрока500);
СтруктураГруппы1.Колонки.Добавить("П000010000103", мОписаниеТиповСтрока10); 
СтруктураГруппы1.Колонки.Добавить("П000010000104", мОписаниеТиповСтрока50);
СтруктураГруппы1.Колонки.Добавить("П000010000105", мОписаниеТиповЧисло16_6);
СтруктураГруппы1.Колонки.Добавить("П000010000106", мОписаниеТиповЧисло15_2);
СтруктураГруппы1.Колонки.Добавить("П000010000107", мОписаниеТиповСтрока3);
СтруктураГруппы1.Колонки.Добавить("П000010000108", мОписаниеТиповЧисло9_4);
СтруктураГруппы1.Колонки.Добавить("П000010000109", мОписаниеТиповСтрокаНеогр);
СтруктураГруппы1.Колонки.Добавить("П000010000110", мОписаниеТиповСтрокаНеогр);
СтруктураГруппы1.Колонки.Добавить("П000010000111", мОписаниеТиповСтрока50);
СтруктураГруппы1.Колонки.Добавить("П000010000112", мОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000113", мОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000114", мОписаниеТиповЧисло20_6);
СтруктураГруппы1.Колонки.Добавить("П000010000115", мОписаниеТиповЧисло15_2);
СтруктураГруппы1.Колонки.Добавить("П000010000116", мОписаниеТиповСтрока16); 
СтруктураГруппы1.Колонки.Добавить("П000010000117", мОписаниеТиповСтрока16); 
СтруктураГруппы1.Колонки.Добавить("П000010000118", мОписаниеТиповСтрока16); 
СтруктураГруппы1.Колонки.Добавить("П000010000119", мОписаниеТиповЧисло15_2); 
СтруктураГруппы1.Колонки.Добавить("П000010000120", мОписаниеТиповЧисло15_2);
СтруктураГруппы1.Колонки.Добавить("П000010000121", мОписаниеТиповСтрока1);
СтруктураГруппы1.Колонки.Добавить("П000010000122", мОписаниеТиповСтрока3);
СтруктураГруппы1.Колонки.Добавить("П000010000123", мОписаниеТиповСтрока50); // Область выбора и представления единицы измерения налоговой базы
СтруктураГруппы1.Колонки.Добавить("П000010000124", мОписаниеТиповСтрока3);  // Скрытая область для хранения кода единицы измерения налоговой базы

СтруктураГруппы2 = Новый ТаблицаЗначений;
СтруктураГруппы2.Колонки.Добавить("П000020000101", мОписаниеТиповДата);
СтруктураГруппы2.Колонки.Добавить("П000020000102", мОписаниеТиповЧисло15_2);
СтруктураГруппы2.Колонки.Добавить("П000020000103", мОписаниеТиповСтрока20);

СтруктураГруппы3 = Новый ТаблицаЗначений;
СтруктураГруппы3.Колонки.Добавить("П000030000101", мОписаниеТиповДата);
СтруктураГруппы3.Колонки.Добавить("П000030000102", мОписаниеТиповЧисло15_2);

// структура многострочных разделов формы
мСтруктураМногострочныхРазделов = Новый Структура;
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, СтруктураГруппы1);
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы2, СтруктураГруппы2);
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы3, СтруктураГруппы3);

// структура исходных строк (минимального кол-ва) многострочных разделов формы
мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы1, КолИсхСтрокИдГруппы1);
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы2, КолИсхСтрокИдГруппы2);
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы3, КолИсхСтрокИдГруппы3);

ЦветЗаполняетсяТолькоВручную     = Новый Цвет(255, 255, 225);
ЦветЗаполняетсяАвтоКорректир     = Новый Цвет(255, 240, 200);
ЦветЗаполняетсяАвтоБезКорректир  = Новый Цвет(230, 240, 220);
ЦветЗаполняетсяАвтоАрифметика    = Новый Цвет(192, 220, 192);  // ЗЕЛЕНЫЙ
ЦветЗаполняетсяОднозначноВручную = Новый Цвет(255, 255, 192);  // ЖЕЛТЫЙ
ЦветЗаполняетсяАвтоматически     = Новый Цвет(255, 255, 255);  // Белый, Ввод запрещен

// структура цветов раскраски автозаполняемых ячеек
мСтруктураЦветовРаскраски = Новый Структура;
мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);
мСтруктураЦветовРаскраски.Вставить("_4", ЦветЗаполняетсяАвтоАрифметика);
мСтруктураЦветовРаскраски.Вставить("_0", ЦветЗаполняетсяОднозначноВручную);
мСтруктураЦветовРаскраски.Вставить("_5", ЦветЗаполняетсяАвтоматически);


// Таблица значений хранит данные дополнительной страницы многостраничных разделов отчета.
// В колонках таблицы хранятся следующие данные:
//    - имя дополнительной страницы (отображается в списке дополнительных страниц);
//    - булево, признак текущей страницы (отображенной в поле табличного документа);
//    - структура, содержащая имена и значения редактируемых ячеек дополнительной страницы;
//    - таблица значений, содержит данные строк многострочного блока показателей страницы.
//
ТаблицаСтраницыРазделаДляМногострочныхРазделов            = Новый ТаблицаЗначений;
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Представление",    мОписаниеТиповСтрока254, "Наименование");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АктивнаяСтраница", мОписаниеТиповБулево);
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Данные");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("ДанныеДопСтрок");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АвтоматическоеПредставление", мОписаниеТиповБулево);

ТаблицаСтраницыСведения = ТаблицаСтраницыРазделаДляМногострочныхРазделов.Скопировать();
СтрПерваяСтраница = ТаблицаСтраницыСведения.Добавить();
СтрПерваяСтраница.Представление      = "Новая страница";
СтрПерваяСтраница.ДанныеДопСтрок = Неопределено; // В разделе есть многострочность, поэтому поле объявлено
СтрПерваяСтраница.АктивнаяСтраница   = Истина;
СтрПерваяСтраница.АвтоматическоеПредставление = Истина;

ТаблицаСтраницыЗачет = ТаблицаСтраницыРазделаДляМногострочныхРазделов.Скопировать();
СтрПерваяСтраница = ТаблицаСтраницыЗачет.Добавить();
СтрПерваяСтраница.Представление      = "Новая страница";
СтрПерваяСтраница.ДанныеДопСтрок = Неопределено; // В разделе есть многострочность, поэтому поле объявлено
СтрПерваяСтраница.АктивнаяСтраница   = Истина;
СтрПерваяСтраница.АвтоматическоеПредставление = Истина;


ТаблицаСтраницыРаздела = РегламентированнаяОтчетность.ПолучитьТаблицуМногостраничногоРаздела();
ТаблицаСтраницыРаздела.Колонки.Добавить("ДополнительныеДанные");

// Структура содержит данные дополнительных страниц раздела 1
мСтруктураМногостраничныхРазделов = Новый Структура;
мСтруктураМногостраничныхРазделов.Вставить("Сведения", ТаблицаСтраницыСведения);
мСтруктураМногостраничныхРазделов.Вставить("Зачет", ТаблицаСтраницызачет);
мСтруктураМногостраничныхРазделов.Вставить("Приложение", ТаблицаСтраницыРаздела);

//// ОПИСАНИЕ ПАРАМЕТРОВ ФОРМЫ ОТЧЕТА
//
// Версия формы
мВерсияФормы = "20/07/2012";

мКодОтчета = "1151099";

ТаблицаСообщений = Новый ТаблицаЗначений;
ТаблицаСообщений.Колонки.Добавить("Раздел");
ТаблицаСообщений.Колонки.Добавить("Графа");
ТаблицаСообщений.Колонки.Добавить("Строка");
ТаблицаСообщений.Колонки.Добавить("Страница");
ТаблицаСообщений.Колонки.Добавить("СтрокаПП");
ТаблицаСообщений.Колонки.Добавить("ИмяЯчейки");
ТаблицаСообщений.Колонки.Добавить("Описание");

мПечатныеформы = Новый СписокЗначений;
мСтрукТЗДокументы = Новый Структура;