Процедура УстановитьРуководителя()
	
	Организация = ЭтотОбъект.Организация;
	Если Организация = Справочники.Организации.ПустаяСсылка() Тогда 
		Возврат;
	КонецЕсли;
	
	ОтветственныеЛица = ОбщегоНазначения.ОтветственныеЛица(Организация, ТекущаяДата());
	ЭтотОбъект.Руководитель = ОтветственныеЛица.Руководитель;
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьРуководителя();
	
КонецПроцедуры

Процедура ПриОткрытии()

	Период = НачалоКвартала(ТекущаяДата());
	СформироватьОтчетПоСтандартнойФорме = Истина;
	ОбновитьНадписьКвартал();
	
	// Установка настроек печати по умолчанию. Если настройки были изменены, они будут загружены при формировании отчета
	ЭлементыФормы.Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ЭлементыФормы.Результат.АвтоМасштаб = Истина;

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	УстановитьРуководителя();
	
КонецПроцедуры

Процедура ОбновитьНадписьКвартал()
	
	ЭлементыФормы.НадписьКвартал.Заголовок = ПредставлениеПериода(НачалоКвартала(Период), КонецКвартала(Период), "ФП = Истина");

КонецПроцедуры

Процедура КнопкаКварталНазадНажатие(Элемент)
	
	Период = КонецКвартала(ДобавитьМесяц(Период, -3));
	ОбновитьНадписьКвартал();
	
КонецПроцедуры

Процедура КнопкаКварталВпередНажатие(Элемент)
	
	Период = КонецКвартала(ДобавитьМесяц(Период, 3));
	ОбновитьНадписьКвартал();
	
КонецПроцедуры

Процедура ДействияФормыДействиеСформировать(Кнопка)
	
	ОбновитьОтчет(ЭлементыФормы.Результат);
	
КонецПроцедуры


Процедура КоманднаяПанельНастройка(Кнопка)
	
	ФормаНастройки = ПолучитьФорму("ФормаНастройки");
	
	ФормаНастройки.ГруппироватьПоКонтрагентам = ГруппироватьПоКонтрагентам;
	ФормаНастройки.КонтрагентДляОтбора = КонтрагентДляОтбора;
	ФормаНастройки.ОтбиратьПоКонтрагенту = ОтбиратьПоКонтрагенту;
	ФормаНастройки.СформироватьОтчетПоСтандартнойФорме = СформироватьОтчетПоСтандартнойФорме;
    ФормаНастройки.Руководитель = Руководитель;
	
	Если ФормаНастройки.ОткрытьМодально() = Истина Тогда
		
		ГруппироватьПоКонтрагентам = ФормаНастройки.ГруппироватьПоКонтрагентам;
		КонтрагентДляОтбора = ФормаНастройки.КонтрагентДляОтбора;
		ОтбиратьПоКонтрагенту = ФормаНастройки.ОтбиратьПоКонтрагенту;
		СформироватьОтчетПоСтандартнойФорме = ФормаНастройки.СформироватьОтчетПоСтандартнойФорме;
	    Руководитель 	= ФормаНастройки.Руководитель;
		
		ОбновитьОтчет(ЭлементыФормы.Результат);
			
	КонецЕсли;	
	
КонецПроцедуры