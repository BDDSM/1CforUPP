// Процедура вызывает необходимый отчет вместо стандартной обработки расшифровки
//
Процедура ВызватьОтчетРасшифровку(Расшифровка)

	ВидОтчета = Расшифровка.ВидОтчета;
	Номенклатура = Расшифровка.Номенклатура;
	ХарактеристикаНоменклатуры = Расшифровка.ХарактеристикаНоменклатуры;
	ОтчетРасшифровка = Отчеты[ВидОтчета].Создать();
	ОтчетНаСистемеКомпоновки = Расшифровка.Свойство("ОтчетНаСистемеКомпоновки");
	Если не ОтчетНаСистемеКомпоновки Тогда
		ОтчетРасшифровка.УстановитьНачальныеНастройки();
		ОтчетРасшифровка.УниверсальныйОтчет.ОчиститьНастройкиПостроителя();
		ОтчетРасшифровка.УниверсальныйОтчет.мВосстанавливатьНастройкиПриОткрытии = Ложь;
	КонецЕсли;
	Если ВидОтчета = "ТоварыВРезервеНаСкладах" Тогда
	
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ДокументРезерва");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Номенклатура");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеКолонки("Склад");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьОтбор("ДокументРезерва", Истина, ВидСравнения.Равно, Заказ,,, Ложь);
		
	ИначеЕсли ВидОтчета = "РазмещенияВЗаказах" Тогда
	    ОтчетРасшифровка.Размещение_ЗаказыПоставщикам 		= истина;
		ОтчетРасшифровка.Размещение_ЗаказыНаПроизводство 	= истина;
		ОтчетРасшифровка.Размещение_ВнутренниеЗаказы 		= истина;
		ОтчетРасшифровка.Потребитель_ЗаказыПокупателей 		= истина;
		ОтчетРасшифровка.Потребитель_ВнутренниеЗаказы 		= истина;
		ОтчетРасшифровка.Потребитель_ЗаказыНаПроизводство 	= истина;
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ЗаказПоставщику");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Номенклатура");
		
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьОтбор("ЗаказПокупателя", Истина, ВидСравнения.Равно, Заказ,,, Ложь);
		
	ИначеЕсли ВидОтчета = "ВедомостьПроизводственныеЗатраты" Тогда
	
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Заказ");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Затрата");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Подразделение");
	
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьОтбор("Заказ", Истина, ВидСравнения.Равно, Заказ,,, Ложь);
	ИначеЕсли ВидОтчета = "АнализДоступностиТоваровНаСкладах" Тогда
		Если не ЗначениеЗаполнено(Номенклатура) Тогда
			Возврат;
		КонецЕсли;
		
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Номенклатура");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ХарактеристикаНоменклатуры");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Склад");
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьОтбор("Номенклатура", Истина, ВидСравнения.Равно, Номенклатура,,, Ложь);
		ОтчетРасшифровка.УниверсальныйОтчет.ДобавитьОтбор("ХарактеристикаНоменклатуры", Истина, ВидСравнения.Равно, ХарактеристикаНоменклатуры,,, Ложь);

	ИначеЕсли ВидОтчета = "ВедомостьПоУчетуЗатрат" Тогда
		
		ФормаРасшифровки = ОтчетРасшифровка.ПолучитьФорму();
		ФормаРасшифровки.ЭтоОтработкаРасшифровки = истина;

        НастройкаКомпоновкиДанных = ОтчетРасшифровка.КомпоновщикНастроек.Настройки;
		НастройкаКомпоновкиДанных.ПараметрыДанных.Элементы.Найти("ВидУчета").Значение = ?(Расшифровка.ВидОтраженияВУчете=Перечисления.ВидыОтраженияВУчете.ОтражатьВРегламентированномУчете,1,0);

		Отбор = новый Массив();
		Отбор.Добавить(новый Структура("Поле,Иерархия,Значение","Заказ",ложь,Заказ));
		Отбор.Добавить(новый Структура("Поле,Иерархия,Значение","РазделУчета",ложь,Перечисления.РазделыУчета.Затраты));
		ОтчетРасшифровка.Настроить(Отбор);
		
		ФормаРасшифровки.Открыть();
        ФормаРасшифровки.ОбновитьОтчет();

		Возврат;
	Иначе
	
		Возврат;
		
	КонецЕсли;
	
	ФормаРасшифровки = ОтчетРасшифровка.ПолучитьФорму();
	ФормаРасшифровки.Открыть();
	ФормаРасшифровки.ОбновитьОтчет();
КонецПроцедуры // ВызватьОтчетРасшифровку()

// Процедура - обработчик события "Нажатие"
// 
Процедура КоманднаяПанель1Сформировать(Кнопка)

	Если НЕ ЗначениеЗаполнено(Заказ) Тогда
		ОбщегоНазначения.Сообщение("Не выбран заказ на производство!", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	ОбновитьОтчет(ЭлементыФормы.ТабДокумент);
	
КонецПроцедуры // КоманднаяПанель1Сформировать()

Процедура ПриОткрытии()

	КонДата = ТекущаяДата();
    УстановитьЗаголовокФормы();
КонецПроцедуры // ПриОткрытии()

Процедура ЗаказОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // ЗаказОчистка()

Процедура ТабДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		ВызватьОтчетРасшифровку(Расшифровка);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаказПриИзменении(Элемент)
	УстановитьЗаголовокФормы();
КонецПроцедуры

Процедура УстановитьЗаголовокФормы()
	Заголовок = "Анализ заказа на производство";
	Если ЗначениеЗаполнено(Заказ) Тогда
		Заголовок = "Анализ заказа на производство: "+СокрЛП(Заказ);
	КонецЕсли;

КонецПроцедуры


ЭлементыФормы.Заказ.Доступность = НЕ ЗапретВыбораЗаказа;