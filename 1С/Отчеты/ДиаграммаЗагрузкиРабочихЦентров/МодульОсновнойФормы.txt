Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПостроительОтчета.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗанятостьРабочихЦентров.Заказ КАК Заказ,
	|	ПРЕДСТАВЛЕНИЕ(ЗанятостьРабочихЦентров.Заказ) КАК ЗаказПредставление,
	|	ЗанятостьРабочихЦентров.РабочийЦентр КАК РабочийЦентр,
	|	ПРЕДСТАВЛЕНИЕ(ЗанятостьРабочихЦентров.РабочийЦентр) КАК РабочийЦентрПредставление,
	|	ЗанятостьРабочихЦентров.Период КАК ПериодНачало,
	|	ЗанятостьРабочихЦентров.ДатаОкончания КАК ПериодОкончание
	|ИЗ
	|	РегистрСведений.ДанныеДляПланированияЗанятостиРабочихЦентров КАК ЗанятостьРабочихЦентров
	|ГДЕ
	|	ЗанятостьРабочихЦентров.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРабочихЦентров.Занят)
	|{ГДЕ
	|	ЗанятостьРабочихЦентров.Проект.* КАК Проект,
	|	ЗанятостьРабочихЦентров.Заказ.* КАК Заказ,
	|	ЗанятостьРабочихЦентров.РабочийЦентр.* КАК РабочийЦентр,
	|	ЗанятостьРабочихЦентров.Период КАК ПериодНачало,
	|	ЗанятостьРабочихЦентров.ДатаОкончания КАК ПериодОкончание}
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗанятостьРабочихЦентров.Заказ,
	|	ЗанятостьРабочихЦентров.РабочийЦентр,
	|	ЗанятостьРабочихЦентров.Период,
	|	ЗанятостьРабочихЦентров.ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	Заказ,
	|	РабочийЦентр,
	|	ПериодНачало,
	|	ПериодОкончание
	|ИТОГИ ПО
	|	Заказ,
	|	РабочийЦентр";
	
	ПостроительОтчета.ДоступныеПоля.Проект.Представление = "Проект";
	ПостроительОтчета.ДоступныеПоля.Заказ.Представление = "Заказ";
	ПостроительОтчета.ДоступныеПоля.РабочийЦентр.Представление = "Рабочий центр";
	ПостроительОтчета.ДоступныеПоля.ПериодНачало.Представление = "Начало периода";
	ПостроительОтчета.ДоступныеПоля.ПериодОкончание.Представление = "Окончание периода";
	
	ПостроительОтчета.Отбор.Добавить("ПериодНачало");
	ПостроительОтчета.Отбор.ПериодНачало.ВидСравнения = ВидСравнения.БольшеИлиРавно;
	ЭлементыФормы.ДатаНач.Данные = "ПостроительОтчета.Отбор.ПериодНачало.Значение";
	
	ПостроительОтчета.Отбор.Добавить("ПериодОкончание");
	ПостроительОтчета.Отбор.ПериодОкончание.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
	ЭлементыФормы.ДатаКон.Данные = "ПостроительОтчета.Отбор.ПериодОкончание.Значение";
	
	ЭлементыФормы.Отбор.НастройкаОтбора.Добавить("ПериодНачало", Ложь);
	ЭлементыФормы.Отбор.НастройкаОтбора.Добавить("ПериодОкончание", Ложь);
	
КонецПроцедуры // ПередОткрытием()

Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	НП = Новый НастройкаПериода;
	
	Если НП.Редактировать() Тогда
		
		ПостроительОтчета.Отбор.ПериодНачало.Значение    = НП.ПолучитьДатуНачала();
		ПостроительОтчета.Отбор.ПериодОкончание.Значение = НП.ПолучитьДатуОкончания();

	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

Процедура КоманднаяПанельФормыСформировать(Кнопка)
	
	Диаграмма = ЭлементыФормы.ДиаграммаГанта;
	Диаграмма.Очистить();
	Диаграмма.АвтоОпределениеПолногоИнтервала = Истина;
	
	Если НЕ ЗначениеЗаполнено(ПостроительОтчета.Отбор.ПериодНачало.Значение) Тогда
		
		ПостроительОтчета.Отбор.ПериодНачало.Использование = Ложь;
		
	Иначе
		
		ПостроительОтчета.Отбор.ПериодНачало.Использование = Истина;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПостроительОтчета.Отбор.ПериодОкончание.Значение) Тогда
		
		ПостроительОтчета.Отбор.ПериодОкончание.Использование = Ложь;
		
	Иначе
		
		ПостроительОтчета.Отбор.ПериодОкончание.Использование = Истина;
		
	КонецЕсли;
	
	ПостроительОтчета.Выполнить();
	
	Если ПостроительОтчета.Результат.Пустой() Тогда
		
		Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
		Диаграмма.УстановитьПолныйИнтервал(ТекущаяДата(), ТекущаяДата());
		Возврат;
		
	КонецЕсли;
	
	//Заказы
	ВыборкаЗаказ = ПостроительОтчета.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказ.Следующий() Цикл
		
		СерияЗаказ = Диаграмма.УстановитьСерию(ВыборкаЗаказ.Заказ);
		
		Если ВыборкаЗаказ.Заказ = Неопределено ИЛИ ВыборкаЗаказ.Заказ.Пустая() Тогда
			
			СерияЗаказ.Текст = "Без заказа";
			
		Иначе
			
			СерияЗаказ.Текст = ВыборкаЗаказ.ЗаказПредставление;
			
		КонецЕсли;

		//Рабочие центры
		ВыборкаРЦ = ВыборкаЗаказ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРЦ.Следующий() Цикл
			
			ТочкаРЦ = Диаграмма.УстановитьТочку(ВыборкаРЦ.РабочийЦентр);
			ТочкаРЦ.Текст = ВыборкаРЦ.РабочийЦентрПредставление;
			
			//Периоды
			ВыборкаПериоды = ВыборкаРЦ.Выбрать(ОбходРезультатаЗапроса.Прямой);
			Пока ВыборкаПериоды.Следующий() Цикл
				
				ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаРЦ, СерияЗаказ);
				Интервал = ЗначениеПериод.Добавить();
				Интервал.Начало = ВыборкаПериоды.ПериодНачало;
				Интервал.Конец  = ВыборкаПериоды.ПериодОкончание;
				
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЦикла;
	
	Диаграмма.ПоказатьУровеньТочек(1);
	
КонецПроцедуры // ДатаКонПриИзменении()

Процедура ДатаНачПриИзменении(Элемент)
	
	Элемент.Значение = НачалоДня(Элемент.Значение);
	
КонецПроцедуры // ДатаНачПриИзменении()

Процедура ДатаКонПриИзменении(Элемент)
	
	Элемент.Значение = КонецДня(Элемент.Значение);
	
КонецПроцедуры // ДатаКонПриИзменении()

Процедура ПриОткрытии()
	
	ЭлементыФормы.ДатаНач.КнопкаОчистки = Ложь;
	ЭлементыФормы.ДатаКон.КнопкаОчистки = Ложь;
	
КонецПроцедуры // ПриОткрытии()