// Функция возвращает счет прочих расходов из плана счетов Хозрасчетный
//
// Параметры
//  ОтражениеНаСчетеЕНВД  - Булево. Параметр, уточняющий, какой счет возвращать
//
Функция ПолучитьСчетПрочихРасходов(ОтражениеНаСчетеЕНВД) Экспорт
	
	Если ОтражениеНаСчетеЕНВД Тогда
	
		Возврат ПланыСчетов.Хозрасчетный.ПрочиеРасходыОблагаемыеЕНВД
	
	Иначе
	
		Возврат ПланыСчетов.Хозрасчетный.ПрочиеРасходыНеОблагаемыеЕНВД
	
	КонецЕсли;
	
КонецФункции // ПолучитьСчетПрочихРасходов()


// Функция проверяет программу бухучета и налоговый режим, для проверки и 
// заполнения строк отражения в учете
Функция ПолучитьСтруктураПараметровПеременныхВеденияУчета(Организация, ПериодРегистрации) Экспорт

	СтруктураЗначенияПеременных = Новый Структура;
	
	// определим налоговый режим
	НалоговыйУчет.ОбновитьДанныеУчетнойПолитикиПоНалоговомуУчету(глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете"), КонецМесяца(ПериодРегистрации), Организация);
	УчетнаяПолитика = глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете")[КонецМесяца(ПериодРегистрации)][Организация];
	УСН = УчетнаяПолитика.УСН;
	СтруктураЗначенияПеременных.Вставить("УСН", УСН);
	СтруктураЗначенияПеременных.Вставить("ОбъектНалогообложенияУСН", УчетнаяПолитика.ОбъектНалогообложенияУСН);
	СтруктураЗначенияПеременных.Вставить("ОрганизацияЯвляетсяПлательщикомЕНВД", УчетнаяПолитика.ОрганизацияЯвляетсяПлательщикомЕНВД);
	СтруктураЗначенияПеременных.Вставить("УплачиватьДобровольныеВзносыВФСС", УчетнаяПолитика.УплачиватьДобровольныеВзносыВФСС);
	
	ПредпринимательНаОСН = Не УСН и ОбщегоНазначения.ПолучитьЗначениеРеквизита(Организация, "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ФизЛицо;
	СтруктураЗначенияПеременных.Вставить("ПредпринимательНаОСН", ПредпринимательНаОСН);
	СтруктураЗначенияПеременных.Вставить("ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете", УчетнаяПолитика.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете);
	
	ЗаполнятьДанныеНУ = Не (УСН или ПредпринимательНаОСН); 
	СтруктураЗначенияПеременных.Вставить("ЗаполнятьДанныеНУ", ЗаполнятьДанныеНУ);
	
	ЗаполнятьСчетаНУ  = Истина;
	СтруктураЗначенияПеременных.Вставить("ЗаполнятьСчетаНУ", ЗаполнятьСчетаНУ);
	
	ЗаполнятьОКАТОКПП  = Истина;
	СтруктураЗначенияПеременных.Вставить("ЗаполнятьОКАТОКПП", ЗаполнятьОКАТОКПП);
	
	УчетПоПодразделениямНаСчетах = Ложь;
	СтруктураЗначенияПеременных.Вставить("УчетПоПодразделениямНаСчетах", УчетПоПодразделениямНаСчетах);
	
	СтруктураЗначенияПеременных.Вставить("ПорядокПризнанияРасходовПоОтпускам", ПолучитьПорядокПризнанияРасходовПоОтпускам(Организация, ПериодРегистрации));
	
	УчетнаяПолитикаПоОценочнымОбязательствам = ПолучитьУчетнуюПолитикуПоОценочнымОбязательствам(Организация, ПериодРегистрации);
	
	СтруктураЗначенияПеременных.Вставить("ОценочныеОбязательстваФормироватьБУ", УчетнаяПолитикаПоОценочнымОбязательствам.ФормироватьБУ);
	СтруктураЗначенияПеременных.Вставить("ОценочныеОбязательстваФормироватьНУ", УчетнаяПолитикаПоОценочнымОбязательствам.ФормироватьНУ);
	СтруктураЗначенияПеременных.Вставить("ОценочныеОбязательстваПорядокРасчета", УчетнаяПолитикаПоОценочнымОбязательствам.ПорядокРасчета);
	СтруктураЗначенияПеременных.Вставить("ОценочноеОбязательствоДляОтпуска", УчетнаяПолитикаПоОценочнымОбязательствам.ОценочноеОбязательствоДляОтпуска);
	
	Возврат СтруктураЗначенияПеременных;
	
КонецФункции

Функция ПолучитьУчетнуюПолитикуПоОценочнымОбязательствам(Организация, Период)
	
	УчетнаяПолитикаПоОценочнымОбязательствам = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(Период) или Период < ДатаДействияПриказа186Н() или Не ЗначениеЗаполнено(Организация) Тогда
		
		//если не заполнены параметры или до 2011 года, учетная политика не применялась
		
		УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьБУ", Ложь);
		УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьНУ", Ложь);
		УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ПорядокРасчета", Перечисления.ПорядокРасчетаОценочныхОбязательств.ПустаяСсылка());
		УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ОценочноеОбязательствоДляОтпуска", Справочники.Резервы.ПустаяСсылка());
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаСреза", КонецГода(Период));
		Запрос.УстановитьПараметр("Организация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаПоРБП.ФормироватьБУ,
		|	УчетнаяПолитикаПоРБП.ФормироватьНУ,
		|	УчетнаяПолитикаПоРБП.ПорядокРасчета,
		|	УчетнаяПолитикаПоРБП.ОценочноеОбязательствоДляОтпуска
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаПоОценочнымОбязательствам.СрезПоследних(&ДатаСреза, Организация = &Организация) КАК УчетнаяПолитикаПоРБП";
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			//значение по умолчанию
			
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьБУ", Ложь);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьНУ", Ложь);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ПорядокРасчета", Перечисления.ПорядокРасчетаОценочныхОбязательств.НачисленияИВзносы);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ОценочноеОбязательствоДляОтпуска", Справочники.Резервы.ПустаяСсылка());
			
		Иначе
			
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьБУ", Выборка.ФормироватьБУ);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ФормироватьНУ", Выборка.ФормироватьНУ);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ПорядокРасчета", Выборка.ПорядокРасчета);
			УчетнаяПолитикаПоОценочнымОбязательствам.Вставить("ОценочноеОбязательствоДляОтпуска", Выборка.ОценочноеОбязательствоДляОтпуска);
			
		КонецЕсли;
		
	КонецЕсли;

	Возврат УчетнаяПолитикаПоОценочнымОбязательствам;

КонецФункции // УчетнаяПолитикаПоОценочнымОбязательствам()


Функция ПолучитьПорядокПризнанияРасходовПоОтпускам(Организация, Период)

	Если НЕ ЗначениеЗаполнено(Период) или Период < ДатаДействияПриказа186Н() или Не ЗначениеЗаполнено(Организация) Тогда
		
		Возврат Перечисления.ПорядокПризнанияРасходовПоОтпускам.ВМесяцеНачисления; //значение по умолчанию
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаСреза", КонецГода(Период));
		Запрос.УстановитьПараметр("Организация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаПоРБП.РасходыПоОтпускам
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаПоРБП.СрезПоследних(&ДатаСреза, Организация = &Организация) КАК УчетнаяПолитикаПоРБП";
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Перечисления.ПорядокПризнанияРасходовПоОтпускам.ВМесяцеНачисления; //значение по умолчанию
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Возврат Выборка.РасходыПоОтпускам;
		КонецЕсли;	
		
	КонецЕсли;

КонецФункции // ПолучитьПорядокПризнанияРасходовПоОтпускам()    

Функция ДатаДействияПриказа186Н() Экспорт

	Возврат Дата('20110101');

КонецФункции // ДатаДействияПриказа186Н()


////////////////////////////////////////////////

// Функция формирует текст запроса для получения временной таблицы - списка начислений, которые входят в базу оценочного обязательства (резерва),
// и размеры ОО в разрезе видов расчета
// Источник данных - регистры БУОсновныеНачисления и БУДополнительныеНачисления
//
// Параметры
//  ОценочныеОбязательстваПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаОценочныхОбязательств
//
// Возвращаемое значение:
//   Текст запроса
//
Функция ПолучитьТекстЗапросаРасчетОценочныхОбязательств(ОценочныеОбязательстваПорядокРасчета) Экспорт

	// параметры запроса
	// ЕстьЕНВД - булево, наличие ЕНВД в периоде регистрации
	// Организация - Организация
	// ПоВсемРезервам - булево, признак формирования запроса без отбора по конкртетному резерву
	// Резерв - СправочникСсылка.Резервы
	// парамПериодРегистрации - период регистрации
	
	// создается таблица ВТСписокНачислений
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	БУНачисления.Резерв КАК Резерв,
	|	БУНачисления.ВидРасчета КАК ВидРасчета,
	|	БУНачисления.СчетДт КАК СчетДт,
	|	БУНачисления.СубконтоДт1 КАК СубконтоДт1,
	|	БУНачисления.СубконтоДт2 КАК СубконтоДт2,
	|	БУНачисления.СубконтоДт3 КАК СубконтоДт3,
	|	БУНачисления.СчетДтНУ,
	|	БУНачисления.СубконтоДтНУ1,
	|	БУНачисления.СубконтоДтНУ2,
	|	БУНачисления.СубконтоДтНУ3,
	|	БУНачисления.ПодразделениеДт КАК ПодразделениеДт,
	|	БУНачисления.ПодразделениеКт,
	|	БУНачисления.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА (НЕ &ЕстьЕНВД)
	|			ТОГДА ЛОЖЬ
	|		КОГДА СчетаУчетаПоДеятельностиЕНВД.Счет ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	СУММА(БУНачисления.Результат) КАК Результат,
	|	БУНачисления.ПроцентОтчислений
	|ПОМЕСТИТЬ ВТСписокНачислений
	|ИЗ
	|	(ВЫБРАТЬ
	|		БУНачисления.СчетДт КАК СчетДт,
	|		БУНачисления.СубконтоДт1 КАК СубконтоДт1,
	|		БУНачисления.СубконтоДт2 КАК СубконтоДт2,
	|		БУНачисления.СубконтоДт3 КАК СубконтоДт3,
	|		БУНачисления.СчетДтНУ КАК СчетДтНУ,
	|		БУНачисления.СубконтоДтНУ1 КАК СубконтоДтНУ1,
	|		БУНачисления.СубконтоДтНУ2 КАК СубконтоДтНУ2,
	|		БУНачисления.СубконтоДтНУ3 КАК СубконтоДтНУ3,
	|		БУНачисления.Результат КАК Результат,
	|		РазмерыОтчисленийВРезервы.Резерв КАК Резерв,
	|		РазмерыОтчисленийВРезервы.Размер КАК ПроцентОтчислений,
	|		БУНачисления.ПодразделениеДт КАК ПодразделениеДт,
	|		БУНачисления.ПодразделениеКт КАК ПодразделениеКт,
	|		БУНачисления.ВидРасчета КАК ВидРасчета,
	|		БУНачисления.ФизЛицо КАК ФизЛицо
	|	ИЗ
	|		РегистрРасчета.БУОсновныеНачисления КАК БУНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерыОтчисленийВРезервы.СрезПоследних(
	|					&парамПериодРегистрации,
	|					Организация = &Организация
	|						И (&ПоВсемРезервам
	|							ИЛИ Резерв = &Резерв)) КАК РазмерыОтчисленийВРезервы
	|			ПО (РазмерыОтчисленийВРезервы.Размер > 0)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Резервы.БазовыеВидыРасчета КАК БазовыеВидыРасчета
	|			ПО (РазмерыОтчисленийВРезервы.Резерв = БазовыеВидыРасчета.Ссылка)
	|				И ((ВЫРАЗИТЬ(БазовыеВидыРасчета.ВидРасчета КАК ПланВидовРасчета.ОсновныеНачисленияОрганизаций)) = БУНачисления.ВидРасчета)
	|	ГДЕ
	|		БУНачисления.ПериодРегистрации = &парамПериодРегистрации
	|		И БУНачисления.ОбособленноеПодразделение = &Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БУНачисления.СчетДт,
	|		БУНачисления.СубконтоДт1,
	|		БУНачисления.СубконтоДт2,
	|		БУНачисления.СубконтоДт3,
	|		БУНачисления.СчетДтНУ,
	|		БУНачисления.СубконтоДтНУ1,
	|		БУНачисления.СубконтоДтНУ2,
	|		БУНачисления.СубконтоДтНУ3,
	|		БУНачисления.Результат,
	|		РазмерыОтчисленийВРезервы.Резерв,
	|		РазмерыОтчисленийВРезервы.Размер,
	|		БУНачисления.ПодразделениеДт,
	|		БУНачисления.ПодразделениеКт,
	|		БУНачисления.ВидРасчета,
	|		БУНачисления.ФизЛицо
	|	ИЗ
	|		РегистрРасчета.БУДополнительныеНачисления КАК БУНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерыОтчисленийВРезервы.СрезПоследних(
	|					&парамПериодРегистрации,
	|					Организация = &Организация
	|						И (&ПоВсемРезервам
	|							ИЛИ Резерв = &Резерв)) КАК РазмерыОтчисленийВРезервы
	|			ПО (РазмерыОтчисленийВРезервы.Размер > 0)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Резервы.БазовыеВидыРасчета КАК БазовыеВидыРасчета
	|			ПО (РазмерыОтчисленийВРезервы.Резерв = БазовыеВидыРасчета.Ссылка)
	|				И ((ВЫРАЗИТЬ(БазовыеВидыРасчета.ВидРасчета КАК ПланВидовРасчета.ДополнительныеНачисленияОрганизаций)) = БУНачисления.ВидРасчета)
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(БУНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|		И БУНачисления.ОбособленноеПодразделение = &Организация) КАК БУНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаПоДеятельностиЕНВД КАК СчетаУчетаПоДеятельностиЕНВД
	|		ПО БУНачисления.СчетДт = СчетаУчетаПоДеятельностиЕНВД.Счет
	|
	|СГРУППИРОВАТЬ ПО
	|	БУНачисления.ВидРасчета,
	|	БУНачисления.Резерв,
	|	БУНачисления.ФизЛицо,
	|	БУНачисления.СубконтоДтНУ3,
	|	БУНачисления.ПодразделениеКт,
	|	БУНачисления.ПодразделениеДт,
	|	БУНачисления.СубконтоДтНУ1,
	|	БУНачисления.СубконтоДтНУ2,
	|	БУНачисления.СчетДтНУ,
	|	БУНачисления.СубконтоДт3,
	|	БУНачисления.СчетДт,
	|	БУНачисления.СубконтоДт2,
	|	БУНачисления.СубконтоДт1,
	|	БУНачисления.ПроцентОтчислений,
	|	ВЫБОР
	|		КОГДА (НЕ &ЕстьЕНВД)
	|			ТОГДА ЛОЖЬ
	|		КОГДА СчетаУчетаПоДеятельностиЕНВД.Счет ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ВидРасчета";
	
	Если ОценочныеОбязательстваПорядокРасчета = Перечисления.ПорядокРасчетаОценочныхОбязательств.Начисления Тогда
		
		ТекстЗапроса = ТекстЗапроса + ";"+
		"ВЫБРАТЬ
		|	РезервыПоВидамРасчета.Резерв,
		|	РезервыПоВидамРасчета.ВидРасчета,
		|	ВЫРАЗИТЬ(РезервыПоВидамРасчета.Результат * РезервыПоВидамРасчета.ПроцентОтчислений / 100 КАК ЧИСЛО(15, 2)) КАК РазмерОтчислений,
		|	0 КАК РазмерОтчисленийВзносы,
		|	0 КАК РазмерОтчисленийФССНесчастныеСлучаи,
		|	РезервыПоВидамРасчета.Результат,
		|	0 КАК РезультатВзносы,
		|	РезервыПоВидамРасчета.ПроцентОтчислений,
		|	ВЫРАЗИТЬ(РезервыПоВидамРасчета.Результат * РезервыПоВидамРасчета.ПроцентОтчислений / 100 КАК ЧИСЛО(15, 2)) КАК ВсегоОтчислений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТСписокНачислений.Резерв КАК Резерв,
		|		ВТСписокНачислений.ВидРасчета КАК ВидРасчета,
		|		СУММА(ВТСписокНачислений.Результат) КАК Результат,
		|		ВТСписокНачислений.ПроцентОтчислений КАК ПроцентОтчислений
		|	ИЗ
		|		ВТСписокНачислений КАК ВТСписокНачислений
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТСписокНачислений.Резерв,
		|		ВТСписокНачислений.ВидРасчета,
		|		ВТСписокНачислений.ПроцентОтчислений) КАК РезервыПоВидамРасчета";
		
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ";"+
		"ВЫБРАТЬ
		|	СписокНачислений.Резерв,
		|	СписокНачислений.ВидРасчета КАК ВидРасчета,
		|	СписокНачислений.ФизЛицо КАК ФизЛицо,
		|	СписокНачислений.ОблагаетсяЕНВД,
		|	СУММА(СписокНачислений.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТБУДоходыСвод
		|ИЗ
		|	ВТСписокНачислений КАК СписокНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокНачислений.ВидРасчета,
		|	СписокНачислений.ФизЛицо,
		|	СписокНачислений.ОблагаетсяЕНВД,
		|	СписокНачислений.Резерв
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокНачислений.ФизЛицо КАК ФизЛицо,
		|	СписокНачислений.Резерв,
		|	СписокНачислений.ВидРасчета КАК ВидРасчета,
		|	СУММА(СписокНачислений.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТБУДоходыСводПоВР
		|ИЗ
		|	ВТСписокНачислений КАК СписокНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокНачислений.ФизЛицо,
		|	СписокНачислений.ВидРасчета,
		|	СписокНачислений.Резерв
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТСписокНачислений.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТСписокФизлиц
		|ИЗ
		|	ВТСписокНачислений КАК ВТСписокНачислений
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтраховыеВзносыИсчисленные.ФизЛицо КАК ФизЛицо,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРСтраховаяЕНВД) КАК ПФРСтраховаяЕНВД,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРНакопительнаяЕНВД) КАК ПФРНакопительнаяЕНВД,
		|	СУММА(СтраховыеВзносыИсчисленные.ФСС) КАК ФСС,
		|	СУММА(СтраховыеВзносыИсчисленные.ФССЕНВД) КАК ФССЕНВД,
		|	СУММА(СтраховыеВзносыИсчисленные.ФФОМС) КАК ФФОМС,
		|	СУММА(СтраховыеВзносыИсчисленные.ФФОМСЕНВД) КАК ФФОМСЕНВД,
		|	СУММА(СтраховыеВзносыИсчисленные.ТФОМС) КАК ТФОМС,
		|	СУММА(СтраховыеВзносыИсчисленные.ТФОМСЕНВД) КАК ТФОМСЕНВД,
		|	СУММА(СтраховыеВзносыИсчисленные.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРПоДополнительномуТарифу) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СтраховыеВзносыИсчисленные.ПФРНаДоплатуКПенсииШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам
		|ПОМЕСТИТЬ ВТВзносыИсчисленные
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыИсчисленные КАК СтраховыеВзносыИсчисленные
		|ГДЕ
		|	СтраховыеВзносыИсчисленные.ОбособленноеПодразделение = &Организация
		|	И НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, МЕСЯЦ) = &парамПериодРегистрации
		|	И СтраховыеВзносыИсчисленные.ФизЛицо В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТСписокФизлиц.ФизЛицо
		|			ИЗ
		|				ВТСписокФизлиц)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтраховыеВзносыИсчисленные.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета,
		|	СУММА(СтраховыеВзносыСведенияОДоходах.Результат - СтраховыеВзносыСведенияОДоходах.Скидка) КАК Результат,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТВзносыДоходыПоВР
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
		|ГДЕ
		|	СтраховыеВзносыСведенияОДоходах.ОбособленноеПодразделение = &Организация
		|	И НАЧАЛОПЕРИОДА(СтраховыеВзносыСведенияОДоходах.Период, МЕСЯЦ) = &парамПериодРегистрации
		|	И СтраховыеВзносыСведенияОДоходах.ФизЛицо В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТСписокФизлиц.ФизЛицо
		|			ИЗ
		|				ВТСписокФизлиц)
		|	И СтраховыеВзносыСведенияОДоходах.Результат - СтраховыеВзносыСведенияОДоходах.Скидка <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТВзносыДоходыПоВР.ФизЛицо,
		|	СУММА(ВТВзносыДоходыПоВР.Результат) КАК Результат,
		|	ВТВзносыДоходыПоВР.ОблагаетсяЕНВД,
		|	СУММА(ВЫБОР
		|			КОГДА ВТВзносыДоходыПоВР.ВидДоходаВходитВБазуПФР
		|				ТОГДА ВТВзносыДоходыПоВР.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаПФР,
		|	СУММА(ВЫБОР
		|			КОГДА ВТВзносыДоходыПоВР.ВидДоходаВходитВБазуФОМС
		|				ТОГДА ВТВзносыДоходыПоВР.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА ВТВзносыДоходыПоВР.ВидДоходаВходитВБазуФСС
		|				ТОГДА ВТВзносыДоходыПоВР.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаФСС,
		|	СУММА(ВЫБОР
		|			КОГДА ВТВзносыДоходыПоВР.ОблагаетсяПоДополнительномуТарифу
		|				ТОГДА ВТВзносыДоходыПоВР.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаДопТариф,
		|	СУММА(ВЫБОР
		|			КОГДА ВТВзносыДоходыПоВР.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам
		|				ТОГДА ВТВзносыДоходыПоВР.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаШахтеры
		|ПОМЕСТИТЬ ВТВзносыДоходыСвод
		|ИЗ
		|	ВТВзносыДоходыПоВР КАК ВТВзносыДоходыПоВР
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТВзносыДоходыПоВР.ФизЛицо,
		|	ВТВзносыДоходыПоВР.ОблагаетсяЕНВД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСписокНачислений.Резерв,
		|	ВТСписокНачислений.ВидРасчета,
		|	ВТСписокНачислений.СчетДт,
		|	ВТСписокНачислений.СубконтоДт1,
		|	ВТСписокНачислений.СубконтоДт2,
		|	ВТСписокНачислений.СубконтоДт3,
		|	ВТСписокНачислений.СчетДтНУ,
		|	ВТСписокНачислений.СубконтоДтНУ1,
		|	ВТСписокНачислений.СубконтоДтНУ2,
		|	ВТСписокНачислений.СубконтоДтНУ3,
		|	ВТСписокНачислений.ПодразделениеДт,
		|	ВТСписокНачислений.ПодразделениеКт,
		|	ВТСписокНачислений.ФизЛицо,
		|	ВТСписокНачислений.ОблагаетсяЕНВД,
		|	ВТСписокНачислений.Результат КАК РезультатБУ,
		|	ВТСписокНачислений.ПроцентОтчислений,
		|	ВЫБОР
		|		КОГДА ВТВзносыДоходыСвод.БазаПФР = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТВзносыДоходыПоВР.Результат / ВТВзносыДоходыСвод.БазаПФР
		|	КОНЕЦ КАК БазаПФР,
		|	ВЫБОР
		|		КОГДА ВТВзносыДоходыСвод.БазаФОМС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТВзносыДоходыПоВР.Результат / ВТВзносыДоходыСвод.БазаФОМС
		|	КОНЕЦ КАК БазаФОМС,
		|	ВЫБОР
		|		КОГДА ВТВзносыДоходыСвод.БазаФСС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТВзносыДоходыПоВР.Результат / ВТВзносыДоходыСвод.БазаФСС
		|	КОНЕЦ КАК БазаФСС,
		|	ВЫБОР
		|		КОГДА ВТВзносыДоходыСвод.БазаДопТариф = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТВзносыДоходыПоВР.Результат / ВТВзносыДоходыСвод.БазаДопТариф
		|	КОНЕЦ КАК БазаДопТариф,
		|	ВЫБОР
		|		КОГДА ВТВзносыДоходыСвод.БазаШахтеры = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТВзносыДоходыПоВР.Результат / ВТВзносыДоходыСвод.БазаШахтеры
		|	КОНЕЦ КАК БазаШахтеры,
		|	ВЫБОР
		|		КОГДА ВТБУДоходыСвод.Результат = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТСписокНачислений.Результат / ВТБУДоходыСвод.Результат
		|	КОНЕЦ КАК ДоляБУ,
		|	ВЫБОР
		|		КОГДА ВТБУДоходыСвод.Результат = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТСписокНачислений.Результат / ВТБУДоходыСводПоВР.Результат
		|	КОНЕЦ КАК ДоляБУФСС_НС,
		|	ВТБУДоходыСводПоВР.Результат
		|ПОМЕСТИТЬ ВТВзносыДоходыБУДоходы
		|ИЗ
		|	ВТСписокНачислений КАК ВТСписокНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВзносыДоходыПоВР КАК ВТВзносыДоходыПоВР
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВзносыДоходыСвод КАК ВТВзносыДоходыСвод
		|			ПО ВТВзносыДоходыПоВР.ФизЛицо = ВТВзносыДоходыСвод.ФизЛицо
		|				И ВТВзносыДоходыПоВР.ОблагаетсяЕНВД = ВТВзносыДоходыСвод.ОблагаетсяЕНВД
		|		ПО ВТСписокНачислений.ФизЛицо = ВТВзносыДоходыПоВР.ФизЛицо
		|			И ВТСписокНачислений.ВидРасчета = ВТВзносыДоходыПоВР.ВидРасчета
		|			И ВТСписокНачислений.ОблагаетсяЕНВД = ВТВзносыДоходыПоВР.ОблагаетсяЕНВД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБУДоходыСвод КАК ВТБУДоходыСвод
		|		ПО ВТСписокНачислений.ФизЛицо = ВТБУДоходыСвод.ФизЛицо
		|			И ВТСписокНачислений.ВидРасчета = ВТБУДоходыСвод.ВидРасчета
		|			И ВТСписокНачислений.ОблагаетсяЕНВД = ВТБУДоходыСвод.ОблагаетсяЕНВД
		|			И ВТСписокНачислений.Резерв = ВТБУДоходыСвод.Резерв
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБУДоходыСводПоВР КАК ВТБУДоходыСводПоВР
		|		ПО ВТСписокНачислений.ФизЛицо = ВТБУДоходыСводПоВР.ФизЛицо
		|			И ВТСписокНачислений.ВидРасчета = ВТБУДоходыСводПоВР.ВидРасчета
		|			И ВТСписокНачислений.Резерв = ВТБУДоходыСводПоВР.Резерв
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВзносыДоходыБУДоходы.Резерв,
		|	ВзносыДоходыБУДоходы.ПроцентОтчислений,
		|	ВзносыДоходыБУДоходы.ФизЛицо КАК ФизЛицо,
		|	ВзносыДоходыБУДоходы.ВидРасчета КАК ВидРасчета,
		|	ВзносыДоходыБУДоходы.РезультатБУ КАК Результат,
		|	ВЫБОР
		|		КОГДА ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ПФРСтраховая, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаПФР
		|	КОНЕЦ КАК ПФРСтраховая,
		|	ВЫБОР
		|		КОГДА НЕ ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ПФРСтраховаяЕНВД, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаПФР
		|	КОНЕЦ КАК ПФРСтраховаяЕНВД,
		|	ВЫБОР
		|		КОГДА ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ПФРНакопительная, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаПФР
		|	КОНЕЦ КАК ПФРНакопительная,
		|	ВЫБОР
		|		КОГДА НЕ ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ПФРНакопительнаяЕНВД, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаПФР
		|	КОНЕЦ КАК ПФРНакопительнаяЕНВД,
		|	ВЫБОР
		|		КОГДА ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ФФОМС, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФОМС
		|	КОНЕЦ КАК ФФОМС,
		|	ВЫБОР
		|		КОГДА НЕ ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ФФОМСЕНВД, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФОМС
		|	КОНЕЦ КАК ФФОМСЕНВД,
		|	ВЫБОР
		|		КОГДА ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ТФОМС, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФОМС
		|	КОНЕЦ КАК ТФОМС,
		|	ВЫБОР
		|		КОГДА НЕ ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ТФОМСЕНВД, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФОМС
		|	КОНЕЦ КАК ТФОМСЕНВД,
		|	ВЫБОР
		|		КОГДА ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ФСС, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФСС
		|	КОНЕЦ КАК ФСС,
		|	ВЫБОР
		|		КОГДА НЕ ВзносыДоходыБУДоходы.ОблагаетсяЕНВД
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВзносыИсчисленные.ФССЕНВД, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаФСС
		|	КОНЕЦ КАК ФССЕНВД,
		|	ЕСТЬNULL(ВзносыИсчисленные.ФССНесчастныеСлучаи, 0) * ВзносыДоходыБУДоходы.ДоляБУФСС_НС * ВзносыДоходыБУДоходы.БазаФСС КАК ФССНесчастныеСлучаи,
		|	ЕСТЬNULL(ВзносыИсчисленные.ПФРПоДополнительномуТарифу, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаДопТариф КАК ПФРПоДополнительномуТарифу,
		|	ЕСТЬNULL(ВзносыИсчисленные.ПФРНаДоплатуКПенсииШахтерам, 0) * ВзносыДоходыБУДоходы.ДоляБУ * ВзносыДоходыБУДоходы.БазаШахтеры КАК ПФРНаДоплатуКПенсииШахтерам,
		|	ВзносыДоходыБУДоходы.СчетДт,
		|	ВзносыДоходыБУДоходы.СубконтоДт1,
		|	ВзносыДоходыБУДоходы.СубконтоДт2,
		|	ВзносыДоходыБУДоходы.СубконтоДт3,
		|	ВзносыДоходыБУДоходы.СчетДтНУ,
		|	ВзносыДоходыБУДоходы.СубконтоДтНУ1,
		|	ВзносыДоходыБУДоходы.СубконтоДтНУ2,
		|	ВзносыДоходыБУДоходы.СубконтоДтНУ3,
		|	ВзносыДоходыБУДоходы.ПодразделениеДт,
		|	ВзносыДоходыБУДоходы.ПодразделениеКт
		|ПОМЕСТИТЬ ВТКоэффициенты
		|ИЗ
		|	ВТВзносыДоходыБУДоходы КАК ВзносыДоходыБУДоходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносыИсчисленные КАК ВзносыИсчисленные
		|		ПО ВзносыДоходыБУДоходы.ФизЛицо = ВзносыИсчисленные.ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезервыПоВидамРасчета.Резерв,
		|	РезервыПоВидамРасчета.ВидРасчета,
		|	ВЫРАЗИТЬ(РезервыПоВидамРасчета.Результат * РезервыПоВидамРасчета.ПроцентОтчислений / 100 КАК ЧИСЛО(15, 2)) КАК РазмерОтчислений,
		|	ВЫРАЗИТЬ(РезервыПоВидамРасчета.РезультатВзносы * РезервыПоВидамРасчета.ПроцентОтчислений / 100 КАК ЧИСЛО(15, 2)) КАК РазмерОтчисленийВзносы,
		|	ВЫРАЗИТЬ(РезервыПоВидамРасчета.РезультатВзносыФССНесчастныеСлучаи * РезервыПоВидамРасчета.ПроцентОтчислений / 100 КАК ЧИСЛО(15, 2)) КАК РазмерОтчисленийФССНесчастныеСлучаи,
		|	РезервыПоВидамРасчета.Результат,
		|	РезервыПоВидамРасчета.РезультатВзносы + РезервыПоВидамРасчета.РезультатВзносыФССНесчастныеСлучаи КАК РезультатВзносы,
		|	РезервыПоВидамРасчета.ПроцентОтчислений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТКоэффициенты.Резерв КАК Резерв,
		|		ВТКоэффициенты.ВидРасчета КАК ВидРасчета,
		|		ВТКоэффициенты.ПроцентОтчислений КАК ПроцентОтчислений,
		|		СУММА(ВТКоэффициенты.Результат) КАК Результат,
		|		СУММА(ВТКоэффициенты.ФССНесчастныеСлучаи) КАК РезультатВзносыФССНесчастныеСлучаи,
		|		СУММА(ВТКоэффициенты.ПФРСтраховая + ВТКоэффициенты.ПФРСтраховаяЕНВД + ВТКоэффициенты.ПФРНакопительная + ВТКоэффициенты.ПФРНакопительнаяЕНВД + ВТКоэффициенты.ФФОМС + ВТКоэффициенты.ФФОМСЕНВД + ВТКоэффициенты.ТФОМС + ВТКоэффициенты.ТФОМСЕНВД + ВТКоэффициенты.ФСС + ВТКоэффициенты.ФССЕНВД + ВТКоэффициенты.ПФРПоДополнительномуТарифу + ВТКоэффициенты.ПФРНаДоплатуКПенсииШахтерам) КАК РезультатВзносы
		|	ИЗ
		|		ВТКоэффициенты КАК ВТКоэффициенты
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТКоэффициенты.ВидРасчета,
		|		ВТКоэффициенты.ПроцентОтчислений,
		|		ВТКоэффициенты.Резерв) КАК РезервыПоВидамРасчета";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаРасчетОценочныхОбязательств()