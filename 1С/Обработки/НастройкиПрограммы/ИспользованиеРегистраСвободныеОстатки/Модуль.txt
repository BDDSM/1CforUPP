Перем мТекущееЗначениеНастройки;
Перем мДатаНачала;

//ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	ПрочитатьТекущиеНастройки();
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	//проверка доступа к изменению настроек учета
	ЭтаФорма.ТолькоПросмотр = не ПолучитьРазрешениеНаИзменениеНастроекУчета();
КонецПроцедуры

Процедура НадписьПодробноНажатие(Элемент)
	ОткрытьСправку(ОбработкаОбъект.Метаданные().Формы.ИспользованиеРегистраСвободныеОстатки);
КонецПроцедуры

Процедура ИспользоватьРегистрСвободныеОстаткиПриИзменении(Элемент)
	
	ОбновитьНадписи();
	УстановитьДоступность();
	
КонецПроцедуры

//ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНЫХ ПАНЕЛЕЙ

Процедура КнопкаОКНажатие(Кнопка)
	
	Если мТекущееЗначениеНастройки = ИспользоватьРегистрСвободныеОстатки Тогда  //настройка не изменилась - ничего делать не надо
		Возврат;
	ИначеЕсли ИспользоватьРегистрСвободныеОстатки И не ПолучитьПодтверждениеПользователя("Изменение настройки ""Использовать регистр Свободные остатки""") Тогда 
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРегистрСвободныеОстатки Тогда
		Если ИспользоватьСвободныеОстаткиДатаНачала = '000101010000'  Тогда
			Предупреждение("Не выбрана дата, с которой будет заполнен регистр ""Свободные остатки""");
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ПроцедурыКонтроляОстатков.УстановитьИспользованиеРегистраСвободныеОстатки(ИспользоватьРегистрСвободныеОстатки, ИспользоватьСвободныеОстаткиДатаНачала) Тогда
		Оповестить("ИспользованиеРегистраСвободныеОстатки");
		ЭтаФорма.Закрыть();
	Иначе
		Предупреждение("Не удалось изменить значение параметра ""Свободные остатки""", 60);
	КонецЕсли;
	
КонецПроцедуры

//ПРОЦЕДУРЫ ИЗМЕНЕНИЯ ВНЕШНЕГО ВИДА ФОРМЫ

Процедура ОбновитьНадписи()
		
	Если мТекущееЗначениеНастройки Тогда
		// Если режим используется, то текст надписи не меняется
		ЭлементыФормы.НадписьДатаНачала.Значение = "Регистр заполнен начиная с:";
		ИспользоватьСвободныеОстаткиДатаНачала = мДатаНачала;
	Иначе
		ЭлементыФормы.НадписьДатаНачала.Значение = "Заполнить регистр с:";
		ИспользоватьСвободныеОстаткиДатаНачала = НачалоМесяца(ТекущаяДата());
		ЭлементыФормы.ИспользоватьСвободныеОстаткиДатаНачала.АвтоОтметкаНезаполненного = истина;
	КонецЕсли; 
	
КонецПроцедуры

Процедура УстановитьДоступность()
	
	НастройкаИзменена = НЕ (мТекущееЗначениеНастройки = ИспользоватьРегистрСвободныеОстатки);
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыИзменитьНастройку.Доступность = НастройкаИзменена;
	
	// Дата доступна, если режим изменяется с "не использовать" на "использовать"
	Если НЕ мТекущееЗначениеНастройки И ИспользоватьРегистрСвободныеОстатки Тогда
		ЭлементыФормы.ИспользоватьСвободныеОстаткиДатаНачала.Доступность = Истина;
	Иначе
		ЭлементыФормы.ИспользоватьСвободныеОстаткиДатаНачала.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

//ПРОЦЕДУРЫ РАБОТЫ С ПЕРЕМЕННЫМИ ФОРМЫ

Процедура ПрочитатьТекущиеНастройки()
	// запомним текущее значение константы "Использовать регистр Свободные Остатки"
	мТекущееЗначениеНастройки = Константы.ИспользоватьРегистрСвободныеОстатки.Получить();
	ИспользоватьРегистрСвободныеОстатки = мТекущееЗначениеНастройки;
	Если мТекущееЗначениеНастройки Тогда
		мДатаНачала = Константы.ДатаНачалаИспользованияРегистраСвободныеОстатки.Получить();
	Иначе
		мДатаНачала = Дата("00010101000000");
	КонецЕсли;
    ОбновитьНадписи();
	УстановитьДоступность();
КонецПроцедуры