Перем МодифицированностьПередЗаписью; // Устанавливается в событии ПередЗаписью


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ СПИСКА РАЗДЕЛОВ

Процедура СписокРазделовПриАктивизацииСтроки(Элемент)
	
	ТекущйийРаздел = ЭлементыФормы.СписокРазделов.ТекущаяСтрока;
	ЭлементыФормы.ПанельРазделов.ТекущаяСтраница = ЭлементыФормы.ПанельРазделов.Страницы[ТекущйийРаздел.ИмяРаздела];
	
КонецПроцедуры

Процедура ЗаполнитьСписокРазделов()
	
	СписокРазделов.Очистить();
	
	ОбработкаОбъект.ДобавитьРаздел(СписокРазделов, "ТорговоеОборудование", "Торговое оборудование", 1);
	ОбработкаОбъект.ДобавитьРаздел(СписокРазделов, "ВыгрузкаТоваров", "Выгрузка товаров в ККМ", 1);
	ОбработкаОбъект.ДобавитьРаздел(СписокРазделов, "Штрихкоды",       "Штрихкоды", 1);
	
КонецПроцедуры // ЗаполнитьСписокРазделов


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура СообщитьОбИзмененииПараметровУчета()
	
	МассивСоединений = ПолучитьСоединенияИнформационнойБазы() ;
	Для Каждого ТекСоединение Из МассивСоединений Цикл
		Если (ТекСоединение.ИмяПриложения = "1CV8") 
		   И (НЕ ТекСоединение.НомерСоединения = НомерСоединенияИнформационнойБазы())
		   И (НЕ ТекСоединение.Пользователь = неопределено) Тогда
			  
				ОбщегоНазначения.Сообщение("Внимание! Вы изменили настройки параметров учета!
						 |В настоящий момент в базе работают пользователи. 
						 |Некоторые настройки могут не начать действовать для них.
						 |Им необходимо перезапустить программу. 
						 |Для Вас новые настройки уже вступили в силу, перезапускать программу не требуется.", СтатусСообщения.Важное);
				Прервать;		 
		КонецЕсли;
	КонецЦикла;			
	
КонецПроцедуры

Функция ПроверитьЗаполнениеПараметров()

	Ошибки = "";
	
	Если НЕ ЗначениеЗаполнено(ПорядокПрисвоенияPLU) Тогда
			Ошибки = Ошибки + Символы.ПС
					+ " - Не задан порядок назначения кодов товара для выгрузки в ККМ в режиме Offline (раздел ""Выгрузка товаров в ККМ"")";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПрефиксШтучногоТовара) Тогда
		Ошибки = Ошибки + Символы.ПС 
				+ " - Не заполнено значение префикса штрих-кода штучного товара (раздел ""Штрихкоды"")";
	Иначе
		Код = КодСимвола(ПрефиксШтучногоТовара);
		Если (Код < 48) Или (Код > 57) Тогда
			Ошибки = Ошибки + Символы.ПС
					+ " - Значение префикса штрих-кода штучного товара может принимать значения от 0 до 9 (раздел ""Штрихкоды"")";
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПрефиксВесовогоТовара) Тогда
		Ошибки = Ошибки + Символы.ПС
				+ " - Не заполнено значение префикса штрих-кода весового товара (раздел ""Штрихкоды"")";
	Иначе
		Код = КодСимвола(ПрефиксВесовогоТовара);
		Если (Код < 48) Или (Код > 57) Тогда
			Ошибки = Ошибки + Символы.ПС
					+ " - Значение префикса штрих-кода весового товара может принимать значения от 0 до 9 (раздел ""Штрихкоды"")";
		ИначеЕсли ПрефиксШтучногоТовара = ПрефиксВесовогоТовара Тогда
			Ошибки = Ошибки + Символы.ПС
					+ " - Значение префикса штрихкода весового товара не должно совпадать со значением префикса штрихкода штучного товара (раздел ""Штрихкоды"")";
		КонецЕсли;
	КонецЕсли;

	Если (ДлинаКодаВесовогоТовара < 1) Или (ДлинаКодаВесовогоТовара > 9) Тогда
			Ошибки = Ошибки + Символы.ПС
					+ " - Значение длины кода весового товара может принимать значения от 1 до 9 (раздел ""Штрихкоды"")";
	КонецЕсли;
	
	ЕстьОшибки = (Ошибки <> "");
	Если ЕстьОшибки Тогда
		ОбщегоНазначения.Сообщение("Обнаружены ошибки:" + Ошибки); 
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибки;

КонецФункции // ПроверитьЗаполнениеПараметров()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПЕЧАТИ

Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	Результат = Истина;

	Если ЭтаФорма.Модифицированность Тогда

		Ответ = Вопрос("Настройки изменены. Для печати необходимо записать настройки.
		               |Записать?",
		               РежимДиалогаВопрос.ОКОтмена, , 
		               КодВозвратаДиалога.Отмена,
		               "Печать");

		Если Ответ = КодВозвратаДиалога.ОК Тогда

			Попытка
				
				Результат = ЗаписатьВФорме();				
				
			Исключение
				Результат = Ложь;
			КонецПопытки;	

		КонецЕсли;

	КонецЕсли;

	Если Результат Тогда
		ОбработкаОбъект.Печать(СписокРазделов, "Настройка торгового оборудования");
	КонецЕсли; 
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ПроверитьЗаполнениеПараметров() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	МодифицированностьПередЗаписью = Модифицированность;
	
	Если Не РаботаСТорговымОборудованием.КорректныйПорядокНазначенияPLU(ПорядокПрисвоенияPLU) Тогда
		Ответ = Вопрос("ВНИМАНИЕ!
					   |Порядок назначения кодов по коду номенклатуры конфликтует с
					   |уже назначенными кодами товаров для выгрузки в ККМ в режиме Offline.
					   |Товары с неуникальными кодами не попадут в список выгрузки.
					   |Установить порядок назначения кодов по коду номенклатуры?",
					   РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			ЭлементыФормы.ПанельНастройкиУчета.ТекущаяСтраница =
				ЭлементыФормы.ПанельНастройкиУчета.Страницы.КодыТовара;
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;	
	
	Если НЕ Отказ И МодифицированностьПередЗаписью Тогда
		СообщитьОбИзмененииПараметровУчета();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Если МодифицированностьПередЗаписью Тогда
		СообщитьОбИзмененииПараметровУчета();
		МодифицированностьПередЗаписью = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЗаполнитьСписокРазделов();
	
КонецПроцедуры