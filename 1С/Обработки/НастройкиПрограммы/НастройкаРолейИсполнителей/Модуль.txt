Перем РазделятьЗадачиПоОрганизациям Экспорт;
Перем РазделятьЗадачиПоРолямИсполнителей Экспорт;

///////////////////////////////////////////////////////////////////////////////
//ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


Процедура УстановитьОтборТаблицаНастройкиРолиИИсполнители()

	ТекущаяСтрока = ЭлементыФормы.СписокПользователей.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущийПользователь = ЭлементыФормы.СписокПользователей.ТекущаяСтрока.Значение;
		ЭлементыФормы.НадписьСотрудник.Заголовок = "Список задач пользователя: " + ТекущийПользователь.Наименование;
	Иначе	
		ТекущийПользователь = Справочники.Пользователи.ПустаяСсылка();
		ЭлементыФормы.НадписьСотрудник.Заголовок = "";
	КонецЕсли;
	
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Исполнитель.Значение = ТекущийПользователь;
	
	УправлениеЗадачамиПереопределяемый.УстановитьОтборТаблицаНастройкиРолиИИсполнителиДополнительно(ЭлементыФормы);

КонецПроцедуры

Процедура СписокПользователейПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборТаблицаНастройкиРолиИИсполнители();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ВремТаблица = РолиИИсполнители.Выгрузить(,"Исполнитель");
	ВремТаблица.Свернуть("Исполнитель");
	СписокПользователей.ЗагрузитьЗначения(ВремТаблица.ВыгрузитьКолонку("Исполнитель"));
	
	Если СписокПользователей.Количество() = 0 Тогда
		Предупреждение("Список пользователей пуст!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	ЭлементыФормы.ТаблицаНастройкиРолей.Колонки.Организация.Видимость = РазделятьЗадачиПоОрганизациям;
	ЭлементыФормы.ТаблицаНастройкиРолей.Колонки.Роль.Видимость = РазделятьЗадачиПоРолямИсполнителей;
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Исполнитель.Использование = Истина;
	
	// Показываем настройки с организациями/без организаций в зависимости от режима использования задач
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Организация.Значение = Справочники.Организации.ПустаяСсылка();
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Организация.ВидСравнения = ?(РазделятьЗадачиПоОрганизациям, ВидСравнения.НеРавно, ВидСравнения.Равно);
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Организация.Использование = Истина;
	
	// Показываем настройки с ролями/без ролей в зависимости от режима использования задач
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Роль.Значение = Справочники.РолиИсполнителей.ПустаяСсылка();
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Роль.ВидСравнения = ?(РазделятьЗадачиПоРолямИсполнителей, ВидСравнения.НеРавно, ВидСравнения.Равно);
	ЭлементыФормы.ТаблицаНастройкиРолей.ОтборСтрок.Роль.Использование = Истина;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЭлементыФормы.СписокПользователей.ТекущаяСтрока = СписокПользователей.Получить(0);
	УстановитьОтборТаблицаНастройкиРолиИИсполнители();
	
	ЭлементыФормы.ТаблицаНастройкиРолей.Колонки.Организация.ТекстШапки	= ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("По организации");

КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Оповестить("НеОбновлятьТаблицуРолиИИсполнители", , ВладелецФормы);
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;	
		КонецЕсли;	
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыПрименитьИзменения(Кнопка)
	
	Оповестить("ОбновитьТаблицуРолиИИсполнители", , ВладелецФормы);
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры