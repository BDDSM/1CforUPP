Перем мТекущееЗначениеНастройки;

//ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ТолькоПросмотр = не ПолучитьРазрешениеНаИзменениеНастроекУчета();
КонецПроцедуры

Процедура ПриОткрытии()
	ПрочитатьТекущиеНастройки();
КонецПроцедуры

Процедура ПереключательВестиРеглУчетРасчетовСПерсоналомСводноПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

//ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНЫХ ПАНЕЛЕЙ

Процедура КнопкаОКНажатие(Кнопка)
	ЗаписатьИзменения();
	ЭтаФорма.Закрыть();
КонецПроцедуры

//ПРОЦЕДУРЫ ИЗМЕНЕНИЯ ВНЕШНЕГО ВИДА ФОРМЫ

Процедура УстановитьДоступность()
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОК.Доступность = НЕ (мТекущееЗначениеНастройки=ВестиРеглУчетРасчетовСПерсоналомСводно) 
КонецПроцедуры

//ПРОЦЕДУРЫ РАБОТЫ С ПЕРЕМЕННЫМИ ФОРМЫ

Процедура ПрочитатьТекущиеНастройки()
	мТекущееЗначениеНастройки = Константы.ВестиРеглУчетРасчетовСПерсоналомСводно.Получить();
	ВестиРеглУчетРасчетовСПерсоналомСводно = мТекущееЗначениеНастройки;
	УстановитьДоступность();
КонецПроцедуры

//ПРОЦЕДУРЫ ИЗМЕНЕНИЯ ДАННЫХ ИБ

Процедура ЗаписатьИзменения()
	//перечитаем еще раз значение чтобы убедиться что его никто не поменял
	мТекущееЗначениеНастройки_старое = мТекущееЗначениеНастройки;
	мТекущееЗначениеНастройки = Константы.ВестиРеглУчетРасчетовСПерсоналомСводно.Получить();
	Если мТекущееЗначениеНастройки_старое<>мТекущееЗначениеНастройки Тогда
		Предупреждение("Настройка была изменена другим пользователем. Для отображения актуальных настроек обновите данные формы");
		Возврат;
	КонецЕсли;
	
	Если мТекущееЗначениеНастройки = ВестиРеглУчетРасчетовСПерсоналомСводно Тогда
		Возврат;
	ИначеЕсли не ПолучитьПодтверждениеПользователя("Изменение настройки ""Способ ведения учета расчетов с персоналом в регл. учете""") Тогда
		Возврат;
	КонецЕсли;

	// изменение значения константы
	МенеджерЗаписи = Константы.ВестиРеглУчетРасчетовСПерсоналомСводно.СоздатьМенеджерЗначения();
	МенеджерЗаписи.Значение = ВестиРеглУчетРасчетовСПерсоналомСводно;
	Попытка
		МенеджерЗаписи.Записать();
		ОбщегоНазначения.СообщитьОСостоянииИзмененияНастройки("Способ ведения учета расчетов с персоналом в регл учете","Изменено значение константы ""Способ ведения учета расчетов с персоналом в регламентированном учете""", "новое значение - "+ ?(ВестиРеглУчетРасчетовСПерсоналомСводно,"Сводно по всем работникам","По каждому работнику"));
	Исключение
		Предупреждение("Не удалось изменить значение константы ""Способ ведения учета расчетов с персоналом в регламентированном учете"": "+ОписаниеОшибки(),60);
		ОбщегоНазначения.СообщитьОСостоянииИзмененияНастройки("Способ ведения учета расчетов с персоналом в регл учете","Не удалось изменить значение константы ""Способ ведения учета расчетов с персоналом в регламентированном учете"": "+ОписаниеОшибки(), ,,СтатусСообщения.Важное);
		ОтменитьТранзакцию();
		Возврат;
	КонецПопытки;
	Оповестить("ВестиРеглУчетРасчетовСПерсоналомСводно");
КонецПроцедуры