Перем ПреобразованныйТекст;

Перем мОтобразилиСсылку;

Перем мПустаяКартинка;
Перем мКартинкаНайти;

Процедура ВыделитьТекст(ВыделяемыйТекст)
	ТекстДокумента = ЭлементыФормы.ПолеHTMLДокумента.Документ.body.createTextRange();
	ТекстДокумента.findText(ВыделяемыйТекст);
	ТекстДокумента.select();
КонецПроцедуры

Процедура НайденныеЗначенияПриАктивизацииСтроки(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ПреобразованныйТекст) Тогда
		ПреобразоватьТекст();
	КонецЕсли;
	
	Индекс = НайденныеЗначения.Индекс(ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока);
	Ссылка = ЭлементыФормы.ПолеHTMLДокумента.Документ.getElementById("index"+Индекс);
	Если Ссылка <> Неопределено Тогда
		offsetTop = Ссылка.offsetTop;
	Иначе
		offsetTop = 0;
	КонецЕсли;
	
	Если offsetTop = 0 Тогда
		ЭлементыФормы.ПолеHTMLДокумента.УстановитьТекст(ПреобразованныйТекст);
	Иначе
		ЭлементыФормы.ПолеHTMLДокумента.Документ.parentWindow.scroll(0,Макс(0,offsetTop-32));
		ВыделитьТекст(Элемент.ТекущаяСтрока.Значение);
		мОтобразилиСсылку = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеHTMLДокументаonclick(Элемент, pEvtObj)
	
	pEvtObj.returnValue = Ложь;
	
	Узел = pEvtObj.srcElement;
	
	href = "";
	
	Пока Истина Цикл
		
		Попытка
			ИмяЦели = Узел.tagName
		Исключение
			Прервать;
		КонецПопытки;
		
		Если ИмяЦели = "A" Тогда
			Попытка
				href = Узел.href;
			Исключение
			КонецПопытки;
			Прервать;
		КонецЕсли; 
		
		Узел = Узел.parentElement;
		
	КонецЦикла;
	
	ПризнакИндекса = "about:index";
	Если Найти(href,ПризнакИндекса)=1 Тогда
		ЭтаФорма.Закрыть(НайденныеЗначения[Число(СтрЗаменить(href,ПризнакИндекса,""))].Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьТекст()
	
	ПреобразованныйТекст = СтрЗаменить(Текст, Символы.ПС, "<BR>");
	
	ПреобразованныйТекст = "<HTML><HEAD>
	|<META http-equiv=Content-Type content=" + """" + "text/html; charset=utf-8" + """" + ">
	|<META content=" + """" + "MSHTML 6.00.2800.1400" + """" + " name=GENERATOR></HEAD>
	|<style type=""text/css"">
	|a { color:#000000 }
	|</style>
	|<BODY><FONT size=2>" + ПреобразованныйТекст + "</FONT></BODY></HTML>";
	
	Для каждого ЭлементСписка из НайденныеЗначения Цикл
		СтрокаЗначения = ЭлементСписка.Значение;
		Индекс = НайденныеЗначения.Индекс(ЭлементСписка);
		СтрокаЗамены = "<a id=index"+ Индекс +" href=""about:index"+Индекс+"""><FONT style=""BACKGROUND-COLOR: #ffd0d0""><STRONG>"+СтрокаЗначения+"</STRONG></FONT></A>";
		Если Найти(ПреобразованныйТекст,СтрокаЗначения) > 0 Тогда
			ПреобразованныйТекст = СтрЗаменить(ПреобразованныйТекст,СтрокаЗначения,СтрокаЗамены);
			ЭлементСписка.Картинка = мКартинкаНайти;
		Иначе
			ЭлементСписка.Картинка = мПустаяКартинка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ПреобразоватьТекст();
	Если НайденныеЗначения.Количество()=0 Тогда
		ЭлементыФормы.НайденныеЗначения.Доступность = Ложь;
		ЭлементыФормы.КоманднаяПанельНайденныхЗначений.Кнопки.Выбрать.Доступность = Ложь;
		ЭлементыФормы.ПолеHTMLДокумента.УстановитьТекст(ПреобразованныйТекст);
	Иначе
		СтрокаСписка = НайденныеЗначения.НайтиПоЗначению(ТекущееЗначение);
		
		Если СтрокаСписка <> Неопределено Тогда
			ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока = СтрокаСписка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НайденныеЗначенияВыбор(Элемент, ЭлементСписка)
	
	ЭтаФорма.Закрыть(Элемент.ТекущаяСтрока.Значение);
	
КонецПроцедуры

Процедура КоманднаяПанель1Выбрать(Кнопка)
	ВыделенныйТекст = ЭлементыФормы.ПолеHTMLДокумента.Документ.selection.CreateRange().text;
	Если ЗначениеЗаполнено(ВыделенныйТекст) Тогда
		ЭтаФорма.Закрыть(ВыделенныйТекст);
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанель2Выбрать(Кнопка)
	
	ЭтаФорма.Закрыть(ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока.Значение);
	
КонецПроцедуры

Процедура КоманднаяПанель1Обновить(Кнопка)
	
	ПреобразоватьТекст();
	ЭлементыФормы.ПолеHTMLДокумента.УстановитьТекст(ПреобразованныйТекст);
	мОтобразилиСсылку = Ложь;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	Если НЕ мОтобразилиСсылку И ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока <> Неопределено Тогда
		Индекс = НайденныеЗначения.Индекс(ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока);
		Ссылка = ЭлементыФормы.ПолеHTMLДокумента.Документ.getElementById("index"+Индекс);
		Если Ссылка <> Неопределено Тогда
			ЭлементыФормы.ПолеHTMLДокумента.Документ.parentWindow.scroll(0,Макс(0,Ссылка.offsetTop-32));
			ВыделитьТекст(ЭлементыФормы.НайденныеЗначения.ТекущаяСтрока.Значение);
			мОтобразилиСсылку = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

мПустаяКартинка    = БиблиотекаКартинок.Выбрать;
мКартинкаНайти     = БиблиотекаКартинок.Найти;

мОтобразилиСсылку = Ложь;