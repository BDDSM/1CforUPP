Процедура ПостроитьСписокВакансий()
	
	ПостроительОтчета = Новый ПостроительОтчета;
	
	ЭлементыФормы.Вакансии.Очистить();
	
	ПостроительОтчета.Текст =
	"ВЫБРАТЬ
	|	ПланШтатка.Должность КАК Должность,
	|	ПланШтатка.Подразделение КАК ПодразделениеКомпании,
	|	ПланШтатка.КоличествоСтавок КАК КоличествоСтавок,
	|	0 КАК КоличествоСтавокОстаток,
	|	ПланШтатка.КоличествоСтавок КАК Нехватка
	|ИЗ
	|	(ВЫБРАТЬ
	|		КадровыйПланСрез.Подразделение КАК Подразделение,
	|		КадровыйПланСрез.Должность КАК Должность,
	|		КадровыйПланСрез.Количество КАК КоличествоСтавок
	|	ИЗ
	|		РегистрСведений.КадровыйПлан.СрезПоследних(&ДатаОтчета, ) КАК КадровыйПланСрез
	|	ГДЕ
	|		КадровыйПланСрез.УдалитьРешение = ЗНАЧЕНИЕ(Перечисление.СостоянияОбъектов.Утвержден)) КАК ПланШтатка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РеальнаяШтатка.Должность,
	|	РеальнаяШтатка.Подразделение,
	|	0,
	|	РеальнаяШтатка.КоличествоСтавокОстаток,
	|	-РеальнаяШтатка.КоличествоСтавокОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
	|		ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
	|		ЗанятыеРабочиеМестаОстатки.КоличествоОстаток КАК КоличествоСтавокОстаток
	|	ИЗ
	|		РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ДатаОтчета, ) КАК ЗанятыеРабочиеМестаОстатки) КАК РеальнаяШтатка
	|ИТОГИ
	|	СУММА(КоличествоСтавок),
	|	СУММА(КоличествоСтавокОстаток),
	|	СУММА(Нехватка)
	|ПО
	|	Должность";
	
	ПостроительОтчета.Параметры.Вставить("ДатаОтчета",ДатаОтчета + 1);
	
	ПостроительОтчета.Выполнить();
	Макет = ПолучитьМакет("МакетВакансий");
	Макет.Параметры.ДатаОтчета = ДатаОтчета;
	ПостроительОтчета.Макет = Макет;

	ПостроительОтчета.Вывести(ЭлементыФормы.Вакансии);
	
	ЭлементыФормы.Вакансии.ПоказатьУровеньГруппировокСтрок(0);
	
КонецПроцедуры

Процедура ДействияФормыСформировать(Кнопка)
	ПостроитьСписокВакансий();
КонецПроцедуры

Процедура ПриОткрытии()
	ДатаОтчета = ТекущаяДата();
	ПостроитьСписокВакансий();
КонецПроцедуры