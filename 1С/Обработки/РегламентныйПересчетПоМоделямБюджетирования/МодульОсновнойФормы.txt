Процедура КнопкаВыполнитьНажатие(Элемент)
	
	Если Не РучнойВыборДокументов Тогда
		
		Если Сценарий.Пустая() Тогда
			Сообщить("Не указан сценарий.");
			Возврат;
		КонецЕсли;
		
		Если ДатаНач='00010101' ИЛИ ДатаКон='00010101' Тогда
			Сообщить("Неверно указан интервал.");
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполнитьПересчетПоМоделям();
		
КонецПроцедуры

// Проверяет даты начала и конца ограничения оборотов и корректирует при необходимости
//
Процедура ПроверкаИнтервала()
	
	Если (ДатаКон='00010101') И (ДатаНач<>'00010101') Тогда
		
		ДатаКон=ОбщегоНазначения.ДатаКонцаПериода(ДатаНач,Сценарий.Периодичность);
		
	ИначеЕсли (ДатаКон<>'00010101') И (ДатаНач='00010101') Тогда
		
		ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(ДатаКон,Сценарий.Периодичность);
		
	ИначеЕсли ДатаКон<ДатаНач Тогда
		
		Если ЭтаФорма.ТекущийЭлемент.Имя="ДатаКон" Тогда
			
			ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(ДатаКон,Сценарий.Периодичность);
			
		Иначе
			
			ДатаКон=ОбщегоНазначения.ДатаКонцаПериода(ДатаНач,Сценарий.Периодичность);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверкаИнтервала()

Процедура УстановитьВидимость()
	
	Если РучнойВыборДокументов Тогда
		ЭлементыФормы.ДокументыДляОбработки.Доступность=Истина;
		ЭлементыФормы.КоманднаяПанельДокументы.Доступность=Истина;
	Иначе
		ЭлементыФормы.ДокументыДляОбработки.Доступность=Ложь;
		ЭлементыФормы.КоманднаяПанельДокументы.Доступность=Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимость()

// Процедура - обработчик нажатия кнопки настройки периода
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	Если НП.Редактировать() Тогда
		
		Если НЕ Сценарий.Пустая() Тогда
			
			ДатаНач=ОбщегоНазначения.ДатаНачалаПериода(НП.ПолучитьДатуНачала(),Сценарий.Периодичность);
			ДатаКон=?(НП.ПолучитьДатуОкончания()='00010101',НП.ПолучитьДатуОкончания(),ОбщегоНазначения.ДатаКонцаПериода(НП.ПолучитьДатуОкончания(),Сценарий.Периодичность));
		Иначе
			
			ДатаНач = НП.ПолучитьДатуНачала();
			ДатаКон = НП.ПолучитьДатуОкончания();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

Процедура ДатаНачПриИзменении(Элемент)
	
	Элемент.Значение=ОбщегоНазначения.ДатаНачалаПериода(Элемент.Значение,Сценарий.Периодичность);
	ПроверкаИнтервала();
	
КонецПроцедуры

Процедура ДатаНачРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	Элемент.Значение=ОбщегоНазначения.ДобавитьИнтервал(Элемент.Значение,Сценарий.Периодичность,Направление);
	
	ПроверкаИнтервала();

КонецПроцедуры

Процедура ДатаКонПриИзменении(Элемент)
	
	Элемент.Значение=ОбщегоНазначения.ДатаКонцаПериода(Элемент.Значение,Сценарий.Периодичность);
	ПроверкаИнтервала();
	
КонецПроцедуры

Процедура ДатаКонРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	Элемент.Значение=ОбщегоНазначения.ДатаКонцаПериода(ОбщегоНазначения.ДобавитьИнтервал(Элемент.Значение,Сценарий.Периодичность,Направление),Сценарий.Периодичность);

	ПроверкаИнтервала();
	
КонецПроцедуры

Процедура КоманднаяПанельДокументыЗаполнить(Кнопка)
	
	Если ДокументыДляОбработки.Количество()>0 И Вопрос("Табличная часть будет очищена.
		|Продолжить?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Нет Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДокументыДляОбработки.Очистить();
	
	ТекстУсловия="
	|РасчетПоМоделиБюджетирования.ПересчитыватьРегламентно = ИСТИНА
	|И РасчетПоМоделиБюджетирования.Проведен";
				 
	 Если ЗначениеЗаполнено(Сценарий) Тогда
		 ТекстУсловия=ТекстУсловия+"
		 |	И РасчетПоМоделиБюджетирования.Сценарий = &Сценарий";
	 КонецЕсли;
				 
	 Если НЕ (ДатаНач='00010101' ИЛИ ДатаКон='00010101') Тогда
		 ТекстУсловия=ТекстУсловия+"
		 |	И РасчетПоМоделиБюджетирования.ДатаРасчета МЕЖДУ &ДатаНач И &ДатаКон";
	 ИначеЕсли НЕ ДатаНач='00010101' Тогда
		 ТекстУсловия=ТекстУсловия+"
		 |	И РасчетПоМоделиБюджетирования.ДатаРасчета >= &ДатаНач";
	 ИначеЕсли НЕ ДатаКон='00010101' Тогда
		 ТекстУсловия=ТекстУсловия+"
		|	И РасчетПоМоделиБюджетирования.ДатаРасчета <= &ДатаКон";	  
	  КонецЕсли;
	  
	 Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	РасчетПоМоделиБюджетирования.Ссылка КАК ДокументРасчет,
	             |	РасчетПоМоделиБюджетирования.Сценарий КАК Сценарий
	             |ИЗ
	             |	Документ.РасчетПоМоделиБюджетирования КАК РасчетПоМоделиБюджетирования
	             |ГДЕ"+ТекстУсловия;
	           				 
	Запрос.УстановитьПараметр("ДатаНач",ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон",КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Сценарий",Сценарий);
	
	Результат=Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока=ДокументыДляОбработки.Добавить();
		НоваяСтрока.РасчетПоМоделиБюджетирования=Результат.ДокументРасчет;
		НоваяСтрока.Сценарий=Результат.Сценарий;
	КонецЦикла;
		
КонецПроцедуры

Процедура ПриОткрытии()
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура РучнойВыборДокументовПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ДокументыДляОбработкиРасчетПоМоделиБюджетированияПриИзменении(Элемент)
	
	ЭлементыФормы.ДокументыДляОбработки.ТекущиеДанные.Сценарий=ЭлементыФормы.ДокументыДляОбработки.ТекущиеДанные.РасчетПоМоделиБюджетирования.Сценарий;
	
КонецПроцедуры