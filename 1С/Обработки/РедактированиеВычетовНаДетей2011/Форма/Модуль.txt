////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СерыйЦвет;
Перем ЗадаватьВопросПриЗакрытии;
Перем ПредыдущееЗначениеОрганизация;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ПриИзмененииОрганизации()

	Заполнение();
	Если СотрудникиСДетьми.Количество() > 0 Тогда
		ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока = СотрудникиСДетьми[0]
	КонецЕсли;
	ОформитьЭУ();

КонецПроцедуры

Функция ДетейВСтрокахВычетов(СтрокиВычета)

	ВсегоДетей = 0;
	Для каждого СтрокаТЧ Из СтрокиВычета Цикл
		ВсегоДетей = Макс(ВсегоДетей, СтрокаТЧ.КоличествоДетей, СтрокаТЧ.КоличествоДетейЗавершения)
	КонецЦикла;
	
	Возврат ВсегоДетей
	
КонецФункции // ДетейВСтрокахВычетов()

Процедура ОформитьЭУ()

	ТекущаяСтрока = ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		ЭлементыФормы.ПанельПереключателей.Свертка = РежимСверткиЭлементаУправления.Верх;
		Возврат
	КонецЕсли;
	
	СтрокиВычета108 = ВычетыСотрудниковСДетьми.НайтиСтроки(Новый Структура("ФизЛицо, КодВычета",ТекущаяСтрока.ФизЛицо, Справочники.ВычетыНДФЛ.Код101));
	ЕстьВычет108 = СтрокиВычета108.Количество() > 0;
	СтрокиВычета110 = ВычетыСотрудниковСДетьми.НайтиСтроки(Новый Структура("ФизЛицо, КодВычета",ТекущаяСтрока.ФизЛицо, Справочники.ВычетыНДФЛ.Код102));
	ЕстьВычет110 = СтрокиВычета110.Количество() > 0;
	СтрокиВычета111 = ВычетыСотрудниковСДетьми.НайтиСтроки(Новый Структура("ФизЛицо, КодВычета",ТекущаяСтрока.ФизЛицо, Справочники.ВычетыНДФЛ.Код111));
	ЕстьВычет111 = СтрокиВычета111.Количество() > 0;
	
	ЭлементыФормы.ПанельПереключателей.Свертка = ?(ЕстьВычет108 Или ЕстьВычет110 Или ЕстьВычет111, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Верх);
	ЭлементыФормы.Панель108.Свертка = ?(ЕстьВычет108, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Лево);
	ЭлементыФормы.ПанельВариантовЗамены108.ТекущаяСтраница = ?(ДетейВСтрокахВычетов(СтрокиВычета108) = 1, ЭлементыФормы.ПанельВариантовЗамены108.Страницы.СтраницаОдногоРебенка, ЭлементыФормы.ПанельВариантовЗамены108.Страницы.СтраницаМногихДетей);
	ЭлементыФормы.Панель110.Свертка = ?(ЕстьВычет110, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Лево);
	ЭлементыФормы.ПанельВариантовЗамены110.ТекущаяСтраница = ?(ДетейВСтрокахВычетов(СтрокиВычета110) = 1, ЭлементыФормы.ПанельВариантовЗамены110.Страницы.СтраницаОдногоРебенка, ЭлементыФормы.ПанельВариантовЗамены110.Страницы.СтраницаМногихДетей);
	ЭлементыФормы.Панель111.Свертка = ?(ЕстьВычет111, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Лево);
	ЭлементыФормы.ПанельВариантовЗамены111.ТекущаяСтраница = ?(ДетейВСтрокахВычетов(СтрокиВычета111) = 1, ЭлементыФормы.ПанельВариантовЗамены111.Страницы.СтраницаОдногоРебенка, ЭлементыФормы.ПанельВариантовЗамены111.Страницы.СтраницаМногихДетей);

	ПорядокОбработки108 = 0;
	ПорядокОбработки110 = 0;
	ПорядокОбработки111 = 0;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыВыполнить(Кнопка)
	
	СтрокиФизлиц = СотрудникиСДетьми.Выгрузить(Новый Структура("ЕстьОшибкиВВычетах", Истина));
	Если СтрокиФизлиц.Количество() <> 0 Тогда
		ОписаниеСотрудников = "";
		Для каждого КлючИЗначение Из ОбщегоНазначения.ПолучитьПредставленияОбъектов(СтрокиФизлиц.ВыгрузитьКолонку("ФизЛицо")) Цикл
			ОписаниеСотрудников = ОписаниеСотрудников + Символы.ПС + КлючИЗначение.Значение
		КонецЦикла;
		РаботаСДиалогами.ВывестиПредупреждение("По следующим сотрудникам выявлены проблемы в новых сведениях о вычетах: " + ОписаниеСотрудников,"Обнаружены ошибки");
		Возврат
	КонецЕсли;
	СохранениеДанных();
	ЗадаватьВопросПриЗакрытии = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТолькоПросмотр = Истина;
	ПредыдущееЗначениеОрганизация = Организация;
	ПриИзмененииОрганизации();
	ЭлементыФормы.НадписьОрганизация.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекущийЭлемент = ЭлементыФормы.СотрудникиСДетьми
	КонецЕсли;
	ЗадаватьВопросПриЗакрытии = Истина;
КонецПроцедуры

Процедура ОбновлениеОтображения()
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЭлементыФормы.НадписьПояснения.Заголовок = "Рабочее место предназначено для замены ""обычных"" вычетов на детей вычетами на второго, третьего (или последующего) ребенка." + Символы.ПС + "Чтобы начать работу, выберите " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организацию") + "."
	ИначеЕсли СотрудникиСДетьми.Количество() = 0 Тогда
		ЭлементыФормы.НадписьПояснения.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("По организации") + " """ + ОбщегоНазначения.ПолучитьПредставленияОбъектов(Организация) + """ уже обработаны все вычеты на детей либо таких вычетов не зарегистрировано! Выберите " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("другую организацию") + "."
	Иначе
		ЭлементыФормы.НадписьПояснения.Заголовок = "Рабочее место предназначено для замены ""обычных"" вычетов на детей, действующих в 2011 году и позже, вычетами на второго, третьего (или последующего) ребенка." + Символы.ПС + "Жирным шрифтом выделены сотрудники, имеющие по данным ИБ двух и более детей."
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЗадаватьВопросПриЗакрытии И ЕстьИзмененияВычетов() Тогда
		// спросим, может сохранить?
		РезультатОтвет = Вопрос("Сохранить текущие изменения?", РежимДиалогаВопрос.ДаНетОтмена,,,"Сохранение данных");
		Если РезультатОтвет = КодВозвратаДиалога.Отмена Тогда 
			Отказ = Истина	
		ИначеЕсли РезультатОтвет = КодВозвратаДиалога.Да Тогда
			СтрокиФизлиц = СотрудникиСДетьми.НайтиСтроки(Новый Структура("ЕстьОшибкиВВычетах", Истина));
			Отказ = СтрокиФизлиц.Количество() > 0;
			Если Отказ Тогда
				РаботаСДиалогами.ВывестиПредупреждение("По некоторым сотрудникам в новых сведениях о вычетах выявлены проблемы. Сохранить эти данные невозможно!","Сохранение данных");
				Возврат
			КонецЕсли;
			СохранениеДанных()
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ПредыдущееЗначениеОрганизация = Организация Тогда
		Возврат
	КонецЕсли;
	
	Если ЕстьИзмененияВычетов() Тогда
		// спросим, может сохранить?
		РезультатОтвет = Вопрос("Сохранить текущие изменения?", РежимДиалогаВопрос.ДаНет,,,"Сохранение данных");
		Если РезультатОтвет = КодВозвратаДиалога.Да Тогда
			СтрокиФизлиц = СотрудникиСДетьми.НайтиСтроки(Новый Структура("ЕстьОшибкиВВычетах", Истина));
			Если СтрокиФизлиц.Количество() <> 0 Тогда
				РаботаСДиалогами.ВывестиПредупреждение("По некоторым сотрудникам в новых сведениях о вычетах выявлены проблемы. Сохранить эти данные невозможно!","Сохранение данных");
				Организация = ПредыдущееЗначениеОрганизация;
				Возврат
			КонецЕсли;
			СохранениеДанных()
		КонецЕсли;
	КонецЕсли;
	
	ПредыдущееЗначениеОрганизация = Организация;
	ПриИзмененииОрганизации();
	
КонецПроцедуры

Процедура КнопкаОбработатьНажатие(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	СтарыеСтроки = НовыеВычетыСотрудниковСДетьми.НайтиСтроки(Новый Структура("ФизЛицо",ТекущаяСтрока.ФизЛицо));
	Если СтарыеСтроки.Количество() > 0 Тогда
		//спросим, не оставить ли?
		РезультатОтвет = Вопрос("Будет произведена замена кодов вычетов, промежуточные результаты редактирования будут потеряны. Продолжить?", РежимДиалогаВопрос.ДаНет,,,"Обработка данных");
		Если РезультатОтвет = КодВозвратаДиалога.Нет Тогда 
			Возврат	
		КонецЕсли;
	КонецЕсли;
	Для каждого СтрокаТЧ Из СтарыеСтроки Цикл
		НовыеВычетыСотрудниковСДетьми.Удалить(СтрокаТЧ)
	КонецЦикла;
	
	ВычетыКОбработке = ВычетыСотрудниковСДетьми.Выгрузить(Новый Структура("ФизЛицо",ТекущаяСтрока.ФизЛицо));
	
	ЗаменаКодаВычета(ВычетыКОбработке, ПорядокОбработки108, Справочники.ВычетыНДФЛ.Код101, Справочники.ВычетыНДФЛ.Код115, Справочники.ВычетыНДФЛ.Код116);
	ЗаменаКодаВычета(ВычетыКОбработке, ПорядокОбработки110, Справочники.ВычетыНДФЛ.Код102, Справочники.ВычетыНДФЛ.Код119, Справочники.ВычетыНДФЛ.Код120);
	ЗаменаКодаВычета(ВычетыКОбработке, ПорядокОбработки111, Справочники.ВычетыНДФЛ.Код111, Справочники.ВычетыНДФЛ.Код123, Справочники.ВычетыНДФЛ.Код124);
	
	ВычетыКОбработке.Сортировать("Физлицо, КодВычета, Период");
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ВычетыКОбработке, НовыеВычетыСотрудниковСДетьми);
	ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТекущаяСтрока = НовыеВычетыСотрудниковСДетьми.Найти(ТекущаяСтрока.ФизЛицо,"ФизЛицо");
	ПроверкаВычетовСотрудника(ТекущаяСтрока.ФизЛицо);
	ТекущаяСтрока.УжеОбработано = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНЫХ ПОЛЕЙ

Процедура ОбработчикОжиданияПриАктивизацииСтроки()
	
	ТекущаяСтрока = ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока;
	НетОтображаемыхСтрок = ТекущаяСтрока = Неопределено;
	Если НетОтображаемыхСтрок Тогда
		ЭлементыФормы.ВычетыСотрудниковСДетьми.ОтборСтрок.ФизЛицо.Установить(Справочники.ФизическиеЛица.ПустаяСсылка());
		ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ОтборСтрок.ФизЛицо.Установить(Справочники.ФизическиеЛица.ПустаяСсылка());
	Иначе 
		ЭлементыФормы.ВычетыСотрудниковСДетьми.ОтборСтрок.ФизЛицо.Установить(ТекущаяСтрока.ФизЛицо);
		ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ОтборСтрок.ФизЛицо.Установить(ТекущаяСтрока.ФизЛицо);
	КонецЕсли;
	
	ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТолькоПросмотр = НетОтображаемыхСтрок;
	
	ОформитьЭУ();
	
КонецПроцедуры

Процедура СотрудникиСДетьмиПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработчикОжиданияПриАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

Процедура СотрудникиСДетьмиПриПолученииДанных(Элемент, ОформленияСтрок)
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.Наименование.Шрифт = ?(ОформлениеСтроки.ДанныеСтроки.КОбязательнойОбработке, ШрифтыСтиля.ШрифтВажнойНадписи, Новый Шрифт());
		ОформлениеСтроки.Ячейки.Наименование.ЦветТекста = ?(ОформлениеСтроки.ДанныеСтроки.ЕстьОшибкиВВычетах, ЦветаСтиля.ТекстСообщенияОПроблемах, ?(ОформлениеСтроки.ДанныеСтроки.УжеОбработано, СерыйЦвет, Новый Цвет()))
	КонецЦикла;
КонецПроцедуры


Процедура ВычетыПриПолученииДанных(Элемент, ОформленияСтрок)
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.КолонкаПериод.Видимость = Ложь;	
		ОформлениеСтроки.Ячейки.ВычетыНаДетейТекст.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.ВычетыНаДетейПрименение.Значение = ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.КоличествоДетей);
	КонецЦикла;
КонецПроцедуры


Процедура НовыеВычетыСотрудниковСДетьмиПослеУдаления(Элемент)
	ТекущаяСтрокаСотрудника = ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока;
	ТекущаяСтрокаСотрудника.УжеОбработано = НовыеВычетыСотрудниковСДетьми.НайтиСтроки(Новый Структура("ФизЛицо", ТекущаяСтрокаСотрудника.ФизЛицо)).Количество() <> 0;
	ПроверкаВычетовСотрудника(ТекущаяСтрокаСотрудника.ФизЛицо);
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если НоваяСтрока И Не Копирование Тогда
		ТекущиеДанные.КодВычета = Справочники.ВычетыНДФЛ.Код101;	
        ТекущиеДанные.КоличествоДетей = 1;
	КонецЕсли;
	
	Элемент.Колонки.ВычетыНаДетейПрименение.ЭлементУправления.Значение = ЗначениеЗаполнено(ТекущиеДанные.КоличествоДетей);
	
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Не ОтменаРедактирования Тогда
		ТекущаяСтрокаСотрудника = ЭлементыФормы.СотрудникиСДетьми.ТекущаяСтрока;
		Если НоваяСтрока Тогда
			Элемент.ТекущаяСтрока.ФизЛицо = ТекущаяСтрокаСотрудника.ФизЛицо
		КонецЕсли;
		ПроверкаВычетовСотрудника(ТекущаяСтрокаСотрудника.ФизЛицо);
		Если Не ТекущаяСтрокаСотрудника.УжеОбработано Тогда
			ТекущаяСтрокаСотрудника.УжеОбработано = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


Процедура НовыеВычетыСотрудниковСДетьмиПериодЗавершенияПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		Элемент.Значение = КонецМесяца(Элемент.Значение)
	КонецЕсли;
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиВычетыНаДетейПрименениеПриИзменении(Элемент)
	ТекущиеДанные = ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТекущиеДанные;
	Если Не Элемент.Значение Тогда
        ТекущиеДанные.КоличествоДетей = 0;
    ИначеЕсли ТекущиеДанные.КодВычета = Справочники.ВычетыНДФЛ.ПустаяСсылка() Тогда
        ТекущиеДанные.КодВычета = Справочники.ВычетыНДФЛ.Код101;
        ТекущиеДанные.КоличествоДетей = 1;
	Иначе 	
        ТекущиеДанные.КоличествоДетей = 1;
	КонецЕсли;
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиКодВычетаНаДетейНачалоВыбора(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = Справочники.ВычетыНДФЛ.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Отбор.ГруппаВычета.ВидСравнения	= ВидСравнения.Равно;
	ФормаВыбора.Отбор.ГруппаВычета.Значение		= Перечисления.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей;
	ФормаВыбора.Отбор.ГруппаВычета.Использование= Истина;
	ФормаВыбора.НачальноеЗначениеВыбора = Элемент.Значение;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиКодВычетаНаДетейПриИзменении(Элемент)
	
	ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.Колонки.ВычетыНаДетейПрименение.ЭлементУправления.Значение = ЗначениеЗаполнено(Элемент.Значение);	
	Если ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТекущиеДанные.КоличествоДетей = 0 Тогда
		ЭлементыФормы.НовыеВычетыСотрудниковСДетьми.ТекущиеДанные.КоличествоДетей = 1;	
	КонецЕсли;
	
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиКодВычетаНаДетейАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, Новый Структура("ГруппаВычета",Перечисления.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей), Тип("СправочникСсылка.ВычетыНДФЛ"));
	
КонецПроцедуры

Процедура НовыеВычетыСотрудниковСДетьмиКодВычетаНаДетейОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, Новый Структура("ГруппаВычета",Перечисления.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей), ЭтаФорма, Тип("СправочникСсылка.ВычетыНДФЛ"), Ложь, Ложь, Неопределено, Ложь);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СерыйЦвет = Новый Цвет(180, 180, 180);