Процедура УстановитьОтметкуНезаполненногоОрганизации()
	ЭлементыФормы.Организация.АвтоОтметкаНезаполненного = ОтражатьВБухгалтерскомУчете;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЭлементыФормы.Организация.ОтметкаНезаполненного = ОтражатьВБухгалтерскомУчете;
	КонецЕсли;	
КонецПроцедуры	

Процедура ЗаполнитьИзмерениеПроведенВХронологическойПоследовательности(ИмяПоследовательности, Дата, ПарОрганизация)
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                     |	ПартионныйУчет.Регистратор
	                     |ИЗ
	                     |	Последовательность."+ИмяПоследовательности+" КАК ПартионныйУчет
	                     |ГДЕ
	                     |	ПартионныйУчет.Период < &Дата
	                     |	И (НЕ ПартионныйУчет.ПроведенВХронологическойПоследовательности)
	                     |	И ПартионныйУчет.Организация = &Организация");
	Запрос.УстановитьПараметр("Дата", Дата);					 
	Запрос.УстановитьПараметр("Организация", ПарОрганизация);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		НаборЗаписей = Последовательности[ИмяПоследовательности].СоздатьНаборЗаписей();
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			Для Каждого ТекущаяЗапись Из НаборЗаписей Цикл
				ТекущаяЗапись.ПроведенВХронологическойПоследовательности = Истина;
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры	

Процедура ИзменитьГраницу(Учет, МоментВремениДокумента, ОтборДляГраницы)
	ПустойМоментВремени = Новый МоментВремени('00010101', Неопределено);
	
	ИмяПоследовательности = ?(Учет = "Упр","ПартионныйУчет","ПартионныйУчетБУ");
	
	ТекущаяГраница = Обработки.ПроведениеПоПартиям.Создать().ПолучитьГраницуПоследовательности(Учет, ОтборДляГраницы.Организация);
	Последовательности[ИмяПоследовательности].ПолучитьГраницу(ОтборДляГраницы);
	
	Если ТекущаяГраница = ПустойМоментВремени Тогда
		Если Вопрос("По организации """ + Организация + """ в последовательности " + ?(Учет = "Упр","управленческого партионного учета","бухгалтерского и налогового партионного учета")+ " границы не существует, создать?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если ТекущаяГраница.Сравнить(МоментВремениДокумента) = -1 
		  И Вопрос("Вы пытаетесь переместить границу последовательности " + ?(Учет = "Упр","управленческого партионного учета","бухгалтерского и налогового партионного учета")+ " по организации """ + Организация + """ вперед, это может привести к некорректным данным о себестоимости! Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда	
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	НачатьТранзакцию();
	
	Последовательности[ИмяПоследовательности].УстановитьГраницу(МоментВремениДокумента, ОтборДляГраницы);
	ЗаполнитьИзмерениеПроведенВХронологическойПоследовательности(ИмяПоследовательности, МоментВремениДокумента.Дата, ОтборДляГраницы.Организация);
	
	ЗафиксироватьТранзакцию();

КонецПроцедуры	

Процедура ОсновныеДействияФормыОсновныеДействияФормыВыполнить(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(ДатаДокумент) Тогда
		Предупреждение("Не указана дата / документ");
		Возврат;
	ИначеЕсли ТипЗнч(ДатаДокумент) = Тип("Дата") Тогда
		МоментВремениДокумента = Новый МоментВремени(ДатаДокумент);
	Иначе
		МоментВремениДокумента = Новый МоментВремени(ДатаДокумент.Дата, ДатаДокумент);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Организация) И ОтражатьВБухгалтерскомУчете Тогда
		Предупреждение("Не указана Организация. При установке границы последовательности по бух. и нал. партионному учету указание организации обязательно");
	    Возврат;
	КонецЕсли;	
		
	ОтборДляГраницы = Новый Структура("Организация, ПроведенВХронологическойПоследовательности", Организация, Ложь);
	
	Если ОтражатьВУправленческомУчете Тогда
		ИзменитьГраницу("Упр", МоментВремениДокумента, ОтборДляГраницы)
	КонецЕсли;
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		ИзменитьГраницу("Бух", МоментВремениДокумента, ОтборДляГраницы)
	КонецЕсли;
	
КонецПроцедуры

Процедура ДатаДокументПриИзменении(Элемент)
	
	ЭлементыФормы.Организация.Доступность = Истина;
	
	Если ДатаДокумент = Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли ТипЗнч(ДатаДокумент) <> Тип("Дата")
	  И ДатаДокумент.Метаданные().Реквизиты.Найти("Организация") <> Неопределено Тогда
		
		Организация = ДатаДокумент.Организация;
		
		Если ОтражатьВБухгалтерскомУчете Тогда
			ЭлементыФормы.Организация.Доступность = Ложь;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтражатьВБухгалтерскомУчетеПриИзменении(Элемент)
	УстановитьОтметкуНезаполненногоОрганизации();
КонецПроцедуры

Процедура ПриОткрытии()
	УстановитьОтметкуНезаполненногоОрганизации();
КонецПроцедуры