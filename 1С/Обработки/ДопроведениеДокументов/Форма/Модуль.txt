//Обработчик нажатия на кнопку выбора периода допроведения
Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(НачалоИнтервалаДопроведения, ?(КонецИнтервалаДопроведения='0001-01-01', КонецИнтервалаДопроведения, КонецДня(КонецИнтервалаДопроведения)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	Если НастройкаПериода.Редактировать() Тогда
		НачалоИнтервалаДопроведения = НастройкаПериода.ПолучитьДатуНачала();
		КонецИнтервалаДопроведения = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры

//Обработчик нажатия на кнопку ВыполнитьДопроведениеДокументов
Процедура ОсновныеДействияФормыВыполнитьДопроведение(Кнопка)
	Если НЕ ЗначениеЗаполнено(НастройкаДопроведенияДокументов) Тогда
		Предупреждение("Не указана настройка допроведения",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	Если СписокОрганизаций.Количество() = 0 Тогда
		Предупреждение("Выбранная настройка не назначен ни одной организации",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(НачалоИнтервалаДопроведения) ИЛИ НЕ ЗначениеЗаполнено(КонецИнтервалаДопроведения) Тогда
		Предупреждение("Не указан период допроведения",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	Если НачалоИнтервалаДопроведения > КонецИнтервалаДопроведения Тогда
		Предупреждение("Неверно указан период: дата начала позже даты окончания",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	ТаблицаОрганизаций = Новый ТаблицаЗначений;
	ТаблицаОрганизаций.Колонки.Добавить("Организация");
	ТаблицаОрганизаций.Колонки.Добавить("ДатаНачалаОтложенногоПроведения");
	Для Каждого ЭлементСписка ИЗ СписокОрганизаций Цикл
		Если НЕ ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		ТекОрганизация = ЭлементСписка.Значение;
		ДатаНачалаОтложенногоПроведения = ОтложенноеПроведениеДокументов.ПолучитьДатуНачалаОтложенногоПроведения(ТекОрганизация);
		НоваяСтрока = ТаблицаОрганизаций.Добавить();
		НоваяСтрока.Организация = ТекОрганизация;
		НоваяСтрока.ДатаНачалаОтложенногоПроведения = ДатаНачалаОтложенногоПроведения;
	КонецЦикла;
	Если ТаблицаОрганизаций.Количество() = 0 Тогда
		Предупреждение("В списке не выбрана ни одна организация",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	
	ПараметрыДопроведения = Новый Структура("ДопроводитьВсеДокументы, 
							|ДатаНачала, ДатаОкончания", 
							ДопроводитьВсеДокументы, НачалоДня(НачалоИнтервалаДопроведения), 
							КонецДня(КонецИнтервалаДопроведения));

	ОтложенноеПроведениеДокументов.ВыполнитьДопроведениеДокументовПоСпискуОрганизаций(ТаблицаОрганизаций, ПараметрыДопроведения);
КонецПроцедуры

//Обработчик события ПриИзменении элемента НастройкаДопроведенияДокументов
Процедура НастройкаДопроведенияДокументовПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(НастройкаДопроведенияДокументов) Тогда
		НачалоИнтервалаДопроведения 	= Дата("00010101000000");
		КонецИнтервалаДопроведения 		= Дата("00010101000000");
		СписокОрганизаций.Очистить();
		Возврат;
	КонецЕсли;
	
	//Определим период допроведения
	ПериодДопроведения 		= ОтложенноеПроведениеДокументов.ОпределитьПериодДопроведения(НастройкаДопроведенияДокументов);
	НачалоИнтервалаДопроведения 	= ПериодДопроведения.НачалоИнтервала;
	КонецИнтервалаДопроведения 	= ПериодДопроведения.КонецИнтервала;
	
	//Определим какие документы необходимо допроводить
	ДопроводитьВсеДокументы = НастройкаДопроведенияДокументов.ДопроводитьВсеДокументы;

	//Определим список организаций
	СписокОрганизаций.Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Организация
	|ИЗ
	|	РегистрСведений.НастройкаОтложенногоПроведения
	|ГДЕ НастройкаДопроведенияДокументов = &ТекРегламент
	|УПОРЯДОЧИТЬ ПО Организация";
	Запрос.УстановитьПараметр("ТекРегламент", НастройкаДопроведенияДокументов);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Предупреждение("Выбранная настройка не назначена ни одной организации",,"Допроведение документов");
		Возврат;
	КонецЕсли;
	СписокОрганизаций.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Организация"));
	СписокОрганизаций.ЗаполнитьПометки(Истина);
КонецПроцедуры

//Обработчик нажатия на кнопку ДиаграммаОтложенногоПроведения
Процедура ДействияФормыДиаграммаОтложенногоПроведения(Кнопка)
	Форма = Отчеты.ДиаграммаОтложенногоПроведения.ПолучитьФорму();
	Форма.Открыть();
	Форма.СформироватьДиаграмму();
КонецПроцедуры