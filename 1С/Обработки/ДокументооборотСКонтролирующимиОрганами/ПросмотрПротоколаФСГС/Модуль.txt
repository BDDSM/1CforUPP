Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Заголовок = "Протокол " + ИмяФайла;
	
	// считываем текст из файла уведомления
	Попытка
		ЧтениеТекста = Новый ЧтениеТекста;
		ЧтениеТекста.Открыть(Протокол);
		ТекстПротокола = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();
	Исключение
		Отказ = Истина;
		Предупреждение("Ошибка чтения содержимого протокола из файла:
						|
						|" + ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
	// определяем дату-время получения
	ДатаВремяОтправки = "";
	Для СчетчикСтрок = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл
		ТекСтрока = СтрПолучитьСтроку(ТекстПротокола, СчетчикСтрок);
		ПозСтрДата = Найти(ТекСтрока, "Дата передачи файла:");
		Если ПозСтрДата > 0 Тогда
			ДатаВремяОтправки = Формат(ДатаВремяИзСтроки(СокрЛП(Сред(ТекСтрока, ПозСтрДата + 20))), "ДЛФ=DDT; ДП=-");
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	// получаем признак того, что протокол положительный
	Если Найти(ТекстПротокола, "Отчёт не принят") > 0 Тогда
		ПротоколЯвляетсяПоложительным = "Нет";
		ЭлементыФормы.НадписьПротоколЯвляетсяПоложительным.ЦветТекста = Новый Цвет(255, 0, 0);
	Иначе
		ПротоколЯвляетсяПоложительным = "Да";
	КонецЕсли;
	
КонецПроцедуры

Функция ДатаВремяИзСтроки(ЗначениеСтр)
	
	ЗначСтр = СокрЛП(ЗначениеСтр);
	
	Поле = "";
	МассивПолейДаты = Новый Массив;
	Для Счетчик = 1 По СтрДлина(ЗначСтр) Цикл
		ТекСимвол = Сред(ЗначСтр, Счетчик, 1);
		Если Найти("T -/:.", ТекСимвол) > 0 Тогда
			МассивПолейДаты.Добавить(Поле);
			Поле = "";
		Иначе
			Поле = Поле + ТекСимвол;
			Если Счетчик = СтрДлина(ЗначСтр) Тогда
				МассивПолейДаты.Добавить(Поле);
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Возврат Дата(Число(МассивПолейДаты[2])
					,Число(МассивПолейДаты[1])
					,Число(МассивПолейДаты[0])
					,Число(?(МассивПолейДаты.ВГраница() >= 3, МассивПолейДаты[3], 0))
					,Число(?(МассивПолейДаты.ВГраница() >= 4, МассивПолейДаты[4], 0))
					,Число(?(МассивПолейДаты.ВГраница() >= 5, МассивПолейДаты[5], 0)));
	Исключение
		Возврат '00010101';
	КонецПопытки;
	
КонецФункции