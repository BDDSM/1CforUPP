Перем ТекущаяСтрокаНаправления;
Перем Организация;
Перем Действие;
Перем Направления;
////////////////////////////////////////////////////////////////////////
////ПАНЕЛЬ ПРЕДОПРЕДЕЛЕННЫЕ
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ТекущаяСтрокаНаправления = КлючУникальности.ТекущаяСтрокаНаправления;
	Организация = КлючУникальности.Организация;
	Действие = КлючУникальности.Действие;
	Направления = КлючУникальности.Направления;
	ДопКодФСС = КлючУникальности.ДопКодФСС;
	Если ТекущаяСтрокаНаправления = Неопределено 
		И Действие <> "Добавить" Тогда
		Сообщить("Выберите строку для редактирования!");
		Отказ = истина;
		Возврат
	КонецЕсли;

КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Действие = "Добавить" Тогда
		ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФНС;
	Иначе
		ТипПолучателя = ТекущаяСтрокаНаправления.ТипПолучателя;
		
	КонецЕсли;
	
	ОбновитьОтображениеПанелиНаправления(1);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////
////ПАНЕЛЬ ДанныеНаправления

Процедура ОбновитьОтображениеПанелиНаправления(Параметр)
	
	КПП = "";
	
	
	Если ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		
		ЭлементыФормы.КПП.Доступность = (СтрДлина(Организация.ИНН) = 10) И НЕ(ЭтаФорма.ВладелецФормы.ДокументОбъект.ПризнакУполномоченногоПредставителя);
		
		ЭлементыФормы.КодПолучателя.Маска = "9999";
		ЭлементыФормы.КодПолучателя.ОграничениеТипа = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(4);
		
		ЭлементыФормы.КПП.Маска = "9999UU999";
		ЭлементыФормы.КПП.ОграничениеТипа = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(9);
		
		ДанныеОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация,ТекущаяДата(),"КодНО");
		Если Действие = "Редактировать" И Параметр = 1  Тогда
			КодПолучателя = ТекущаяСтрокаНаправления.КодПолучателя;
			КПП = ТекущаяСтрокаНаправления.КПП;
		ИначеЕсли Действие = "Скопировать"  И Параметр = 1 Тогда 
			КодПолучателя = ТекущаяСтрокаНаправления.КодПолучателя;
		Иначе
			КодПолучателя = ?(ДанныеОрганизации <> Неопределено,ДанныеОрганизации.КодНО,"");
			КПП = ?(Направления.Найти(ТипПолучателя) <> Неопределено,"", Организация.КПП);
		КонецЕсли;
		
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		
		ЭлементыФормы.КПП.Доступность = Ложь;
		ЭлементыФормы.КодПолучателя.ОграничениеТипа = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(7);
		ЭлементыФормы.КодПолучателя.Маска = "999-999";
		
		Если Действие = "Добавить" ИЛИ (Действие = "Редактировать" И Параметр = 2)Тогда
			Если ПустаяСтрока(Организация.КодОрганаПФР) Или СтрДлина(Организация.КодОрганаПФР) < 7 Тогда 
				КодПолучателя = Лев(Организация.РегистрационныйНомерПФР,7);
			Иначе
				КодПолучателя = Организация.КодОрганаПФР;
			КонецЕсли;
		Иначе 
			КодПолучателя = ТекущаяСтрокаНаправления.КодПолучателя;
		КонецЕсли;
		
		
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		
		ЭлементыФормы.КПП.Доступность = Ложь;
		ЭлементыФормы.КодПолучателя.ОграничениеТипа = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(4);
		ЭлементыФормы.КодПолучателя.Маска = "9999";
		
		Если Действие = "Добавить" ИЛИ (Действие = "Редактировать" И Параметр = 2) Тогда
			КодПолучателя = ?(ПустаяСтрока(ДопКодФСС),Лев(Организация.РегистрационныйНомерФСС,4),Лев(ДопКодФСС,4));
		Иначе
			КодПолучателя = ТекущаяСтрокаНаправления.КодПолучателя;
		КонецЕсли;
		
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
		
		ЭлементыФормы.КПП.Доступность = Ложь;
		ЭлементыФормы.КодПолучателя.ОграничениеТипа = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(5);
		ЭлементыФормы.КодПолучателя.Маска = "99-99";
		
		Если Действие = "Добавить" ИЛИ (Действие = "Редактировать" И Параметр = 2) Тогда
			КодПолучателя = Организация.КодОрганаФСГС;
		Иначе
			КодПолучателя = ТекущаяСтрокаНаправления.КодПолучателя;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыФормы.КПП.АвтоОтметкаНезаполненного = ЭлементыФормы.КПП.Доступность;
	ЭлементыФормы.КПП.ОтметкаНезаполненного = ЭлементыФормы.КПП.Доступность;
	ЭлементыФормы.КПП.АвтоВыборНезаполненного = ЭлементыФормы.КПП.Доступность;
	
КонецПроцедуры

Процедура РедактироватьНаправление()
	
	Если ТекущаяСтрокаНаправления = Неопределено Тогда
		Возврат;
	ИначеЕсли ПроверитьЗаполненностьПолейНаправления() Тогда
		СтрокаНаправления = ТекущаяСтрокаНаправления;
		СтрокаНаправления.ТипПолучателя = ТипПолучателя;
		СтрокаНаправления.КодПолучателя = КодПолучателя;
		СтрокаНаправления.КПП = КПП;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьНаправление()
	
	Если ТипПолучателя = "" Тогда
		Возврат;
	ИначеЕсли ПроверитьЗаполненностьПолейНаправления() Тогда 
		
		НоваяСтрокаНаправления = ЭтаФорма.ВладелецФормы.ДокументОбъект.Получатели.Добавить();
		НоваяСтрокаНаправления.ТипПолучателя = ТипПолучателя;
		НоваяСтрокаНаправления.КодПолучателя = КодПолучателя;
		НоваяСтрокаНаправления.КПП = КПП;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТипПолучателяПриИзменении(Элемент)
	
	ОбновитьОтображениеПанелиНаправления(2);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОсновныеДействияФормыДобавить(Кнопка)
	
	Если Действие = "Добавить" Или Действие = "Скопировать" Тогда
		ДобавитьНаправление();
	Иначе
		РедактироватьНаправление();
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьЗаполненностьПолейНаправления()
	
	Если ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		
		Если НЕ(КонтекстЭДО.ПроверитьЦифровойКодЗаданнойДлины(КодПолучателя, 4)) Тогда
			Предупреждение("Не введен код получателя." + Символы.ПС);
			Возврат Ложь;
		ИначеЕсли ЭлементыФормы.КПП.Доступность И НЕ(КонтекстЭДО.ПроверитьКПП(КПП)) Тогда
			Предупреждение("Не корректно заполнен КПП." + Символы.ПС);
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		
		Если СтрДлина(СокрЛП(КодПолучателя))<> 7 Тогда
			Предупреждение("Не введен код получателя." + Символы.ПС);
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		
		Если НЕ(КонтекстЭДО.ПроверитьЦифровойКодЗаданнойДлины(КодПолучателя, 4)) Тогда
			Предупреждение("Не введен код получателя (код подчиненности ФСС)." + Символы.ПС);
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
		
		Если СтрДлина(СокрЛП(КодПолучателя))<> 5 Тогда
			Предупреждение("Не введен код получателя." + Символы.ПС);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		
		СтруктураОтбора = Новый Структура("ТипПолучателя, КодПолучателя, КПП",ТипПолучателя, КодПолучателя, КПП);
		НайденныеСтроки = ЭтаФорма.ВладелецФормы.ДокументОбъект.Получатели.НайтиСтроки(СтруктураОтбора);
		
		Если НайденныеСтроки.Количество()>0 Тогда
			
			Если Действие <> "Скопировать" И ТекущаяСтрокаНаправления <> Неопределено И НайденныеСтроки.Количество() = 1
				И ТекущаяСтрокаНаправления.КодПолучателя= КодПолучателя
				И ТекущаяСтрокаНаправления.ТипПолучателя= ТипПолучателя
				И ТекущаяСтрокаНаправления.КПП= КПП Тогда
			Иначе
				
				Предупреждение("Получатель с таким кодом налоговой и КПП уже добавлен.");
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе СтруктураОтбора = Новый Структура("ТипПолучателя",ТипПолучателя);
		
		НайденныеСтроки = ЭтаФорма.ВладелецФормы.ДокументОбъект.Получатели.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество()>0 Тогда
			Если Действие <> "Скопировать" И ТекущаяСтрокаНаправления <> Неопределено И НайденныеСтроки.Количество() = 1
				И ТекущаяСтрокаНаправления.ТипПолучателя= ТипПолучателя Тогда
			Иначе
				Предупреждение("Направление сдачи в " + ТипПолучателя + " уже добавлено.");
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ТипПолучателяОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Возврат;
КонецПроцедуры