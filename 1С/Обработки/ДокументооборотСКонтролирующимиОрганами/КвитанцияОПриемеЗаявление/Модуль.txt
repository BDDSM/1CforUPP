Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// извлекаем файл квитанции из содержимого сообщения
	СтрКвитанции = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.КвитанцияОПриемеЗаявления, ИмяФайлаКвитанции);
	Если СтрКвитанции.Количество() = 0 Тогда
		Предупреждение("Квитанция о приеме заявления не обнаружена среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрКвитанция = СтрКвитанции[0];
	
	// записываем вложение во временный файл
	ФайлКвитанции = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрКвитанция.Данные.Получить().Записать(ФайлКвитанции);
	Исключение
		Сообщить("Ошибка выгрузки квитанции о приеме во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем квитанцию из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлКвитанции, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлКвитанции);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла квитанции о приеме:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// анализируем XML
	УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелСвКвит = УзелДокумент.Строки.Найти("СвКвит", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСвКвит) Тогда
		Сообщить("Некорректная структура XML квитанции о приеме: не обнаружен узел ""СвКвит"".", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	// разбираем узел с перечнем ошибок
	УзлыОшибки = УзелСвКвит.Строки.НайтиСтроки(Новый Структура("Имя", "Ошибки"));
	Для Каждого УзелОшибки Из УзлыОшибки Цикл
		
		УзелКодОшиб = УзелОшибки.Строки.Найти("КодОшиб", "Имя");
		УзелТекстОшиб = УзелОшибки.Строки.Найти("ТекстОшиб", "Имя");
		
		КодОшибки = ?(ЗначениеЗаполнено(УзелКодОшиб), СокрЛП(УзелКодОшиб.Значение), "");
		ТекстОшибки = ?(ЗначениеЗаполнено(УзелТекстОшиб), СокрЛП(УзелТекстОшиб.Значение), "");
		
		НовСтр = Ошибки.Добавить();
		НовСтр.КодОшибки = КодОшибки;
		НовСтр.Описание = ТекстОшибки;
		
	КонецЦикла;
	
	Если Ошибки.Количество() = 0 Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.ВыявленныеНарушения.Видимость = Ложь;
		ЭлементыФормы.ОсновнаяПанель.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
	КонецЕсли;
	
	// разбираем узел с общими сведениями
	ОбщиеСведения = Новый Структура;
	Для Каждого УзелОбщСвед Из УзелСвКвит.Строки Цикл
		ОбщиеСведения.Вставить(УзелОбщСвед.Имя, СокрЛП(УзелОбщСвед.Значение));
	КонецЦикла;
	Если ОбщиеСведения.Свойство("ДатаНапр") И ОбщиеСведения.Свойство("ВремНапр") Тогда
		ОбщиеСведения.Вставить("ДатаВремяНапр", ОбщиеСведения.ДатаНапр + " " + ОбщиеСведения.ВремНапр);
	КонецЕсли;
	
	// устанавливаем заголовок страницы панели, содержащей список нарушений
	ЭлементыФормы.ОсновнаяПанель.Страницы.ВыявленныеНарушения.Заголовок = ЭлементыФормы.ОсновнаяПанель.Страницы.ВыявленныеНарушения.Заголовок + " (" + Формат(Ошибки.Количество(), "ЧН=; ЧГ=") + ")";
	
	Если ОбщиеСведения.Свойство("ИмяПринятФайла") Тогда
		ИмяПринятФайла = ОбщиеСведения.ИмяПринятФайла;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаВремяНапр") Тогда
		ДатаВремяНапр = ОбщиеСведения.ДатаВремяНапр;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("КНД") Тогда
		КНД = ОбщиеСведения.КНД;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НаимВидДок") Тогда
		НаимВидДок = ОбщиеСведения.НаимВидДок;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НомерДокНП") Тогда
		НомерДокНП = ОбщиеСведения.НомерДокНП;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаДокНП") Тогда
		ДатаДокНП = ОбщиеСведения.ДатаДокНП;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаПост") Тогда
		ДатаПост = ОбщиеСведения.ДатаПост;
	КонецЕсли;
	Если ОбщиеСведения.Свойство("ДатаПрин") Тогда
		ДатаПрин = ОбщиеСведения.ДатаПрин;
	КонецЕсли;
	Если ОбщиеСведения.Свойство("РегНом") Тогда
		РегНом = ОбщиеСведения.РегНом;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ОбщиеСведенияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры