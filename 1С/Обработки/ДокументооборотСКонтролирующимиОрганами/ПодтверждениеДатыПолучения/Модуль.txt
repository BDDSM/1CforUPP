Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// извлекаем файл подтверждения даты получения из содержимого сообщения
	ТипыДИВ = Новый Массив;
	ТипыДИВ.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПодтверждениеДатыПолучения);
	ТипыДИВ.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПодтверждениеОператораФСГС);
	СтрПодтверждения = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, ТипыДИВ, ИмяФайлаПодтверждения);
	Если СтрПодтверждения.Количество() = 0 Тогда
		Предупреждение("Подтверждение даты получения не обнаружено среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрПодтверждение = СтрПодтверждения[0];
	
	// записываем вложение во временный файл
	ФайлПодтверждения = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрПодтверждение.Данные.Получить().Записать(ФайлПодтверждения);
	Исключение
		Сообщить("Ошибка выгрузки подтверждения даты получения во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем подтверждение из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлПодтверждения, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлПодтверждения);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла подтверждения даты получения:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если СтрПодтверждение.Тип = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПодтверждениеОператораФСГС Тогда
		
		УзелПодтверждениеОператора = ДеревоXML.Строки.Найти("подтверждениеОператора", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелПодтверждениеОператора) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""ПодтверждениеОператора"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелПосылка = УзелПодтверждениеОператора.Строки.Найти("посылка", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелПосылка) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Посылка"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелОтправитель = УзелПосылка.Строки.Найти("отправитель", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелОтправитель) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Отправитель"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелДокументы = УзелПосылка.Строки.Найти("документы", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелДокументы) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Документы"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		// получаем идентификаторы поступивших файлов
		ИдентификаторыДокументов = "";
		Для каждого УзелДокумент Из УзелДокументы.Строки Цикл
			ИдентификаторДокумента = УзелДокумент.Строки.Найти("идентификаторДокумента", "Имя");
			Если ЗначениеЗаполнено(ИдентификаторДокумента) Тогда
				ИдентификаторыДокументов = ИдентификаторыДокументов + ?(ПустаяСтрока(ИдентификаторыДокументов), "", ",") + СокрЛП(ИдентификаторДокумента.Значение);
			КонецЕсли;
		КонецЦикла;
		
		// извлекаем имена поступивших файлов из сообщений цикла обмена по идентификаторам
		ИмяПоступившегоФайла = "";
		Если НЕ ПустаяСтрока(ИдентификаторыДокументов) Тогда
			
			СообщенияЦиклаОбмена = КонтекстЭДО.ПолучитьСообщенияЦиклаОбмена(Сообщение.ЦиклОбмена).ВыгрузитьКолонку("Ссылка");
			ТипыДИВ = Новый Массив;
			ТипыДИВ.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПисьмоФСГС);
			ТипыДИВ.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПриложениеПисьмаФСГС);
			ТипыДИВ.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ФайлОтчетностиФСГС);
			ДокументыЦиклаОбмена = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(СообщенияЦиклаОбмена,, ТипыДИВ);
			
			Для каждого СтрДокументЦиклаОбмена Из ДокументыЦиклаОбмена Цикл
				Если Найти(ИдентификаторыДокументов, СокрЛП(СтрДокументЦиклаОбмена.Идентификатор)) > 0 Тогда
					ИмяПоступившегоФайла = ИмяПоступившегоФайла + ?(ПустаяСтрока(ИмяПоступившегоФайла), "", ", ") + СокрЛП(СтрДокументЦиклаОбмена.ИмяФайла);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// получаем сведения о дате и времени поступившего файла
		УзелДатаВремяОтправки = УзелПодтверждениеОператора.Строки.Найти("датаВремяОтправки", "Имя");
		Если ЗначениеЗаполнено(УзелДатаВремяОтправки) Тогда
			ДатаВремяОтправки = СокрЛП(XMLЗначение(Тип("Дата"), УзелДатаВремяОтправки.Значение));
			ДатаОтправки = Сокрлп(ДатаВремяОтправки);
		КонецЕсли;
		
		// сведения о подтвердившей организации
		УзелИдентификаторОтправителя = УзелОтправитель.Строки.Найти("идентификатор", "Имя");
		Если ЗначениеЗаполнено(УзелИдентификаторОтправителя) Тогда
			ИдентификаторОтправителя = СокрЛП(УзелИдентификаторОтправителя.Значение);
		Иначе
			ИдентификаторОтправителя = "";
		КонецЕсли;
		ИдентификаторСпецоператора = Лев(ИдентификаторОтправителя, Найти(ИдентификаторОтправителя, ".") - 1);
		ОрганизацияСтр = "Спецоператор связи" + ?(ПустаяСтрока(ИдентификаторСпецоператора), "", ": " + ИдентификаторСпецоператора);
	
	Иначе
		
		УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
			Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелСвОтпр = УзелДокумент.Строки.Найти("СвОтпр", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелСвОтпр) Тогда
			Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения о налоговом органе - отправителе подтверждения.", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		УзелКодНО = УзелСвОтпр.Строки.Найти("КодНО", "Имя");
		Если УзелКодНО = Неопределено Тогда
			Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения о налоговом органе - отправителе подтверждения.", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ОрганизацияСтр = "Налоговый орган " + СокрЛП(УзелКодНО.Значение);
		
		// получаем сведения подтверждения
		УзелСведПодтв = УзелДокумент.Строки.Найти("СведПодтв", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелСведПодтв) Тогда
			Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения подтверждения.", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		// получаем сведения об имени поступившего файла
		УзелИдФайлПол = УзелСведПодтв.Строки.Найти("ИдФайлПол", "Имя", Истина);
		Если ЗначениеЗаполнено(УзелИдФайлПол) Тогда
			ИмяПоступившегоФайла = СокрЛП(УзелИдФайлПол.Значение);
		КонецЕсли;
		
		// получаем сведения о дате и времени поступившего файла
		УзелДатаОтпр = УзелСведПодтв.Строки.Найти("ДатаОтпр", "Имя");
		УзелВремяОтпр = УзелСведПодтв.Строки.Найти("ВремяОтпр", "Имя");
		Если ЗначениеЗаполнено(УзелДатаОтпр) Тогда
			ДатаОтправки = СокрЛП(УзелДатаОтпр.Значение);
		КонецЕсли;
		Если ЗначениеЗаполнено(УзелВремяОтпр) Тогда
			ДатаОтправки = СокрЛП(ДатаОтправки + " " + СокрЛП(УзелВремяОтпр.Значение));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияСтрОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры