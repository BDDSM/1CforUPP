Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// извлекаем файл квитанции из содержимого сообщения
	СтрСообщенияОПроверке = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.СообщениеОПроверке, ИмяФайлаСообщенияОПроверке);
	Если СтрСообщенияОПроверке.Количество() = 0 Тогда
		Предупреждение("Сообщение о проверке заявления не обнаружено среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрСообщениеОПроверке = СтрСообщенияОПроверке[0];
	
	// записываем вложение во временный файл
	ФайлСообщенияОПроверке = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрСообщениеОПроверке.Данные.Получить().Записать(ФайлСообщенияОПроверке);
	Исключение
		Сообщить("Ошибка выгрузки сообщения о проверке во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем сообщение о проверке из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлСообщенияОПроверке, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлСообщенияОПроверке);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла сообщения о проверке:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// анализируем XML
	УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
		Сообщить("Некорректная структура XML сообщения о проверке: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
		Сообщить("Некорректная структура XML сообщения о проверке: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	УзелСвПроверка = УзелДокумент.Строки.Найти("СвПроверка", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСвПроверка) Тогда
		Сообщить("Некорректная структура XML сообщения о проверке: не обнаружен узел ""СвПроверка"".", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	// разбираем узел с общими сведениями
	ОбщиеСведения = Новый Структура;
	Для Каждого УзелОбщСвед Из УзелСвПроверка.Строки Цикл
		ОбщиеСведения.Вставить(УзелОбщСвед.Имя, СокрЛП(УзелОбщСвед.Значение));
	КонецЦикла;
	
	// разбираем сведения об отметке
	УзелОтметка = УзелСвПроверка.Строки.Найти("Отметка", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелОтметка) Тогда
		Сообщить("Некорректная структура XML сообщения о проверке: не обнаружен узел ""Отметка"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	Иначе
		КодОтметка = СокрЛП(УзелОтметка.Значение);
		Если КодОтметка = "1" Тогда
			Отметка = "Проставлена отметка об уплате косвенных налогов.";
			ЭлементыФормы.ПанельУведомлениеОбОтказе.Свертка = РежимСверткиЭлементаУправления.Верх;
		ИначеЕсли КодОтметка = "2" Тогда
			Отметка = "Проставлена отметка об освобождении от налогобложения.";
			ЭлементыФормы.ПанельУведомлениеОбОтказе.Свертка = РежимСверткиЭлементаУправления.Верх;
		ИначеЕсли КодОтметка = "3" Тогда
			Отметка = "Принято решение об отказе в проставлении отметки";
		Иначе
			Сообщить("Некорректная структура XML сообщения о проверке: некорректное значение узла ""Отметка"".", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;	
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НомерДокНП") Тогда
		НомерДокНП = ОбщиеСведения.НомерДокНП;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаДокНП") Тогда
		ДатаДокНП = ОбщиеСведения.ДатаДокНП;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("РегНомДок") Тогда
		РегНомДок = ОбщиеСведения.РегНомДок;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаРегЭл") Тогда
		ДатаРегЭл = ОбщиеСведения.ДатаРегЭл;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаРегБум") Тогда
		ДатаРегБум = ОбщиеСведения.ДатаРегБум;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НомерУведОткз") Тогда
		НомерУведОткз = ОбщиеСведения.НомерУведОткз;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаУведОткз") Тогда
		ДатаУведОткз = ОбщиеСведения.РегНомДок;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ОбщиеСведенияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры