// флажки сформированности данных по разделам
Перем мДанныеРасчетыЗарплатаСформированы;
Перем мДанныеПлановыеДанныеСформированы;
Перем мДанныеНалоговаяКарточкаСформированы;
Перем мДанныеКадровыеДанныеСформированы;
Перем мДанныеОсновноеСформированы;
Перем мДанныеНалогиСформированы;
Перем мДанныеКарточкаПоЕСНСформированы;
Перем мДанныеКарточкаПоОПССформированы;
Перем мПоказыватьЕСН;

// нужны для заполнения документа начисления зарплаты
Перем МестаРаботыФизлица;
Перем ДанныеПоНачислениям;
Перем ДанныеПоПлановымРасчетам;
Перем МестаРаботыИСостоянияФизлица;
Перем КадровыеДанные;
Перем КадровыеДанныеПриемНаРаботу;

// нужны для заполнения данных о налогах
Перем ДанныеПоНалогам;

// отчеты раздела РасчетыЗарплата
Перем РегламентированнаяФорма1НДФЛОтчет;
Перем АнализНачисленийРаботникамОрганизацийОтчет;
Перем РасчетныеЛисткиОрганизацийОтчет;
Перем УнифицированнаяФормаТ13Отчет;
Перем СЗВ4Отчет;

// отчеты раздела КадровыеДанные
Перем Т2ОрганизацийОтчет;

// отчеты раздела Налоги
Перем РегламентированнаяФормаКарточкаПоЕСНОтчет;
Перем РегламентированнаяФормаКарточкаПоОПСОтчет;

// для управления группировками отчетов раздела РасчетыЗарплата
Перем ОбластьАнализНачисленийРаботникамОрганизаций;
Перем ОбластьРегламентированнаяФорма1НДФЛ;
Перем ОбластьРасчетныеЛисткиОрганизаций;
Перем ОбластьУнифицированнаяФормаТ13;
Перем ОбластьСЗВ4;

// для управления группировками отчетов раздела КадровыеДанные
Перем ОбластьТ2Организаций;

// для управления группировками отчетов раздела Налоги
Перем ОбластьРегламентированнаяФормаКарточкаПоЕСН;
Перем ОбластьРегламентированнаяФормаКарточкаПоОПС;

// отчеты раздела ПлановыеДанные
Перем УнифицированнаяФормаТ3Отчет;

// для управления группировками отчетов раздела ПлановыеДанные
Перем ОбластьУнифицированнаяФормаТ3;

Перем ПрежнийМесяц; // для контроля смены месяца

Перем мФизлицоОбъект;
Перем мФизлицоФорма;

// Потребуется для вывода отчетов в отдельных окнах
Перем ДанныеТабличныхДокументов1НДФЛ;
Перем ДанныеТабличныхДокументовАнализНачислений;
Перем ДанныеТабличныхДокументовРасчетныеЛистки;
Перем ДанныеТабличныхДокументовТ13;
Перем ДанныеТабличныхДокументовТ3;
Перем ДанныеТабличныхДокументовСЗВ4;
Перем ДанныеТабличныхДокументовТ2;
Перем ДанныеТабличныхДокументовЕСН;
Перем ДанныеТабличныхДокументовОПС;

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	
	ЭлементыФормы.ПервичныеДокументыОрганизаций.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭлементыФормы.УчетКадровОрганизаций.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭлементыФормы.НалогиОрганизаций.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	УстановкиПриОткрытии();
	УстановитьЗаголовкиОрганизацийВФорме();
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	УстановкиПриОткрытии();
КонецПроцедуры

Процедура ПриЗакрытии()
	// запомним последнее использованное физлицо
	СохранитьЗначение("УправлениеДаннымиРаботника.Физлицо", Физлицо);
	СохранитьЗначение("УправлениеДаннымиРаботника.ИмяРаздела", ИмяРаздела);
	СохранитьЗначение("УправлениеДаннымиРаботника.Месяц", Месяц);
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ОбластьАнализНачисленийРаботникамОрганизаций = НеОпределено;
	ОбластьРегламентированнаяФорма1НДФЛ = НеОпределено;
	ОбластьРасчетныеЛисткиОрганизаций = НеОпределено;
	ОбластьУнифицированнаяФормаТ13 = НеОпределено;
	ОбластьСЗВ4 = НеОпределено;
	ОбластьУнифицированнаяФормаТ3 = НеОпределено;
	ОбластьТ2Организаций = НеОпределено;
	ОбластьРегламентированнаяФормаКарточкаПоЕСН = НеОпределено;
	ОбластьРегламентированнаяФормаКарточкаПоОПС = НеОпределено;
КонецПроцедуры

Процедура ОбработкаЗаписиНовогоОбъекта(Объект, Источник)
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.ФизическиеЛица")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Физлицо = Объект.Ссылка;
		ПриИзмененииФизлица();
		мДанныеРасчетыЗарплатаСформированы = Ложь;
		мДанныеПлановыеДанныеСформированы = Ложь;
		мДанныеНалоговаяКарточкаСформированы = Ложь;
		мДанныеКадровыеДанныеСформированы = Ложь;
		мДанныеОсновноеСформированы = Ложь;
		мДанныеНалогиСформированы = Ложь;
		мДанныеКарточкаПоЕСНСформированы = Ложь;
		мДанныеКарточкаПоОПССформированы = Ложь;
		ОбновитьДанные();
		
		// записали документ начисления
	ИначеЕсли 
		ТипЗнч(Объект) = Тип("ДокументОбъект.ПремииРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ОплатаПраздничныхИВыходныхДнейОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ОплатаСверхурочныхЧасов") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.НачислениеДивидендовОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.РегистрацияРазовыхНачисленийРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.РегистрацияРазовыхУдержанийРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.СдельныйНаряд") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.РегистрацияПростоевРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.РасчетПриУвольненииРаботникаОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.НачислениеОтпускаРаботникамОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.НачислениеПоБольничномуЛисту") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.НачислениеЗарплатыРаботникамОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.НевыходыВОрганизациях") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ОплатаПоСреднемуЗаработку") 
		Или ТипЗнч(Объект) = Тип("ДокументСсылка.ПремииРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ОплатаПраздничныхИВыходныхДнейОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ОплатаСверхурочныхЧасов") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.НачислениеДивидендовОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.РегистрацияРазовыхНачисленийРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.РегистрацияРазовыхУдержанийРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.СдельныйНаряд") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.РегистрацияПростоевРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.РасчетПриУвольненииРаботникаОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.НачислениеОтпускаРаботникамОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.НачислениеПоБольничномуЛисту") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.НачислениеЗарплатыРаботникамОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.НевыходыВОрганизациях") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку") 
		Тогда
		мДанныеРасчетыЗарплатаСформированы = Ложь;
		ОбновитьДанные();
		
		// записали документ плановых данных
	ИначеЕсли 
		ТипЗнч(Объект) = Тип("ДокументОбъект.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ИсполнительныйЛист") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.КадровоеПеремещениеОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ОтпускПоУходуЗаРебенком") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ПриемНаРаботуВОрганизацию") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.УвольнениеИзОрганизаций") 
		Или ТипЗнч(Объект) = Тип("ДокументСсылка.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ИсполнительныйЛист") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.КадровоеПеремещениеОрганизаций") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ОтпускПоУходуЗаРебенком") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ПриемНаРаботуВОрганизацию") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.УвольнениеИзОрганизаций") 
		Тогда
		мДанныеПлановыеДанныеСформированы = Ложь;
		ОбновитьДанные();
		
		// записали регламентный документ 
	ИначеЕсли 
		ТипЗнч(Объект) = Тип("ДокументОбъект.РасчетЕСН") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.РасчетСтраховыхВзносов") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ОтражениеЗарплатыВРеглУчете") 
		или ТипЗнч(Объект) = Тип("ДокументОбъект.ВводСведенийОРеглУчетеПлановыхНачисленийРаботниковОрганизаций") 
		Или ТипЗнч(Объект) = Тип("ДокументСсылка.РасчетЕСН") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.РасчетСтраховыхВзносов") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ОтражениеЗарплатыВРеглУчете") 
		или ТипЗнч(Объект) = Тип("ДокументСсылка.ВводСведенийОРеглУчетеПлановыхНачисленийРаботниковОрганизаций") 
		Тогда
		мДанныеНалогиСформированы = Ложь;
		ОбновитьДанные();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановкиПриОткрытии()
	
	// если не установлено физлицо
	Если Физлицо = Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
		
		// ...выберем последнего использованного
		Физлицо = ВосстановитьЗначение("УправлениеДаннымиРаботника.Физлицо");
		Если Физлицо <> Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
			// проверим, можно ли работать с эим физлицом
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ФизическиеЛица.Ссылка КАК Физлицо,
			|	ФизическиеЛица.ПометкаУдаления
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|ГДЕ
			|	ФизическиеЛица.Ссылка = &Ссылка
			|");
			Запрос.УстановитьПараметр("Ссылка",Физлицо);
			Если Запрос.Выполнить().Пустой() Тогда
				Физлицо = Справочники.ФизическиеЛица.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		// ...или получим первого
		Если Физлицо = Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ФизическиеЛица.Ссылка Как Физлицо
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|ГДЕ
			|	(НЕ ФизическиеЛица.ЭтоГруппа)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФизическиеЛица.Наименование");
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Физлицо = Выборка.Физлицо;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// если не задано имя раздела
	Если ПустаяСтрока(ИмяРаздела) Тогда
		// ...восстановим последний использованный раздел данных
		ИмяРаздела = ВосстановитьЗначение("УправлениеДаннымиРаботника.ИмяРаздела");
		//... или назначим по умолчанию Основное
		Если ПустаяСтрока(ИмяРаздела) Тогда
			ИмяРаздела = "Основное";
		КонецЕсли;
	КонецЕсли;
	
	// если не задан месяц
	Если Месяц = '00010101' Тогда
		// ...восстановим последний использованный месяц
		Месяц = ВосстановитьЗначение("УправлениеДаннымиРаботника.Месяц");
		//... или назначим по умолчанию Основное
		Если Месяц = '00010101' Тогда
			Месяц = НачалоМесяца(РабочаяДата);
		КонецЕсли;
	КонецЕсли;
	ПриИзмененииФизлица();
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(Месяц);
	
	// используем отбор по физлицам
	ПервичныеДокументыОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Использование = Истина;
	ПервичныеДокументыОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	УчетКадровОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Использование = Истина;
	УчетКадровОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	ПлановыеДанныеДляРасчета.Отбор.ДокументыПоРаботникуОрганизации.Использование = Истина;
	ПлановыеДанныеДляРасчета.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	НалогиОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Использование = Истина;
	НалогиОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	
	// ... и по месяцу
	ПервичныеДокументыОрганизаций.Отбор.Месяц.Использование = Истина;
	ПервичныеДокументыОрганизаций.Отбор.Месяц.Значение = Месяц;
	НалогиОрганизаций.Отбор.Месяц.Использование = Истина;
	НалогиОрганизаций.Отбор.Месяц.Значение = Месяц;
	
	// Управление доступностью для пользователей с разными ролями
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		
		Если РольДоступна("КадровикРегламентированныхДанных") Или РольДоступна("УдалитьКадровикРегламентированныхДанныхБезОграниченияПрав") Тогда
			
			Если НЕ РольДоступна("РасчетчикРегламентированнойЗарплаты") И НЕ РольДоступна("УдалитьРасчетчикРегламентированнойЗарплатыБезОграниченияПрав") Тогда
				
				// Пользователь может пользоваться только кадровыми данными
				ЭлементыФормы.КоманднаяПанельОсновная.Кнопки.Налоги.Доступность				= Ложь;
				ЭлементыФормы.КоманднаяПанельОсновная.Кнопки.ПлановыеДанные.Доступность		= Ложь;
				ЭлементыФормы.КоманднаяПанельОсновная.Кнопки.РасчетыЗарплата.Доступность	= Ложь;
				Если ИмяРаздела = "Налоги" ИЛИ ИмяРаздела = "ПлановыеДанные" ИЛИ ИмяРаздела = "РасчетыЗарплата" Тогда
					ИмяРаздела = "Основное"
				КонецЕсли; 
				
			КонецЕсли; 
			
		ИначеЕсли РольДоступна("РасчетчикРегламентированнойЗарплаты") Или РольДоступна("УдалитьРасчетчикРегламентированнойЗарплатыБезОграниченияПрав") Тогда
			
			// Пользователь может пользоваться только расчетными данными
			ЭлементыФормы.КоманднаяПанельОсновная.Кнопки.Основное.Доступность		= Ложь;
			ЭлементыФормы.КоманднаяПанельОсновная.Кнопки.КадровыеДанные.Доступность	= Ложь;
			Если ИмяРаздела = "Основное" ИЛИ ИмяРаздела = "КадровыеДанные" Тогда
				ИмяРаздела = "ПлановыеДанные"
			КонецЕсли; 
			
		Иначе  // не может быть
			
		КонецЕсли; 
		
	КонецЕсли;
	
	ОбновитьДанные();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура СформироватьДанные(ИмяРаздела)
	
	Если ИмяРаздела = "Основное" Тогда
		СформироватьДанныеОсновное();
	ИначеЕсли ИмяРаздела = "КадровыеДанные" Тогда
		СформироватьДанныеКадровыеДанные();
	ИначеЕсли ИмяРаздела = "ПлановыеДанные" Тогда
		СформироватьДанныеПлановыеДанные();
	ИначеЕсли ИмяРаздела = "РасчетыЗарплата" Тогда
		СформироватьДанныеРасчетыЗарплата();
	ИначеЕсли ИмяРаздела = "Налоги" Тогда
		СформироватьДанныеНалоги();
	КонецЕсли;
	
КонецПроцедуры

// установка переменных модуля и значений отбора при смене месяца
// после этого вызова должна быть вызвана процедура ОбновитьДанные()
// для того, чтобы установленные значения были отработаны
//
Процедура ПриИзмененииМесяца()
	
	мДанныеРасчетыЗарплатаСформированы = Ложь;
	мДанныеПлановыеДанныеСформированы = Ложь;
	мДанныеКадровыеДанныеСформированы = Ложь;
	мДанныеНалогиСформированы = Ложь;
	мДанныеОсновноеСформированы = Ложь;
	Если Год(Месяц) <> Год(ПрежнийМесяц) Тогда
		мДанныеНалоговаяКарточкаСформированы = Ложь;
		мДанныеКарточкаПоЕСНСформированы = Ложь;
		мДанныеКарточкаПоОПССформированы = Ложь;
	КонецЕсли;
	
	// установим отбор
	ПервичныеДокументыОрганизаций.Отбор.Месяц.Значение = Месяц;
	НалогиОрганизаций.Отбор.Месяц.Значение = Месяц;
	
КонецПроцедуры

Процедура ОбновитьДанные()
	
	Заголовок = Физлицо;
	ПрежнийМесяц = Месяц;
	мПоказыватьЕСН = Месяц < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
	ЭлементыФормы.ПанельДанных.ТекущаяСтраница = ЭлементыФормы.ПанельДанных.Страницы[ИмяРаздела];
	Для Каждого Кнопка Из ЭлементыФормы.КоманднаяПанельОсновная.Кнопки Цикл
		Если Кнопка.Имя = ИмяРаздела Тогда
			Кнопка.Пометка = Истина;
			Кнопка.КнопкаПоУмолчанию = Истина;
		Иначе
			Кнопка.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	ТекущаяОбласть = ЭлементыФормы[ИмяРаздела].ТекущаяОбласть;
	СформироватьДанные(ИмяРаздела);
	ЭлементыФормы[ИмяРаздела].ТекущаяОбласть = ТекущаяОбласть;
	ЭлементыФормы.СписокФизлиц.ТекущаяСтрока = Физлицо;
	
КонецПроцедуры

// процедура предназначена для установки значения реквизитов обработки с проверкой
// текущего значения и, если нужно, изменением переменных модуля формы и 
// обновлением данных
// Параметры:
//  ПарамФизлицо - ссылка на физлицо
//  ПарамИмяРаздела - строка "Основное", "КадровыеДанные", "РасчетыЗарплата" или "Налоги"
//  ПарамМесяц - дата
//
Процедура УстановитьРеквизиты(ПарамФизлицо, ПарамИмяРаздела, ПарамМесяц) Экспорт
	
	Если Физлицо <> ПарамФизлицо Тогда
		Физлицо = ПарамФизлицо;
		ПриИзмененииФизлица();
	КонецЕсли;
	
	ИмяРаздела = ПарамИмяРаздела;
	
	Если Месяц <> НачалоМесяца(ПарамМесяц) Тогда
		Месяц = НачалоМесяца(ПарамМесяц);
		ПриИзмененииМесяца();
	КонецЕсли;
	
	ОбновитьДанные();
	
КонецПроцедуры

// процедура выполняется при изменении физлица
// выполнеятся установка переменных модуля и значения отборов
//
Процедура ПриИзмененииФизлица()
	
	ПервичныеДокументыОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	УчетКадровОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	НалогиОрганизаций.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	ПлановыеДанныеДляРасчета.Отбор.ДокументыПоРаботникуОрганизации.Значение = Физлицо;
	
	мДанныеРасчетыЗарплатаСформированы = Ложь;
	мДанныеПлановыеДанныеСформированы = Ложь;
	мДанныеНалоговаяКарточкаСформированы = Ложь;
	мДанныеКадровыеДанныеСформированы = Ложь;
	мДанныеОсновноеСформированы = Ложь;
	мДанныеНалогиСформированы = Ложь;    
	мДанныеКарточкаПоЕСНСформированы = Ложь;
	мДанныеКарточкаПоОПССформированы = Ложь;
	Если Не Физлицо.Пустая() Тогда
		мФизлицоОбъект = Физлицо.ПолучитьОбъект();
		мФизлицоФорма = мФизлицоОбъект.ПолучитьФорму(, ЭтаФорма);
	Иначе
		мФизлицоОбъект = НеОпределено;
		мФизлицоФорма = НеОпределено;
	КонецЕсли;
	
КонецПроцедуры

// получает сведения о местах работы
// заполняет массив МестаРаботыФизлица
Процедура ПолучитьСведенияОМестахРаботы()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Работники.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ПериодЗавершения
	|		ИНАЧЕ Работники.Период
	|	КОНЕЦ КАК Период,
	|	Работники.Сотрудник,
	|	Работники.Сотрудник.Наименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ОбособленноеПодразделениеЗавершения
	|		ИНАЧЕ Работники.ОбособленноеПодразделение
	|	КОНЕЦ КАК ОбособленноеПодразделение,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Работники.ПериодЗавершения <= &Месяц
	|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА Работники.ОбособленноеПодразделениеЗавершения.Наименование
	|			ИНАЧЕ Работники.ОбособленноеПодразделение.Наименование
	|		КОНЕЦ, """") КАК ОбособленноеПодразделениеНаименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ Работники.ПодразделениеОрганизации
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ДолжностьЗавершения
	|		ИНАЧЕ Работники.Должность
	|	КОНЕЦ КАК Должность,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ГрафикРаботыЗавершения
	|		ИНАЧЕ Работники.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	Работники.Организация.Наименование КАК ОрганизацияНаименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ПодразделениеОрганизацииЗавершения.Наименование
	|		ИНАЧЕ Работники.ПодразделениеОрганизации.Наименование
	|	КОНЕЦ КАК ПодразделениеНаименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ДолжностьЗавершения.Наименование
	|		ИНАЧЕ Работники.Должность.Наименование
	|	КОНЕЦ КАК ДолжностьНаименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &Месяц
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ГрафикРаботыЗавершения.Наименование
	|		ИНАЧЕ Работники.ГрафикРаботы.Наименование
	|	КОНЕЦ КАК ГрафикРаботыНаименование,
	|	Работники.Сотрудник.Код КАК ТабельныйНомер
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&Месяц, Сотрудник.Физлицо = &Физлицо) КАК Работники
	|ГДЕ
	|	ВЫБОР
	|			КОГДА Работники.ПериодЗавершения <= &Месяц
	|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА Работники.ПричинаИзмененияСостоянияЗавершения
	|			ИНАЧЕ Работники.ПричинаИзмененияСостояния
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Работники.Организация,
	|	Работники.Период,
	|	Работники.Сотрудник,
	|	Работники.Сотрудник.Наименование,
	|	Работники.ОбособленноеПодразделение,
	|	Работники.ОбособленноеПодразделение.Наименование,
	|	Работники.ПодразделениеОрганизации,
	|	Работники.Должность,
	|	Работники.ГрафикРаботы,
	|	Работники.Организация.Наименование,
	|	Работники.ПодразделениеОрганизации.Наименование,
	|	Работники.Должность.Наименование,
	|	Работники.ГрафикРаботы.Наименование,
	|	Работники.Сотрудник.Код
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК Работники
	|ГДЕ
	|	Работники.Сотрудник.Физлицо = &Физлицо
	|	И Работники.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|	И Работники.Период > &Начало
	|	И Работники.Период < &Конец
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Работники.Организация,
	|	Работники.ПериодЗавершения,
	|	Работники.Сотрудник,
	|	Работники.Сотрудник.Наименование,
	|	Работники.ОбособленноеПодразделениеЗавершения,
	|	Работники.ОбособленноеПодразделениеЗавершения.Наименование,
	|	Работники.ПодразделениеОрганизацииЗавершения,
	|	Работники.ДолжностьЗавершения,
	|	Работники.ГрафикРаботыЗавершения,
	|	Работники.Организация.Наименование,
	|	Работники.ПодразделениеОрганизацииЗавершения.Наименование,
	|	Работники.ДолжностьЗавершения.Наименование,
	|	Работники.ГрафикРаботыЗавершения.Наименование,
	|	Работники.Сотрудник.Код
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК Работники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ПериодыПерекрытия
	|		ПО (ПериодыПерекрытия.Период <= Работники.ПериодЗавершения)
	|			И (ПериодыПерекрытия.Период > Работники.Период)
	|			И (ПериодыПерекрытия.Сотрудник = Работники.Сотрудник)
	|ГДЕ
	|	Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	И Работники.ПериодЗавершения > &Начало
	|	И Работники.ПериодЗавершения <= &Конец
	|	И Работники.Сотрудник.Физлицо = &Физлицо
	|	И Работники.ПричинаИзмененияСостоянияЗавершения <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|	И ПериодыПерекрытия.Период ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Период УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ");
	
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	Запрос.УстановитьПараметр("Уволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.УстановитьПараметр("Начало", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("Конец", КонецМесяца(Месяц));
	
	Выборка = Запрос.Выполнить().Выбрать();
	МестаРаботыФизлица.Очистить();
	
	Пока Выборка.Следующий() Цикл
		// заполняем рабочие места
		ПредставлениеРабочегоМеста = Выборка.ОбособленноеПодразделениеНаименование + "; " + Выборка.ПодразделениеНаименование;
		МестаРаботыФизлица.Добавить(
		Новый Структура(
		"Организация,
		|Период,
		|Сотрудник,
		|СотрудникНаименование,
		|ОбособленноеПодразделение,
		|ОбособленноеПодразделениеНаименование,
		|Подразделение,
		|Должность,
		|ТабельныйНомер,
		|ГрафикРаботы,
		|ОрганизацияНаименование,
		|ПодразделениеНаименование,
		|ДолжностьНаименование,
		|ГрафикРаботыНаименование,
		|Представление",
		Выборка.Организация,
		Выборка.Период,
		Выборка.Сотрудник,
		Выборка.СотрудникНаименование,
		Выборка.ОбособленноеПодразделение,
		Выборка.ОбособленноеПодразделениеНаименование,
		Выборка.Подразделение,
		Выборка.Должность,
		Выборка.ТабельныйНомер,
		Выборка.ГрафикРаботы,
		Выборка.ОрганизацияНаименование,
		Выборка.ПодразделениеНаименование,
		Выборка.ДолжностьНаименование,
		Выборка.ГрафикРаботыНаименование,
		ПредставлениеРабочегоМеста));
	КонецЦикла;
КонецПроцедуры

// получает сведения о местах работы и состояния
// заполняет массив МестаРаботыИСостоянияФизлица
Процедура ПолучитьМестаРаботыИСостоянияРаботника()
	
	Запрос = Новый запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	""РаботникиОрганизаций"" КАК ВидРегистра,
	|	РаботникиОрганизацийСрезПоследних.Организация КАК Организация,
	|	РаботникиОрганизацийСрезПоследних.Организация.Наименование,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.Период
	|	КОНЕЦ КАК Период,
	|	РаботникиОрганизацийСрезПоследних.Сотрудник.Физлицо КАК Физлицо,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ОбособленноеПодразделениеЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ОбособленноеПодразделение
	|	КОНЕЦ КАК ОбособленноеПодразделение,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ОбособленноеПодразделениеЗавершения.Наименование
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ОбособленноеПодразделение.Наименование
	|	КОНЕЦ КАК ОбособленноеПодразделениеНаименование,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизацииЗавершения.Наименование
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации.Наименование
	|	КОНЕЦ КАК ПодразделениеОрганизацииНаименование,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ДолжностьЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.Должность
	|	КОНЕЦ КАК Должность,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ДолжностьЗавершения.Наименование
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.Должность.Наименование
	|	КОНЕЦ КАК ДолжностьНаименование,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния = &Прием
	|				И (НЕ(РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|						И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)))
	|			ТОГДА ""Принят""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВЫБОР
	|						КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|								И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостоянияЗавершения
	|						ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния
	|					КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|					ТОГДА ""Уволен""
	|				ИНАЧЕ ""Перемещен""
	|			КОНЕЦ
	|	КОНЕЦ КАК Состояние,
	|	РаботникиОрганизацийСрезПоследних.Сотрудник,
	|	РаботникиОрганизацийСрезПоследних.Сотрудник.Наименование,
	|	РаботникиОрганизацийСрезПоследних.Сотрудник.Код КАК ТабельныйНомер,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ГрафикРаботыЗавершения
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &Месяц
	|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацийСрезПоследних.ГрафикРаботыЗавершения.Наименование
	|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ГрафикРаботы.Наименование
	|	КОНЕЦ КАК ГрафикРаботыНаименование
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&Месяц, Сотрудник.Физлицо = &Физлицо) КАК РаботникиОрганизацийСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""СостояниеРаботников"",
	|	NULL,
	|	NULL,
	|	ВЫБОР
	|		КОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения <= &Месяц
	|				И СостояниеРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения
	|		ИНАЧЕ СостояниеРаботниковОрганизаций.Период
	|	КОНЕЦ,
	|	СостояниеРаботниковОрганизаций.Сотрудник.Физлицо,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ВЫБОР
	|		КОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения <= &Месяц
	|				И СостояниеРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА СостояниеРаботниковОрганизаций.СостояниеЗавершения
	|		ИНАЧЕ СостояниеРаботниковОрганизаций.Состояние
	|	КОНЕЦ,
	|	СостояниеРаботниковОрганизаций.Сотрудник,
	|	СостояниеРаботниковОрганизаций.Сотрудник.Наименование,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций.СрезПоследних(&Месяц, Сотрудник.Физлицо = &Физлицо) КАК СостояниеРаботниковОрганизаций
	|ГДЕ
	|	(НЕ(ВЫБОР
	|					КОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения <= &Месяц
	|							И СостояниеРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА СостояниеРаботниковОрганизаций.СостояниеЗавершения
	|					ИНАЧЕ СостояниеРаботниковОрганизаций.Состояние
	|				КОНЕЦ = ЗНАЧЕНИЕ(перечисление.СостоянияРаботникаОрганизации.Работает)
	|				И ВЫБОР
	|					КОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения <= &Месяц
	|							И СостояниеРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения
	|					ИНАЧЕ СостояниеРаботниковОрганизаций.Период
	|				КОНЕЦ < &Начало))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Период УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Месяц",		КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("Физлицо",	Физлицо);
	Запрос.УстановитьПараметр("Начало",		НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("Конец",		КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("Прием",		Перечисления.ПричиныИзмененияСостояния.ПриемНаРаботу);
	
	Выборка = Запрос.Выполнить().Выбрать();
	МестаРаботыИСостоянияФизлица.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		МестаРаботыИСостоянияФизлица.Добавить(
		Новый Структура(
		"Организация,
		|ВидРегистра,
		|Период,
		|ОбособленноеПодразделение,
		|ОбособленноеПодразделениеНаименование,
		|Подразделение,
		|Должность,
		|ТабельныйНомер,
		|Состояние,
		|Сотрудник,
		|СотрудникНаименование,
		|ГрафикРаботы,
		|ОрганизацияНаименование,
		|ПодразделениеНаименование,
		|ДолжностьНаименование,
		|ГрафикРаботыНаименование",
		Выборка.Организация,
		Выборка.ВидРегистра,
		Выборка.Период,
		Выборка.ОбособленноеПодразделение,
		Выборка.ОбособленноеПодразделениеНаименование,
		Выборка.ПодразделениеОрганизации,
		Выборка.Должность,
		Выборка.ТабельныйНомер,
		Выборка.Состояние,
		Выборка.Сотрудник,
		Выборка.СотрудникНаименование,
		Выборка.ГрафикРаботы,
		Выборка.ОрганизацияНаименование,
		Выборка.ПодразделениеОрганизацииНаименование,
		Выборка.ДолжностьНаименование,
		Выборка.ГрафикРаботыНаименование));
		
	КонецЦикла;
	
КонецПроцедуры

// установить физлицо в качетсве текущего
Процедура УстановитьФизлицо(ВыбранноеФизлицо)
	Физлицо = ВыбранноеФизлицо;
	ПриИзмененииФизлица();
	ОбновитьДанные();
КонецПроцедуры

// вывод в отдельных окнах отчетов
Процедура ТабличныйДокументОтдельно(ТабличныйДокумент, Заголовок)
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.Показать(Заголовок);
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
//  КОМАНДЫ ФОРМЫ

Процедура КоманднаяПанельОсновнаяДействие(Кнопка)
	ИмяРаздела = Кнопка.Имя;
	ОбновитьДанные();
КонецПроцедуры

Процедура ОбновитьОтчет(Кнопка)
	мДанныеРасчетыЗарплатаСформированы = Ложь;
	мДанныеПлановыеДанныеСформированы = Ложь;
	мДанныеНалоговаяКарточкаСформированы = Ложь;
	мДанныеКадровыеДанныеСформированы = Ложь;
	мДанныеОсновноеСформированы = Ложь;
	мДанныеНалогиСформированы = Ложь;
	мДанныеКарточкаПоЕСНСформированы = Ложь;
	мДанныеКарточкаПоОПССформированы = Ложь;
	ОбновитьДанные();
	
КонецПроцедуры

Процедура ФизлицоПриИзменении(Элемент)
	
	ПриИзмененииФизлица();
	ОбновитьДанные();
	
КонецПроцедуры

Процедура ФизлицоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		СтандартнаяОбработка = Ложь;
		
		Физлицо = ВыбранноеЗначение.Физлицо;
		
		ПриИзмененииФизлица();
		ОбновитьДанные();
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельФизлицаДействиеВыбрать(Кнопка)
	
	ПроцедурыУправленияПерсоналом.ОткрытьФормуВыбораСотрудникаОрганизации(ЭлементыФормы.Физлицо, Физлицо, Истина, КонецМесяца(Месяц), , 1, , Физлицо);
	
КонецПроцедуры

Процедура КоманднаяПанельФизлицаДействиеНовый(Кнопка)
	Форма = Справочники.ФизическиеЛица.ПолучитьФормуНовогоЭлемента(, ЭтаФорма);
	Форма.Открыть();
	
КонецПроцедуры

Процедура КоманднаяПанельФизлицаДействиеОткрыть(Кнопка)
	Если мФизлицоФорма <> НеОпределено Тогда 
		мФизлицоФорма.Открыть();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ ОСНОВНОЕ

// формирует раздел
Процедура СформироватьДанныеОсновное()
	
	Если мДанныеОсновноеСформированы Тогда
		Возврат;
	КонецЕсли;
	мДанныеОсновноеСформированы = Истина;
	
	// обновить динамические списки
	СписокФизлиц.Обновить();
	
	// для заполнения табличного документа с отчетами
	ВыходнойДокумент = ЭлементыФормы.Основное;
	
	ВыходнойДокумент.Очистить();
	
	Если мФизлицоОбъект <> НеОпределено Тогда
		мФизлицоОбъект.Печать(ВыходнойДокумент, КонецМесяца(Месяц));
	КонецЕсли;
	
	ВыходнойДокумент.ТолькоПросмотр = Истина;
	ВыходнойДокумент.Защита = Истина;
	
КонецПроцедуры

Процедура НадписьОсновнаяФормаНажатие(Элемент)
	Если мФизлицоФорма <> НеОпределено Тогда
		мФизлицоФорма.Открыть();
	КонецЕсли;    
КонецПроцедуры

Процедура НадписьТрудоваяДеятельностьНажатие(Элемент)
	Если мФизлицоОбъект <> НеОпределено Тогда
		Форма = мФизлицоОбъект.ПолучитьФорму("ФормаВводаДанныхТруд", ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьВоинскийУчетНажатие(Элемент)
	Если мФизлицоОбъект <> НеОпределено и мФизлицоФорма <> НеОпределено Тогда
		Если мФизлицоФорма.мНаборЗаписейВоинскийУчет.Количество() > 0 Тогда
			СтруктураОтбора = Новый Структура("ФизЛицо, Период");
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, мФизлицоФорма.мНаборЗаписейВоинскийУчет[0]);
			ПараметрыФормы = Новый Структура("Ключ", РегистрыСведений.ВоинскийУчет.СоздатьКлючЗаписи(СтруктураОтбора));
		Иначе
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("ФизЛицо, Период", ФизЛицо, '19000101'));
		КонецЕсли; 
		ОткрытьФорму("РегистрСведений.ВоинскийУчет.ФормаЗаписи", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

Процедура НадписьНДФЛНажатие(Элемент)
	Если мФизлицоОбъект <> НеОпределено Тогда
		Форма = мФизлицоОбъект.ПолучитьФорму("ФормаВводаДанныхНДФЛ", ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьСЗВКНажатие(Элемент)
	Если мФизлицоОбъект <> НеОпределено Тогда
		Форма = мФизлицоОбъект.ПолучитьФорму("ФормаВводаДанныхСЗВК", ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура СписокФизлицВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	УстановитьФизлицо(ВыбраннаяСтрока);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура КоманднаяПанельСписокФизлицВыбратьФизлицо(Кнопка)
	Если ЭлементыФормы.СписокФизлиц.ТекущаяСтрока  <> НеОпределено Тогда
		УстановитьФизлицо(ЭлементыФормы.СписокФизлиц.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ ПЛАНОВЫЕ ДАННЫЕ

// формирует раздел
Процедура СформироватьДанныеПлановыеДанные()
	
	Если мДанныеПлановыеДанныеСформированы Тогда
		Возврат;
	КонецЕсли;
	мДанныеПлановыеДанныеСформированы = Истина;
	
	ПолучитьСведенияОПлановыхНачислениях();
	
	// настройка надписей раздела Действия и документы
	ЭлементыФормы.НадписьПлановыеНачисления.Заголовок = "Изменить плановые начисления";
	ЭлементыФормы.НадписьПлановыеУдержания.Заголовок = "Изменить плановые удержания";
	ЭлементыФормы.НадписьИсполнительныйЛист.Заголовок = "Ввести исполнительный лист";
	Для Каждого ИнфоПоОрганизации Из ДанныеПоПлановымРасчетам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ИсполнительныйЛист Цикл
			ЭлементыФормы.НадписьИсполнительныйЛист.Заголовок = "Исполнительный лист";
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	// для заполнения табличного документа с отчетами
	ВыходнойДокумент = ЭлементыФормы.ПлановыеДанные;
	
	// восстановление раскрытых группировок отчета
	ГруппировкаУнифицированнаяФормаТ3 = Ложь;
	Если ОбластьУнифицированнаяФормаТ3 <> НеОпределено и ОбластьУнифицированнаяФормаТ3.Видимость Тогда
		ГруппировкаУнифицированнаяФормаТ3 = Истина;
	КонецЕсли;
	
	ВыходнойДокумент.Очистить();
	
	Макет = ПолучитьМакет("ПлановыеДанные");
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьФизлицо = Макет.ПолучитьОбласть("Физлицо");
	ОбластьФизлицо.Параметры.Физлицо = Физлицо;
	ОбластьФизлицо.Параметры.Месяц = Формат(Месяц, "ДФ='ММММ гггг ''г.'''");
	ВыходнойДокумент.Вывести(ОбластьФизлицо);
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамНачало",		Месяц);
	Запрос.УстановитьПараметр("парамКонец",			КонецМесяца(Месяц));
	
	Подразделения = Новый Массив();
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		Подразделения.Добавить(МестоРаботы.Подразделение);
	КонецЦикла;
	
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос.УстановитьПараметр("Физлицо" , Физлицо);
	Запрос.УстановитьПараметр("Подразделения" , Подразделения);
	Запрос.УстановитьПараметр("парамДатаВалютногоУчета" , НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("парамПустаяВалюта" , Справочники.Валюты.ПустаяСсылка());
	Запрос.УстановитьПараметр("парамВалютаРегламентУчета" , ВалютаРеглУчета);
	Запрос.УстановитьПараметр("парамМассивСпособовРасчетовБезВалюты" , ПроведениеРасчетовПереопределяемый.ПолучитьСписокСпособовРасчетаНеТребующихУказанияВалюты());
	Запрос.УстановитьПараметр("Ежемесячно", Перечисления.ХарактерВыплатыПоДоговору.Ежемесячно);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Работники.Организация КАК Организация,
	|	Работники.Организация.Наименование КАК ОрганизацияНаименование,
	|	Работники.Физлицо КАК Физлицо,
	|	ЛОЖЬ КАК ПоШтатномуРасписанию,
	|	НачисленияРаботниковОрганизации.Период КАК Период,
	|	ЕСТЬNULL(НачисленияРаботниковОрганизации.ОсновноеНачисление, ЛОЖЬ) КАК ОсновноеНачисление,
	|	НачисленияРаботниковОрганизации.ВидРасчета КАК ВидРасчета,
	|	НачисленияРаботниковОрганизации.ВидРасчета.Код КАК ВидРасчетаКод,
	|	НачисленияРаботниковОрганизации.ВидРасчета.Наименование КАК ВидРасчетаНаименование,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА НачисленияРаботниковОрганизации.ВидРасчета.ТребуетВводаТарифногоРазряда
	|				ТОГДА ТарифныеСтавки.Размер
	|			ИНАЧЕ НачисленияРаботниковОрганизации.Размер
	|		КОНЕЦ, 0) КАК Размер,
	|	НачисленияРаботниковОрганизации.Валюта КАК Валюта,
	|	НачисленияРаботниковОрганизации.Валюта.Наименование КАК ВалютаНаименование,
	|	ВЫБОР
	|		КОГДА НачисленияРаботниковОрганизации.Валюта = &парамВалютаРегламентУчета
	|				ИЛИ НачисленияРаботниковОрганизации.Валюта = &парамПустаяВалюта
	|			ТОГДА 1
	|		ИНАЧЕ Валюты.Курс / Валюты.Кратность
	|	КОНЕЦ КАК КурсВалюты,
	|	ВЫБОР
	|		КОГДА НачисленияРаботниковОрганизации.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Действует,
	|	Работники.ПодразделениеОрганизации КАК Подразделение,
	|	Работники.ПодразделениеОрганизации.Наименование КАК ПодразделениеНаименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаботникиОрганизации.Организация КАК Организация,
	|		РаботникиОрганизации.Сотрудник.Физлицо КАК Физлицо,
	|		РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|		ВЫБОР
	|			КОГДА РаботникиОрганизации.ПериодЗавершения <= &парамНачало
	|					И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизации.ПодразделениеОрганизацииЗавершения
	|			ИНАЧЕ РаботникиОрганизации.ПодразделениеОрганизации
	|		КОНЕЦ КАК ПодразделениеОрганизации
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&парамНачало, Сотрудник.Физлицо = &Физлицо) КАК РаботникиОрганизации
	|	
	|	ОБЪЕДИНИТЬ 
	|	
	|	ВЫБРАТЬ
	|		РаботникиОрганизации.Организация,
	|		РаботникиОрганизации.Сотрудник.Физлицо,
	|		РаботникиОрганизации.Сотрудник,
	|		РаботникиОрганизации.ПодразделениеОрганизации
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|	ГДЕ
	|		РаботникиОрганизации.Период > &парамНачало
	|		И РаботникиОрганизации.Период <= &парамКонец
	|		И РаботникиОрганизации.Сотрудник.Физлицо = &Физлицо
	|	
	|	ОБЪЕДИНИТЬ 
	|	
	|	ВЫБРАТЬ
	|		Работники.Организация,
	|		Работники.Сотрудник.Физлицо,
	|		Работники.Сотрудник,
	|		Работники.ПодразделениеОрганизацииЗавершения
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций КАК Работники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ПериодыПерекрытия
	|			ПО ПериодыПерекрытия.Период <= Работники.ПериодЗавершения
	|				И ПериодыПерекрытия.Период > Работники.Период
	|				И ПериодыПерекрытия.Сотрудник = Работники.Сотрудник
	|	ГДЕ
	|		Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		И Работники.ПериодЗавершения > &парамНачало
	|		И Работники.ПериодЗавершения <= &парамКонец
	|		И Работники.Сотрудник.Физлицо = &Физлицо
	|		И ПериодыПерекрытия.Период ЕСТЬ NULL 
	|	
	|	ОБЪЕДИНИТЬ 
	|	
	|	ВЫБРАТЬ
	|		ДоговорникиОрганизаций.Организация,
	|		ДоговорникиОрганизаций.Физлицо,
	|		ДоговорникиОрганизаций.Ссылка,
	|		ДоговорникиОрганизаций.ПодразделениеОрганизации
	|	ИЗ
	|		Справочник.СотрудникиОрганизаций КАК ДоговорникиОрганизаций
	|	ГДЕ
	|		ДоговорникиОрганизаций.Физлицо = &Физлицо
	|		И ДоговорникиОрганизаций.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
	|		И ДоговорникиОрганизаций.Актуальность) КАК Работники
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НачисленияРаботниковОрганизации.Период КАК Период,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ВидРасчетаИзмерение.Код ЕСТЬ NULL 
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ КАК ОсновноеНачисление,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|						И НачисленияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА НачисленияРаботниковОрганизации.ВидРасчетаЗавершения
	|				ИНАЧЕ НачисленияРаботниковОрганизации.ВидРасчета
	|			КОНЕЦ КАК ВидРасчета,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|						И НачисленияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА НачисленияРаботниковОрганизации.Показатель1Завершения
	|				ИНАЧЕ НачисленияРаботниковОрганизации.Показатель1
	|			КОНЕЦ КАК Размер,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|						И НачисленияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА НачисленияРаботниковОрганизации.Валюта1Завершения
	|				ИНАЧЕ НачисленияРаботниковОрганизации.Валюта1
	|			КОНЕЦ КАК Валюта,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|						И НачисленияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА НачисленияРаботниковОрганизации.ДействиеЗавершения
	|				ИНАЧЕ НачисленияРаботниковОрганизации.Действие
	|			КОНЕЦ КАК Действие,
	|			НачисленияРаботниковОрганизации.Сотрудник.Физлицо КАК Физлицо,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|						И НачисленияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА НачисленияРаботниковОрганизации.ТарифныйРазряд1Завершения
	|				ИНАЧЕ НачисленияРаботниковОрганизации.ТарифныйРазряд1
	|			КОНЕЦ КАК ТарифныйРазряд,
	|			НачисленияРаботниковОрганизации.Сотрудник КАК Сотрудник
	|		ИЗ
	|			РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&парамНачало, Сотрудник.Физлицо = &Физлицо) КАК НачисленияРаботниковОрганизации
	|		
	|		ОБЪЕДИНИТЬ 
	|		
	|		ВЫБРАТЬ
	|			НачисленияРаботниковОрганизации.Период,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ВидРасчетаИзмерение.Код ЕСТЬ NULL 
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ,
	|			НачисленияРаботниковОрганизации.ВидРасчета,
	|			НачисленияРаботниковОрганизации.Показатель1,
	|			НачисленияРаботниковОрганизации.Валюта1,
	|			НачисленияРаботниковОрганизации.Действие,
	|			НачисленияРаботниковОрганизации.Сотрудник.Физлицо,
	|			НачисленияРаботниковОрганизации.ТарифныйРазряд1,
	|			НачисленияРаботниковОрганизации.Сотрудник
	|		ИЗ
	|			РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК НачисленияРаботниковОрганизации
	|		ГДЕ
	|			НачисленияРаботниковОрганизации.Период > &парамНачало
	|			И НачисленияРаботниковОрганизации.Период <= &парамКонец
	|			И НачисленияРаботниковОрганизации.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|			И НачисленияРаботниковОрганизации.Сотрудник.Физлицо = &Физлицо
	|		
	|		ОБЪЕДИНИТЬ 
	|		
	|		ВЫБРАТЬ
	|			НачисленияРаботниковОрганизации.ПериодЗавершения,
	|			ВЫБОР
	|				КОГДА НачисленияРаботниковОрганизации.ВидРасчетаИзмерение.Код ЕСТЬ NULL 
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ,
	|			НачисленияРаботниковОрганизации.ВидРасчетаЗавершения,
	|			НачисленияРаботниковОрганизации.Показатель1Завершения,
	|			НачисленияРаботниковОрганизации.Валюта1Завершения,
	|			НачисленияРаботниковОрганизации.ДействиеЗавершения,
	|			НачисленияРаботниковОрганизации.Сотрудник.Физлицо,
	|			НачисленияРаботниковОрганизации.ТарифныйРазряд1Завершения,
	|			НачисленияРаботниковОрганизации.Сотрудник
	|		ИЗ
	|			РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК НачисленияРаботниковОрганизации
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК НачисленияРаботниковПроверка
	|				ПО НачисленияРаботниковОрганизации.Сотрудник = НачисленияРаботниковПроверка.Сотрудник
	|					И НачисленияРаботниковОрганизации.ВидРасчетаИзмерение = НачисленияРаботниковПроверка.ВидРасчетаИзмерение
	|					И НачисленияРаботниковПроверка.Период > НачисленияРаботниковОрганизации.Период
	|					И НачисленияРаботниковПроверка.Период <= НачисленияРаботниковОрганизации.ПериодЗавершения
	|		ГДЕ
	|			НачисленияРаботниковОрганизации.ПериодЗавершения > &парамНачало
	|			И НачисленияРаботниковОрганизации.ПериодЗавершения <= &парамКонец
	|			И НачисленияРаботниковОрганизации.ДействиеЗавершения <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|			И НачисленияРаботниковОрганизации.Сотрудник.Физлицо = &Физлицо
	|			И НачисленияРаботниковПроверка.Сотрудник.Физлицо ЕСТЬ NULL ) КАК НачисленияРаботниковОрганизации
	|		ПО НачисленияРаботниковОрганизации.Сотрудник = Работники.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&парамКонец, ) КАК ТарифныеСтавки
	|		ПО НачисленияРаботниковОрганизации.ТарифныйРазряд = ТарифныеСтавки.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Период = &парамДатаВалютногоУчета)
	|			И Валюты.Валюта = НачисленияРаботниковОрганизации.Валюта
	|
	|ОБЪЕДИНИТЬ 
	|
	|ВЫБРАТЬ
	|	Работники.Организация,
	|	Работники.Организация.Наименование,
	|	Работники.Физлицо,
	|	ИСТИНА,
	|	Работники.Период,
	|	ЛОЖЬ,
	|	ШтатноеРасписание.ВидНадбавки,
	|	ШтатноеРасписание.ВидНадбавки.Код,
	|	ШтатноеРасписание.ВидНадбавки.Наименование,
	|	ШтатноеРасписание.РазмерНадбавки,
	|	ШтатноеРасписание.Валюта,
	|	ШтатноеРасписание.Валюта.Наименование,
	|	ВЫБОР
	|		КОГДА ШтатноеРасписание.Валюта = &парамВалютаРегламентУчета
	|				ИЛИ ШтатноеРасписание.Валюта = &парамПустаяВалюта
	|			ТОГДА 1
	|		ИНАЧЕ Валюты.Курс / Валюты.Кратность
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Работники.Уволен
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	Работники.ПодразделениеОрганизации,
	|	Работники.ПодразделениеОрганизации.Наименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА РаботникиОрганизации.ПериодЗавершения <= &парамНачало
	|					И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизации.ПериодЗавершения
	|			ИНАЧЕ РаботникиОрганизации.Период
	|		КОНЕЦ КАК Период,
	|		РаботникиОрганизации.Организация КАК Организация,
	|		РаботникиОрганизации.Сотрудник.Физлицо КАК Физлицо,
	|		РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|		ВЫБОР
	|			КОГДА РаботникиОрганизации.ПериодЗавершения <= &парамНачало
	|					И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизации.ПодразделениеОрганизацииЗавершения
	|			ИНАЧЕ РаботникиОрганизации.ПодразделениеОрганизации
	|		КОНЕЦ КАК ПодразделениеОрганизации,
	|		ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА РаботникиОрганизации.ПериодЗавершения <= &парамНачало
	|							И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА РаботникиОрганизации.ПричинаИзмененияСостоянияЗавершения
	|					ИНАЧЕ РаботникиОрганизации.ПричинаИзмененияСостояния
	|				КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК Уволен,
	|		ВЫБОР
	|			КОГДА РаботникиОрганизации.ПериодЗавершения <= &парамНачало
	|					И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизации.ДолжностьЗавершения
	|			ИНАЧЕ РаботникиОрганизации.Должность
	|		КОНЕЦ КАК Должность
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&парамНачало, Сотрудник.Физлицо = &Физлицо) КАК РаботникиОрганизации
	|	
	|	ОБЪЕДИНИТЬ 
	|	
	|	ВЫБРАТЬ
	|		РаботникиОрганизации.Период,
	|		РаботникиОрганизации.Организация,
	|		РаботникиОрганизации.Сотрудник.Физлицо,
	|		РаботникиОрганизации.Сотрудник,
	|		РаботникиОрганизации.ПодразделениеОрганизации,
	|		ЛОЖЬ,
	|		РаботникиОрганизации.Должность
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|	ГДЕ
	|		РаботникиОрганизации.Период > &парамНачало
	|		И РаботникиОрганизации.Период <= &парамКонец
	|		И РаботникиОрганизации.Сотрудник.Физлицо = &Физлицо
	|	
	|	ОБЪЕДИНИТЬ 
	|	
	|	ВЫБРАТЬ
	|		Работники.ПериодЗавершения,
	|		Работники.Организация,
	|		Работники.Сотрудник.Физлицо,
	|		Работники.Сотрудник,
	|		Работники.ПодразделениеОрганизацииЗавершения,
	|		ЛОЖЬ,
	|		Работники.ДолжностьЗавершения
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций КАК Работники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ПериодыПерекрытия
	|			ПО ПериодыПерекрытия.Период <= Работники.ПериодЗавершения
	|				И ПериодыПерекрытия.Период > Работники.Период
	|				И ПериодыПерекрытия.Сотрудник = Работники.Сотрудник
	|	ГДЕ
	|		Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		И Работники.ПериодЗавершения > &парамНачало
	|		И Работники.ПериодЗавершения <= &парамКонец
	|		И Работники.Сотрудник.Физлицо = &Физлицо
	|		И ПериодыПерекрытия.Период ЕСТЬ NULL ) КАК Работники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.Период КАК Период,
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.ВидНадбавки КАК ВидНадбавки,
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.Показатель1 КАК РазмерНадбавки,
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.Валюта1 КАК Валюта,
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.Должность КАК Должность,
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|		ИЗ
	|			РегистрСведений.НадбавкиПоШтатномуРасписаниюОрганизаций.СрезПоследних(&парамНачало, ПодразделениеОрганизации В (&Подразделения)) КАК НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних
	|		ГДЕ
	|			НадбавкиПоШтатномуРасписаниюОрганизацииСрезПоследних.Показатель1 > 0
	|		
	|		ОБЪЕДИНИТЬ 
	|		
	|		ВЫБРАТЬ
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.Период,
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.ВидНадбавки,
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.Показатель1,
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.Валюта1,
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.Должность,
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.ПодразделениеОрганизации
	|		ИЗ
	|			РегистрСведений.НадбавкиПоШтатномуРасписаниюОрганизаций КАК НадбавкиПоШтатномуРасписаниюОрганизаций
	|		ГДЕ
	|			НадбавкиПоШтатномуРасписаниюОрганизаций.Период > &парамНачало
	|			И НадбавкиПоШтатномуРасписаниюОрганизаций.Период <= &парамКонец
	|			И НадбавкиПоШтатномуРасписаниюОрганизаций.ПодразделениеОрганизации В(&Подразделения)) КАК ШтатноеРасписание
	|		ПО ШтатноеРасписание.ПодразделениеОрганизации = Работники.ПодразделениеОрганизации
	|			И ШтатноеРасписание.Должность = Работники.Должность
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Период = &парамДатаВалютногоУчета)
	|			И Валюты.Валюта = ШтатноеРасписание.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновноеНачисление УБЫВ,
	|	ВидРасчетаКод,
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// выводим сведения о плановых даных
	Область = Макет.ПолучитьОбласть("ПлановыеДанныеЗаголовок");
	ВыходнойДокумент.Вывести(Область);
	Область = Макет.ПолучитьОбласть("ПлановыеДанныеСтрока");
	Пока Выборка.Следующий() Цикл
		Область.Параметры.Заполнить(Выборка);
		Если Выборка.Валюта = NULL или Выборка.Валюта = ВалютаРеглУчета или Выборка.Валюта.Пустая() Тогда
			Область.Области.Валюта.Текст = "";
		КонецЕсли;
		ВыходнойДокумент.Вывести(Область);
		
		ОбластьЯчеек = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, ,ВыходнойДокумент.ВысотаТаблицы, );
		Если Выборка.ОсновноеНачисление Тогда
			// основные начисления выделяем жирным шрифтом
			Шрифт = ОбластьЯчеек.Шрифт;
			ОбластьЯчеек.Шрифт = Новый Шрифт(Шрифт,,, Истина);
		ИначеЕсли Выборка.ПоШтатномуРасписанию Тогда
			// начисления по штатному расписанию - курсивом
			Шрифт = ОбластьЯчеек.Шрифт;
			ОбластьЯчеек.Шрифт = Новый Шрифт(Шрифт,,,, Истина);
		КонецЕсли;
		Если не Выборка.Действует Тогда
			// недействующие плановые данные выделяем цветом
			ОбластьЯчеек.ЦветТекста = WebЦвета.ТемноСерый;
		КонецЕсли;
		
	КонецЦикла;
	
	// плановые удержания
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УдержанияРаботниковОрганизации.Период КАК Период,
	|	УдержанияРаботниковОрганизации.Организация КАК Организация,
	|	УдержанияРаботниковОрганизации.Организация.Наименование КАК ОрганизацияНаименование,
	|	УдержанияРаботниковОрганизации.ВидРасчета КАК ВидРасчета,
	|	УдержанияРаботниковОрганизации.ВидРасчета.Наименование КАК ВидРасчетаНаименование,
	|	УдержанияРаботниковОрганизации.ДокументОснование КАК ДокументОснование,
	|	УдержанияРаботниковОрганизации.Валюта1 КАК Валюта,
	|	ВЫБОР
	|		КОГДА УдержанияРаботниковОрганизации.Валюта1 = &парамВалютаРегламентУчета
	|				ИЛИ УдержанияРаботниковОрганизации.Валюта1 = &парамПустаяВалюта
	|			ТОГДА 1
	|		ИНАЧЕ Валюты.Курс
	|	КОНЕЦ КАК КурсВалюты,
	|	УдержанияРаботниковОрганизации.Показатель1 КАК Размер,
	|	УдержанияРаботниковОрганизации.Действует
	|ИЗ
	|	(ВЫБРАТЬ
	|		УдержанияРаботниковОрганизации.Период КАК Период,
	|		УдержанияРаботниковОрганизации.Организация КАК Организация,
	|		УдержанияРаботниковОрганизации.ВидРасчета КАК ВидРасчета,
	|		УдержанияРаботниковОрганизации.ДокументОснование КАК ДокументОснование,
	|		ВЫБОР
	|			КОГДА УдержанияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|					И УдержанияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА УдержанияРаботниковОрганизации.Показатель1Завершения
	|			ИНАЧЕ УдержанияРаботниковОрганизации.Показатель1
	|		КОНЕЦ КАК Показатель1,
	|		ВЫБОР
	|			КОГДА УдержанияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|					И УдержанияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА УдержанияРаботниковОрганизации.Валюта1Завершения
	|			ИНАЧЕ УдержанияРаботниковОрганизации.Валюта1
	|		КОНЕЦ КАК Валюта1,
	|		ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА УдержанияРаботниковОрганизации.ПериодЗавершения <= &парамНачало
	|							И УдержанияРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА УдержанияРаботниковОрганизации.ДействиеЗавершения
	|					ИНАЧЕ УдержанияРаботниковОрганизации.Действие
	|				КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК Действует
	|	ИЗ
	|		РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций.СрезПоследних(&парамНачало, Физлицо = &Физлицо) КАК УдержанияРаботниковОрганизации
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		УдержанияРаботниковОрганизации.Период,
	|		УдержанияРаботниковОрганизации.Организация,
	|		УдержанияРаботниковОрганизации.ВидРасчета,
	|		УдержанияРаботниковОрганизации.ДокументОснование,
	|		УдержанияРаботниковОрганизации.Показатель1,
	|		УдержанияРаботниковОрганизации.Валюта1,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций КАК УдержанияРаботниковОрганизации
	|	ГДЕ
	|		УдержанияРаботниковОрганизации.Период > &парамНачало
	|		И УдержанияРаботниковОрганизации.Период <= &парамКонец
	|		И УдержанияРаботниковОрганизации.ФизЛицо = &Физлицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		УдержанияРаботниковОрганизации.ПериодЗавершения,
	|		УдержанияРаботниковОрганизации.Организация,
	|		УдержанияРаботниковОрганизации.ВидРасчета,
	|		УдержанияРаботниковОрганизации.ДокументОснование,
	|		УдержанияРаботниковОрганизации.Показатель1Завершения,
	|		УдержанияРаботниковОрганизации.Валюта1Завершения,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций КАК УдержанияРаботниковОрганизации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций КАК УдержанияРаботниковОрганизацииПроверка
	|			ПО (УдержанияРаботниковОрганизацииПроверка.Организация = УдержанияРаботниковОрганизации.Организация)
	|				И (УдержанияРаботниковОрганизацииПроверка.ФизЛицо = УдержанияРаботниковОрганизации.ФизЛицо)
	|				И (УдержанияРаботниковОрганизацииПроверка.ВидРасчета = УдержанияРаботниковОрганизации.ВидРасчета)
	|				И (УдержанияРаботниковОрганизацииПроверка.ДокументОснование = УдержанияРаботниковОрганизации.ДокументОснование)
	|				И (УдержанияРаботниковОрганизацииПроверка.Период > УдержанияРаботниковОрганизации.Период)
	|				И (УдержанияРаботниковОрганизацииПроверка.Период <= УдержанияРаботниковОрганизации.Период)
	|	ГДЕ
	|		УдержанияРаботниковОрганизации.ПериодЗавершения > &парамНачало
	|		И УдержанияРаботниковОрганизации.ПериодЗавершения <= &парамКонец
	|		И УдержанияРаботниковОрганизации.ФизЛицо = &Физлицо
	|		И УдержанияРаботниковОрганизацииПроверка.ФизЛицо ЕСТЬ NULL ) КАК УдержанияРаботниковОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Период = &парамДатаВалютногоУчета)
	|			И (Валюты.Валюта = УдержанияРаботниковОрганизации.Валюта1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период");
	
	Запрос.УстановитьПараметр("парамНачало" , Месяц);
	Запрос.УстановитьПараметр("парамКонец" , КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("Физлицо" , Физлицо);
	Запрос.УстановитьПараметр("парамДатаВалютногоУчета" , НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("парамПустаяВалюта" , Справочники.Валюты.ПустаяСсылка());
	Запрос.УстановитьПараметр("парамВалютаРегламентУчета" , Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Уволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ВыходнойДокумент.Вывести(ПустаяСтрока);
	Область = Макет.ПолучитьОбласть("ПлановыеДанныеСтрока");
	Пока Выборка.Следующий() Цикл
		Область.Параметры.Заполнить(Выборка);
		Если Выборка.Валюта = ВалютаРеглУчета или Выборка.Валюта.Пустая() Тогда
			Область.Области.Валюта.Текст = "";
		КонецЕсли;
		ВыходнойДокумент.Вывести(Область);
		ОбластьЯчеек = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, ,ВыходнойДокумент.ВысотаТаблицы, );
		Если Выборка.Действует Тогда
			// удержания выделяем цветом
			ОбластьЯчеек.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
		Иначе
			// недействующие плановые данные выделяем цветом
			ОбластьЯчеек.ЦветТекста = WebЦвета.ТемноСерый;
		КонецЕсли;
		
	КонецЦикла;
	
	// плановые авансы
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АвансыРаботникамОрганизаций.Организация,
	|	АвансыРаботникамОрганизаций.Организация.Наименование КАК ОрганизацияНаименование,
	|	АвансыРаботникамОрганизаций.СуммаАванса КАК Размер
	|ИЗ
	|	РегистрСведений.АвансыРаботникамОрганизаций КАК АвансыРаботникамОрганизаций
	|ГДЕ
	|	АвансыРаботникамОрганизаций.Физлицо = &Физлицо");
	
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ВыходнойДокумент.Вывести(ПустаяСтрока);
	Область = Макет.ПолучитьОбласть("ПлановыйАвансСтрока");
	Пока Выборка.Следующий() Цикл
		Область.Параметры.Заполнить(Выборка);
		ВыходнойДокумент.Вывести(Область);
		Если МестаРаботыФизлица.Количество() = 0 Тогда
			// недействующие плановые данные выделяем цветом
			ОбластьЯчеек = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, ,ВыходнойДокумент.ВысотаТаблицы, );
			ОбластьЯчеек.ЦветТекста = WebЦвета.ТемноСерый;
		КонецЕсли;
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("ПлановыеДанныеПодвал");
	ВыходнойДокумент.Вывести(Область);
	
	// отчеты
	ОтчетУнифицированнаяФормаТ3 = Отчеты.УнифицированнаяФормаТ3.Создать();
	
	ОбработанныеОрганизации = Новый Соответствие;
	Сч = 1;
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		Если ОбработанныеОрганизации[МестоРаботы.ОбособленноеПодразделение] = НеОпределено Тогда
			
			ОбработанныеОрганизации[МестоРаботы.ОбособленноеПодразделение] = Истина;
			
			Область = Макет.ПолучитьОбласть("Организация");
			Область.Параметры.ОрганизацияНаименование = МестоРаботы.ОбособленноеПодразделениеНаименование;
			Область.Параметры.Организация = МестоРаботы.ОбособленноеПодразделение;
			ВыходнойДокумент.Вывести(Область);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			
			// УнифицированнаяФормаТ3
			// заголовок
			Область = Макет.ПолучитьОбласть("УнифицированнаяФормаТ3");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаУнифицированнаяФормаТ3.Имя;
			Область.Области.ГиперссылкаУнифицированнаяФормаТ3.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаУнифицированнаяФормаТ3);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьУнифицированнаяФормаТ3 = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			УнифицированнаяФормаТ3Отчет = Новый ТабличныйДокумент;
			
			ВыведенныеПодразделения = Новый Соответствие;
			
			// сформируем штатное распиание по всем местам работы
			Для Каждого ВнМестоРаботы Из МестаРаботыФизлица Цикл
				Если ВнМестоРаботы.ОбособленноеПодразделение <> МестоРаботы.ОбособленноеПодразделение Тогда
					// перебираем только места работы обрабатываемой организации
					Продолжить;
				КонецЕсли;
				
				Если ВыведенныеПодразделения.Получить(МестоРаботы.Подразделение) <> НеОпределено Тогда
					// штатное расписание по подразделению уже вывели
					Продолжить;
				КонецЕсли;
				
				ТиповыеОтчеты.УстановитьПараметр(ОтчетУнифицированнаяФормаТ3.КомпоновщикНастроек, "Период",             НачалоМесяца(Месяц));
				ТиповыеОтчеты.ДобавитьОтбор(ОтчетУнифицированнаяФормаТ3.КомпоновщикНастроек, "Организация",              МестоРаботы.ОбособленноеПодразделение);
				ТиповыеОтчеты.ДобавитьОтбор(ОтчетУнифицированнаяФормаТ3.КомпоновщикНастроек, "ПодразделениеОрганизации", МестоРаботы.Подразделение);
				
				ВыведенныеПодразделения[МестоРаботы.Подразделение] = МестоРаботы.Подразделение;
				
				ОтчетУнифицированнаяФормаТ3.СформироватьОтчет(УнифицированнаяФормаТ3Отчет);
				// таким способом размещаются отчеты без форматов строк
				ОбластьПрямоугольная = УнифицированнаяФормаТ3Отчет.Область(1, 1, УнифицированнаяФормаТ3Отчет.ВысотаТаблицы, УнифицированнаяФормаТ3Отчет.ШиринаТаблицы);
				НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + УнифицированнаяФормаТ3Отчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
				// назначим ширину колонок у новой области формата строк
				Для Счетчик = 1 По УнифицированнаяФормаТ3Отчет.ШиринаТаблицы Цикл
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = УнифицированнаяФормаТ3Отчет.Область(1, Счетчик).ШиринаКолонки;
				КонецЦикла;
				ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
			КонецЦикла;
			
			// Для вывода отчетов в отдельных окнах
			ДанныеТабличныхДокументовТ3.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),УнифицированнаяФормаТ3Отчет);
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
		КонецЕсли;
		
		Сч = Сч + 1;
		
	КонецЦикла;
	ВыходнойДокумент.ТолькоПросмотр = Истина;
	ВыходнойДокумент.Защита = Истина;
	
КонецПроцедуры

Процедура ПолучитьСведенияОПлановыхНачислениях()
	Перем СвойствоИнфоПоОрганизации;
	
	// получим сведения о местах работы
	ПолучитьСведенияОМестахРаботы();
	
	ДанныеПоПлановымРасчетам.Очистить();
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		// просто наполняем соответствие организациями работника
		ИнфоПоОрганизации = Новый Структура(
		"Наименование,
		|ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций,
		|ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций,
		|ИсполнительныйЛист", 
		МестоРаботы.ОбособленноеПодразделениеНаименование,
		Новый Массив,
		Новый Массив,
		Новый Массив);
		ДанныеПоПлановымРасчетам[МестоРаботы.ОбособленноеПодразделение] = ИнфоПоОрганизации;
		
	КонецЦикла;
	
	Запрос = Новый Запрос();
	
	// нужно проверить наличие документов изменения плановых данных
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	""ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций"" КАК ВидДокумента,
	|	Начисления.Ссылка.Организация КАК Организация,
	|	Начисления.Ссылка КАК Документ,
	|	Начисления.Ссылка.Представление КАК Представление
	|ИЗ
	|	Документ.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.ДатаДействия >= &Месяц
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций"",
	|	Удержания.Ссылка.Организация,
	|	Удержания.Ссылка,
	|	Удержания.Ссылка.Представление
	|ИЗ
	|	Документ.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.ФизЛицо = &Физлицо
	|	И Удержания.ДатаДействия >= &Месяц
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ИсполнительныйЛист"",
	|	Удержания.Ссылка.Организация,
	|	Удержания.Ссылка,
	|	Удержания.Ссылка.Представление
	|ИЗ
	|	Документ.ИсполнительныйЛист КАК Удержания
	|ГДЕ
	|	Удержания.Физлицо = &Физлицо";
	
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ДанныеПоПлановымРасчетам[Выборка.Организация] = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		ДанныеПоПлановымРасчетам[Выборка.Организация].Свойство(Выборка.ВидДокумента, СвойствоИнфоПоОрганизации);
		СвойствоИнфоПоОрганизации.Добавить(Новый Структура("Документ, Представление", Выборка.Документ, Выборка.Представление));
	КонецЦикла;
	
КонецПроцедуры

Процедура НадписьПлановыеНачисленияНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоПлановымРасчетам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, "Изменить плановые начисления по организации " + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьПлановыеНачисления);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Сотрудник = Справочники.СотрудникиОрганизаций.ПустаяСсылка();
		// для заполнения сотрудника перебираем рабочие места
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				Сотрудник = МестоРаботы.Сотрудник;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		НовыйДокумент.ДобавитьСтрокиНачисленийПоРаботнику(Сотрудник);
		Если НовыйДокумент.ОсновныеНачисления.Количество() = 0 Тогда
			Строка = НовыйДокумент.ОсновныеНачисления.Добавить();
			Строка.Физлицо = Физлицо;
			// для заполнения сотрудника перебираем рабочие места
			Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
				Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
					Строка.Сотрудник = МестоРаботы.Сотрудник;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьПлановыеУдержанияНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоПлановымРасчетам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, "Изменить плановые удержания по организации " + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьПлановыеУдержания);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.Удержания.Добавить();
		Строка.Физлицо = Физлицо;
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьИсполнительныйЛистНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоПлановымРасчетам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ИсполнительныйЛист Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ИсполнительныйЛист.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, "Исполнительный лист по организации " + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьИсполнительныйЛист);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ИсполнительныйЛист.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		НовыйДокумент.Физлицо = Физлицо;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьШтаноеРасписаниеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		СтрокаМеню = СписокМеню.Добавить(МестоРаботы, МестоРаботы.ОбособленноеПодразделениеНаименование + " (" + МестоРаботы.ПодразделениеНаименование + ")");
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Подразделение = СписокМеню[0].Значение.Подразделение;
		Организация = СписокМеню[0].Значение.Организация;
		Должность = СписокМеню[0].Значение.Должность;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьШтаноеРасписание);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
		Подразделение = Значение.Подразделение;
		Организация = Значение.Организация;
		Должность = Значение.Должность;
	КонецЕсли;
	Форма = РегистрыСведений.ШтатноеРасписаниеОрганизаций.ПолучитьФормуСписка();
	Форма.УстановитьСтроку(Организация, Подразделение, Должность);
	Форма.Открыть();
КонецПроцедуры

Процедура НадписьПлановыйАвансНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	ИспользованныеОрганизации = Новый Соответствие;
	
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		Если ИспользованныеОрганизации[МестоРаботы.ОбособленноеПодразделение] = НеОпределено Тогда
			СтрокаМеню = СписокМеню.Добавить(МестоРаботы, "По организации " + МестоРаботы.ОбособленноеПодразделениеНаименование);
			ИспользованныеОрганизации[МестоРаботы.ОбособленноеПодразделение] = МестоРаботы.ОбособленноеПодразделение;
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Организация = СписокМеню[0].Значение.Организация;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьПлановыйАванс);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
		Организация = Значение.Организация;
	КонецЕсли;
	Форма = РегистрыСведений.АвансыРаботникамОрганизаций.ПолучитьФормуСписка();
	Форма.УстановитьСтроку(Организация, Физлицо);
	Форма.Открыть();
КонецПроцедуры

// обработка гиперссылок - действия по открытию форм в отдельных окнах
//
Процедура ПлановыеДанныеВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Сред(Область.Имя, 4) = "ГиперссылкаУнифицированнаяФормаТ3" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовТ3.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Штатное расписание");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ РАСЧЕТЫ, ЗАРПЛАТА

// формирует раздел
Процедура СформироватьДанныеРасчетыЗарплата()
	Если мДанныеРасчетыЗарплатаСформированы Тогда
		Возврат;
	КонецЕсли;
	мДанныеРасчетыЗарплатаСформированы = Истина;
	// обновить динамические списки
	ПервичныеДокументыОрганизаций.Обновить();
	
	ПолучитьСведенияОНачислениях();
	
	// Сбор данных о состоянии работника в организациях.
	ПолучитьМестаРаботыИСостоянияРаботника();
	
	// настройка надписей раздела Действия и документы
	ЭлементыФормы.НадписьНачислениеЗарплаты.Заголовок = "Начислить зарплату";
	ЭлементыФормы.НадписьПремии.Заголовок = "Начислить премию";
	ЭлементыФормы.НадписьВыплата.Заголовок = "Выплатить зарплату";
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.НачислениеЗарплатыРаботникамОрганизаций Цикл
			ЭлементыФормы.НадписьНачислениеЗарплаты.Заголовок = "Начисление зарплаты";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ПремииРаботниковОрганизаций Цикл
			ЭлементыФормы.НадписьПремии.Заголовок = "Начисление премии";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ЗарплатаКВыплатеОрганизаций Цикл
			ЭлементыФормы.НадписьВыплата.Заголовок = "Платежная ведомость";
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	// заполнения табличного документа с отчетами
	ВыходнойДокумент = ЭлементыФормы.РасчетыЗарплата;
	
	// восстановление раскрытых группировок отчета
	ГруппировкаАнализНачисленийРаботникамОрганизацийРаскрыта = Ложь;
	Если ОбластьАнализНачисленийРаботникамОрганизаций <> НеОпределено и ОбластьАнализНачисленийРаботникамОрганизаций.Видимость Тогда
		ГруппировкаАнализНачисленийРаботникамОрганизацийРаскрыта = Истина;
	КонецЕсли;
	ГруппировкаРасчетныеЛисткиОрганизацийРаскрыта = Ложь;
	Если ОбластьРасчетныеЛисткиОрганизаций <> НеОпределено и ОбластьРасчетныеЛисткиОрганизаций.Видимость Тогда
		ГруппировкаРасчетныеЛисткиОрганизацийРаскрыта = Истина;
	КонецЕсли;
	ГруппировкаРегламентированнаяФорма1НДФЛРаскрыта = Ложь;
	Если ОбластьРегламентированнаяФорма1НДФЛ <> НеОпределено и ОбластьРегламентированнаяФорма1НДФЛ.Видимость Тогда
		ГруппировкаРегламентированнаяФорма1НДФЛРаскрыта = Истина;
	КонецЕсли;
	ГруппировкаУнифицированнаяФормаТ13Раскрыта = Ложь;
	Если ОбластьУнифицированнаяФормаТ13 <> НеОпределено и ОбластьУнифицированнаяФормаТ13.Видимость Тогда
		ГруппировкаУнифицированнаяФормаТ13Раскрыта = Истина;
	КонецЕсли;
	ГруппировкаСЗВ4Раскрыта = Ложь;
	Если ОбластьСЗВ4 <> НеОпределено и ОбластьСЗВ4.Видимость Тогда
		ГруппировкаСЗВ4Раскрыта = Истина;
	КонецЕсли;
	
	ВыходнойДокумент.Очистить();
	
	Макет = ПолучитьМакет("РасчетыЗарплата");
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьФизлицо = Макет.ПолучитьОбласть("Физлицо");
	ОбластьФизлицо.Параметры.Физлицо = Физлицо;
	ОбластьФизлицо.Параметры.Месяц = Формат(Месяц, "ДФ='ММММ гггг ''г.'''");
	ВыходнойДокумент.Вывести(ОбластьФизлицо);
	
	ГрафикиРаботника = Новый Массив;
	
	// выводим сведения о местах работы
	Если МестаРаботыИСостоянияФизлица.Количество() <> 0 Тогда
		Область = Макет.ПолучитьОбласть("МестаРаботыЗаголовок");
		ВыходнойДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("МестаРаботыСтрока");
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			Область.Параметры.Заполнить(МестоРаботы);
			ВыходнойДокумент.Вывести(Область);
			ГрафикиРаботника.Добавить(МестоРаботы.ГрафикРаботы);
		КонецЦикла;
	КонецЕсли;
	
	// получим сведения о графике работы
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГрафикиРаботы.Ссылка,
	|	ГрафикиРаботы.Наименование КАК ГрафикРаботыНаименование
	|ИЗ
	|	Справочник.ГрафикиРаботы КАК ГрафикиРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО ГрафикиРаботы.Ссылка = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И (ГрафикиРаботыПоВидамВремени.Дата МЕЖДУ &Начало И &Конец)
	|ГДЕ
	|	ГрафикиРаботы.Ссылка В(&ГрафикиРаботника)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикиРаботы.Ссылка,
	|	ГрафикиРаботы.Наименование
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ГрафикиРаботыПоВидамВремени.ОсновноеЗначение) = 0
	|		ИЛИ СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) = 0)");
	
	Запрос.УстановитьПараметр("Начало", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("Конец", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("ГрафикиРаботника", ГрафикиРаботника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() <> 0 Тогда
		Графики = Макет.ПолучитьОбласть("ГрафикиНеЗаполнены");
		Графики.Параметры.ТекстГрафикиНеЗаполнены = "ВНИМАНИЕ! Не заполнены один или несколько графиков работы, по которым оплачиваются повременные начисления сотрудника! Повременные начисления не могут быть выполнены.";
		НеЗаполненныеГрафики = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			НеЗаполненныеГрафики[Выборка.ГрафикРаботы] = Выборка.ГрафикРаботыНаименование;
		КонецЦикла;
		Графики.Параметры.НеЗаполненныеГрафики = НеЗаполненныеГрафики;
		ВыходнойДокумент.Вывести(Графики);
		
	КонецЕсли;
	
	// получим сведения о состоянии начислений
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Месяц));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Организация КАК Организация,
	|	ИСТИНА КАК ФлагПлановыеНачисления
	|ПОМЕСТИТЬ ВТПлановыеНачисления
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&Месяц, Сотрудник.Физлицо = &Физлицо) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|ГДЕ
	|	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеНачисленияРаботниковОрганизаций.Организация,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|ГДЕ
	|	ПлановыеНачисленияРаботниковОрганизаций.Сотрудник.Физлицо = &Физлицо
	|	И ПлановыеНачисленияРаботниковОрганизаций.Период > &ДатаНачала
	|	И ПлановыеНачисленияРаботниковОрганизаций.Период < &ДатаОкончания
	|	И ПлановыеНачисленияРаботниковОрганизаций.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.Организация КАК СсылкаОрганизация,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка КАК НепроведенныйДокумент
	|ПОМЕСТИТЬ ВТНепроведенныеНачисления
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК НачислениеЗарплатыРаботникамОрганизацийНачисления
	|ГДЕ
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник.Физлицо = &Физлицо
	|	И НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления.Ссылка.Организация,
	|	НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления.Ссылка
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.ДополнительныеНачисления КАК НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления
	|ГДЕ
	|	НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления.Сотрудник.Физлицо = &Физлицо
	|	И НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ НачислениеЗарплатыРаботникамОрганизацийДополнительныеНачисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.ПремииРаботниковОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.НевыходыВОрганизациях.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И (НЕ Начисления.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистрация.Ссылка.Организация,
	|	Регистрация.Ссылка
	|ИЗ
	|	Документ.РегистрацияПростоевРаботниковОрганизаций КАК Регистрация
	|ГДЕ
	|	Регистрация.Сотрудник.Физлицо = &Физлицо
	|	И Регистрация.ПериодРегистрации = &Месяц
	|	И (НЕ Регистрация.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Отпуск.Ссылка.Организация,
	|	Отпуск.Ссылка
	|ИЗ
	|	Документ.НачислениеОтпускаРаботникамОрганизаций КАК Отпуск
	|ГДЕ
	|	Отпуск.Сотрудник.Физлицо = &Физлицо
	|	И Отпуск.ПериодРегистрации = &Месяц
	|	И (НЕ Отпуск.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчет.Ссылка.Организация,
	|	Расчет.Ссылка
	|ИЗ
	|	Документ.РасчетПриУвольненииРаботникаОрганизаций КАК Расчет
	|ГДЕ
	|	Расчет.Сотрудник.Физлицо = &Физлицо
	|	И Расчет.ПериодРегистрации = &Месяц
	|	И (НЕ Расчет.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Больничный.Ссылка.Организация,
	|	Больничный.Ссылка
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК Больничный
	|ГДЕ
	|	Больничный.Сотрудник.Физлицо = &Физлицо
	|	И Больничный.ПериодРегистрации = &Месяц
	|	И (НЕ Больничный.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СреднийЗаработок.Ссылка.Организация,
	|	СреднийЗаработок.Ссылка
	|ИЗ
	|	Документ.ОплатаПоСреднемуЗаработку КАК СреднийЗаработок
	|ГДЕ
	|	СреднийЗаработок.Сотрудник.Физлицо = &Физлицо
	|	И СреднийЗаработок.ПериодРегистрации = &Месяц
	|	И (НЕ СреднийЗаработок.Проведен)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СсылкаОрганизация";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОсновныеНачисленияРаботниковОрганизаций.Организация КАК Организация,
	|	ОсновныеНачисленияРаботниковОрганизаций.Регистратор КАК ПроведенныйДокумент
	|ПОМЕСТИТЬ ВТПроведенныеНачисления
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	|ГДЕ
	|	ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &Физлицо
	|	И ОсновныеНачисленияРаботниковОрганизаций.ПериодРегистрации = &Месяц
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДополнительныеНачисленияРаботниковОрганизаций.Организация,
	|	ДополнительныеНачисленияРаботниковОрганизаций.Регистратор
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
	|ГДЕ
	|	ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &Физлицо
	|	И ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации = &Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроведенныеНачисления.Организация КАК Организация,
	|	ИСТИНА КАК ФлагПроведенныеНачисления,
	|	ПроведенныеНачисления.ПроведенныйДокумент КАК ПроведенныйДокумент,
	|	ВЫБОР
	|		КОГДА НепроведенныеНачисления.НепроведенныйДокумент ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФлагНепроведенныеНачисления,
	|	НепроведенныеНачисления.НепроведенныйДокумент КАК НепроведенныйДокумент
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ВТПроведенныеНачисления КАК ПроведенныеНачисления
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТНепроведенныеНачисления КАК НепроведенныеНачисления
	|	ПО ПроведенныеНачисления.Организация = ВЫБОР
	|			КОГДА НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА НепроведенныеНачисления.СсылкаОрганизация
	|			ИНАЧЕ НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация
	|		КОНЕЦ
	|			
	|ОБЪЕДИНИТЬ
	|				
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НепроведенныеНачисления.СсылкаОрганизация
	|		ИНАЧЕ НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	NULL,
	|	ИСТИНА,
	|	НепроведенныеНачисления.НепроведенныйДокумент
	|ИЗ
	|	ВТНепроведенныеНачисления КАК НепроведенныеНачисления
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПроведенныеНачисления КАК ПроведенныеНачисления
	|	ПО ПроведенныеНачисления.Организация = ВЫБОР
	|			КОГДА НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА НепроведенныеНачисления.СсылкаОрганизация
	|			ИНАЧЕ НепроведенныеНачисления.СсылкаОрганизация.ГоловнаяОрганизация
	|		КОНЕЦ
	|
	|ГДЕ
	|   ПроведенныеНачисления.Организация ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ПлановыеНачисления.Организация КАК Организация,
	|	ПлановыеНачисления.Организация.Наименование КАК ОрганизацияНаименование,
	|	ПлановыеНачисления.ФлагПлановыеНачисления КАК ФлагПлановыеНачисления,
	|	Начисления.ПроведенныйДокумент КАК ПроведенныйДокумент,
	|	ПРЕДСТАВЛЕНИЕ(Начисления.ПроведенныйДокумент) КАК ПроведенныйДокументПредставление,
	|	Начисления.НепроведенныйДокумент КАК НепроведенныйДокумент,
	|	ПРЕДСТАВЛЕНИЕ(Начисления.НепроведенныйДокумент) КАК НепроведенныйДокументПредставление,
	|	ЕСТЬNULL(Начисления.ФлагПроведенныеНачисления, ЛОЖЬ) КАК ФлагПроведенныеНачисления,
	|	ЕСТЬNULL(Начисления.ФлагНепроведенныеНачисления, ЛОЖЬ) КАК ФлагНепроведенныеНачисления
	|ИЗ
	|	ВТПлановыеНачисления КАК ПлановыеНачисления
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
	|	ПО ПлановыеНачисления.Организация = Начисления.Организация
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|		
	|ВЫБРАТЬ
	|	Начисления.Организация,
	|	Начисления.Организация.Наименование,
	|	ЛОЖЬ,
	|	Начисления.ПроведенныйДокумент,
	|	ПРЕДСТАВЛЕНИЕ(Начисления.ПроведенныйДокумент),
	|	Начисления.НепроведенныйДокумент,
	|	ПРЕДСТАВЛЕНИЕ(Начисления.НепроведенныйДокумент),
	|	Начисления.ФлагПроведенныеНачисления,
	|	Начисления.ФлагНепроведенныеНачисления
	|ИЗ
	|	ВТНачисления КАК Начисления
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеНачисления КАК ПлановыеНачисления
	|	ПО ПлановыеНачисления.Организация = Начисления.Организация
	|	
	|ГДЕ
	|	ПлановыеНачисления.Организация ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПлановыеНачисления = Новый Соответствие;
	ПлановыеНачисленияНеВыполнены = Новый Соответствие;
	ЛишниеНачисления = Новый Соответствие;
	НепроведенныеНачисления = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ФлагПлановыеНачисления Тогда
			ПлановыеНачисления[Выборка.Организация] = Выборка.ОрганизацияНаименование;
			Если не Выборка.ФлагПроведенныеНачисления Тогда
				ПлановыеНачисленияНеВыполнены[Выборка.Организация] = Выборка.ОрганизацияНаименование;
			КонецЕсли;
		КонецЕсли;
		Если Выборка.ФлагПроведенныеНачисления и не Выборка.ФлагПлановыеНачисления и Выборка.ПроведенныйДокумент <> null Тогда
			ЛишниеНачисления[Выборка.ПроведенныйДокумент] = Выборка.ПроведенныйДокументПредставление;
		КонецЕсли;
		Если Выборка.ФлагНепроведенныеНачисления и Выборка.НепроведенныйДокумент <> null Тогда
			НепроведенныеНачисления[Выборка.НепроведенныйДокумент] = Выборка.НепроведенныйДокументПредставление;
		КонецЕсли;
		
	КонецЦикла;        
	
	Если ПлановыеНачисления.Количество() = 0 Тогда
		ТекстПлановыеНачисления = "По сотруднику не предусмотрены плановые начисления. ";
	Иначе
		Если ПлановыеНачисленияНеВыполнены.Количество() <> 0 Тогда
			ТекстПлановыеНачисления = "ВНИМАНИЕ! По сотруднику предусмотрены плановые начисления по " + 
			?(ПлановыеНачисления.Количество() = 1, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации "), ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организациям: "));
			Первый = Истина;
			Для Каждого ОргЭлемент Из ПлановыеНачисления Цикл
				Если не Первый Тогда
					ТекстПлановыеНачисления = ТекстПлановыеНачисления + ", ";
				КонецЕсли;
				Первый = Ложь;
				ТекстПлановыеНачисления = ТекстПлановыеНачисления + ОргЭлемент.Значение;
			КонецЦикла;
			ТекстПлановыеНачисления = ТекстПлановыеНачисления + 
			". При этом начисления не выполнены или не проведены для " + 
			?(ПлановыеНачисленияНеВыполнены.Количество() = 1, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации "), ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организациям: "));
			Первый = Истина;
			Для Каждого ОргЭлемент Из ПлановыеНачисленияНеВыполнены Цикл
				Если не Первый Тогда
					ТекстПлановыеНачисления = ТекстПлановыеНачисления + ", ";
				КонецЕсли;
				Первый = Ложь;
				ТекстПлановыеНачисления = ТекстПлановыеНачисления + ОргЭлемент.Значение;
			КонецЦикла;
			ТекстПлановыеНачисления = ТекстПлановыеНачисления + ".";
		Иначе
			ТекстПлановыеНачисления = 
			"По сотруднику на данный месяц предусмотрены и выполнены начисления по " + 
			?(ПлановыеНачисления.Количество() = 1, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации "), ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организациям: "));
			Первый = Истина;
			Для Каждого ОргЭлемент Из ПлановыеНачисления Цикл
				Если не Первый Тогда
					ТекстПлановыеНачисления = ТекстПлановыеНачисления + ", ";
				КонецЕсли;
				Первый = Ложь;
				ТекстПлановыеНачисления = ТекстПлановыеНачисления + ОргЭлемент.Значение;
			КонецЦикла;
			ТекстПлановыеНачисления = ТекстПлановыеНачисления + 
			". Для анализа полноты выполненных начислений можно воспользоваться разделом ""Анализ начислений"".";
		КонецЕсли;
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ПлановыеНачисления");
	Область.Параметры.ТекстПлановыеНачисления = ТекстПлановыеНачисления;
	ВыходнойДокумент.Вывести(Область);
	
	Если ЛишниеНачисления.Количество() > 0 Тогда
		ТекстЛишниеНачисления = "Обратите внимание! По сотруднику не предусмотрены, но при этом выполнены начисления по " + 
		?(ЛишниеНачисления.Количество() = 1, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации "), ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организациям: "));
		Первый = Истина;
		Для Каждого ОргЭлемент Из ЛишниеНачисления Цикл
			Если не Первый Тогда
				ТекстЛишниеНачисления = ТекстЛишниеНачисления + ", ";
			КонецЕсли;
			Первый = Ложь;
			ТекстЛишниеНачисления = ТекстЛишниеНачисления + ОргЭлемент.Значение;
		КонецЦикла;
		ТекстЛишниеНачисления = ТекстЛишниеНачисления + ".";
		Область = Макет.ПолучитьОбласть("ЛишниеНачисления");
		Область.Параметры.ТекстЛишниеНачисления = ТекстЛишниеНачисления;
		Область.Параметры.ЛишниеНачисленияСписок = ЛишниеНачисления;
		ВыходнойДокумент.Вывести(Область);
	КонецЕсли;
	
	Если НепроведенныеНачисления.Количество() > 0 Тогда
		Область = Макет.ПолучитьОбласть("ПроведенностьНачислений");
		Область.Параметры.ТекстПроведенностьНачислений = "ВНИМАНИЕ! По сотруднику существуют непроведенные документы начислений. Эти документы не отражены в учете!";
		Область.Параметры.НепроведенныеДокументыНачислений = НепроведенныеНачисления;
		ВыходнойДокумент.Вывести(Область);
	КонецЕсли;
	
	// отчеты
	ОтчетРегламентированнаяФорма1НДФЛ = ?(Год(Месяц) < 2011, Отчеты.РегламентированнаяФорма1НДФЛ.Создать(), Отчеты.РегистрНалоговогоУчетаПоНДФЛ.Создать());
	ОтчетРасчетныеЛисткиОрганизаций = Отчеты.РасчетныеЛисткиОрганизаций.Создать();
	ОтчетАнализНачисленийРаботникамОрганизаций = Отчеты.АнализНачисленийРаботникамОрганизаций.Создать();
	ОтчетУнифицированнаяФормаТ13 = Отчеты.УнифицированнаяФормаТ13.Создать();
	
	ОбработанныеОрганизации = Новый Соответствие;
	ФормироватьНалоговуюКарточку = НЕ мДанныеНалоговаяКарточкаСформированы;
	Сч = 1;
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		Если ОбработанныеОрганизации[МестоРаботы.Организация] = НеОпределено Тогда
			
			ОбработанныеОрганизации[МестоРаботы.Организация] = Истина;
			
			Область = Макет.ПолучитьОбласть("Организация");
			Область.Параметры.ОрганизацияНаименование = МестоРаботы.ОрганизацияНаименование;
			Область.Параметры.Организация = МестоРаботы.Организация;
			ВыходнойДокумент.Вывести(Область);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			
			// АнализНачисленийРаботникамОрганизаций
			// заголовок
			Область = Макет.ПолучитьОбласть("АнализНачислений");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаАнализНачисленийРаботникамОрганизаций.Имя;
			Область.Области.ГиперссылкаАнализНачисленийРаботникамОрганизаций.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаАнализНачисленийРаботникамОрганизацийРаскрыта);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьАнализНачисленийРаботникамОрганизаций = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			ТиповыеОтчеты.УстановитьПараметр(ОтчетАнализНачисленийРаботникамОрганизаций.КомпоновщикНастроек, "НачалоПериода", НачалоМесяца(Месяц));
			ТиповыеОтчеты.УстановитьПараметр(ОтчетАнализНачисленийРаботникамОрганизаций.КомпоновщикНастроек, "КонецПериода", КонецМесяца(Месяц));
			ТиповыеОтчеты.ДобавитьОтбор(ОтчетАнализНачисленийРаботникамОрганизаций.КомпоновщикНастроек, "Сотрудник.ФизЛицо", МестоРаботы.Сотрудник.ФизЛицо);
			
			АнализНачисленийРаботникамОрганизацийОтчет = Новый ТабличныйДокумент;
			ОтчетАнализНачисленийРаботникамОрганизаций.СформироватьОтчет(АнализНачисленийРаботникамОрганизацийОтчет,, Ложь);
			
			//
			// Для вывода отчетов в отдельных окнах
			ДанныеТабличныхДокументовАнализНачислений.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),АнализНачисленийРаботникамОрганизацийОтчет);
			//
			// таким способом размещаются отчеты без форматов строк
			ОбластьПрямоугольная = АнализНачисленийРаботникамОрганизацийОтчет.Область(1, 1, АнализНачисленийРаботникамОрганизацийОтчет.ВысотаТаблицы, АнализНачисленийРаботникамОрганизацийОтчет.ШиринаТаблицы);
			НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
			ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + АнализНачисленийРаботникамОрганизацийОтчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
			// назначим ширину колонок у новой области формата строк
			Для Счетчик = 1 По АнализНачисленийРаботникамОрганизацийОтчет.ШиринаТаблицы Цикл
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = АнализНачисленийРаботникамОрганизацийОтчет.Область(, Счетчик, , Счетчик).ШиринаКолонки; 
			КонецЦикла;
			ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			// РасчетныеЛисткиОрганизаций
			// заголовок
			Область = Макет.ПолучитьОбласть("РасчетныеЛистки");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаРасчетныеЛисткиОрганизаций.Имя;
			Область.Области.ГиперссылкаРасчетныеЛисткиОрганизаций.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаРасчетныеЛисткиОрганизацийРаскрыта);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьРасчетныеЛисткиОрганизаций = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			ТиповыеОтчеты.УстановитьПараметр(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек,  "НачалоПериода", НачалоМесяца(Месяц));
			ТиповыеОтчеты.УстановитьПараметр(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек,  "КонецПериода", КонецМесяца(Месяц));
			ТиповыеОтчеты.УстановитьПараметр(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек,  "Группировать", ложь);
			ТиповыеОтчеты.УстановитьПараметр(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек,  "ВидРасчетногоЛистка", "Подробно");
			ТиповыеОтчеты.ДобавитьОтбор(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек, "Организация", МестоРаботы.ОбособленноеПодразделение);
			ТиповыеОтчеты.ДобавитьОтбор(ОтчетРасчетныеЛисткиОрганизаций.КомпоновщикНастроек, "ФизЛицо", Физлицо);
			
			
			РасчетныеЛисткиОрганизацийОтчет = Новый ТабличныйДокумент;
			ОтчетРасчетныеЛисткиОрганизаций.СформироватьОтчет(РасчетныеЛисткиОрганизацийОтчет);
			
			// Для вывода отчетов в отдельных окнах
			ДанныеТабличныхДокументовРасчетныеЛистки.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),РасчетныеЛисткиОрганизацийОтчет);
			
			// таким способом размещаются отчеты без форматов строк
			ОбластьПрямоугольная = РасчетныеЛисткиОрганизацийОтчет.Область(1, 1, РасчетныеЛисткиОрганизацийОтчет.ВысотаТаблицы, РасчетныеЛисткиОрганизацийОтчет.ШиринаТаблицы);
			НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
			ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + РасчетныеЛисткиОрганизацийОтчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
			// назначим ширину колонок у новой области формата строк
			Для Счетчик = 1 По РасчетныеЛисткиОрганизацийОтчет.ШиринаТаблицы Цикл
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = РасчетныеЛисткиОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
			КонецЦикла;
			ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			// РегламентированнаяФорма1НДФЛ
			// заголовок
			Область = Макет.ПолучитьОбласть("НалоговаяКарточка");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаРегламентированнаяФорма1НДФЛ.Имя;
			Область.Области.ГиперссылкаРегламентированнаяФорма1НДФЛ.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаРегламентированнаяФорма1НДФЛРаскрыта);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьРегламентированнаяФорма1НДФЛ = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			Если ФормироватьНалоговуюКарточку Тогда
				ТиповыеОтчеты.УстановитьПараметр(ОтчетРегламентированнаяФорма1НДФЛ.КомпоновщикНастроек,  "НачалоПериода", НачалоГода(Месяц));
				ТиповыеОтчеты.УстановитьПараметр(ОтчетРегламентированнаяФорма1НДФЛ.КомпоновщикНастроек,  "КонецПериода", КонецГода(Месяц));
				ТиповыеОтчеты.ДобавитьОтбор(ОтчетРегламентированнаяФорма1НДФЛ.КомпоновщикНастроек, "Организация", МестоРаботы.Организация);
				ТиповыеОтчеты.ДобавитьОтбор(ОтчетРегламентированнаяФорма1НДФЛ.КомпоновщикНастроек, "ФизЛицо", Физлицо);
				РегламентированнаяФорма1НДФЛОтчет = Новый ТабличныйДокумент;
				ОтчетРегламентированнаяФорма1НДФЛ.СформироватьОтчет(РегламентированнаяФорма1НДФЛОтчет);
				// Для вывода отчетов в отдельных окнах
				ДанныеТабличныхДокументов1НДФЛ.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),РегламентированнаяФорма1НДФЛОтчет);
				
			КонецЕсли;
			
			// таким способом размещаются отчеты с форматами строк
			ОбластьСтроки = РегламентированнаяФорма1НДФЛОтчет.Область(1, , РегламентированнаяФорма1НДФЛОтчет.ВысотаТаблицы, );
			ВыходнойДокумент.ВставитьОбласть(ОбластьСтроки, ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы + 1, , ВыходнойДокумент.ВысотаТаблицы + РегламентированнаяФорма1НДФЛОтчет.ВысотаТаблицы, ));
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			// УнифицированнаяФормаТ13
			// заголовок
			Область = Макет.ПолучитьОбласть("УнифицированнаяФормаТ13");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаУнифицированнаяФормаТ13.Имя;
			Область.Области.ГиперссылкаУнифицированнаяФормаТ13.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаУнифицированнаяФормаТ13Раскрыта);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьУнифицированнаяФормаТ13 = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			ФильтрСоответствия = Новый Соответствие;
			ФильтрСоответствия.Вставить("Организация", МестоРаботы.ОбособленноеПодразделение);
			ФильтрСоответствия.Вставить("ОтборОрганизации", ЗначениеЗаполнено(МестоРаботы.ОбособленноеПодразделение));
			ФильтрСоответствия.Вставить("ВидСравненияОрганизации", ВидСравнения.Равно);
			ФильтрСоответствия.Вставить("ПериодРегистрации",  НачалоМесяца(Месяц));

			
			// Так как Т-13 формируется по сотрудникам, а места работы будут отфильтрованы по организациям,
			// необходимы взять всех сотрудников одной организации и передать их в качестве отбора в отчет
			СписокСотрудников = Новый СписокЗначений;
			Для Каждого МестоРаботыДляОтбора Из МестаРаботыФизлица Цикл
				Если МестоРаботыДляОтбора.Организация <> МестоРаботы.Организация Тогда
					Продолжить;
				КонецЕсли;
				
				Если СписокСотрудников.НайтиПоЗначению(МестоРаботыДляОтбора.Сотрудник) = Неопределено Тогда
					СписокСотрудников.Добавить(МестоРаботыДляОтбора.Сотрудник);
				КонецЕсли;
			КонецЦикла;
			
			ФильтрСоответствия.Вставить("Работник", СписокСотрудников);
			ФильтрСоответствия.Вставить("ОтборРаботника", СписокСотрудников.Количество()>0);
			ФильтрСоответствия.Вставить("ВидСравненияРаботника", ВидСравнения.ВСписке);
			ОтчетУнифицированнаяФормаТ13.УстановитьФильтр(ФильтрСоответствия);
			
			
			УнифицированнаяФормаТ13Отчет = Новый ТабличныйДокумент;
			ОтчетУнифицированнаяФормаТ13.СформироватьОтчет(УнифицированнаяФормаТ13Отчет, "Табель");
			// Для вывода отчетов в отдельных окнах
			ДанныеТабличныхДокументовТ13.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),УнифицированнаяФормаТ13Отчет);
			
			// таким способом размещаются отчеты без форматов строк
			ОбластьПрямоугольная = УнифицированнаяФормаТ13Отчет.Область(1, 1, УнифицированнаяФормаТ13Отчет.ВысотаТаблицы, УнифицированнаяФормаТ13Отчет.ШиринаТаблицы);
			НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
			ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + УнифицированнаяФормаТ13Отчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
			// назначим ширину колонок у новой области формата строк
			Для Счетчик = 1 По УнифицированнаяФормаТ13Отчет.ШиринаТаблицы Цикл
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = УнифицированнаяФормаТ13Отчет.Область(1, Счетчик).ШиринаКолонки;
			КонецЦикла;
			ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			// СЗВ4
			// заголовок
			Область = Макет.ПолучитьОбласть("СЗВ4");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаСЗВ4.Имя;
			Область.Области.ИмяСЗВ.Текст =  ?(НачалоМесяца(Месяц) < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами(),"СЗВ-4","СЗВ-6");
			Область.Области.ГиперссылкаСЗВ4.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаСЗВ4Раскрыта);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			
			ОбластьСЗВ4 = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			СЗВ4Отчет = Новый ТабличныйДокумент;
			ВыводНаПечатьПоФизЛицу(СЗВ4Отчет, Месяц, ФизЛицо, МестоРаботы.Организация);
				
			// Для вывода отчетов в отдельных окнах
			ДанныеТабличныхДокументовСЗВ4.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="), СЗВ4Отчет);
				
			Если НачалоМесяца(Месяц) < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
				
				// таким способом размещаются отчеты без форматов строк
				ОбластьПрямоугольная = СЗВ4Отчет.Область(1, 1, СЗВ4Отчет.ВысотаТаблицы, СЗВ4Отчет.ШиринаТаблицы);
				НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + СЗВ4Отчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
				// назначим ширину колонок у новой области формата строк
				Для Счетчик = 1 По СЗВ4Отчет.ШиринаТаблицы Цикл
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = СЗВ4Отчет.Область(1, Счетчик).ШиринаКолонки;
				КонецЦикла;
				ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
				
			Иначе
				
				// таким способом размещаются отчеты с форматами строк
				ОбластьСтроки = СЗВ4Отчет.Область(1, , СЗВ4Отчет.ВысотаТаблицы, );
				ВыходнойДокумент.ВставитьОбласть(ОбластьСтроки, ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы + 1, , ВыходнойДокумент.ВысотаТаблицы + СЗВ4Отчет.ВысотаТаблицы, ));
			
			КонецЕсли;
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			Сч = Сч + 1;
			
		КонецЕсли;
	КонецЦикла;
	ВыходнойДокумент.ТолькоПросмотр = Истина;
	ВыходнойДокумент.Защита = Истина;
	
КонецПроцедуры

// получает сведения, которые требуются для отработки действий
// 
Процедура ПолучитьСведенияОНачислениях()
	Перем СвойствоИнфоПоОрганизации;
	
	// получим сведения о местах работы
	ПолучитьСведенияОМестахРаботы();
	
	ДанныеПоНачислениям.Очистить();
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		// просто наполняем соответствие организациями работника
		ИнфоПоОрганизации = Новый Структура(
		"Наименование,
		|НачислениеЗарплатыРаботникамОрганизаций,
		|НачислениеПоБольничномуЛисту,
		|НачислениеОтпускаРаботникамОрганизаций,
		|ЗарплатаКВыплатеОрганизаций,
		|ПремииРаботниковОрганизаций,
		|РегистрацияРазовыхНачисленийРаботниковОрганизаций", 
		МестоРаботы.ОбособленноеПодразделениеНаименование,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив);
		ДанныеПоНачислениям[МестоРаботы.ОбособленноеПодразделение] = ИнфоПоОрганизации;
		
	КонецЦикла;
	
	Запрос = Новый Запрос();
	
	// нужно проверить наличие документов начисления, больничных, отпусков, изменения плановых начислений, 
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	""НачислениеЗарплатыРаботникамОрганизаций"" КАК ВидДокумента,
	|	Начисления.Ссылка.Организация КАК Организация,
	|	Начисления.Ссылка КАК Документ,
	|	Начисления.Ссылка.Представление КАК Представление
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И Начисления.Ссылка.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеЗарплатыРаботникамОрганизаций.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""НачислениеЗарплатыРаботникамОрганизаций"",
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка,
	|	Начисления.Ссылка.Представление
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.ДополнительныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И Начисления.Ссылка.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеЗарплатыРаботникамОрганизаций.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""НачислениеПоБольничномуЛисту"",
	|	Больничный.Организация,
	|	Больничный.Ссылка,
	|	Больничный.Представление
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК Больничный
	|ГДЕ
	|	Больничный.Сотрудник.Физлицо = &Физлицо
	|	И Больничный.ПериодРегистрации = &Месяц
	|	И Больничный.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеПоБольничномуЛисту.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""НачислениеОтпускаРаботникамОрганизаций"",
	|	Отпуск.Организация,
	|	Отпуск.Ссылка,
	|	Отпуск.Представление
	|ИЗ
	|	Документ.НачислениеОтпускаРаботникамОрганизаций КАК Отпуск
	|ГДЕ
	|	Отпуск.Сотрудник.Физлицо = &Физлицо
	|	И Отпуск.ПериодРегистрации = &Месяц
	|	И Отпуск.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеОтпускаРаботникамОрганизаций.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ЗарплатаКВыплатеОрганизаций"",
	|	Зарплата.Ссылка.Организация,
	|	Зарплата.Ссылка,
	|	Зарплата.Ссылка.Представление
	|ИЗ
	|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК Зарплата
	|ГДЕ
	|	Зарплата.Физлицо = &Физлицо
	|	И Зарплата.Ссылка.ПериодРегистрации = &Месяц
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПремииРаботниковОрганизаций"",
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка,
	|	Начисления.Ссылка.Представление
	|ИЗ
	|	Документ.ПремииРаботниковОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И Начисления.Ссылка.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.ПремииРаботниковОрганизаций.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РегистрацияРазовыхНачисленийРаботниковОрганизаций"",
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка,
	|	Начисления.Ссылка.Представление
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И Начисления.Ссылка.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РегистрацияРазовыхНачисленийРаботниковОрганизаций"",
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка,
	|	Начисления.Ссылка.Представление
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник.Физлицо = &Физлицо
	|	И Начисления.Ссылка.ПериодРегистрации = &Месяц
	|	И Начисления.Ссылка.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ДанныеПоНачислениям[Выборка.Организация] = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		ДанныеПоНачислениям[Выборка.Организация].Свойство(Выборка.ВидДокумента, СвойствоИнфоПоОрганизации);
		СвойствоИнфоПоОрганизации.Добавить(Новый Структура("Документ, Представление", Выборка.Документ, Выборка.Представление));
	КонецЦикла;
	
КонецПроцедуры

// обработка основных действий раздела РАСЧЕТЫ, ЗАРПЛАТА
Процедура НадписьНачислениеЗарплатыНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.НачислениеЗарплатыРаботникамОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.НачислениеЗарплатыРаботникамОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Начислить по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьНачислениеЗарплаты);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		// это создание начислений для организации
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			// регистируем для каких подразделений уже создали начисления зарплаты
			ОбработанныеПодразделения = Новый Соответствие;
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				Если ОбработанныеПодразделения[МестоРаботы.Подразделение] = НеОпределено Тогда
					НовыйДокумент = Документы.НачислениеЗарплатыРаботникамОрганизаций.СоздатьДокумент();
					НовыйДокумент.Организация = МестоРаботы.ОбособленноеПодразделение;
					НовыйДокумент.ПериодРегистрации = Месяц;
					НовыйДокумент.ПодразделениеОрганизации = МестоРаботы.Подразделение;
					ОбработанныеПодразделения[МестоРаботы.Подразделение] = МестоРаботы.Подразделение;
					НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
					НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийНачислениеЗарплатыРаботникамОрганизаций.ПолныйРасчет;
					НовыйДокумент.ПериодНачисления = Перечисления.ПериодНачисленияЗарплаты.ТекущийМесяц;
					НовыйДокумент.ПериодНачисленияДатаНачала = НачалоМесяца(Месяц);
					НовыйДокумент.ПериодНачисленияДатаОкончания = КонецМесяца(Месяц);
					НовыйДокумент.Автозаполнение();
					Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
					Форма.Открыть();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьПремииНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ПремииРаботниковОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ПремииРаботниковОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Начислить премию по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьПремии);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ПремииРаботниковОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.ПериодРегистрации = Месяц;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.Начисления.Добавить();
		Строка.Физлицо = Физлицо;
		// для заполнения сотрудника перебираем рабочие места
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				Строка.Сотрудник = МестоРаботы.Сотрудник;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Строка.Авторасчет = Истина;
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьРазовоеНачислениеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.РегистрацияРазовыхНачисленийРаботниковОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.РегистрацияРазовыхНачисленийРаботниковОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Ввести разовое начисление по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьРазовоеНачисление);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.РегистрацияРазовыхНачисленийРаботниковОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.ПериодРегистрации = Месяц;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.ОсновныеНачисления.Добавить();
		Строка.Физлицо = Физлицо;
		// для заполнения сотрудника перебираем рабочие места
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				Строка.Сотрудник = МестоРаботы.Сотрудник;
				Строка.ПодразделениеОрганизации = МестоРаботы.Подразделение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Строка.Авторасчет = Истина;
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьВыплатаНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ЗарплатаКВыплатеОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ЗарплатаКВыплатеОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Выплатить зарплату по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьВыплата);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ЗарплатаКВыплатеОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.ПериодРегистрации = Месяц;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		
		НовыйДокумент.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
		НовыйДокумент.СпособВыплаты = Перечисления.СпособыВыплатыЗарплаты.ЧерезКассу;
		
		// Настройки расчета
		ЗарплатаКВыплатеОрганизацийСервис.ЗаполнитьПоНастройкам(НовыйДокумент);
		НовыйДокумент.ДнейНеВыплаты = 0;
		
		ФизЛица = Новый Массив;
		ФизЛица.Добавить(Физлицо);
		НовыйДокумент.ДобавитьРаботников(ФизЛица);
		НовыйДокумент.Рассчитать(ФизЛица);
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьБольничныйНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.НачислениеПоБольничномуЛисту Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.НачислениеПоБольничномуЛисту.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Ввести больничный лист по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьБольничный);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.НачислениеПоБольничномуЛисту.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.ПериодРегистрации = Месяц;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		НовыйДокумент.Физлицо = Физлицо;
		// для заполнения сотрудника перебираем рабочие места
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				НовыйДокумент.Сотрудник = МестоРаботы.Сотрудник;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьОтпускНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНачислениям Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.НачислениеОтпускаРаботникамОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.НачислениеОтпускаРаботникамОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Отправить в отпуск по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		СтрокаМеню = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьОтпуск);
		Если СтрокаМеню = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		Значение = СтрокаМеню.Значение;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.НачислениеОтпускаРаботникамОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.ПериодРегистрации = Месяц;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		НовыйДокумент.Физлицо = Физлицо;
		// для заполнения сотрудника перебираем рабочие места
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				НовыйДокумент.Сотрудник = МестоРаботы.Сотрудник;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего начисления
		Форма = Значение.ПолучитьФорму();
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

// обработка гиперссылок - действия по открытию форм в отдельных окнах
//
Процедура РасчетыЗарплатаВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Сред(Область.Имя, 4) = "ГиперссылкаРасчетныеЛисткиОрганизаций" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовРасчетныеЛистки.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Расчетный листок (" + Физлицо + ")");
	ИначеЕсли Сред(Область.Имя, 4) = "ГиперссылкаАнализНачисленийРаботникамОрганизаций" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовАнализНачислений.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Анализ начислений (" + Физлицо + ")");
	ИначеЕсли Сред(Область.Имя, 4) = "ГиперссылкаРегламентированнаяФорма1НДФЛ" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументов1НДФЛ.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Налоговая карточка (" + Физлицо + ")");
	ИначеЕсли Сред(Область.Имя, 4) = "ГиперссылкаУнифицированнаяФормаТ13" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовТ13.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Табель Т-13 (" + Физлицо + ")");
	ИначеЕсли Сред(Область.Имя, 4) = "ГиперссылкаСЗВ4" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовСЗВ4.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "СЗВ-4 (" + Физлицо + ")");
	КонецЕсли;
КонецПроцедуры

Процедура РасчетыЗарплатаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Если ТипЗнч(Расшифровка) = Тип("Соответствие") Тогда
		СтандартнаяОбработка = Ложь;
		Если Расшифровка.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		Если Расшифровка.Количество() = 1 Тогда
			Для Каждого ЭлементСоответствия Из Расшифровка Цикл
				ОткрытьЗначение(ЭлементСоответствия.Ключ);
				Возврат;
			КонецЦикла;
		КонецЕсли;
		СписокМеню = Новый СписокЗначений;
		Для Каждого ЭлементСоответствия Из Расшифровка Цикл
			СписокМеню.Добавить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение);
		КонецЦикла;
		Результат = ВыбратьИзМеню(СписокМеню);
		Если Результат = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		ОткрытьЗначение(Результат.Значение);
	КонецЕсли;
КонецПроцедуры

Процедура ДействиеНовыйДокумент(Кнопка)
	Форма = Документы[Кнопка.Имя].ПолучитьФормуНовогоДокумента(, ЭтаФорма);
	Форма.Открыть();
КонецПроцедуры

// Формирует фрагмент выходного файла по работнику и выводит печатную форму СЗВ4.
//
// Параметры:
//		ДокументРезультат	-	ТабличныйДокумент - табличный документ, куда быдет выведена печатная форма
//		ДатаАктуальности	-	дата - дата, на которую формируются данные
//		ФизЛицо				-	Справочники.ФизическиеЛица - физлицо, на которого формируются данные
//		Организация			-	Справочники.Организации - организация, по которой формируются данные
//
Процедура ВыводНаПечатьПоФизЛицу(ДокументРезультат, ДатаАктуальности, ФизЛицо, Организация)
	
	Если ДатаАктуальности < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
		
		ТипФормДокументов = "СЗВ-4-1";
		ТекстФайла = "";
		
		// Формирование фрагмента файла передачи В ПФР для печати формы "СЗВ-4-1"
		// Запрос по регистру ЗаписиОСтаже и СуммыВзносовДляСЗВ4
		Запрос = Новый запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиОСтаже.ФизЛицо КАК ФизЛицо,
		|	ЗаписиОСтаже.Год КАК Год,
		|	ЗаписиОСтаже.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	ЗаписиОСтаже.НомерОсновнойЗаписи КАК НомерОсновнойЗаписи,
		|	ЗаписиОСтаже.НомерДополнительнойЗаписи КАК НомерДополнительнойЗаписи,
		|	ЗаписиОСтаже.ДатаНачалаПериода КАК ДатаНачалаПериода,
		|	ЗаписиОСтаже.ДатаОкончанияПериода КАК ДатаОкончанияПериода,
		|	ЗаписиОСтаже.ТерриториальныеУсловия КАК ТерриториальныеУсловия,
		|	ЗаписиОСтаже.ТерриториальныеУсловия.Код КАК ТерриториальныеУсловияКод,
		|	ЗаписиОСтаже.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
		|	ЗаписиОСтаже.ОсобыеУсловияТруда.Код КАК ОсобыеУсловияТрудаКод,
		|	ЗаписиОСтаже.КодПозицииСписка КАК КодПозицииСписка,
		|	ЗаписиОСтаже.КодПозицииСписка.Код КАК КодПозицииСпискаКод,
		|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа КАК ОснованиеИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа.Код КАК ОснованиеИсчисляемогоСтажаКод,
		|	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа КАК ПервыйПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа КАК ВторойПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа КАК ТретийПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ОснованиеВыслугиЛет КАК ОснованиеВыслугиЛет,
		|	ЗаписиОСтаже.ОснованиеВыслугиЛет.Код КАК ОснованиеВыслугиЛетКод,
		|	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет КАК ПервыйПараметрВыслугиЛет,
		|	ЗаписиОСтаже.ВторойПараметрВыслугиЛет КАК ВторойПараметрВыслугиЛет,
		|	ЗаписиОСтаже.ТретийПараметрВыслугиЛет КАК ТретийПараметрВыслугиЛет,
		|	СуммыВзносовДляСЗВ4.НачисленоНаСтраховуюЧастьПФР,
		|	СуммыВзносовДляСЗВ4.НачисленоНаНакопительнуюЧастьПФР,
		|	ЕСТЬNULL(СуммыВзносовДляСЗВ4.ДнейБолезни, 0) КАК ДнейБолезни,
		|	ЕСТЬNULL(СуммыВзносовДляСЗВ4.ДнейОтпускаЗаСвойСчет, 0) КАК ДнейОтпускаЗаСвойСчет
		|ИЗ
		|	РегистрСведений.ЗаписиОСтажеДляСЗВ4 КАК ЗаписиОСтаже
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыВзносовДляСЗВ4 КАК СуммыВзносовДляСЗВ4
		|		ПО ЗаписиОСтаже.ФизЛицо = СуммыВзносовДляСЗВ4.ФизЛицо
		|			И ЗаписиОСтаже.Год = СуммыВзносовДляСЗВ4.Год
		|			И ЗаписиОСтаже.ОбособленноеПодразделение = СуммыВзносовДляСЗВ4.ОбособленноеПодразделение
		|ГДЕ
		|	ЗаписиОСтаже.ФизЛицо В(&ФизЛицо)
		|	И ЗаписиОСтаже.Год В(&Год)
		|	И ЗаписиОСтаже.ОбособленноеПодразделение В(&ОбособленноеПодразделение)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбособленноеПодразделение,
		|	Год,
		|	ФизЛицо,
		|	НомерОсновнойЗаписи,
		|	НомерДополнительнойЗаписи";
		Запрос.УстановитьПараметр("ФизЛицо" , ФизЛицо);
		Запрос.УстановитьПараметр("Год" , Год(ДатаАктуальности));
		Запрос.УстановитьПараметр("ОбособленноеПодразделение" , Организация);
		
		// Сбор данных и формирование фрагмента файла
		ВыборкаПоРаботникиОрганизации	= Запрос.Выполнить().Выбрать();
		Если ВыборкаПоРаботникиОрганизации.Следующий() Тогда
			
			ТипЗаписиИН = "ИН41";
			ДокументФорматЗаписей = Документы.СведенияОТрудовомСтажеИЗаработкеСЗВ4.ПолучитьМакет("ФорматЗаписей");
			ФорматЗаписиИН = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи(ТипЗаписиИН,ДокументФорматЗаписей);
			Фамилия		= "";
			Имя			= "";
			Отчество	= "";
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ФизЛицо.Наименование,Фамилия,Имя,Отчество);
			
			// адрес для информирования
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ФизЛицо",					ФизЛицо);
			Запрос.УстановитьПараметр("ДатаАктуальности",			ДатаАктуальности);
			Запрос.УстановитьПараметр("ВидАдресаДляИнформирования",	Справочники.ВидыКонтактнойИнформации.ИнформАдресФизЛица);
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	"","" + КонтактнаяИнформация.Поле1 + "","" + КонтактнаяИнформация.Поле2 + "","" + КонтактнаяИнформация.Поле3 + "","" + КонтактнаяИнформация.Поле4 + "","" + КонтактнаяИнформация.Поле5 + "","" + КонтактнаяИнформация.Поле6 + "","" + КонтактнаяИнформация.Поле7 + "","" + КонтактнаяИнформация.Поле8 + "","" + КонтактнаяИнформация.Поле9 + "","" + ВЫБОР
			|		КОГДА КонтактнаяИнформация.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
			|			ТОГДА ""дом""
			|		ИНАЧЕ ""владение""
			|	КОНЕЦ + "","" + ВЫБОР
			|		КОГДА КонтактнаяИнформация.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
			|			ТОГДА ""корпус""
			|		ИНАЧЕ ""строение""
			|		КОНЕЦ + "","" + ВЫБОР
			|	КОГДА КонтактнаяИнформация.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
			|		ТОГДА ""кв.""
			|		ИНАЧЕ ""оф.""
			|	КОНЕЦ КАК АдресИзРегистра
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|
			|ГДЕ
			|	КонтактнаяИнформация.Вид = &ВидАдресаДляИнформирования И
			|	КонтактнаяИнформация.Объект = &Физлицо";
			
			РезультатЗапроса	= Запрос.Выполнить();
			ВыборкаПоРаботнику	= РезультатЗапроса.Выбрать();
			АдресДляИнформирования = ?(ВыборкаПоРаботнику.Следующий(), ВыборкаПоРаботнику.АдресИзРегистра, "");
			Если СокрЛП(СтрЗаменить(АдресДляИнформирования, ",","")) <> "" Тогда
				АдресДляИнформирования = АдресДляИнформирования;
			Иначе	
				АдресДляИнформирования = "";
			КонецЕсли; 
			
			// Подготовка расчетных данных
			ЗаписьИН = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиИН);
			ЗаписьИН.ТипЗаписи							= ТипЗаписиИН;
			ЗаписьИН.НомерДокументаВПачке				= 1;
			ЗаписьИН.СтраховойНомер						= ФизЛицо.СтраховойНомерПФР;
			ЗаписьИН.Фамилия							= ВРЕГ(Фамилия);
			ЗаписьИН.Имя								= ВРЕГ(Имя);
			ЗаписьИН.Отчество							= ВРЕГ(Отчество);
			ЗаписьИН.РасчетныйПериодГод					= Год(ДатаАктуальности);
			ЗаписьИН.АдресДляИнформирования				= ВРЕГ(АдресДляИнформирования);
			ЗаписьИН.ДатаЗаполнения						= ДатаАктуальности;
			ЗаписьИН.НачисленоСтраховаяЧасть			= ВыборкаПоРаботникиОрганизации.НачисленоНаСтраховуюЧастьПФР;
			ЗаписьИН.НачисленоНакопительнаяЧасть		= ВыборкаПоРаботникиОрганизации.НачисленоНаНакопительнуюЧастьПФР;
			ЗаписьИН.ВременнаяНетрудоспособностьМесяцев	= Цел(ВыборкаПоРаботникиОрганизации.ДнейБолезни/30);
			ЗаписьИН.ВременнаяНетрудоспособностьДней	= ВыборкаПоРаботникиОрганизации.ДнейБолезни%30;
			ЗаписьИН.ОтпускМесяцев						= Цел(ВыборкаПоРаботникиОрганизации.ДнейОтпускаЗаСвойСчет/30);
			ЗаписьИН.ОтпускДней							= ВыборкаПоРаботникиОрганизации.ДнейОтпускаЗаСвойСчет%30;
			
			// Сбор данных о стаже
			ЧислоОсновныхЗаписейОСтаже = 0;					//Переменный для хранения строк о стаже работника (для формы СЗВ-4-1)
			СтрокиСтажРаботникаОрганизации = "";
			РаботникОрганизацииИмеетЛьготныйСтаж = ЛОЖЬ;	// Признак наличия у работника основания для льготного стажа
			Пока ВыборкаПоРаботникиОрганизации.СледующийПоЗначениюПоля("НомерОсновнойЗаписи")	Цикл
				
				ЧислоОсновныхЗаписейОСтаже = ЧислоОсновныхЗаписейОСтаже + 1; 
				
				//Берем пустую запись "Основная запись о стаже"
				ФорматЗаписиСТАЖ = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи("СТАЖ",ДокументФорматЗаписей);
				ЗаписьСТАЖ = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиСТАЖ);
				ЗаписьСТАЖ.ТипЗаписи = "СТАЖ";
				ЗаписьСТАЖ.НомерПериода = ВыборкаПоРаботникиОрганизации.НомерОсновнойЗаписи;
				ЗаписьСТАЖ.ДатаНачалаПериода = ВыборкаПоРаботникиОрганизации.ДатаНачалаПериода;
				ЗаписьСТАЖ.ДатаОкончанияПериода = ВыборкаПоРаботникиОрганизации.ДатаОкончанияПериода;
				ЗаписьСТАЖ.ОсобыеУсловияТрудаКод = ВыборкаПоРаботникиОрганизации.ОсобыеУсловияТрудаКод;
				ЗаписьСТАЖ.КодПозицииСписка = ВыборкаПоРаботникиОрганизации.КодПозицииСпискаКод;
				ЗаписьСТАЖ.ОснованиеИсчисляемогоСтажаКод = ВыборкаПоРаботникиОрганизации.ОснованиеИсчисляемогоСтажаКод;
				ЗаписьСТАЖ.ПервыйПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ПервыйПараметрИсчисляемогоСтажа;
				ЗаписьСТАЖ.ВторойПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ВторойПараметрИсчисляемогоСтажа;
				ЗаписьСТАЖ.ТретийПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ТретийПараметрИсчисляемогоСтажа;
				ЗаписьСТАЖ.ОснованиеВыслугиЛетКод = ВыборкаПоРаботникиОрганизации.ОснованиеВыслугиЛетКод;
				ЗаписьСТАЖ.ПервыйПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ПервыйПараметрВыслугиЛет;
				ЗаписьСТАЖ.ВторойПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ВторойПараметрВыслугиЛет;
				ЗаписьСТАЖ.ТретийПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ТретийПараметрВыслугиЛет;
				ЗаписьСТАЖ.ТерриториальныеУсловияКод = ВыборкаПоРаботникиОрганизации.ТерриториальныеУсловияКод;
				
				//Переменный для хранения дополнительных записей о стаже
				ЧислоДополнительныхЗаписейОСтаже = 0;
				СтрокиДополнительныеЗаписи = "";
				
				Пока ВыборкаПоРаботникиОрганизации.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи")	Цикл
					
					//В дополнительной записи с номером 0 находится информация об основной записи
					Если ВыборкаПоРаботникиОрганизации.НомерДополнительнойЗаписи = 0 Тогда
						Продолжить;
					КонецЕсли;	
					
					РаботникОрганизацииИмеетЛьготныйСтаж = Истина;
					ЧислоДополнительныхЗаписейОСтаже = ЧислоДополнительныхЗаписейОСтаже + 1;
					
					//Берем пустую запись "Запись о льготном стаже"
					ФорматЗаписиЛГОТ = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи("ЛГОТ",ДокументФорматЗаписей);
					ЗаписьЛГОТ = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиЛГОТ);
					ЗаписьЛГОТ.ТипЗаписи = "ЛГОТ";
					ЗаписьЛГОТ.НомерЗаписи = ВыборкаПоРаботникиОрганизации.НомерДополнительнойЗаписи;
					ЗаписьЛГОТ.ОсобыеУсловияТрудаКод = ВыборкаПоРаботникиОрганизации.ОсобыеУсловияТрудаКод;
					ЗаписьЛГОТ.КодПозицииСписка = ВыборкаПоРаботникиОрганизации.КодПозицииСпискаКод;
					ЗаписьЛГОТ.ОснованиеИсчисляемогоСтажаКод = ВыборкаПоРаботникиОрганизации.ОснованиеИсчисляемогоСтажаКод;
					ЗаписьЛГОТ.ПервыйПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ПервыйПараметрИсчисляемогоСтажа;
					ЗаписьЛГОТ.ВторойПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ВторойПараметрИсчисляемогоСтажа;
					ЗаписьЛГОТ.ТретийПараметрИсчисляемогоСтажа = ВыборкаПоРаботникиОрганизации.ТретийПараметрИсчисляемогоСтажа;
					ЗаписьЛГОТ.ОснованиеВыслугиЛетКод = ВыборкаПоРаботникиОрганизации.ОснованиеВыслугиЛетКод;
					ЗаписьЛГОТ.ПервыйПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ПервыйПараметрВыслугиЛет;
					ЗаписьЛГОТ.ВторойПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ВторойПараметрВыслугиЛет;
					ЗаписьЛГОТ.ТретийПараметрВыслугиЛет = ВыборкаПоРаботникиОрганизации.ТретийПараметрВыслугиЛет;
					ЗаписьЛГОТ.ТерриториальныеУсловияКод = ВыборкаПоРаботникиОрганизации.ТерриториальныеУсловияКод;
					
					//Сохраним запись ЛГОТ
					СтрокавФайл = ПроцедурыПерсонифицированногоУчета.ПолучитьСтрокуПоДаннымЗаписи(ЗаписьЛГОТ, ФорматЗаписиЛГОТ);
					СтрокиДополнительныеЗаписи = СтрокиДополнительныеЗаписи + СтрокавФайл + Символы.ПС;
					
				КонецЦикла;// Выборка по дополнительным записям о стаже
				
				ЗаписьСТАЖ.ЧислоДополнительныхЗаписей = ЧислоДополнительныхЗаписейОСтаже;
				
				//Сохраним запись СТАЖ
				СтрокавФайл = ПроцедурыПерсонифицированногоУчета.ПолучитьСтрокуПоДаннымЗаписи(ЗаписьСТАЖ,ФорматЗаписиСТАЖ);
				СтрокиСтажРаботникаОрганизации  = СтрокиСтажРаботникаОрганизации + СтрокавФайл + Символы.ПС;
				
				//Добавим записи ЛГОТ
				СтрокиСтажРаботникаОрганизации  = СтрокиСтажРаботникаОрганизации + ВРЕГ(СтрокиДополнительныеЗаписи);
				
			КонецЦикла;// Выборка по основным записям о стаже
			
			// Формирование файла
			ЗаписьИН.ЧислоОсновныхЗаписейОСтаже = ЧислоОсновныхЗаписейОСтаже;
			
			//	Добавляем в файл записи ИН41, СТАЖ И ЛГОТ
			СтрокавФайл = ПроцедурыПерсонифицированногоУчета.ПолучитьСтрокуПоДаннымЗаписи(ЗаписьИН,ФорматЗаписиИН);
			ТекстФайла	=	ТекстФайла	+	СтрокавФайл + Символы.ПС + ВРег(СтрокиСтажРаботникаОрганизации);
			
		КонецЕсли;
		
		// Вывод печатной формы
		Если ТекстФайла <> "" Тогда
			
			ОтветственныеЛица = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаАктуальности);
			
			Макет = Документы.СведенияОТрудовомСтажеИЗаработкеСЗВ4.ПолучитьМакет("ФормаСЗВ_4_1");
			ФорматЗаписиИН41 = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи("ИН41", ДокументФорматЗаписей);
			ФорматЗаписиСТАЖ = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи("СТАЖ", ДокументФорматЗаписей);
			ФорматЗаписиЛГОТ = ПроцедурыПерсонифицированногоУчета.ПолучитьФорматЗаписи("ЛГОТ", ДокументФорматЗаписей);
			
			// Рабочие области макета
			ОбластьШапка 	= Макет.ПолучитьОбласть("Шапка");
			ОбластьПодвал 	= Макет.ПолучитьОбласть("Подвал");
			ОбластьСтаж 	= Макет.ПолучитьОбласть("Стаж");
			
			// Заполнение данных шапки и подвала
			ОбластьШапка.Параметры.РегНомерПФР = Организация.РегистрационныйНомерПФР;
			ОбластьШапка.Параметры.НаименованиеОрганизации = Организация.Наименование;
			ОбластьШапка.Параметры.Год = Год(ДатаАктуальности);
			
			ОбластьПодвал.Параметры.ДолжностьРуководителя		 	= ОтветственныеЛица.РуководительДолжность;
			ОбластьПодвал.Параметры.РасшифровкаПодписиРуководителя	= ОтветственныеЛица.Руководитель;
			ОбластьПодвал.Параметры.Дата				 			= ПроцедурыПерсонифицированногоУчета.ДатаВОтчет(ДатаАктуальности);
			
			Н=1;//Номер строки первой записи ИН41
			
			//Читаем запись ИН41
			СтрокаФайла = СтрПолучитьСтроку(ТекстФайла, Н);
			Н= Н + 1;
			ЗаписьИН41 = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиИН41);
			СтрОшибка = "";
			Если НЕ ПроцедурыПерсонифицированногоУчета.РазложитьСтрокуВСтруктуруЗаписи(СтрокаФайла, "ИН41", ФорматЗаписиИН41, ЗаписьИН41, СтрОшибка) Тогда
				Возврат;
			КонецЕсли;	
			
			// Данные по физлицу
			ОбластьШапка.Параметры.СтраховойНомерПФР				= ЗаписьИН41.СтраховойНомер;
			ОбластьШапка.Параметры.ФИО							= "" + ЗаписьИН41.Фамилия + " " + ЗаписьИН41.Имя + " " + ЗаписьИН41.Отчество;
			ОбластьШапка.Параметры.Адрес						= ВРег(РегламентированнаяОтчетность.ПредставлениеАдреса(НРег(ЗаписьИН41.АдресДляИнформирования), 0, ""));
			ОбластьШапка.Параметры.НачисленоСтраховаяЧасть		= ЗаписьИН41.НачисленоСтраховаяЧасть;
			ОбластьШапка.Параметры.НачисленоНакопительнаяЧасть	= ЗаписьИН41.НачисленоНакопительнаяЧасть;
			ОбластьШапка.Параметры.НачисленоДополнительныйТариф	= ЗаписьИН41.НачисленоДополнительныйТариф;
			ОбластьШапка.Параметры.ДнейБолел					= ПроцедурыПерсонифицированногоУчета.ПредставлениеПериодаБолезниОтпуска(ЗаписьИН41.ВременнаяНетрудоспособностьМесяцев,ЗаписьИН41.ВременнаяНетрудоспособностьДней);
			ОбластьШапка.Параметры.ДнейГулял					= ПроцедурыПерсонифицированногоУчета.ПредставлениеПериодаБолезниОтпуска(ЗаписьИН41.ОтпускМесяцев, ЗаписьИН41.ОтпускДней);
			ДокументРезультат.Вывести(ОбластьШапка);
			
			//Цикл по основным записям о стаже
			Для НомерОснованойЗаписи = 1 по  ЗаписьИН41.ЧислоОсновныхЗаписейОСтаже цикл
				
				//Читаем запись СТАЖ
				СтрокаФайла = СтрПолучитьСтроку(ТекстФайла,Н);
				Н= Н + 1;
				ЗаписьСТАЖ = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиСТАЖ);
				СтрОшибка = "";
				Если НЕ ПроцедурыПерсонифицированногоУчета.РазложитьСтрокуВСтруктуруЗаписи(СтрокаФайла, "СТАЖ", ФорматЗаписиСТАЖ, ЗаписьСТАЖ, СтрОшибка) Тогда
					Возврат;
				КонецЕсли;	
				
				ОбластьСтаж.Параметры.НомерПериода				= НомерОснованойЗаписи;
				ОбластьСтаж.Параметры.ДатаНачалаПериода 		= ЗаписьСТАЖ.ДатаНачалаПериода;
				ОбластьСтаж.Параметры.ДатаОкончанияПериода		= ЗаписьСТАЖ.ДатаОкончанияПериода;
				ОбластьСтаж.Параметры.ТерриториальныеУсловияКод	= ЗаписьСТАЖ.ТерриториальныеУсловияКод;
				ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод		= ЗаписьСТАЖ.ОсобыеУсловияТрудаКод;
				ОбластьСтаж.Параметры.КодПозицииСписка			= ЗаписьСТАЖ.КодПозицииСписка;
				
				// Представление параметров ИТС
				ОбластьСтаж.Параметры.ОснованиеИТС				= ЗаписьСТАЖ.ОснованиеИсчисляемогоСтажаКод;
				ОбластьСтаж.Параметры.ДопИТС					= ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ЗаписьСТАЖ);
				
				// Представление параметров выслуги лет
				ОбластьСтаж.Параметры.ОснованиеВыслуги			= ЗаписьСТАЖ.ОснованиеВыслугиЛетКод;
				ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровПенсииЗаВыслугуЛет(ЗаписьСТАЖ, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
				
				ДокументРезультат.Вывести(ОбластьСтаж);
				
				//Цикл по дополнительным записям о стаже
				Для НомерДополнительнойЗаписи = 1 по  ЗаписьСТАЖ.ЧислоДополнительныхЗаписей цикл
					
					//Читаем запись ЛГОТ
					СтрокаФайла = СтрПолучитьСтроку(ТекстФайла,Н);
					Н= Н + 1;
					ЗаписьЛГОТ = ПроцедурыПерсонифицированногоУчета.ПолучитьСтруктуруЗаписи(ФорматЗаписиЛГОТ);
					СтрОшибка = "";
					Если НЕ ПроцедурыПерсонифицированногоУчета.РазложитьСтрокуВСтруктуруЗаписи(СтрокаФайла, "ЛГОТ", ФорматЗаписиЛГОТ, ЗаписьЛГОТ, СтрОшибка) Тогда
						Прервать;
					КонецЕсли;	
					
					// Выводим запись ЛГОТ
					ОбластьСтаж.Параметры.НомерПериода				= "";
					ОбластьСтаж.Параметры.ДатаНачалаПериода			= "";
					ОбластьСтаж.Параметры.ДатаОкончанияПериода		= "";
					ОбластьСтаж.Параметры.ТерриториальныеУсловияКод	= ЗаписьЛГОТ.ТерриториальныеУсловияКод;
					ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод		= ЗаписьЛГОТ.ОсобыеУсловияТрудаКод;
					ОбластьСтаж.Параметры.КодПозицииСписка			= ЗаписьЛГОТ.КодПозицииСписка;
					
					// Представление параметров ИТС
					ОбластьСтаж.Параметры.ОснованиеИТС				= ЗаписьЛГОТ.ОснованиеИсчисляемогоСтажаКод;
					ОбластьСтаж.Параметры.ДопИТС					= ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ЗаписьЛГОТ);
					
					// Представление параметров выслуги лет
					ОбластьСтаж.Параметры.ОснованиеВыслуги			= ЗаписьЛГОТ.ОснованиеВыслугиЛетКод;
					ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровПенсииЗаВыслугуЛет(ЗаписьЛГОТ, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
					
					ДокументРезультат.Вывести(ОбластьСтаж);
					
				КонецЦикла;//Цикл по дополнительным записям о стаже	
				
			КонецЦикла;// По основным записям о стаже	
			
			// Подвал формы
			ДокументРезультат.Вывести(ОбластьПодвал);
			
		КонецЕсли;
		
	Иначе
		
		РезультатЗапросаПоЗаписямСтажа = Неопределено;
		Отказ = Ложь;
		
		Если ДатаАктуальности < '20100701' Тогда
			ОтчетныйПериод = '20100101';
		ИначеЕсли ДатаАктуальности < '20110101' Тогда
			ОтчетныйПериод = '20100701'
		Иначе
			ОтчетныйПериод = НачалоКвартала(ДатаАктуальности);
		КонецЕсли;
		
		СтруктураДопДанных = Новый Структура;
		ОтветственныеЛица = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаАктуальности);
		СтруктураДопДанных.Вставить("Ссылка", Неопределено);
		СтруктураДопДанных.Вставить("ТипСведенийСЗВ", Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ);
		СтруктураДопДанных.Вставить("Дата", ДатаАктуальности);
		СтруктураДопДанных.Вставить("ОтчетныйПериод", ОтчетныйПериод); 
		СтруктураДопДанных.Вставить("Год", Год(ДатаАктуальности));
		СтруктураДопДанных.Вставить("НомерПачки", "00000");
		СтруктураДопДанных.Вставить("НачисленоСтраховая", 0);
		СтруктураДопДанных.Вставить("УплаченоСтраховая", 0);
		СтруктураДопДанных.Вставить("НачисленоНакопительная", 0);
		СтруктураДопДанных.Вставить("УплаченоНакопительная", 0);
		СтруктураДопДанных.Вставить("КатегорияЗастрахованныхЛиц", Перечисления.КатегорииЗастрахованныхЛицПФР.НР);
		СтруктураДопДанных.Вставить("ТипФормДокументов", "СЗВ-6-1");
		СтруктураДопДанных.Вставить("ФорматФайла", Перечисления.ФорматФайлаПФР.Версия07);
		СтруктураДопДанных.Вставить("РуководительДолжность", ОтветственныеЛица.РуководительДолжность);
		СтруктураДопДанных.Вставить("Руководитель", ОтветственныеЛица.Руководитель);
		СтруктураОрганизации = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Организация,"НаименованиеПолное, НаименованиеСокращенное, ИНН, КПП, КодПоОКПО, РегистрационныйНомерПФР, ЮрФизЛицо, ОГРН, НаименованиеОКОПФ");
		СтруктураДопДанных.Вставить("ОКПО", СтруктураОрганизации.КодПоОКПО);
		СтруктураДопДанных.Вставить("ОрганизацияРегистрационныйНомерПФР", СтруктураОрганизации.РегистрационныйНомерПФР);
		СтруктураДопДанных.Вставить("ОрганизацияЮрФизЛицо", СтруктураОрганизации.ЮрФизЛицо);
		СтруктураДопДанных.Вставить("ОрганизацияОГРН", СтруктураОрганизации.ОГРН);
		СтруктураДопДанных.Вставить("ОрганизацияНаименованиеОКОПФ", СтруктураОрганизации.НаименованиеОКОПФ);
		СтруктураДопДанных.Вставить("ОрганизацияИНН", СтруктураОрганизации.ИНН);
		СтруктураДопДанных.Вставить("ОрганизацияКПП", СтруктураОрганизации.КПП);
		СтруктураДопДанных.Вставить("ОрганизацияНаименованиеПолное", СтруктураОрганизации.НаименованиеПолное);
		СтруктураДопДанных.Вставить("ОрганизацияНаименованиеСокращенное", СтруктураОрганизации.НаименованиеСокращенное);
		
		ТаблицаФизлиц = Новый ТаблицаЗначений;
		ТаблицаФизлиц.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
		ТаблицаФизлиц.Колонки.Добавить("ФизЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ТаблицаФизлиц.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаФизлиц.Колонки.Добавить("КатегорияЗастрахованныхЛиц", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииЗастрахованныхЛицПФР"));
		ТаблицаФизлиц.Колонки.Добавить("ОтчетныйПериод", Новый ОписаниеТипов("Дата"));
		
		НомерСтроки = 1;
		Для каждого ЗначениеПеречисления Из Перечисления.КатегорииЗастрахованныхЛицПФР Цикл 		
			СтрокаПроверки = ТаблицаФизлиц.Добавить();
			СтрокаПроверки.ФизЛицо = ФизЛицо;
			СтрокаПроверки.Организация = Организация;
			СтрокаПроверки.ОтчетныйПериод = ОтчетныйПериод;
			СтрокаПроверки.КатегорияЗастрахованныхЛиц = ЗначениеПеречисления;
			СтрокаПроверки.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		МассивФизлиц = Новый Массив(1);
        МассивФизлиц[0] = ФизЛицо;
		
		ТаблицаВзносов = ПроцедурыПерсонифицированногоУчетаПолныеПрава.ДанныеОСтажеИВзносах(ДатаАктуальности, ОтчетныйПериод, Организация, МассивФизлиц); 
			
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиОСтаже.ФизЛицо КАК ФизЛицо,
		|	ЗаписиОСтаже.НомерСтроки,
		|	ЗаписиОСтаже.Организация,
		|	ЗаписиОСтаже.ОтчетныйПериод,
		|	ЗаписиОСтаже.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц
		|ПОМЕСТИТЬ ВТФизЛица
		|ИЗ
		|	&ТаблицаФизлиц КАК ЗаписиОСтаже
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	КатегорияЗастрахованныхЛиц";
		Запрос.УстановитьПараметр("ТаблицаФизлиц", ТаблицаФизлиц);
		Запрос.Выполнить();
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиОСтаже.ФизЛицо КАК ФизЛицо,
		|	ЗаписиОСтаже.АдресДляИнформирования,
		|	ЗаписиОСтаже.Фамилия,
		|	ЗаписиОСтаже.Имя,
		|	ЗаписиОСтаже.Отчество,
		|	ЗаписиОСтаже.НачисленоСтраховая,
		|	ЗаписиОСтаже.НачисленоНакопительная,
		|	ЗаписиОСтаже.УплаченоСтраховая,
		|	ЗаписиОСтаже.УплаченоНакопительная,
		|	ЗаписиОСтаже.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц,
		|	ЗаписиОСтаже.СтраховойНомерПФР КАК СтраховойНомерПФР
		|ПОМЕСТИТЬ ВТВзносы
		|ИЗ
		|	&ТаблицаФизлиц КАК ЗаписиОСтаже
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	КатегорияЗастрахованныхЛиц";
		Запрос.УстановитьПараметр("ТаблицаФизлиц", ТаблицаВзносов);
		Запрос.Выполнить();

		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизлицоПоКатегориям.ФизЛицо КАК ФизЛицо,
		|	ФизлицоПоКатегориям.Организация,
		|	ФизлицоПоКатегориям.НомерСтроки КАК НомерСтроки,
		|	ФизлицоПоКатегориям.ОтчетныйПериод,
		|	ФизлицоПоКатегориям.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц,
		|	Взносы.АдресДляИнформирования,
		|	Взносы.Фамилия,
		|	Взносы.Имя,
		|	Взносы.Отчество,
		|	Взносы.СтраховойНомерПФР,
		|	Взносы.НачисленоСтраховая,
		|	Взносы.НачисленоНакопительная,
		|	Взносы.УплаченоСтраховая,
		|	Взносы.УплаченоНакопительная,
		|	Взносы.Фамилия + "" "" + Взносы.Имя + "" "" + Взносы.Отчество КАК ФизЛицоНаименование,
		|	ЗаписиОСтаже.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	ЗаписиОСтаже.Год КАК Год,
		|	ЗаписиОСтаже.НомерОсновнойЗаписи КАК НомерОсновнойЗаписи,
		|	ЗаписиОСтаже.НомерДополнительнойЗаписи КАК НомерДополнительнойЗаписи,
		|	ЗаписиОСтаже.ДатаНачалаПериода КАК ДатаНачалаПериода,
		|	ЗаписиОСтаже.ДатаОкончанияПериода КАК ДатаОкончанияПериода,
		|	ЗаписиОСтаже.ТерриториальныеУсловия КАК ТерриториальныеУсловия,
		|	ЗаписиОСтаже.ТерриториальныеУсловия.Код КАК ТерриториальныеУсловияКод,
		|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ТерриториальныеУсловияСтавка,
		|	ЗаписиОСтаже.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
		|	ЗаписиОСтаже.ОсобыеУсловияТруда.Код КАК ОсобыеУсловияТрудаКод,
		|	ЗаписиОСтаже.КодПозицииСписка КАК КодПозицииСписка,
		|	ЗаписиОСтаже.КодПозицииСписка.Код КАК КодПозицииСпискаКод,
		|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа КАК ОснованиеИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа.Код КАК ОснованиеИсчисляемогоСтажаКод,
		|	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа КАК ПервыйПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа КАК ВторойПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа КАК ТретийПараметрИсчисляемогоСтажа,
		|	ЗаписиОСтаже.ОснованиеВыслугиЛет КАК ОснованиеВыслугиЛет,
		|	ЗаписиОСтаже.ОснованиеВыслугиЛет.Код КАК ОснованиеВыслугиЛетКод,
		|	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет КАК ПервыйПараметрВыслугиЛет,
		|	ЗаписиОСтаже.ВторойПараметрВыслугиЛет КАК ВторойПараметрВыслугиЛет,
		|	ЗаписиОСтаже.ТретийПараметрВыслугиЛет КАК ТретийПараметрВыслугиЛет
		|ИЗ
		|	ВТФизЛица КАК ФизлицоПоКатегориям
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносы КАК Взносы
		|		ПО ФизлицоПоКатегориям.ФизЛицо = Взносы.ФизЛицо
		|			И ФизлицоПоКатегориям.КатегорияЗастрахованныхЛиц = Взносы.КатегорияЗастрахованныхЛиц
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиОСтажеДляСЗВ4 КАК ЗаписиОСтаже
		|		ПО ФизлицоПоКатегориям.ФизЛицо = ЗаписиОСтаже.ФизЛицо
		|			И ФизлицоПоКатегориям.ОтчетныйПериод = ЗаписиОСтаже.ОтчетныйПериод
		|			И ФизлицоПоКатегориям.Организация = ЗаписиОСтаже.ОбособленноеПодразделение
		|			И ФизлицоПоКатегориям.КатегорияЗастрахованныхЛиц = ЗаписиОСтаже.КатегорияЗастрахованныхЛиц
		|ГДЕ
		|	(Взносы.ФизЛицо ЕСТЬ НЕ NULL 
		|			ИЛИ ЗаписиОСтаже.ФизЛицо ЕСТЬ НЕ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		РезультатЗапросаПоЗаписямСтажа = Запрос.Выполнить();
		
		ТекстФайла = ПроцедурыПерсонифицированногоУчета.ФайлСведенийОВзносахИСтаже(СтруктураДопДанных, РезультатЗапросаПоЗаписямСтажа, 1, "", Отказ);
		ДеревоПФР = ПроцедурыПерсонифицированногоУчета.ПреобразоватьВДеревоXML(ТекстФайла);
		
		Если ДеревоПФР = Неопределено Тогда
			Возврат  
		КонецЕсли;
		ДеревоПФР = ДеревоПФР.Строки.Найти("ФайлПФР","Имя");
		Если ДеревоПФР = Неопределено Тогда
			Возврат  
		КонецЕсли;
		
		СтрокаДереваПачкаВходящихДокументов = ДеревоПФР.Строки.Найти("ПачкаВходящихДокументов","Имя");
		Если СтрокаДереваПачкаВходящихДокументов <> Неопределено Тогда
			
			СтрокаДереваВходящаяОпись = СтрокаДереваПачкаВходящихДокументов.Строки.Найти("ВХОДЯЩАЯ_ОПИСЬ_ПО_СТРАХОВЫМ_ВЗНОСАМ", "Имя");
			Если СтрокаДереваВходящаяОпись <> Неопределено Тогда
				
				// Формы СЗВ-6-хх
				ДокументРезультат = Новый ТабличныйДокумент;
				Макет = Документы.СведенияОТрудовомСтажеИЗаработкеСЗВ4.ПолучитьМакет("ФормаСЗВ_6_1");
				
				// Рабочие области макета
				ОбластьШапка 	= Макет.ПолучитьОбласть("Шапка");
				ОбластьДокумент = Макет.ПолучитьОбласть("Строка");
				ОбластьПодвал 	= Макет.ПолучитьОбласть("Подвал");
				ОбластьСтаж = Макет.ПолучитьОбласть("Стаж");
				
				ИнформацияОПачке = ПроцедурыПерсонифицированногоУчета.ДанныеОписиАДВ_6_3(СтрокаДереваВходящаяОпись, СтруктураДопДанных);
				
				// копируем данные описи в остальные документы
				ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры,ИнформацияОПачке);
				ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры,ИнформацияОПачке);
				
				//Цикл по документам файла
				ВыводилиДанные = Ложь;
				НомерДокумента = 0;
				Для Каждого СтрокаДереваСЗВ6 Из СтрокаДереваПачкаВходящихДокументов.Строки Цикл
					
					//Читаем запись документа
					Если СтрокаДереваСЗВ6.Имя <> "СВЕДЕНИЯ_О_СТРАХОВЫХ_ВЗНОСАХ_И_СТРАХОВОМ_СТАЖЕ_ЗЛ" Тогда
						Продолжить;
					КонецЕсли;
					
					Для СчПараметр = 0 По ОбластьСтаж.Параметры.Количество() - 1 Цикл
						ОбластьСтаж.Параметры[СчПараметр] = ""
					КонецЦикла;
					
					Если Не ВыводилиДанные Тогда
						ВыводилиДанные = Истина;
					Иначе 
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ОбластьШапка.Параметры.КодКатегории = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваСЗВ6, "КодКатегории");
					ДокументРезультат.Вывести(ОбластьШапка);
				
					// Данные по физлицу
					ОбластьДокумент.Параметры.СтраховойНомерПФР = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваСЗВ6, "СтраховойНомер");
					СтрокаДереваФИО = СтрокаДереваСЗВ6.Строки.Найти("ФИО");
					Если СтрокаДереваФИО <> Неопределено Тогда
						ОбластьДокумент.Параметры.ФИО = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваФИО, "Фамилия") + " " + ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваФИО, "Имя") + " " + ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваФИО, "Отчество");
					Иначе
						ОбластьДокумент.Параметры.ФИО = "";
					КонецЕсли;
					
					СтрокаДереваАдресЗЛ = СтрокаДереваСЗВ6.Строки.Найти("АдресЗЛ");
					Если СтрокаДереваАдресЗЛ <> Неопределено Тогда
						ОбластьДокумент.Параметры.Адрес = ВРег(ПроцедурыПерсонифицированногоУчета.ПредставлениеАдресадляПФР(СтрокаДереваАдресЗЛ));
						Если ОбластьДокумент.Параметры.Адрес = " 0" Тогда
							ОбластьДокумент.Параметры.Адрес = "";
						КонецЕсли;
					Иначе
						ОбластьДокумент.Параметры.Адрес = "";
					КонецЕсли;
					
					СтрокаДереваВзносы = СтрокаДереваСЗВ6.Строки.Найти("СуммаВзносовНаСтраховую");
					Если СтрокаДереваВзносы <> Неопределено Тогда
						ОбластьДокумент.Параметры.НачисленоСтраховая = Формат(Число(ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВзносы, "Начислено")), "ЧДЦ=2");
						ОбластьДокумент.Параметры.УплаченоСтраховая = Формат(Число(ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВзносы, "Уплачено")), "ЧДЦ=2");
					Иначе
						ОбластьДокумент.Параметры.НачисленоСтраховая = "";
						ОбластьДокумент.Параметры.УплаченоСтраховая = "";
					КонецЕсли;
					
					СтрокаДереваВзносы = СтрокаДереваСЗВ6.Строки.Найти("СуммаВзносовНаНакопительную");
					Если СтрокаДереваВзносы <> Неопределено Тогда
						ОбластьДокумент.Параметры.НачисленоНакопительная = Формат(Число(ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВзносы, "Начислено")), "ЧДЦ=2");
						ОбластьДокумент.Параметры.УплаченоНакопительная = Формат(Число(ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВзносы, "Уплачено")), "ЧДЦ=2");
					Иначе
						ОбластьДокумент.Параметры.НачисленоНакопительная = "";
						ОбластьДокумент.Параметры.УплаченоНакопительная = "";
					КонецЕсли;
					
					ДокументРезультат.Вывести(ОбластьДокумент);
					НомерДокумента = 0;
					
					//Цикл по основным записям о стаже
					СтрокиДереваСтажевыйПериод = СтрокаДереваСЗВ6.Строки.НайтиСтроки(Новый Структура("Имя","СтажевыйПериод"));
					Если СтрокиДереваСтажевыйПериод.Количество() > 0 Тогда
						Для Каждого СтрокаДереваПериодРаботы Из СтрокиДереваСтажевыйПериод Цикл
							
							НомерДокумента = НомерДокумента + 1;
							
							Для СчПараметр = 0 По ОбластьСтаж.Параметры.Количество() - 1 Цикл
								ОбластьСтаж.Параметры[СчПараметр] = ""
							КонецЦикла;
							ОбластьСтаж.Параметры.НомерСтроки = НомерДокумента;
							ОбластьСтаж.Параметры.ДатаНачалаПериода 	= ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваПериодРаботы, "ДатаНачалаПериода");
							ОбластьСтаж.Параметры.ДатаОкончанияПериода 	= ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваПериодРаботы, "ДатаКонцаПериода");
							СтрокиДереваОсобенностиУчета = СтрокаДереваПериодРаботы.Строки.НайтиСтроки(Новый Структура("Имя","ЛьготныйСтаж"));
							Если СтрокиДереваОсобенностиУчета.Количество() > 0 Тогда
								Для Каждого СтрокаДереваОсобенностиУчета Из СтрокиДереваОсобенностиУчета Цикл
									
									Если ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваОсобенностиУчета, "НомерСтроки") > "1" Тогда
										ОбластьСтаж.Параметры.НомерСтроки 			= "";
										ОбластьСтаж.Параметры.ДатаНачалаПериода 	= "";
										ОбластьСтаж.Параметры.ДатаОкончанияПериода 	= "";
									КонецЕсли;
									
									СтрокаДереваОсобенностиУчета = СтрокаДереваОсобенностиУчета.Строки.Найти("ОсобенностиУчета");
									
									ЗаписьТУ = Новый Структура("Коэффициент,ПервыйПараметрИсчисляемогоСтажа,ВторойПараметрИсчисляемогоСтажа",0,0,0);
									
									СтрокаДереваТерриториальныеУсловия = СтрокаДереваОсобенностиУчета.Строки.Найти("ТерриториальныеУсловия");
									Если СтрокаДереваТерриториальныеУсловия <> Неопределено Тогда
										ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваТерриториальныеУсловия, "ОснованиеТУ");
										ЗаписьТУ.Вставить("Коэффициент", ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваТерриториальныеУсловия, "Коэффициент"));
									Иначе
										ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = "";
										ЗаписьТУ.Вставить("Коэффициент", 0);
									КонецЕсли;
									
									СтрокаДереваОсобыеУсловияТруда = СтрокаДереваОсобенностиУчета.Строки.Найти("ОсобыеУсловияТруда");
									Если СтрокаДереваОсобыеУсловияТруда <> Неопределено Тогда
										ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваОсобыеУсловияТруда, "ОснованиеОУТ");
										ОбластьСтаж.Параметры.КодПозицииСписка = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваОсобыеУсловияТруда, "ПозицияСписка");
									Иначе
										ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = "";
										ОбластьСтаж.Параметры.КодПозицииСписка = "";
									КонецЕсли;
									
									ЗаписьЛгот = Новый Структура("ОснованиеИсчисляемогоСтажаКод,ПервыйПараметрИсчисляемогоСтажа,ВторойПараметрИсчисляемогоСтажа,ТретийПараметрИсчисляемогоСтажа","",0,0,"");
									
									СтрокаДереваДекретДети = СтрокаДереваОсобенностиУчета.Строки.Найти("ДекретДети");
									Если СтрокаДереваДекретДети <> Неопределено Тогда
										ЗаписьЛгот.Вставить("ТретийПараметрИсчисляемогоСтажа", СтрокаДереваДекретДети.Значение);
									КонецЕсли;
									
									СтрокаДереваИсчисляемыйСтаж = СтрокаДереваОсобенностиУчета.Строки.Найти("ИсчисляемыйСтаж");
									Если СтрокаДереваИсчисляемыйСтаж <> Неопределено Тогда
										
										// Представление параметров ИТС
										ОбластьСтаж.Параметры.ОснованиеИТС = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваИсчисляемыйСтаж, "ОснованиеИС");
										
										СтрокаДереваВыработкаВЧасах = СтрокаДереваИсчисляемыйСтаж.Строки.Найти("ВыработкаВчасах");
										СтрокаДереваВыработкаКалендарная = СтрокаДереваИсчисляемыйСтаж.Строки.Найти("ВыработкаКалендарная");
										ПервыйПараметрИсчисляемогоСтажа = 0;
										ВторойПараметрИсчисляемогоСтажа = 0;
										Если СтрокаДереваВыработкаВЧасах <> Неопределено Тогда
											ПервыйПараметрИсчисляемогоСтажа = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаВЧасах, "Часы");
											ВторойПараметрИсчисляемогоСтажа = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаВЧасах, "Минуты");
										ИначеЕсли СтрокаДереваВыработкаКалендарная <> Неопределено Тогда
											ПервыйПараметрИсчисляемогоСтажа = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаКалендарная, "ВсеМесяцы");
											ВторойПараметрИсчисляемогоСтажа = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаКалендарная, "ВсеДни");
										КонецЕсли;
										
										ЗаписьЛгот.Вставить("ОснованиеИсчисляемогоСтажаКод", ОбластьСтаж.Параметры.ОснованиеИТС);
										
										Если ЗначениеЗаполнено(ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод) Или ЗначениеЗаполнено(ОбластьСтаж.Параметры.ОснованиеИТС) Тогда
											ЗаписьЛгот.Вставить("ПервыйПараметрИсчисляемогоСтажа", Число(ПервыйПараметрИсчисляемогоСтажа));
											ЗаписьЛгот.Вставить("ВторойПараметрИсчисляемогоСтажа", Число(ВторойПараметрИсчисляемогоСтажа));
										Иначе
											ЗаписьТУ.Вставить("ПервыйПараметрИсчисляемогоСтажа", Число(ПервыйПараметрИсчисляемогоСтажа));
											ЗаписьТУ.Вставить("ВторойПараметрИсчисляемогоСтажа", Число(ВторойПараметрИсчисляемогоСтажа));
										КонецЕсли;
										
									КонецЕсли;
									
									ОбластьСтаж.Параметры.ДопИТС = ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ЗаписьЛГОТ);
									ОбластьСтаж.Параметры.ДопТУ = ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровТерриториальныхУсловий(ЗаписьТУ);
									
									СтрокаДереваВыслугаЛет = СтрокаДереваОсобенностиУчета.Строки.Найти("ВыслугаЛет");
									Если СтрокаДереваВыслугаЛет <> Неопределено Тогда
										
										// Представление параметров выслуги лет
										ОбластьСтаж.Параметры.ОснованиеВыслуги = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыслугаЛет, "ОснованиеВЛ");
										
										
										СтрокаДереваВыработкаВЧасах = СтрокаДереваВыслугаЛет.Строки.Найти("ВыработкаВчасах");
										СтрокаДереваВыработкаКалендарная = СтрокаДереваВыслугаЛет.Строки.Найти("ВыработкаКалендарная");
										
										Если СтрокаДереваВыработкаВЧасах <> Неопределено Тогда
											ПервыйПараметрВыслугиЛет = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаВЧасах, "Часы");
											ВторойПараметрВыслугиЛет = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаВЧасах, "Минуты");
										ИначеЕсли СтрокаДереваВыработкаКалендарная <> Неопределено Тогда
											ПервыйПараметрВыслугиЛет = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаКалендарная, "ВсеМесяцы");
											ВторойПараметрВыслугиЛет = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыработкаКалендарная, "ВсеДни");
										Иначе
											ПервыйПараметрВыслугиЛет = 0;
											ВторойПараметрВыслугиЛет = 0;
										КонецЕсли;
										ТретийПараметрВыслугиЛет = ПроцедурыПерсонифицированногоУчета.ИзвлечьДанное(СтрокаДереваВыслугаЛет, "ДоляСтавки");
										ЗаписьЛгот = Новый Структура;
										ЗаписьЛгот.Вставить("ОснованиеВыслугиЛетКод",ОбластьСтаж.Параметры.ОснованиеВыслуги);
										ЗаписьЛгот.Вставить("ПервыйПараметрВыслугиЛет",Число(ПервыйПараметрВыслугиЛет));
										ЗаписьЛгот.Вставить("ВторойПараметрВыслугиЛет",Число(ВторойПараметрВыслугиЛет));
										ЗаписьЛгот.Вставить("ТретийПараметрВыслугиЛет",Число(ТретийПараметрВыслугиЛет));
										
										
										ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровПенсииЗаВыслугуЛет(ЗаписьЛГОТ, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
										
									КонецЕсли;
									
									ДокументРезультат.Вывести(ОбластьСтаж);
									
								КонецЦикла;
							Иначе
								ДокументРезультат.Вывести(ОбластьСтаж);
							КонецЕсли;
							
						КонецЦикла;
					Иначе
						НомерДокумента = НомерДокумента + 1;
						ОбластьСтаж.Параметры.НомерСтроки = НомерДокумента;
						ДокументРезультат.Вывести(ОбластьСтаж);
					КонецЕсли; // Записи о стаже
					
					// Подвал формы на одного физлица
					ДокументРезультат.Вывести(ОбластьПодвал);
					
				КонецЦикла; // по документам
				
			КонецЕсли;
			
		КонецЕсли;
	
		
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ  КАДРОВЫЕ ДАННЫЕ

// формирует раздел
Процедура СформироватьДанныеКадровыеДанные()
	
	Если мДанныеКадровыеДанныеСформированы Тогда
		Возврат;
	КонецЕсли;
	мДанныеКадровыеДанныеСформированы = Истина;
	
	// обновить динамические списки
	УчетКадровОрганизаций.Обновить();
	
	// Сбор данных о состоянии работника в организациях.
	ПолучитьМестаРаботыИСостоянияРаботника();
	
	ПолучитьКадровыеДанные();
	
	// настройка надписей раздела Действия и документы
	ЭлементыФормы.НадписьПриемНаРаботу.Заголовок = "Принять на работу";
	ЭлементыФормы.НадписьКадровоеПеремещение.Заголовок = "Переместить";
	ЭлементыФормы.НадписьУвольнение.Заголовок = "Уволить";
	ЭлементыФормы.НадписьАнкетаЗастрахованногоЛица.Заголовок = "Отправить анкету в ПФР";
	ЭлементыФормы.НадписьЗаявлениеОбОбмене.Заголовок = "Отправить заявление в ПФР";
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ПриемНаРаботуВОрганизацию Цикл
			ЭлементыФормы.НадписьПриемНаРаботу.Заголовок = "Прием на работу";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.КадровоеПеремещениеОрганизаций Цикл
			ЭлементыФормы.НадписьКадровоеПеремещение.Заголовок = "Кадровое перемещение";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.УвольнениеИзОрганизаций Цикл
			ЭлементыФормы.НадписьУвольнение.Заголовок = "Увольнение";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.АнкетаЗастрахованногоЛица Цикл
			ЭлементыФормы.НадписьАнкетаЗастрахованногоЛица.Заголовок = "Анкета застрахованного лица";
			Прервать;
		КонецЦикла;
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства Цикл
			ЭлементыФормы.НадписьЗаявлениеОбОбмене.Заголовок = "Заявление об обмене...(в ПФР)";
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	// Вывод данных в табличный документ
	ВыходнойДокумент = ЭлементыФормы.КадровыеДанные;
	
	// восстановление раскрытых группировок отчета
	ГруппировкаТ2Раскрыта = Ложь;
	Если ОбластьТ2Организаций <> НеОпределено и ОбластьТ2Организаций.Видимость Тогда
		ГруппировкаТ2Раскрыта = Истина;
	КонецЕсли;
	ВыходнойДокумент.Очистить();
	Макет = ПолучитьМакет("КадровыеДанные");
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьФизлицо = Макет.ПолучитьОбласть("Физлицо");
	ОбластьФизлицо.Параметры.Физлицо = Физлицо;
	ВыходнойДокумент.Вывести(ОбластьФизлицо);
	
	Если МестаРаботыИСостоянияФизлица.Количество() <> 0 Тогда
		
		Область = Макет.ПолучитьОбласть("МестаРаботыЗаголовок");
		ВыходнойДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("МестаРаботыСтрока");
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			
			Область.Параметры.Заполнить(МестоРаботы);
			ВыходнойДокумент.Вывести(Область);
			
		КонецЦикла;
		
		// Поиск не проведенных документов
		РезультатВыборки = НеПроведенныеКадровыеДокументы();
		
		Если НЕ РезультатВыборки.Пустой() Тогда
			
			Область = Макет.ПолучитьОбласть("ДокументыНеПроведены");
			Область.Параметры.ТекстДокументыНеПроведены = "ВНИМАНИЕ! Обнаружены не проведенные кадровые документы!";
			ВыходнойДокумент.Вывести(Область);
			
		КонецЕсли; 
		
		// Отчеты
		ОтчетТ2Организаций = Отчеты.УнифицированнаяФормаТ2.Создать();
		ОбработанныеСотрудники = Новый Соответствие;
		Сч = 1;
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			// Отчеты выводятся только по регистру РаботникиОрганизаций.
			Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
				
				Если ОбработанныеСотрудники[МестоРаботы.Сотрудник] = НеОпределено Тогда
					
					ОбработанныеСотрудники[МестоРаботы.Сотрудник] = Истина;
					Область = Макет.ПолучитьОбласть("Организация");
					Область.Параметры.ОрганизацияНаименование = МестоРаботы.СотрудникНаименование;
					Область.Параметры.Организация = МестоРаботы.Сотрудник;
					ВыходнойДокумент.Вывести(Область);
					ВыходнойДокумент.Вывести(ПустаяСтрока);
					
					Область = Макет.ПолучитьОбласть("ОтчетТ2");
					ИмяЯчейкиОтчета = Область.Области.ГиперссылкаТ2.Имя;
					Область.Области.ГиперссылкаТ2.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
					ВыходнойДокумент.Вывести(ПустаяСтрока);
					ВыходнойДокумент.Вывести(Область);
					
					// начнем группировку
					ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаТ2Раскрыта);
					ВыходнойДокумент.Вывести(ПустаяСтрока);
					ОбластьТ2Организаций = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
					
					ТекущаяДата = ОбщегоНазначения.ПолучитьРабочуюДату();
					
					ТиповыеОтчеты.УстановитьПараметр(ОтчетТ2Организаций.КомпоновщикНастроек, "Период",       КонецГода(ТекущаяДата));
					ТиповыеОтчеты.УстановитьПараметр(ОтчетТ2Организаций.КомпоновщикНастроек, "КонецПериода", КонецГода(ТекущаяДата));
					ТиповыеОтчеты.ДобавитьОтбор(ОтчетТ2Организаций.КомпоновщикНастроек,      "Сотрудник",    МестоРаботы.Сотрудник);
					
					Т2ОрганизацийОтчет = Новый ТабличныйДокумент;
					ОтчетТ2Организаций.СформироватьОтчет(Т2ОрганизацийОтчет, Истина);
					ТиповыеОтчеты.УдалитьОтбор(ОтчетТ2Организаций.КомпоновщикНастроек,"Сотрудник");
					
					// Для вывода отчетов в отдельных окнах
					ДанныеТабличныхДокументовТ2.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),Т2ОрганизацийОтчет);
					
					// таким способом размещаются отчеты без форматов строк
					
					НачалоТаблицы = 1;
					КонецТаблицы = 64;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					НачалоТаблицы = 65;
					КонецТаблицы = 122;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					НачалоТаблицы = 123;
					КонецТаблицы = 177;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					НачалоТаблицы = 178;
					КонецТаблицы = 192;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					НачалоТаблицы = 193;
					КонецТаблицы = 209;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					НачалоТаблицы = 210;
					КонецТаблицы = Т2ОрганизацийОтчет.ВысотаТаблицы;
					ВысотаТаблицы = КонецТаблицы - НачалоТаблицы + 1;
					ОбластьПрямоугольная = Т2ОрганизацийОтчет.Область(НачалоТаблицы, 1, КонецТаблицы, Т2ОрганизацийОтчет.ШиринаТаблицы);
					НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
					// назначим ширину колонок у новой области формата строк
					Для Счетчик = 1 По Т2ОрганизацийОтчет.ШиринаТаблицы Цикл
						ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = Т2ОрганизацийОтчет.Область(1, Счетчик).ШиринаКолонки;
					КонецЦикла;
					ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
					
					ВыходнойДокумент.ЗакончитьГруппуСтрок();
					
					Сч = Сч + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	ВыходнойДокумент.ТолькоПросмотр = Истина;
	ВыходнойДокумент.Защита = Истина;
	
КонецПроцедуры

// получает сведения, которые требуются для отработки действий
// 
Процедура ПолучитьКадровыеДанные()
	
	Перем СвойствоИнфоПоОрганизации;
	
	КадровыеДанные.Очистить();
	КадровыеДанныеПриемНаРаботу.Очистить();
	// Формирование КадровыДанных по местам работы работника
	Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
		
		Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
			
			ИнфоПоКадрамОрганизации = Новый Структура(
			"Наименование,
			|ПриемНаРаботуВОрганизацию,
			|КадровоеПеремещениеОрганизаций,
			|ОтпускаОрганизаций,
			|КомандировкиОрганизаций,
			|УвольнениеИзОрганизаций, 
			|АнкетаЗастрахованногоЛица,
			|ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства", 
			МестоРаботы.ОбособленноеПодразделениеНаименование,
			Новый Массив,
			Новый Массив,
			Новый Массив,
			Новый Массив,
			Новый Массив,
			Новый Массив,
			Новый Массив);
			КадровыеДанные[МестоРаботы.ОбособленноеПодразделение] = ИнфоПоКадрамОрганизации;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Формирование КадровыДанных по всем организациям (для формирования списка документов "Прием на работу в организиацию").
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка,
	|	Организации.Наименование
	|ИЗ
	|	Справочник.Организации КАК Организации");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ИнфоПоКадрамОрганизации = Новый Структура(
		"Наименование,
		|ПриемНаРаботуВОрганизацию,
		|КадровоеПеремещениеОрганизаций,
		|ОтпускаОрганизаций,
		|КомандировкиОрганизаций,
		|УвольнениеИзОрганизаций, 
		|АнкетаЗастрахованногоЛица,
		|ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства", 
		Выборка.Наименование,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив,
		Новый Массив);
		КадровыеДанныеПриемНаРаботу[Выборка.Ссылка] = ИнфоПоКадрамОрганизации;
	КонецЦикла;
	
	Если МестаРаботыИСостоянияФизлица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сведения.ВидДокумента,
	|	Сведения.Организация,
	|	Сведения.Представление,
	|	Сведения.Документ
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		""КадровоеПеремещениеОрганизаций"" КАК ВидДокумента,
	|		КадровоеПеремещениеОрганизацийРаботникиОрганизации.Ссылка.Организация КАК Организация,
	|		КадровоеПеремещениеОрганизацийРаботникиОрганизации.Ссылка.Ссылка КАК Документ,
	|		КадровоеПеремещениеОрганизацийРаботникиОрганизации.Ссылка.Представление КАК Представление
	|	ИЗ
	|		Документ.КадровоеПеремещениеОрганизаций.РаботникиОрганизации КАК КадровоеПеремещениеОрганизацийРаботникиОрганизации
	|	ГДЕ
	|		КадровоеПеремещениеОрганизацийРаботникиОрганизации.Сотрудник.ФизЛицо = &ФизЛицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ 
	|		""ПриемНаРаботуВОрганизацию"",
	|		ПриемНаРаботуВОрганизациюРаботникиОрганизации.Ссылка.Организация,
	|		ПриемНаРаботуВОрганизациюРаботникиОрганизации.Ссылка,
	|		ПриемНаРаботуВОрганизациюРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПриемНаРаботуВОрганизациюРаботникиОрганизации
	|	ГДЕ
	|		ПриемНаРаботуВОрганизациюРаботникиОрганизации.Сотрудник.ФизЛицо = &ФизЛицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ 
	|		""УвольнениеИзОрганизаций"",
	|		УвольнениеИзОрганизацийРаботникиОрганизации.Ссылка.Организация,
	|		УвольнениеИзОрганизацийРаботникиОрганизации.Ссылка,
	|		УвольнениеИзОрганизацийРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольнениеИзОрганизацийРаботникиОрганизации
	|	ГДЕ
	|		УвольнениеИзОрганизацийРаботникиОрганизации.Сотрудник.Физлицо = &Физлицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		""КомандировкиОрганизаций"",
	|		КомандировкиОрганизацийРаботникиОрганизации.Ссылка.Организация,
	|		КомандировкиОрганизацийРаботникиОрганизации.Ссылка,
	|		КомандировкиОрганизацийРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК КомандировкиОрганизацийРаботникиОрганизации
	|	ГДЕ
	|		КомандировкиОрганизацийРаботникиОрганизации.Сотрудник.ФизЛицо = &ФизЛицо
	|		И КомандировкиОрганизацийРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		""ОтпускаОрганизаций"",
	|		ОтпускаОрганизацийРаботникиОрганизации.Ссылка.Организация,
	|		ОтпускаОрганизацийРаботникиОрганизации.Ссылка,
	|		ОтпускаОрганизацийРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.ОтпускаОрганизаций.РаботникиОрганизации КАК ОтпускаОрганизацийРаботникиОрганизации
	|	ГДЕ
	|		ОтпускаОрганизацийРаботникиОрганизации.Сотрудник.ФизЛицо = &ФизЛицо
	|		И ОтпускаОрганизацийРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		""АнкетаЗастрахованногоЛица"",
	|		АнкетаЗастрахованногоЛицаРаботникиОрганизации.Ссылка.Организация,
	|		АнкетаЗастрахованногоЛицаРаботникиОрганизации.Ссылка,
	|		АнкетаЗастрахованногоЛицаРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.АнкетаЗастрахованногоЛица.РаботникиОрганизации КАК АнкетаЗастрахованногоЛицаРаботникиОрганизации
	|	ГДЕ
	|		АнкетаЗастрахованногоЛицаРаботникиОрганизации.ФизЛицо = &ФизЛицо
	|		И АнкетаЗастрахованногоЛицаРаботникиОрганизации.Ссылка.Дата МЕЖДУ &Начало И &Конец
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		""ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства"",
	|		ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации.Ссылка.Организация,
	|		ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации.Ссылка,
	|		ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации.Ссылка.Представление
	|	ИЗ
	|		Документ.ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства.РаботникиОрганизации КАК ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации
	|	ГДЕ
	|		ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации.ФизЛицо = &ФизЛицо
	|		И ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельстваРаботникиОрганизации.Ссылка.Дата МЕЖДУ &Начало И &Конец) КАК Сведения";
	
	Запрос.УстановитьПараметр("ФизЛицо", Физлицо);
	Запрос.УстановитьПараметр("Начало", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("Конец", КонецМесяца(Месяц));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если КадровыеДанные[Выборка.Организация] = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		КадровыеДанные[Выборка.Организация].Свойство(Выборка.ВидДокумента, СвойствоИнфоПоОрганизации);
		СвойствоИнфоПоОрганизации.Добавить(Новый Структура("Документ, Представление", Выборка.Документ, Выборка.Представление));
		Если КадровыеДанныеПриемНаРаботу[Выборка.Организация] = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		КадровыеДанныеПриемНаРаботу[Выборка.Организация].Свойство(Выборка.ВидДокумента, СвойствоИнфоПоОрганизации);
		СвойствоИнфоПоОрганизации.Добавить(Новый Структура("Документ, Представление", Выборка.Документ, Выборка.Представление));
	КонецЦикла;
	
КонецПроцедуры

// Формирует список непроведенных кадровых документов
Функция НеПроведенныеКадровыеДокументы()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.ВозвратНаРаботуОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратНаРаботуОрганизаций КАК ВозвратНаРаботуОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = ВозвратНаРаботуОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаВозврата МЕЖДУ &Начало И &Конец
	|	И (НЕ ВозвратНаРаботуОрганизаций.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КомандировкиОрганизаций КАК КомандировкиОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = КомандировкиОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	И (НЕ КомандировкиОрганизаций.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.ОтпускаОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтпускаОрганизаций КАК ОтпускаОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = ОтпускаОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	И (НЕ ОтпускаОрганизаций.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.НеявкиИБолезниОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НеявкиИБолезниОрганизаций КАК НеявкиИБолезниОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = НеявкиИБолезниОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	И (НЕ НеявкиИБолезниОрганизаций.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуВОрганизацию КАК ПриемНаРаботуВОрганизацию
	|		ПО ТЧРаботникиОрганизации.Ссылка = ПриемНаРаботуВОрганизацию.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаПриема МЕЖДУ &Начало И &Конец
	|	И (НЕ ПриемНаРаботуВОрганизацию.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.КадровоеПеремещениеОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещениеОрганизаций КАК КадровоеПеремещениеОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = КадровоеПеремещениеОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаНачала МЕЖДУ &Начало И &Конец
	|	И (НЕ КадровоеПеремещениеОрганизаций.Проведен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО СотрудникиОрганизаций.Ссылка = ТЧРаботникиОрганизации.Сотрудник
	|			И (СотрудникиОрганизаций.Физлицо = &ФизЛицо)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УвольнениеИзОрганизаций КАК УвольнениеИзОрганизаций
	|		ПО ТЧРаботникиОрганизации.Ссылка = УвольнениеИзОрганизаций.Ссылка
	|ГДЕ
	|	ТЧРаботникиОрганизации.ДатаУвольнения МЕЖДУ &Начало И &Конец
	|	И (НЕ УвольнениеИзОрганизаций.Проведен)";
	
	Запрос.УстановитьПараметр("ФизЛицо",	Физлицо);
	Запрос.УстановитьПараметр("Начало",		НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("Конец",		КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("Проведен",	Ложь);
	
	Возврат Запрос.Выполнить()
	
КонецФункции

Процедура НадписьПриемНаРаботуНажатие(Элемент)
	
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанныеПриемНаРаботу Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ПриемНаРаботуВОрганизацию Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ПриемНаРаботуВОрганизацию.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Принять в организацию ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьПриемНаРаботу);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Физлицо",			Физлицо);
		Запрос.УстановитьПараметр("ДатаАктуальности",	ОбщегоНазначения.ПолучитьРабочуюДату());
		Запрос.УстановитьПараметр("Организация",		Значение);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СписокСотрудников.Сотрудник КАК Сотрудник,
		|	СписокСотрудников.Физлицо КАК Физлицо
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(РаботникиОсновноеМесто.Сотрудник) КАК Сотрудник,
		|		РаботникиОсновноеМесто.Сотрудник.Физлицо КАК Физлицо,
		|		1 КАК Приоритет
		|	ИЗ
		|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|				&ДатаАктуальности,
		|				Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)
		|					И Сотрудник.Физлицо = &Физлицо
		|					И (Сотрудник.Организация = &Организация
		|						ИЛИ Сотрудник.ОбособленноеПодразделение = &Организация)) КАК РаботникиОсновноеМесто
		|	ГДЕ
		|		ВЫБОР
		|				КОГДА РаботникиОсновноеМесто.ПериодЗавершения <= &ДатаАктуальности
		|						И РаботникиОсновноеМесто.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|					ТОГДА РаботникиОсновноеМесто.ПричинаИзмененияСостоянияЗавершения
		|				ИНАЧЕ РаботникиОсновноеМесто.ПричинаИзмененияСостояния
		|			КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		РаботникиОсновноеМесто.Сотрудник.Физлицо
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		МАКСИМУМ(РаботникиСовместительство.Сотрудник),
		|		РаботникиСовместительство.Сотрудник.Физлицо,
		|		2
		|	ИЗ
		|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|				&ДатаАктуальности,
		|				Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)
		|					И Сотрудник.Физлицо = &Физлицо
		|					И (Сотрудник.Организация = &Организация
		|						ИЛИ Сотрудник.ОбособленноеПодразделение = &Организация)) КАК РаботникиСовместительство
		|	ГДЕ
		|		ВЫБОР
		|				КОГДА РаботникиСовместительство.ПериодЗавершения <= &ДатаАктуальности
		|						И РаботникиСовместительство.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|					ТОГДА РаботникиСовместительство.ПричинаИзмененияСостоянияЗавершения
		|				ИНАЧЕ РаботникиСовместительство.ПричинаИзмененияСостояния
		|			КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		РаботникиСовместительство.Сотрудник.Физлицо
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		МАКСИМУМ(СотрудникиДУ.Ссылка),
		|		СотрудникиДУ.Физлицо,
		|		3
		|	ИЗ
		|		Справочник.СотрудникиОрганизаций КАК СотрудникиДУ
		|	ГДЕ
		|		СотрудникиДУ.Физлицо = &Физлицо
		|		И СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|		И (СотрудникиДУ.Организация = &Организация
		|				ИЛИ СотрудникиДУ.ОбособленноеПодразделение = &Организация)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		СотрудникиДУ.Физлицо) КАК СписокСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СписокФизическихЛиц.Физлицо КАК Физлицо,
		|			МИНИМУМ(СписокФизическихЛиц.Приоритет) КАК Приоритет
		|		ИЗ
		|			(ВЫБРАТЬ
		|				РаботникиОсновноеМесто.Сотрудник.Физлицо КАК Физлицо,
		|				1 КАК Приоритет
		|			ИЗ
		|				РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|						&ДатаАктуальности,
		|						Сотрудник.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|							И Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)
		|							И Сотрудник.Физлицо = &Физлицо
		|							И (Сотрудник.Организация = &Организация
		|								ИЛИ Сотрудник.ОбособленноеПодразделение = &Организация)) КАК РаботникиОсновноеМесто
		|			ГДЕ
		|				ВЫБОР
		|						КОГДА РаботникиОсновноеМесто.ПериодЗавершения <= &ДатаАктуальности
		|								И РаботникиОсновноеМесто.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|							ТОГДА РаботникиОсновноеМесто.ПричинаИзмененияСостоянияЗавершения
		|						ИНАЧЕ РаботникиОсновноеМесто.ПричинаИзмененияСостояния
		|					КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|			
		|			ОБЪЕДИНИТЬ
		|			
		|			ВЫБРАТЬ
		|				РаботникиСовместительство.Сотрудник.Физлицо,
		|				2
		|			ИЗ
		|				РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|						&ДатаАктуальности,
		|						Сотрудник.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|							И Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)
		|							И Сотрудник.Физлицо = &Физлицо
		|							И (Сотрудник.Организация = &Организация
		|								ИЛИ Сотрудник.ОбособленноеПодразделение = &Организация)) КАК РаботникиСовместительство
		|			ГДЕ
		|				ВЫБОР
		|						КОГДА РаботникиСовместительство.ПериодЗавершения <= &ДатаАктуальности
		|								И РаботникиСовместительство.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|							ТОГДА РаботникиСовместительство.ПричинаИзмененияСостоянияЗавершения
		|						ИНАЧЕ РаботникиСовместительство.ПричинаИзмененияСостояния
		|					КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|			
		|			ОБЪЕДИНИТЬ
		|			
		|			ВЫБРАТЬ
		|				СотрудникиДУ.Физлицо,
		|				3
		|			ИЗ
		|				Справочник.СотрудникиОрганизаций КАК СотрудникиДУ
		|			ГДЕ
		|				СотрудникиДУ.Физлицо = &Физлицо
		|				И СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|				И (СотрудникиДУ.Организация = &Организация
		|						ИЛИ СотрудникиДУ.ОбособленноеПодразделение = &Организация)) КАК СписокФизическихЛиц
		|		
		|		СГРУППИРОВАТЬ ПО
		|			СписокФизическихЛиц.Физлицо) КАК СписокФизическихЛиц
		|		ПО СписокСотрудников.Физлицо = СписокФизическихЛиц.Физлицо
		|			И СписокСотрудников.Приоритет = СписокФизическихЛиц.Приоритет";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НовыйДокумент = Документы.ПриемНаРаботуВОрганизацию.ПолучитьФормуНовогоДокумента();
		НовыйДокумент.Организация	= Значение;
		
		Если Выборка.Следующий() Тогда
			НовыйДокумент.Заполнить(Выборка.Сотрудник);
		КонецЕсли;
		
		НовыйДокумент.Открыть();
		
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НадписьУвольнениеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.УвольнениеИзОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.УвольнениеИзОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Уволить из организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьУвольнение);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		// это создание приема на работу для организации
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			// регистируем для каких подразделений уже создали документ
			ОбработанныеПодразделения = Новый Соответствие;
			Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
				Если ОбработанныеПодразделения[МестоРаботы.Подразделение] = НеОпределено Тогда
					НовыйДокумент = Документы.УвольнениеИзОрганизаций.СоздатьДокумент();
					НовыйДокумент.Организация = МестоРаботы.ОбособленноеПодразделение;
					ОбработанныеПодразделения[МестоРаботы.Подразделение] = МестоРаботы.Подразделение;
					НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
					Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
					Строка.ФизЛицо = Физлицо;
					// для заполнения сотрудника перебираем рабочие места
					Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
						Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
							Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
								Строка.Сотрудник = МестоРаботы.Сотрудник;
							КонецЕсли; 
						КонецЕсли;
					КонецЦикла;
					Строка.ДатаУвольнения = ОбщегоНазначения.ПолучитьРабочуюДату();
					Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
					Форма.Открыть();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьКадровоеПеремещениеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.КадровоеПеремещениеОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.КадровоеПеремещениеОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Перемещение по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьКадровоеПеремещение);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.КадровоеПеремещениеОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
		Строка.ФизЛицо = Физлицо;
		Строка.ДатаНачала = ОбщегоНазначения.ПолучитьРабочуюДату();
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
				Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
					Строка.Сотрудник = МестоРаботы.Сотрудник;
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьВОтпускНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ОтпускаОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ОтпускаОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Отпуск по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьВОтпуск);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ОтпускаОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
		Строка.ФизЛицо = Физлицо;
		Строка.ДатаНачала = ОбщегоНазначения.ПолучитьРабочуюДату();
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
				Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
					Строка.Сотрудник = МестоРаботы.Сотрудник;
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьКомандировкаНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.КомандировкиОрганизаций Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.КомандировкиОрганизаций.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Командировка по организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьКомандировка);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.КомандировкиОрганизаций.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
		Строка.ФизЛицо = Физлицо;
		Строка.ДатаНачала = ОбщегоНазначения.ПолучитьРабочуюДату();
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			Если МестоРаботы.ВидРегистра = "РаботникиОрганизаций" Тогда
				Если МестоРаботы.ОбособленноеПодразделение = Значение Тогда
					Строка.Сотрудник = МестоРаботы.Сотрудник;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьАнкетаЗастрахованногоЛицаНажатие(Элемент)
	
	Если мФизлицоОбъект = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	// Ввод документов "АнкетаЗастрахованногоЛица" только для лиц без страховых номеров.
	Если ПустаяСтрока(СтрЗаменить(мФизлицоОбъект.СтраховойНомерПФР,"-","")) Тогда
		
		СписокМеню = Новый СписокЗначений;
		Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
			Для Каждого Документ из ИнфоПоОрганизации.Значение.АнкетаЗастрахованногоЛица Цикл
				СписокМеню.Добавить(Документ.Документ, Документ.Представление);
			КонецЦикла;
			Если ИнфоПоОрганизации.Значение.АнкетаЗастрахованногоЛица.Количество() = 0 Тогда
				СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Отправить анкету в ПФР от организации ") + ИнфоПоОрганизации.Значение.Наименование);
			КонецЕсли;
		КонецЦикла;
		Если СписокМеню.Количество() = 1 Тогда
			Значение = СписокМеню[0].Значение;
		Иначе
			ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьАнкетаЗастрахованногоЛица);
			Если ЗначениеВыбора <> Неопределено Тогда
				Значение = ЗначениеВыбора.Значение;
			Иначе
				Возврат
			КонецЕсли; 
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
			НовыйДокумент = Документы.АнкетаЗастрахованногоЛица.СоздатьДокумент();
			НовыйДокумент.Организация = Значение;
			НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
			Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
			Строка.ФизЛицо = Физлицо;
			НовыйДокумент.ЗаполнитьДанныеПоФизЛицу(Физлицо, Строка);
			Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
			Форма.Открыть();
		Иначе
			// это открытие существующего документа
			Форма = Значение.ПолучитьФорму(, ЭтаФорма);
			Форма.Открыть();
		КонецЕсли;
	Иначе
		Предупреждение("Физическому лицу уже присвоен страховой номер ПФР");
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура НадписьЗаявлениеОбОбменеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из КадровыеДанные Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Отправить заявление в ПФР от организации ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьЗаявлениеОбОбмене);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		НовыйДокумент = Документы.ЗаявлениеОбОбменеДубликатеСтраховогоСвидетельства.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		Строка = НовыйДокумент.РаботникиОрганизации.Добавить();
		Строка.ФизЛицо							= Физлицо;
		Строка.ПолВСвидетельстве				= Физлицо.Пол;
		Строка.ДатаРожденияВСвидетельстве		= Физлицо.ДатаРождения;
		Строка.СтраховойНомерПФРВСвидетельстве	= Физлицо.СтраховойНомерПФР;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

// обработка гиперссылок - действия по открытию форм в отдельных окнах
//
Процедура КадровыеДанныеВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Сред(Область.Имя,4) = "ГиперссылкаТ2" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовТ2.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Личная карточка Т2 (" + Физлицо + ")");
	КонецЕсли;
	
КонецПроцедуры

Процедура КадровыеДанныеОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Соответствие") Тогда
		СтандартнаяОбработка = Ложь;
		Если Расшифровка.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		Если Расшифровка.Количество() = 1 Тогда
			Для Каждого ЭлементСоответствия Из Расшифровка Цикл
				ОткрытьЗначение(ЭлементСоответствия.Ключ);
				Возврат;
			КонецЦикла;
		КонецЕсли;
		СписокМеню = Новый СписокЗначений;
		Для Каждого ЭлементСоответствия Из Расшифровка Цикл
			СписокМеню.Добавить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение);
		КонецЦикла;
		Результат = ВыбратьИзМеню(СписокМеню);
		Если Результат = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		ОткрытьЗначение(Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ НАЛОГИ

// формирует раздел
Процедура СформироватьДанныеНалоги()
	
	Если мДанныеНалогиСформированы Тогда
		Возврат;
	КонецЕсли;
	мДанныеНалогиСформированы = Истина;
	
	// обновить динамические списки
	НалогиОрганизаций.Обновить();
	
	// Сбор данных о состоянии работника в организациях.
	ПолучитьМестаРаботыИСостоянияРаботника();
	
	ПолучитьСведенияОНалогах();
	
	// настройка надписей раздела Действия и документы
	ЭлементыФормы.НадписьРасчетЕСН.Заголовок = ?(мПоказыватьЕСН,"Рассчитать ЕСН","Начислить страховые взносы");
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНалогам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.РасчетЕСН Цикл
			ЭлементыФормы.НадписьРасчетЕСН.Заголовок = ?(мПоказыватьЕСН,"Расчет ЕСН","Расчет страховых взносов");
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	// заполнения табличного документа с отчетами
	ВыходнойДокумент = ЭлементыФормы.Налоги;
	
	// восстановление раскрытых группировок отчета
	Если мПоказыватьЕСН Тогда
		ГруппировкаРегламентированнаяФормаКарточкаПоЕСН = Ложь;
		Если ОбластьРегламентированнаяФормаКарточкаПоЕСН <> НеОпределено и ОбластьРегламентированнаяФормаКарточкаПоЕСН.Видимость Тогда
			ГруппировкаРегламентированнаяФормаКарточкаПоЕСН = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ГруппировкаРегламентированнаяФормаКарточкаПоОПС = Ложь;
	Если ОбластьРегламентированнаяФормаКарточкаПоОПС <> НеОпределено и ОбластьРегламентированнаяФормаКарточкаПоОПС.Видимость Тогда
		ГруппировкаРегламентированнаяФормаКарточкаПоОПС = Истина;
	КонецЕсли;
	
	ВыходнойДокумент.Очистить();
	
	Макет = ПолучитьМакет("Налоги");
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьФизлицо = Макет.ПолучитьОбласть("Физлицо");
	ОбластьФизлицо.Параметры.Физлицо = Физлицо;
	ОбластьФизлицо.Параметры.Месяц = Формат(Месяц, "ДФ='ММММ гггг ''г.'''");
	ВыходнойДокумент.Вывести(ОбластьФизлицо);
	
	// выводим сведения о местах работы
	Если МестаРаботыИСостоянияФизлица.Количество() <> 0 Тогда
		Область = Макет.ПолучитьОбласть("МестаРаботыЗаголовок");
		ВыходнойДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("МестаРаботыСтрока");
		Для Каждого МестоРаботы Из МестаРаботыИСостоянияФизлица Цикл
			Область.Параметры.Заполнить(МестоРаботы);
			ВыходнойДокумент.Вывести(Область);
		КонецЦикла;
	КонецЕсли;
	
	// Поиск не проведенных документов
	РезультатВыборки = НеПроведенныеДокументыПоНалогам();
	
	Если НЕ РезультатВыборки.Пустой() Тогда
		
		Область = Макет.ПолучитьОбласть("ДокументыНеПроведены");
		Область.Параметры.ТекстДокументыНеПроведены = "ВНИМАНИЕ! Обнаружены не проведенные документы по расчету налогов!";
		ВыходнойДокумент.Вывести(Область);
		
	КонецЕсли; 
	
	// отчеты
	ОтчетРегламентированнаяФормаКарточкаПоЕСН = Отчеты.РегламентированнаяФормаКарточкаПоЕСН.Создать();
	Если мПоказыватьЕСН Тогда
		ОтчетРегламентированнаяФормаКарточкаПоОПС = Отчеты.РегламентированнаяФормаКарточкаПоОПС.Создать();
	Иначе
		ОтчетРегламентированнаяФормаКарточкаПоОПС = Отчеты.КарточкаУчетаПоСтраховымВзносам.Создать();
	КонецЕсли;
	
	ОбработанныеОрганизации = Новый Соответствие;
	ФормироватьКарточкуЕСН = НЕ мДанныеКарточкаПоЕСНСформированы;
	ФормироватьКарточкуОПС = НЕ мДанныеКарточкаПоОПССформированы;
	Сч = 1;
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		Если ОбработанныеОрганизации[МестоРаботы.Организация] = НеОпределено Тогда
			
			ОбработанныеОрганизации[МестоРаботы.Организация] = Истина;
			
			Область = Макет.ПолучитьОбласть("Организация");
			Область.Параметры.ОрганизацияНаименование = МестоРаботы.ОрганизацияНаименование;
			Область.Параметры.Организация = МестоРаботы.Организация;
			ВыходнойДокумент.Вывести(Область);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			
			// РегламентированнаяФормаКарточкаПоЕСН
			Если мПоказыватьЕСН Тогда
				// заголовок
				Область = Макет.ПолучитьОбласть("КарточкаЕСН");
				ИмяЯчейкиОтчета = Область.Области.ГиперссылкаЕСН.Имя;
				Область.Области.ГиперссылкаЕСН.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
				ВыходнойДокумент.Вывести(ПустаяСтрока);
				ВыходнойДокумент.Вывести(Область);
				
				// начнем группировку
				ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаРегламентированнаяФормаКарточкаПоЕСН);
				ВыходнойДокумент.Вывести(ПустаяСтрока);
				ОбластьРегламентированнаяФормаКарточкаПоЕСН = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
				
				Если ФормироватьКарточкуЕСН Тогда
					ОтчетРегламентированнаяФормаКарточкаПоЕСН.ФизЛицо = Физлицо;
					ОтчетРегламентированнаяФормаКарточкаПоЕСН.НалоговыйПериод = Год(Месяц);
					ОтчетРегламентированнаяФормаКарточкаПоЕСН.Организация = МестоРаботы.Организация;
					РегламентированнаяФормаКарточкаПоЕСНОтчет = Новый ТабличныйДокумент;
					ОтчетРегламентированнаяФормаКарточкаПоЕСН.СформироватьОтчет(РегламентированнаяФормаКарточкаПоЕСНОтчет);
					
					// Для вывода отчетов в отдельных окнах
					ДанныеТабличныхДокументовЕСН.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),РегламентированнаяФормаКарточкаПоЕСНОтчет);
					
				КонецЕсли;
				
				// таким способом размещаются отчеты без форматов строк
				ОбластьПрямоугольная = РегламентированнаяФормаКарточкаПоЕСНОтчет.Область(1, 1, РегламентированнаяФормаКарточкаПоЕСНОтчет.ВысотаТаблицы, РегламентированнаяФормаКарточкаПоЕСНОтчет.ШиринаТаблицы);
				НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + РегламентированнаяФормаКарточкаПоЕСНОтчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
				// назначим ширину колонок у новой области формата строк
				Для Счетчик = 1 По РегламентированнаяФормаКарточкаПоЕСНОтчет.ШиринаТаблицы Цикл
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = РегламентированнаяФормаКарточкаПоЕСНОтчет.Область(1, Счетчик).ШиринаКолонки;
				КонецЦикла;
				ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
				
				ВыходнойДокумент.ЗакончитьГруппуСтрок();
			КонецЕсли;
			
			// РегламентированнаяФормаКарточкаПоОПС
			// заголовок
			Область = Макет.ПолучитьОбласть("КарточкаОПС");
			ИмяЯчейкиОтчета = Область.Области.ГиперссылкаОПС.Имя;
			Область.Области.ГиперссылкаОПС.Имя = Формат(Сч,"ЧЦ=3; ЧВН=") + ИмяЯчейкиОтчета;
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ВыходнойДокумент.Вывести(Область);
			
			// начнем группировку
			ВыходнойДокумент.НачатьГруппуСтрок(, ГруппировкаРегламентированнаяФормаКарточкаПоОПС);
			ВыходнойДокумент.Вывести(ПустаяСтрока);
			ОбластьРегламентированнаяФормаКарточкаПоОПС = ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы, , ВыходнойДокумент.ВысотаТаблицы, );
			
			Если ФормироватьКарточкуОПС Тогда
				Если мПоказыватьЕСН Тогда
					ОтчетРегламентированнаяФормаКарточкаПоОПС.ФизЛицо = Физлицо;
					ОтчетРегламентированнаяФормаКарточкаПоОПС.НалоговыйПериод = Год(Месяц);
					ОтчетРегламентированнаяФормаКарточкаПоОПС.Организация = МестоРаботы.Организация;
					РегламентированнаяФормаКарточкаПоОПСОтчет = Новый ТабличныйДокумент;
					ОтчетРегламентированнаяФормаКарточкаПоОПС.СформироватьОтчет(РегламентированнаяФормаКарточкаПоОПСОтчет);
					
					// Для вывода отчетов в отдельных окнах
					ДанныеТабличныхДокументовОПС.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),РегламентированнаяФормаКарточкаПоОПСОтчет);
					
				Иначе
					
					ТиповыеОтчеты.УстановитьПараметр(ОтчетРегламентированнаяФормаКарточкаПоОПС.КомпоновщикНастроек, "НачалоПериода",НачалоГода(Месяц));
					ТиповыеОтчеты.УстановитьПараметр(ОтчетРегламентированнаяФормаКарточкаПоОПС.КомпоновщикНастроек, "КонецПериода", КонецГода(Месяц));
					ТиповыеОтчеты.ДобавитьОтбор(ОтчетРегламентированнаяФормаКарточкаПоОПС.КомпоновщикНастроек,      "Физлицо",    Физлицо);
					
					РегламентированнаяФормаКарточкаПоОПСОтчет = Новый ТабличныйДокумент;
					ОтчетРегламентированнаяФормаКарточкаПоОПС.СформироватьОтчет(РегламентированнаяФормаКарточкаПоОПСОтчет, Истина);
					ДанныеТабличныхДокументовОПС.Вставить(Формат(Сч,"ЧЦ=3; ЧВН="),РегламентированнаяФормаКарточкаПоОПСОтчет);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если мПоказыватьЕСН Тогда
				// таким способом размещаются отчеты без форматов строк
				ОбластьПрямоугольная = РегламентированнаяФормаКарточкаПоОПСОтчет.Область(1, 1, РегламентированнаяФормаКарточкаПоОПСОтчет.ВысотаТаблицы, РегламентированнаяФормаКарточкаПоОПСОтчет.ШиринаТаблицы);
				НачалоНовогоФорматаСтрок = ВыходнойДокумент.ВысотаТаблицы + 1;
				ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, , НачалоНовогоФорматаСтрок + РегламентированнаяФормаКарточкаПоОПСОтчет.ВысотаТаблицы - 1, ).СоздатьФорматСтрок();
				// назначим ширину колонок у новой области формата строк
				Для Счетчик = 1 По РегламентированнаяФормаКарточкаПоОПСОтчет.ШиринаТаблицы Цикл
					ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки = РегламентированнаяФормаКарточкаПоОПСОтчет.Область(1, Счетчик).ШиринаКолонки;
				КонецЦикла;
				ВыходнойДокумент.ВставитьОбласть(ОбластьПрямоугольная, ВыходнойДокумент.Область(НачалоНовогоФорматаСтрок, 1));
			Иначе
				// таким способом размещаются отчеты с форматами строк
				ОбластьСтроки = РегламентированнаяФормаКарточкаПоОПСОтчет.Область(1, , РегламентированнаяФормаКарточкаПоОПСОтчет.ВысотаТаблицы, );
				ВыходнойДокумент.ВставитьОбласть(ОбластьСтроки, ВыходнойДокумент.Область(ВыходнойДокумент.ВысотаТаблицы + 1, , ВыходнойДокумент.ВысотаТаблицы + РегламентированнаяФормаКарточкаПоОПСОтчет.ВысотаТаблицы, ));
			КонецЕсли;
			
			ВыходнойДокумент.ЗакончитьГруппуСтрок();
			
			Сч = Сч +1;
			
		КонецЕсли;
	КонецЦикла;
	ВыходнойДокумент.ТолькоПросмотр = Истина;
	ВыходнойДокумент.Защита = Истина;
	
КонецПроцедуры

// получает сведения, которые требуются для отработки действий
// 
Процедура ПолучитьСведенияОНалогах()
	
	Перем СвойствоИнфоПоОрганизации;
	
	// получим сведения о местах работы
	ПолучитьСведенияОМестахРаботы();
	
	ДанныеПоНалогам.Очистить();
	Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
		// просто наполняем соответствие организациями работника
		ИнфоПоОрганизации = Новый Структура(
		"Наименование,
		|РасчетЕСН,
		|ОтражениеЗарплатыВРеглУчете", 
		МестоРаботы.ОрганизацияНаименование,
		Новый Массив,
		Новый Массив);
		ДанныеПоНалогам[МестоРаботы.Организация] = ИнфоПоОрганизации;
		
	КонецЦикла;
	
	Запрос = новый запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сведения.ВидДокумента,
	|	Сведения.Организация,
	|	Сведения.Документ,
	|	Сведения.Представление
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		РасчетЕСНИсчисленныйЕСН.Ссылка КАК Документ,
	|		РасчетЕСНИсчисленныйЕСН.Ссылка.Организация КАК Организация,
	|		РасчетЕСНИсчисленныйЕСН.Ссылка.Представление КАК Представление,
	|		""РасчетЕСН"" КАК ВидДокумента
	|	ИЗ
	|		Документ.РасчетЕСН.ИсчисленныйЕСН КАК РасчетЕСНИсчисленныйЕСН
	|	ГДЕ
	|		РасчетЕСНИсчисленныйЕСН.ФизЛицо = &ФизЛицо
	|		И РасчетЕСНИсчисленныйЕСН.Ссылка.ПериодРегистрации = &Месяц
	|		И &ПоказыватьЕСН
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка,
	|		РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка.Организация,
	|		РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка.Представление,
	|		""РасчетЕСН""
	|	ИЗ
	|		Документ.РасчетСтраховыхВзносов.ИсчисленныеСтраховыеВзносы КАК РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы
	|	ГДЕ
	|		РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.ФизЛицо = &ФизЛицо
	|		И РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка.ПериодРегистрации = &Месяц
	|		И (НЕ &ПоказыватьЕСН)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка,
	|		ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка.Организация,
	|		ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка.Представление,
	|		""ОтражениеЗарплатыВРеглУчете""
	|	ИЗ
	|		Документ.ОтражениеЗарплатыВРеглУчете.ОсновныеНачисления КАК ОтражениеЗарплатыВРеглУчетеОсновныеНачисления
	|	ГДЕ
	|		ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Сотрудник.Физлицо = &ФизЛицо
	|		И ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка.ПериодРегистрации = &Месяц
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка,
	|		ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка.Организация,
	|		ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка.Представление,
	|		""ОтражениеЗарплатыВРеглУчете""
	|	ИЗ
	|		Документ.ОтражениеЗарплатыВРеглУчете.ДополнительныеНачисления КАК ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления
	|	ГДЕ
	|		ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Сотрудник.Физлицо = &ФизЛицо
	|		И ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка.ПериодРегистрации = &Месяц) КАК Сведения";
	
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	Запрос.УстановитьПараметр("ПоказыватьЕСН", мПоказыватьЕСН);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ДанныеПоНалогам[Выборка.Организация] = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		ДанныеПоНалогам[Выборка.Организация].Свойство(Выборка.ВидДокумента, СвойствоИнфоПоОрганизации);
		СвойствоИнфоПоОрганизации.Добавить(Новый Структура("Документ, Представление", Выборка.Документ, Выборка.Представление));
	КонецЦикла;
	
КонецПроцедуры

// Формирует список непроведенных кадровых документов
Функция НеПроведенныеДокументыПоНалогам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетЕСНИсчисленныйЕСН.Ссылка
	|ИЗ
	|	Документ.РасчетЕСН.ИсчисленныйЕСН КАК РасчетЕСНИсчисленныйЕСН
	|ГДЕ
	|	РасчетЕСНИсчисленныйЕСН.ФизЛицо = &ФизЛицо
	|	И (НЕ РасчетЕСНИсчисленныйЕСН.Ссылка.Проведен)
	|	И РасчетЕСНИсчисленныйЕСН.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И &ПоказыватьЕСН
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов.ИсчисленныеСтраховыеВзносы КАК РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы
	|ГДЕ
	|	РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.ФизЛицо = &ФизЛицо
	|	И (НЕ РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка.Проведен)
	|	И РасчетСтраховыхВзносовИсчисленныеСтраховыеВзносы.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И (НЕ &ПоказыватьЕСН)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОсновныеНачисления КАК ОтражениеЗарплатыВРеглУчетеОсновныеНачисления
	|ГДЕ
	|	(НЕ ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка.Проведен)
	|	И ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И ОтражениеЗарплатыВРеглУчетеОсновныеНачисления.Сотрудник.Физлицо = &ФизЛицо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ДополнительныеНачисления КАК ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления
	|ГДЕ
	|	(НЕ ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка.Проведен)
	|	И ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И ОтражениеЗарплатыВРеглУчетеДополнительныеНачисления.Сотрудник.Физлицо = &ФизЛицо";
	
	Запрос.УстановитьПараметр("ФизЛицо", Физлицо);
	Запрос.УстановитьПараметр("ПериодРегистрации",Месяц);
	Запрос.УстановитьПараметр("ПоказыватьЕСН", мПоказыватьЕСН);
	
	Возврат Запрос.Выполнить();
	
КонецФункции // НеПроведенныеДокументыПоНалогам()

Процедура НадписьРасчетЕСННажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНалогам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.РасчетЕСН Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.РасчетЕСН.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, ?(мПоказыватьЕСН,"Рассчитать ЕСН ","Рассчитать страховые взносы ") + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьРасчетЕСН);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		// это создание документа Расчет ЕСН
		Для Каждого МестоРаботы Из МестаРаботыФизлица Цикл
			// регистируем для каких подразделений уже создали документ
			ОбработанныеПодразделения = Новый Соответствие;
			Если МестоРаботы.Организация = Значение Тогда
				Если ОбработанныеПодразделения[МестоРаботы.Подразделение] = НеОпределено Тогда
					Если мПоказыватьЕСН Тогда
						НовыйДокумент = Документы.РасчетЕСН.СоздатьДокумент();
					Иначе
						НовыйДокумент = Документы.РасчетСтраховыхВзносов.СоздатьДокумент();
					КонецЕсли;
					НовыйДокумент.Организация = МестоРаботы.Организация;
					ОбработанныеПодразделения[МестоРаботы.Подразделение] = МестоРаботы.Подразделение;
					НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
					НовыйДокумент.ПериодРегистрации = Месяц;
					НовыйДокумент.ПодразделениеОрганизации = МестоРаботы.Подразделение;
					Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
					Форма.Открыть();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьОтражениеЗПвРеглУчетеНажатие(Элемент)
	СписокМеню = Новый СписокЗначений;
	Для Каждого ИнфоПоОрганизации Из ДанныеПоНалогам Цикл
		Для Каждого Документ из ИнфоПоОрганизации.Значение.ОтражениеЗарплатыВРеглУчете Цикл
			СписокМеню.Добавить(Документ.Документ, Документ.Представление);
		КонецЦикла;
		Если ИнфоПоОрганизации.Значение.ОтражениеЗарплатыВРеглУчете.Количество() = 0 Тогда
			СписокМеню.Добавить(ИнфоПоОрганизации.Ключ, "Отражение зарплаты в регл. учете " + ИнфоПоОрганизации.Значение.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокМеню.Количество() = 1 Тогда
		Значение = СписокМеню[0].Значение;
	Иначе
		ЗначениеВыбора = ВыбратьИзМеню(СписокМеню, ЭлементыФормы.НадписьОтражениеЗПвРеглУчете);
		Если ЗначениеВыбора <> Неопределено Тогда
			Значение = ЗначениеВыбора.Значение;
		Иначе
			Возврат
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
		// это создание документа "Отражение зарплаты в регл учете"
		НовыйДокумент = Документы.ОтражениеЗарплатыВРеглУчете.СоздатьДокумент();
		НовыйДокумент.Организация = Значение;
		НовыйДокумент.Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		НовыйДокумент.ПериодРегистрации = Месяц;
		Форма = НовыйДокумент.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	Иначе
		// это открытие существующего документа
		Форма = Значение.ПолучитьФорму(, ЭтаФорма);
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

// обработка гиперссылок - действия по открытию форм в отдельных окнах
//
Процедура НалогиВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Сред(Область.Имя,4) = "ГиперссылкаЕСН" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовЕСН.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Карточка по ЕСН (" + Физлицо + ")");
	ИначеЕсли Сред(Область.Имя,4) = "ГиперссылкаОПС" Тогда
		ВыводимыйТабличныйДокумент = ДанныеТабличныхДокументовОПС.Получить(Лев(Область.Имя,3));
		ТабличныйДокументОтдельно(ВыводимыйТабличныйДокумент, "Карточка по страховым взносам (" + Физлицо + ")");
	КонецЕсли;
	
КонецПроцедуры

Процедура НалогиОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Соответствие") Тогда
		СтандартнаяОбработка = Ложь;
		Если Расшифровка.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		Если Расшифровка.Количество() = 1 Тогда
			Для Каждого ЭлементСоответствия Из Расшифровка Цикл
				ОткрытьЗначение(ЭлементСоответствия.Ключ);
				Возврат;
			КонецЦикла;
		КонецЕсли;
		СписокМеню = Новый СписокЗначений;
		Для Каждого ЭлементСоответствия Из Расшифровка Цикл
			СписокМеню.Добавить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение);
		КонецЦикла;
		Результат = ВыбратьИзМеню(СписокМеню);
		Если Результат = НеОпределено Тогда
			Возврат;
		КонецЕсли;
		ОткрытьЗначение(Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// МЕСЯЦ СТРОКОЙ

Процедура МесяцСтрокаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	РаботаСДиалогами.ДатаКакМесяцАвтоПодборТекста(Текст, ТекстАвтоПодбора, СтандартнаяОбработка);
КонецПроцедуры

Процедура МесяцСтрокаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	РаботаСДиалогами.ДатаКакМесяцОкончаниеВводаТекста(Текст, Значение, СтандартнаяОбработка);
КонецПроцедуры

Процедура МесяцСтрокаПриИзменении(Элемент)
	РаботаСДиалогами.ДатаКакМесяцПодобратьДатуПоТексту(Элемент.Значение, Месяц);
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(Месяц);
	ПриИзмененииМесяца();
	ОбновитьДанные();    
КонецПроцедуры

Процедура МесяцСтрокаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	Месяц = ДобавитьМесяц(Месяц, Направление);
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(Месяц);
	ПриИзмененииМесяца();
	ОбновитьДанные();    
КонецПроцедуры

Процедура МесяцСтрокаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура МесяцСтрокаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	РаботаСДиалогами.НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, Месяц, ЭтаФорма);
	ПриИзмененииМесяца();
	ОбновитьДанные();    
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьДанныеОФизлице" И Параметр = Физлицо Тогда
		Физлицо = Физлицо;
		мДанныеРасчетыЗарплатаСформированы = Ложь;
		мДанныеПлановыеДанныеСформированы = Ложь;
		мДанныеНалоговаяКарточкаСформированы = Ложь;
		мДанныеКадровыеДанныеСформированы = Ложь;
		мДанныеОсновноеСформированы = Ложь;
		мДанныеНалогиСформированы = Ложь;
		мДанныеКарточкаПоЕСНСформированы = Ложь;
		мДанныеКарточкаПоОПССформированы = Ложь;
		ОбновитьДанные();
	ИначеЕсли ИмяСобытия = "ОбновитьДанныеВоинскогоУчета" И Параметр = Физлицо Тогда
		
		ОбщегоНазначения.ПолучитьСвязанныеДанныеФизлица(Физлицо, мФизлицоФорма.мНаборЗаписейВоинскийУчет);
		
	КонецЕсли;
	// Вставить содержимое обработчика.
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ

мДанныеРасчетыЗарплатаСформированы = Ложь;
мДанныеПлановыеДанныеСформированы = Ложь;
мДанныеНалоговаяКарточкаСформированы = Ложь;
мДанныеКадровыеДанныеСформированы = Ложь;
мДанныеОсновноеСформированы = Ложь;
мДанныеНалогиСформированы = Ложь;
мДанныеКарточкаПоЕСНСформированы = Ложь;
мДанныеКарточкаПоОПССформированы = Ложь;

ПрежнийМесяц = '00010101';

МестаРаботыФизлица = Новый Массив;
МестаРаботыИСостоянияФизлица = Новый Массив;
ДанныеПоНачислениям = Новый Соответствие;
ДанныеПоПлановымРасчетам = Новый Соответствие;
КадровыеДанные = Новый Соответствие;
КадровыеДанныеПриемНаРаботу = Новый Соответствие;
ДанныеПоНалогам = Новый Соответствие;

// соответствия табличных документов
ДанныеТабличныхДокументов1НДФЛ = Новый Соответствие;
ДанныеТабличныхДокументовАнализНачислений = Новый Соответствие;
ДанныеТабличныхДокументовРасчетныеЛистки = Новый Соответствие;
ДанныеТабличныхДокументовТ13 = Новый Соответствие;
ДанныеТабличныхДокументовТ3 = Новый Соответствие;
ДанныеТабличныхДокументовСЗВ4 = Новый Соответствие;
ДанныеТабличныхДокументовТ2 = Новый Соответствие;
ДанныеТабличныхДокументовЕСН = Новый Соответствие;
ДанныеТабличныхДокументовОПС = Новый Соответствие;