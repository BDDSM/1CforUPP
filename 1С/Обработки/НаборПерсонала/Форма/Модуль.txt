////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Переменные механизма встроенной справки
Перем ОтображатьСправкуФормы;
Перем ЕстьДоступныеУчетныеЗаписи;

// Переменные механизма Не разобранные письма
Перем ПолеДляУпорядочиванияНеРазобранныхПисем;

// Переменные механизма Кандидаты
Перем ТекстМакетаОписанияЗаявки;
Перем ЕстьПисьмаПоЗаявке;

// Переменная, хранящая ссылку на текущего кандидата. Необходима для того, чтобы не выполнять действия при активизации строки кандидата каждый раз
Перем ТекущаяЗаявка;

// Письма, на основании которого создаем кандидата
Перем ПисьмаДляЗаявки;

// Письмо, для которого надо автоматически установить рассмотрено
Перем ПисьмоКРассмотрению;

// Кандидат для сотрудника
Перем ЗаявкаДляСотрудника;

// Количество элементов, которое необходимо показать в контекстном меню дерева заявок кандидатов
Перем мКоличествоЭлементовВПодменю;

// Содержит ссылку на кандидата, если для неразобранного письма было определено, что отправитель
// уже переписывался с компанией ранее и в письме была указан кандидат
Перем мНайденнаяЗаявка;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ВернутьВыделенныеСтроки(Знач ВыделенныеСтроки)
	
	МассивСтрок = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если МассивСтрок.Найти(ВыделеннаяСтрока) = Неопределено Тогда
			МассивСтрок.Добавить(ВыделеннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции

Процедура ПодготовитьСправкуФормы()
	
	НетДоступныхУчетныхЗаписей	= ?(ПоказыватьНеРазобранныеПисьма И Не ЕстьДоступныеУчетныеЗаписи, "block", "none");
	ЕстьНеразобранныеПисьма		= ?(ПоказыватьНеРазобранныеПисьма И ЕстьДоступныеУчетныеЗаписи И КоличествоНеРазобранныхПисем > 0, "block", "none");
	
	ЦветФонаСправки		= РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветФона());
	ЦветСсылкиСправки	= РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки());
	
	ЭлементыФормы.ПанельСправкиФормы.ЦветРамки			= РаботаСДиалогами.ВстроеннаяСправка_ЦветРамки();
	ЭлементыФормы.НадписьЗакрытьСправкуФормы.ЦветФона	= РаботаСДиалогами.ВстроеннаяСправка_ЦветФона();
	ЭлементыФормы.НадписьЗакрытьСправкуФормы.ЦветТекста	= РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки();
	
	ТекстМакетаВстроеннойСправки =
	"<HTML>
	|	<HEAD>
	|		<META http-equiv=Content-Type content=""text/html; charset=utf-8"">" + РаботаСДиалогами.ВстроеннаяСправка_СтилиДокумента() + "
	|	</HEAD>
	|
	|	<BODY aLink="+ЦветСсылкиСправки+" vLink="+ЦветСсылкиСправки+" link="+ЦветСсылкиСправки+" bgColor="+ЦветФонаСправки+" scroll=auto><FONT face=""MS Sans Serif"" size=1>
	|		<IMG src="+РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.КартинкаВстроеннойСправкиФормы, ЭлементыФормы.ВстроеннаяСправка)+">
	|		<DIV id=НетДоступныхУчетныхЗаписей style=""DISPLAY:"+НетДоступныхУчетныхЗаписей+""">У вас нет ни одной учетной записи электронной почты для переписки с кандидатами. Вы можете <A id=Команда href=""V8:NewAccount"">создать новую учетную запись</A> либо <A id=Команда href=""V8:HideUnreadMail"">скрыть панель с не разобранными письмами</A>, если вы не хотите работать с электронной почтой.</DIV>
	|		<DIV id=ЕстьНеразобранныеПисьма1 style=""DISPLAY:"+ЕстьНеразобранныеПисьма+""">Создать нового кандидата по неразобранному письму можно, перетащив это письмо мышкой в область кандидатов.</DIV>
	|		<DIV id=ЕстьНеразобранныеПисьма2 style=""DISPLAY:"+ЕстьНеразобранныеПисьма+""">Прикрепить неразобранное письмо к существующему кандидату можно, перетащив это письмо мышкой в кандидата или в список писем по кандидату.</DIV>
	|		<DIV>Кандидатов можно переносить мышкой из группы, по которой они упорядочены, в другую группу.</DIV>
	|		<DIV>Список действий с кандидатом можно увидеть в контекстном меню, которое вызывается при нажатии на правую кнопку мыши на выбранного кандидата.</DIV>
	|		<DIV>Проверить, обращался ли кандидат к нам ранее, можно с помощью обработки ""Поиск данных"" (Меню Сервис). В поле поиска необходимо ввести строку поиска, например ФИО кандидата.</DIV>
	|		<DIV>Когда кандидат успешно прошел все собеседования, и по нему принято решение о приеме кандидата на работу, можно сразу создать сотрудника по кандидату. Если у кандидата заполнено резюме, данные этого резюме будут использованы при создании сотрудника.</DIV>
	|	</FONT></BODY>
	|</HTML>";
	
	ЭлементыФормы.ВстроеннаяСправка.УстановитьТекст(ТекстМакетаВстроеннойСправки);
	
КонецПроцедуры

Процедура ОбновитьСправкуФормы(ПолеДанных = Неопределено)
	
	ДокHTML = ЭлементыФормы.ВстроеннаяСправка.Документ;
	
	Если ПолеДанных = Неопределено ИЛИ ПолеДанных = "ПоказыватьНеРазобранныеПисьма" ИЛИ ПолеДанных = "НетДоступныхУчетныхЗаписей" Тогда
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "НетДоступныхУчетныхЗаписей", ПоказыватьНеРазобранныеПисьма И НЕ ЕстьДоступныеУчетныеЗаписи, "block");
	КонецЕсли;
	
	Если ПолеДанных = Неопределено ИЛИ ПолеДанных = "ПоказыватьНеРазобранныеПисьма" ИЛИ ПолеДанных = "ЕстьНеразобранныеПисьма" Тогда
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ЕстьНеразобранныеПисьма1", ПоказыватьНеРазобранныеПисьма И ЕстьДоступныеУчетныеЗаписи И КоличествоНеРазобранныхПисем > 0, "block");
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ЕстьНеразобранныеПисьма2", ПоказыватьНеРазобранныеПисьма И ЕстьДоступныеУчетныеЗаписи И КоличествоНеРазобранныхПисем > 0, "block");
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСправкуФормы()

Процедура УстановитьВидимостьСправкиФормы(ОбновитьСправку = Истина)
	
	РаботаСДиалогамиЗК.ИзменитьВидимостьПанелиПоГоризонтали(ЭтаФорма, ОтображатьСправкуФормы, "ОсновнаяПанель", "СправкиФормы");
	
	ЭлементыФормы.КоманднаяПанельСправкиФормы.Кнопки.ПереключениеСправкиФормы.Пометка = ОтображатьСправкуФормы;
	
	Если ОтображатьСправкуФормы Тогда
		ПодключитьОбработчикИзмененияДанных("ПоказыватьНеРазобранныеПисьма",	"ОбновитьСправкуФормы");
		ПодключитьОбработчикИзмененияДанных("КоличествоНеРазобранныхПисем",		"ОбновитьСправкуФормы");
		Если ОбновитьСправку Тогда
			ОбновитьСправкуФормы();
		КонецЕсли;
		
	Иначе
		ОтключитьОбработчикИзмененияДанных("ПоказыватьНеРазобранныеПисьма");
		ОтключитьОбработчикИзмененияДанных("КоличествоНеРазобранныхПисем");
		
	КонецЕсли;
	
КонецПроцедуры


Процедура УстановитьВидимостьНеРазобранныхПисем()
	
	Если ПоказыватьНеРазобранныеПисьма Тогда
		ЭлементыФормы.НадписьУправлениеВидимостьюСпискаПисем.Заголовок = "Скрыть не разобранные письма";
	Иначе
		ЭлементыФормы.НадписьУправлениеВидимостьюСпискаПисем.Заголовок = "Показать не разобранные письма";
	КонецЕсли;
	
	РаботаСДиалогамиЗК.ИзменитьВидимостьПанелиПоВертикали(ЭтаФорма, ПоказыватьНеРазобранныеПисьма, "РабочаяОбласть", "НеразобранныеПисьма");
	
КонецПроцедуры

Процедура УстановитьТекстПустогоНеРазобранногоПисьма()
	
	Если КоличествоНеРазобранныхПисем > 0 Тогда
		ТекстСообщения = "Письмо не выбрано. Текст письма не может быть показан";
	Иначе
		ТекстСообщения = "Нет не разобранных писем";
	КонецЕсли;
	
	ЭлементыФормы.ПолеHTMLДокументаНеРазобранныеПисьма.УстановитьТекст("<BODY scroll=auto rightMargin=0><FONT face=""MS Sans Serif"" size=1><P align=center>"+ТекстСообщения+"</P></FONT></BODY>");
	
КонецПроцедуры

Процедура НеРазобранныеПисьмаПриАктивизацииСтроки()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		УстановитьТекстПустогоНеРазобранногоПисьма();
		Если АктивизированоНеРазобранноеПисьмо Тогда
			АктивизированоНеРазобранноеПисьмо = Ложь;
		КонецЕсли;
		
		ЭлементыФормы.ПолеКартинкиИнформацияОПисьме.Видимость	= Ложь;
		ЭлементыФормы.НадписьИнформацияОПисьме.Видимость		= Ложь;
		ЭлементыФормы.НадписьОткрытьСообщения.Видимость			= Ложь;
		ЭлементыФормы.НадписьОткрытьЗаявку.Видимость			= Ложь;
		
		Возврат;
	КонецЕсли;
	
	Если Не АктивизированоНеРазобранноеПисьмо Тогда
		АктивизированоНеРазобранноеПисьмо = Истина;
	КонецЕсли;
	
	ВыбранноеЭлектронноеПисьмо = ДанныеСтроки.Ссылка;
	
	Если ВыбранноеЭлектронноеПисьмо.ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.HTMLСКартинками Тогда
		НайденноеСоответствие = глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем").Получить(ВыбранноеЭлектронноеПисьмо);
		Если НайденноеСоответствие = Неопределено Тогда
			КопияТекстаПисьма = ВыбранноеЭлектронноеПисьмо.ТекстПисьма;
			УправлениеЭлектроннойПочтой.ПропарситьHTMLИДВ_ТекстКартинки(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), ТекущийПользователь, ВыбранноеЭлектронноеПисьмо, КопияТекстаПисьма);
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "</BODY>", "</FONT></BODY>");
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "<BODY", "<BODY scroll=auto");
			ЭлементыФормы.ПолеHTMLДокументаНеРазобранныеПисьма.УстановитьТекст(КопияТекстаПисьма);
			
		Иначе
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "</BODY>", "</FONT></BODY>");
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "<BODY", "<BODY scroll=auto");
			ЭлементыФормы.ПолеHTMLДокументаНеРазобранныеПисьма.УстановитьТекст(НайденноеСоответствие);
			
		КонецЕсли;
		
	ИначеЕсли ВыбранноеЭлектронноеПисьмо.ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.Текст Тогда
		ЭлементыФормы.ПолеHTMLДокументаНеРазобранныеПисьма.УстановитьТекст(УправлениеЭлектроннойПочтой.ВернутьТекстПисьмаВФорматеHTML(ВыбранноеЭлектронноеПисьмо.ТекстПисьма));
		
	Иначе
		ТекстПисьма = ВыбранноеЭлектронноеПисьмо.ТекстПисьма;
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "</BODY>", "</FONT></BODY>");
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "<BODY", "<BODY scroll=auto");
		ЭлементыФормы.ПолеHTMLДокументаНеРазобранныеПисьма.УстановитьТекст(ТекстПисьма);
		
	КонецЕсли;
	
	ЭлементыФормы.ПолеКартинкиИнформацияОПисьме.Видимость	= Истина;
	ЭлементыФормы.НадписьИнформацияОПисьме.Видимость		= Истина;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Отправитель",	ДанныеСтроки.ОтправительАдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("ДатаПолучения",	ДанныеСтроки.ДатаПолучения);
	Запрос.УстановитьПараметр("Ссылка",			ДанныеСтроки.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА ЭлектронноеПисьмо.ДатаТранспорта <= ДОБАВИТЬКДАТЕ(&ДатаПолучения, МЕСЯЦ, -1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьСтарыеПисьма,
	|	ВЫБОР
	|		КОГДА ЭлектронноеПисьмо.ЗаявкаКандидата = ЗНАЧЕНИЕ(Справочник.ЗаявкиКандидатов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьЗаявка,
	|	ЭлектронноеПисьмо.ЗаявкаКандидата,
	|	НАЧАЛОПЕРИОДА(ВЫБОР
	|			КОГДА ЭлектронноеПисьмо.ДатаТранспорта > ЭлектронноеПисьмо.Дата
	|				ТОГДА ЭлектронноеПисьмо.Дата
	|			ИНАЧЕ ЭлектронноеПисьмо.ДатаТранспорта
	|		КОНЕЦ, ДЕНЬ) КАК ДатаПолучения
	|ИЗ
	|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
	|ГДЕ
	|	ЭлектронноеПисьмо.ОтправительАдресЭлектроннойПочты = &Отправитель
	|	И ЭлектронноеПисьмо.Ссылка <> &Ссылка
	|	И НАЧАЛОПЕРИОДА(ВЫБОР
	|				КОГДА ЭлектронноеПисьмо.ДатаТранспорта > ЭлектронноеПисьмо.Дата
	|					ТОГДА ЭлектронноеПисьмо.Дата
	|				ИНАЧЕ ЭлектронноеПисьмо.ДатаТранспорта
	|			КОНЕЦ, ДЕНЬ) <= &ДатаПолучения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭлектронноеПисьмо.ДатаТранспорта УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПоказыватьПисьма	= Истина;
		ПоказыватьЗаявку	= Выборка.ЕстьЗаявка;
		мНайденнаяЗаявка	= Выборка.ЗаявкаКандидата;
		ЭлементыФормы.НадписьОткрытьСообщения.Заголовок			= "Открыть сообщения от " + ДанныеСтроки.Отправитель;
		
		Если Выборка.ЕстьСтарыеПисьма Тогда
			ЭлементыФормы.ПолеКартинкиИнформацияОПисьме.Картинка	= БиблиотекаКартинок.СообщениеОПроблемах;
			ЭлементыФормы.НадписьИнформацияОПисьме.Заголовок		= "Переписка с данным отправителем уже велась (" + Формат(Выборка.ДатаПолучения, "ДФ='МММ гггг ""года""'") + ")";
		Иначе
			ЭлементыФормы.ПолеКартинкиИнформацияОПисьме.Картинка	= БиблиотекаКартинок.СообщениеВажнаяИнформация;
			ЭлементыФормы.НадписьИнформацияОПисьме.Заголовок		= "Предыдущее письмо от этого отправителя получено " + Формат(Выборка.ДатаПолучения, "ДЛФ=DD");
		КонецЕсли;
		
	Иначе
		ПоказыватьПисьма	= Ложь;
		ПоказыватьЗаявку	= Ложь;
		мНайденнаяЗаявка	= Неопределено;
		ЭлементыФормы.ПолеКартинкиИнформацияОПисьме.Картинка	= БиблиотекаКартинок.СообщениеИнформация;
		ЭлементыФормы.НадписьИнформацияОПисьме.Заголовок		= "Переписка с данным отправителем еще не велась";
		
	КонецЕсли;
	
	ЭлементыФормы.НадписьОткрытьСообщения.Видимость	= ПоказыватьПисьма;
	ЭлементыФормы.НадписьОткрытьЗаявку.Видимость	= ПоказыватьЗаявку;
	
КонецПроцедуры

Процедура ОбновитьСписокНеРазобранныхПисем()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	ЗначениеГруппировки	= 0;
	ЗначениеЭлемента	= 0;
	Если ДанныеСтроки <> Неопределено Тогда
		Если ДанныеСтроки.Ссылка.Пустая() Тогда
			ЗначениеГруппировки = ДанныеСтроки.ЗначениеГруппировки;
		Иначе
			Если ДанныеСтроки.Родитель <> Неопределено Тогда
				ЗначениеГруппировки = ДанныеСтроки.Родитель.ЗначениеГруппировки;
			КонецЕсли;
			ЗначениеЭлемента = ДанныеСтроки.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	ОтображатьИерархию = ПолучитьГруппировкуУпорядочивания(ПолеДляУпорядочиванияНеРазобранныхПисем);
	
	СписокСвернутыхГрупп = Новый Соответствие;
	Если ОтображатьИерархию Тогда
		Для Каждого СтрокаДерева Из ДеревоНеРазобранныхПисем.Строки Цикл
			Если ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.Развернут(СтрокаДерева) Тогда
				Продолжить;
			КонецЕсли;
			
			СписокСвернутыхГрупп.Вставить(СтрокаДерева.ЗначениеГруппировки, СтрокаДерева.ЗначениеГруппировки);
		КонецЦикла;
	КонецЕсли;
	
	ДеревоНеРазобранныхПисем.Строки.Очистить();
	
	Выборка = ПолучитьСписокНеРазобранныхПисем(ПолеДляУпорядочиванияНеРазобранныхПисем);
	
	ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.Колонки.Тема.ОтображатьИерархию = ОтображатьИерархию;
	
	Если Выборка <> Неопределено Тогда
		Если ОтображатьИерархию Тогда
			Пока Выборка.СледующийПоЗначениюПоля(ПолеДляУпорядочиванияНеРазобранныхПисем) Цикл
				ЗначениеГруппировки = Выборка[ПолеДляУпорядочиванияНеРазобранныхПисем];
				Если ТипЗнч(ЗначениеГруппировки) = Тип("Дата") Тогда
					ИмяГруппировки = ФорматДаты(ЗначениеГруппировки, "Дата не установлена");
				Иначе
					ИмяГруппировки = ЗначениеГруппировки;
				КонецЕсли;
				
				Родитель = ДеревоНеРазобранныхПисем.Строки.Найти(ИмяГруппировки, "ЗначениеГруппировки");
				Если Родитель = Неопределено Тогда
					Родитель = ДеревоНеРазобранныхПисем.Строки.Добавить();
					Родитель.ЗначениеГруппировки = ИмяГруппировки;
				КонецЕсли;
				
				Пока Выборка.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(Родитель.Строки.Добавить(), Выборка);
				КонецЦикла;
				
				Родитель.Тема	= "" + ИмяГруппировки + " (" + Родитель.Строки.Количество() + ")";
				Если СписокСвернутыхГрупп.Получить(Родитель.ЗначениеГруппировки) = Неопределено Тогда
					ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.Развернуть(Родитель, Истина);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ДеревоНеРазобранныхПисем.Строки.Добавить(), Выборка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	НужнаяСтрока = ДеревоНеРазобранныхПисем.Строки.Найти(ЗначениеГруппировки, "ЗначениеГруппировки");
	Если ЗначениеЗаполнено(ЗначениеЭлемента) Тогда
		Если НужнаяСтрока <> Неопределено Тогда
			НужнаяСтрока = НужнаяСтрока.Строки.Найти(ЗначениеЭлемента, "Ссылка");
		КонецЕсли;
		Если НужнаяСтрока = Неопределено Тогда
			НужнаяСтрока = ДеревоЗаявокКандидатов.Строки.Найти(ЗначениеЭлемента, "Ссылка", Истина);
		КонецЕсли;
	КонецЕсли;
	Если НужнаяСтрока <> Неопределено Тогда
		ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущаяСтрока = НужнаяСтрока;
	КонецЕсли;
	
	Если Выборка <> Неопределено Тогда
		КоличествоНеРазобранныхПисем = Выборка.Количество();
	Иначе
		КоличествоНеРазобранныхПисем = 0;
	КонецЕсли;
	
	ЭлементыФормы.НадписьКоличествоНеРазобранныхПисем.Заголовок = " Не разобранные письма ("+КоличествоНеРазобранныхПисем+")";
	
	УстановитьТекстПустогоНеРазобранногоПисьма();
	
КонецПроцедуры

Процедура ПолучениеПисем()
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиЭлектроннойПочты) Тогда
		Предупреждение("У вас нет прав для работы с электронной почтой");
		Возврат;
	КонецЕсли;
	
	УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), ТекущийПользователь, мДоступныеУчетныеЗаписиДляЧтения, , , Истина);
	
	ОбновитьСписокНеРазобранныхПисем();
	ОбновитьСписокЗаявок();
	
КонецПроцедуры

Процедура УдалитьНеРазобранноеПисьмо(ДанныеСтроки)
	
	Если ДанныеСтроки.Родитель = Неопределено Тогда
		ДеревоНеРазобранныхПисем.Строки.Удалить(ДанныеСтроки);
	Иначе
		Родитель = ДанныеСтроки.Родитель;
		Родитель.Строки.Удалить(ДанныеСтроки);
		Если Родитель.Строки.Количество() = 0 Тогда
			ДеревоНеРазобранныхПисем.Строки.Удалить(Родитель);
		Иначе
			Родитель.Тема = Лев(Родитель.Тема, Найти(Родитель.Тема, "(")) + Родитель.Строки.Количество() + ")";
		КонецЕсли;
	КонецЕсли;
	
	КоличествоНеРазобранныхПисем = КоличествоНеРазобранныхПисем - 1;
	ЭлементыФормы.НадписьКоличествоНеРазобранныхПисем.Заголовок = " Не разобранные письма ("+КоличествоНеРазобранныхПисем+")";
	
КонецПроцедуры


Процедура ОбновитьОписаниеЗаявки()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДокHTML = ЭлементыФормы.ПолеHTMLДокументаОписаниеЗаявки.Документ;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ЗаявкаКандидата",	ДанныеСтроки.Ссылка);
	
	Запрос.Текст = НаборПерсоналаПереопределяемый.ПолучитьТекстЗапросаПоЗаявкам();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ИсточникИнформацииИд	= "";
	Если Выборка.ИсточникИнформацииНеЗаполнен Тогда
		ИсточникИнформации		= "не указан";
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ИсточникИнформацииУказан", Ложь);
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ИсточникИнформацииНеУказан", Истина);
	Иначе
		ИсточникИнформации		= Строка(Выборка.ИсточникИнформации);
		ИсточникИнформацииИд	= Строка(Выборка.ИсточникИнформации.УникальныйИдентификатор());
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ИсточникИнформацииУказан", Истина);
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ИсточникИнформацииНеУказан", Ложь);
	КонецЕсли;
	
	ГруппаЗаявокИд	= "";
	Если Выборка.ГруппаЗаявокНеЗаполнена Тогда
		ГруппаЗаявок	= "групп";
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ГруппаЗаявокУказана", Ложь);
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ГруппаЗаявокНеУказана", Истина);
	Иначе
		ГруппаЗаявок	= Строка(Выборка.ГруппаЗаявок);
		ГруппаЗаявокИд	= Строка(Выборка.ГруппаЗаявок.УникальныйИдентификатор());
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ГруппаЗаявокУказана", Истина);
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ГруппаЗаявокНеУказана", Ложь);
	КонецЕсли;
	
	ВыборкаФайлов = ПолучитьСписокФайловПоЗаявке(ДанныеСтроки.Ссылка);
	Если ВыборкаФайлов.Количество() > 0 Тогда
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ЕстьФайлы", Истина);
		СписокФайлов	= "";
		Пока ВыборкаФайлов.Следующий() Цикл
			ИдФайла = Строка(ВыборкаФайлов.Ссылка.УникальныйИдентификатор());
			СписокФайлов = СписокФайлов + "<A id=Команда href=""V8:OpenFile"" target="""+ИдФайла+""">" + ВыборкаФайлов.Наименование + "</A>&#160;&#160;";
		КонецЦикла;
	Иначе
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ЕстьФайлы", Ложь);
	КонецЕсли;
	
	РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "РазделительЗаявки", Не Выборка.КомментарийНеЗаполнен, "block");
	РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ДатаОткрытияЗаявки", Не Выборка.ДатаОткрытияНеУказана);
	РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "КомментарийЗаявки", Не Выборка.КомментарийНеЗаполнен, "block");
	
	ДокHTML.getElementById("ДатаОткрытияЗаявки").innerText			= "Кандидат рассматривается с "+Формат(Выборка.ДатаОткрытия, "ДФ='d ММММ'")+". ";
	ДокHTML.getElementById("ИсточникИнформации").innerHTML			= "<A id=Команда href=""V8:ChangeInformationSource"" target="""+ИсточникИнформацииИд+""">"+ИсточникИнформации+"</A>. ";
	ДокHTML.getElementById("ГруппаЗаявок").innerHTML				= "<A id=Команда href=""V8:ChangeApplicationGroup"" target="""+ГруппаЗаявокИд+""">"+ГруппаЗаявок+"</A>. ";
	ДокHTML.getElementById("СписокФайлов").innerHTML				= СписокФайлов;
	ДокHTML.getElementById("КомментарийЗаявки").innerHTML			= Выборка.Комментарий;
	
	НаборПерсоналаПереопределяемый.ДополнитьДокHTML(ДокHTML, ДанныеСтроки, Выборка, ЭтаФорма);
	
КонецПроцедуры // ОбновитьОписаниеЗаявки()

Процедура ЗаявкиКандидатовПриАктивизацииСтроки()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборПерсоналаПереопределяемый.СписокКандидатовПриАктивизацииСтрокиДополнительно(ЭтаФорма, ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов);
	
	Если ДанныеСтроки.Ссылка.Пустая() Тогда
		ЕстьПисьмаПоЗаявке = Ложь;
		Если АктивизированаЗаявкаКандидата Тогда
			АктивизированаЗаявкаКандидата = Ложь;
		КонецЕсли;
		ПерепискаПоЗаявке.Отбор.ЗаявкаКандидата.Значение = Справочники.ЗаявкиКандидатов.ПолучитьСсылку();
		ЭлементыФормы.ПолеHTMLДокументаОписаниеЗаявки.УстановитьТекст("<BODY scroll=auto rightMargin=0></BODY>");
		ТекущаяЗаявка = Неопределено;
		УстановитьТекстПустогоПисьмаПоЗаявке();
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка = ТекущаяЗаявка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не АктивизированаЗаявкаКандидата Тогда
		АктивизированаЗаявкаКандидата = Истина;
	КонецЕсли;
	
	ТекущаяЗаявка = ДанныеСтроки.Ссылка;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ЗаявкаКандидата",	ДанныеСтроки.Ссылка);
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.ЭлектронноеПисьмо) Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ЭлектронноеПисьмо.Ссылка
		|ИЗ
		|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
		|ГДЕ
		|	ЭлектронноеПисьмо.ЗаявкаКандидата = &ЗаявкаКандидата
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектронноеПисьмо.ДатаОтправления УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		ЕстьПисьмаПоЗаявке = Выборка.Следующий();
	Иначе
		ЕстьПисьмаПоЗаявке = Ложь;
	КонецЕсли;
	
	ПерепискаПоЗаявке.Отбор.ЗаявкаКандидата.Значение = ТекущаяЗаявка;
	
	Если ЕстьПисьмаПоЗаявке Тогда
		ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущаяСтрока = Выборка.Ссылка;
	Иначе
		УстановитьТекстПустогоПисьмаПоЗаявке();
	КонецЕсли;
	
	Запрос.Текст = НаборПерсоналаПереопределяемый.ПолучитьТекстЗапросаПоЗаявкам();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ИсточникИнформации1		= "none";
	ИсточникИнформации2		= "none";
	ИсточникИнформацииИд	= "";
	Если Выборка.ИсточникИнформацииНеЗаполнен Тогда
		ИсточникИнформации2		= "inline";
		ИсточникИнформации		= "не указан";
	Иначе
		ИсточникИнформации1		= "inline";
		ИсточникИнформации		= Строка(Выборка.ИсточникИнформации);
		ИсточникИнформацииИд	= Строка(Выборка.ИсточникИнформации.УникальныйИдентификатор());
	КонецЕсли;
	
	ГруппаЗаявок1	= "none";
	ГруппаЗаявок2	= "none";
	ГруппаЗаявокИд	= "";
	Если Выборка.ГруппаЗаявокНеЗаполнена Тогда
		ГруппаЗаявок2	= "inline";
		ГруппаЗаявок	= "групп";
	Иначе
		ГруппаЗаявок1	= "inline";
		ГруппаЗаявок	= Строка(Выборка.ГруппаЗаявок);
		ГруппаЗаявокИд	= Строка(Выборка.ГруппаЗаявок.УникальныйИдентификатор());
	КонецЕсли;
	
	ВыборкаФайлов = ПолучитьСписокФайловПоЗаявке(ДанныеСтроки.Ссылка);
	Если ВыборкаФайлов.Количество() > 0 Тогда
		ЕстьФайлы	= "inline";
		СписокФайлов	= "";
		Пока ВыборкаФайлов.Следующий() Цикл
			ИдФайла = Строка(ВыборкаФайлов.Ссылка.УникальныйИдентификатор());
			СписокФайлов = СписокФайлов + "<A id=Команда href=""V8:OpenFile"" target="""+ИдФайла+""">" + ВыборкаФайлов.Наименование + "</A>&#160;&#160;";
		КонецЦикла;
	Иначе
		ЕстьФайлы	= "none";
	КонецЕсли;
	
	Если Выборка.ДатаОткрытияНеУказана Тогда
		ЕстьДатаОткрытия	= "none";
	Иначе
		ЕстьДатаОткрытия	= "inline";
	КонецЕсли;
	
	Если Выборка.КомментарийНеЗаполнен Тогда
		ЕстьКомментарий	= "none";
	Иначе
		ЕстьКомментарий	= "block";
	КонецЕсли;
	
	ТекстМакетаОписанияЗаявки =
	"<HTML>
	|	<HEAD>
	|		<META http-equiv=Content-Type content=""text/html; charset=utf-8"">
	|		<STYLE type=text/css>
	|			DIV {
	|				PADDING: 1px 0px 1px 1px;
	|				BORDER-TOP-COLOR: #dadac4;
	|				BORDER-RIGHT-COLOR: #dadac4;
	|				BORDER-BOTTOM-COLOR: #dadac4;
	|				BORDER-LEFT-COLOR: #dadac4;
	|			}
	|			A {
	|				PADDING-TOP: 1px;
	|				PADDING-BOTTOM: 1px;
	|			}
	|			BODY {
	|				SCROLLBAR-FACE-COLOR: #fffbf0;
	|				SCROLLBAR-HIGHLIGHT-COLOR: #dadac4;
	|				SCROLLBAR-3DLIGHT-COLOR: #fff; MARGIN: 5px;
	|				SCROLLBAR-ARROW-COLOR: #708090;
	|				SCROLLBAR-TRACK-COLOR: #fffbf0;
	|				SCROLLBAR-DARKSHADOW-COLOR: #fff;
	|				MARGIN: 5px 0px 5px 5px;
	|			}
	|		</STYLE>
	|	</HEAD>
	|
	|	<BODY aLink=#000 vLink=#000 link=#000 scroll=auto><FONT face=""MS Sans Serif"" size=1>
	|		<DIV>
	|			<P id=ДатаОткрытияЗаявки style=""DISPLAY: "+ЕстьДатаОткрытия+""">Кандидат рассматривается с "+Формат(Выборка.ДатаОткрытия, "ДФ='d ММММ'")+". </P>
	|
	|			<P id=ИсточникИнформацииУказан style=""DISPLAY:"+ИсточникИнформации1+""">Источник, из которого кандидат узнал о вакансии: </P>
	|			<P id=ИсточникИнформацииНеУказан style=""DISPLAY:"+ИсточникИнформации2+""">Источник, из которого кандидат узнал о вакансии, </P>
	|			<P id=ИсточникИнформации style=""DISPLAY:inline""><A id=Команда href=""V8:ChangeInformationSource"" target="""+ИсточникИнформацииИд+""">"+ИсточникИнформации+"</A>. </P>
	|
	|
	|			<P id=ГруппаЗаявокУказана style=""DISPLAY:"+ГруппаЗаявок1+""">Кандидат отнесен к группе: </P>
	|			<P id=ГруппаЗаявокНеУказана style=""DISPLAY:"+ГруппаЗаявок2+""">Кандидат не отнесен ни к одной из </P>
	|			<P id=ГруппаЗаявок style=""DISPLAY:inline""><A id=Команда href=""V8:ChangeApplicationGroup"" target="""+ГруппаЗаявокИд+""">"+ГруппаЗаявок+"</A>. </P>";
	
	НаборПерсоналаПереопределяемый.ДополнитьМакет(ЭтаФорма, Выборка, ТекстМакетаОписанияЗаявки, ДанныеСтроки.Ссылка);
	
	ТекстМакетаОписанияЗаявки = ТекстМакетаОписанияЗаявки + "
	|			<P id=ЕстьФайлы style=""DISPLAY:"+ЕстьФайлы+""">К кандидату приложены файлы: </P>
	|			<P id=СписокФайлов style=""DISPLAY:"+ЕстьФайлы+""">"+СписокФайлов+". </P>
	|		</DIV>
	|		<HR id=РазделительЗаявки style=""DISPLAY:"+ЕстьКомментарий+"""/>
	|		<DIV id=КомментарийЗаявки style=""DISPLAY:"+ЕстьКомментарий+""">"+СтрЗаменить(Выборка.Комментарий, Символ(10), "<BR>")+"</DIV>
	|	</FONT></BODY>
	|</HTML>";
	
	ЭлементыФормы.ПолеHTMLДокументаОписаниеЗаявки.УстановитьТекст(ТекстМакетаОписанияЗаявки);
	
КонецПроцедуры

Процедура ОбновитьСписокЗаявок()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	ЗначениеГруппировки	= 0;
	ЗначениеЭлемента	= 0;
	Если ДанныеСтроки <> Неопределено Тогда
		Если ДанныеСтроки.Ссылка.Пустая() Тогда
			ЗначениеГруппировки = ДанныеСтроки.ЗначениеГруппировки;
		Иначе
			Если ДанныеСтроки.Родитель <> Неопределено Тогда
				ЗначениеГруппировки = ДанныеСтроки.Родитель.ЗначениеГруппировки;
			КонецЕсли;
			ЗначениеЭлемента = ДанныеСтроки.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	ОтображатьИерархию = ПолучитьГруппировкуУпорядочивания(ПолеДляУпорядочиванияЗаявок);
	
	СписокСвернутыхГрупп = Новый Соответствие;
	Если ОтображатьИерархию Тогда
		Для Каждого СтрокаДерева Из ДеревоЗаявокКандидатов.Строки Цикл
			Если ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.Развернут(СтрокаДерева) Тогда
				Продолжить;
			КонецЕсли;
			
			СписокСвернутыхГрупп.Вставить(СтрокаДерева.ЗначениеГруппировки, СтрокаДерева.ЗначениеГруппировки);
		КонецЦикла;
	КонецЕсли;
	
	ДеревоЗаявокКандидатов.Строки.Очистить();
	
	Выборка = НаборПерсоналаПереопределяемый.ПолучитьСписокЗаявок(ПолеДляУпорядочиванияЗаявок, мДоступныеУчетныеЗаписиДляЧтения);
	
	ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.Колонки.Представление.ОтображатьИерархию = ОтображатьИерархию;
	
	Если ОтображатьИерархию Тогда
		Пока Выборка.СледующийПоЗначениюПоля(ПолеДляУпорядочиванияЗаявок) Цикл
			ЗначениеГруппировки = Выборка[ПолеДляУпорядочиванияЗаявок];
			Если ТипЗнч(ЗначениеГруппировки) = Тип("Дата") Тогда
				ИмяГруппировки = ФорматДаты(ЗначениеГруппировки, "Еще не рассматривались");
			Иначе
				ИмяГруппировки = ЗначениеГруппировки;
			КонецЕсли;
			Если ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка.СостоянияЗаявокКандидатов") И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Состояние не указано";
			ИначеЕсли ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка.Вакансии") И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Вакансия не назначена";
			ИначеЕсли ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка.ПодразделенияОрганизаций") И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Подразделение не назначено";
			ИначеЕсли ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка.Пользователи") И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Ответственный не назначен";
			ИначеЕсли ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка." + НаборПерсоналаПереопределяемый.ПолучитьНаименованиеСправочникаДолжности()) И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Должность не назначена";
			ИначеЕсли ТипЗнч(ИмяГруппировки) = Тип("СправочникСсылка.ГруппыЗаявокКандидатов") И ИмяГруппировки.Пустая() Тогда
				ИмяГруппировки = "Группа не назначена";
			Иначе
				НаборПерсоналаПереопределяемый.ПроверитьДополнительныеУсловияИмяГруппировки(ИмяГруппировки);
			КонецЕсли;
			
			Если ТипЗнч(ЗначениеГруппировки) = Тип("Дата") Тогда
				Родитель = ДеревоЗаявокКандидатов.Строки.Найти(ИмяГруппировки, "ЗначениеГруппировки");
			Иначе
				Родитель = ДеревоЗаявокКандидатов.Строки.Найти(ЗначениеГруппировки, "ЗначениеГруппировки");
			КонецЕсли;
			Если Родитель = Неопределено Тогда
				Родитель = ДеревоЗаявокКандидатов.Строки.Добавить();
				Родитель.ВажностьКартинка = -1;
				Если ТипЗнч(ЗначениеГруппировки) = Тип("Дата") Тогда
					Родитель.ЗначениеГруппировки = ИмяГруппировки;
				Иначе
					Родитель.ЗначениеГруппировки = ЗначениеГруппировки;
				КонецЕсли;
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				Строка = Родитель.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(Строка, Выборка);
				Строка.ВажностьКартинка	= ?(Выборка.Важность.Пустая(), 1, Перечисления.Важность.Индекс(Выборка.Важность));
			КонецЦикла;
			
			Родитель.Наименование	= ИмяГруппировки;
			Родитель.Количество		= Родитель.Строки.Количество();
			Если СписокСвернутыхГрупп.Получить(Родитель.ЗначениеГруппировки) = Неопределено Тогда
				ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.Развернуть(Родитель, Истина);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Пока Выборка.Следующий() Цикл
			Строка = ДеревоЗаявокКандидатов.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, Выборка);
			Строка.ВажностьКартинка	= ?(Выборка.Важность.Пустая(), 1, Перечисления.Важность.Индекс(Выборка.Важность));
		КонецЦикла;
		
	КонецЕсли;
	
	НужнаяСтрока = ДеревоЗаявокКандидатов.Строки.Найти(ЗначениеГруппировки, "ЗначениеГруппировки");
	Если ЗначениеЗаполнено(ЗначениеЭлемента) Тогда
		Если НужнаяСтрока <> Неопределено Тогда
			НужнаяСтрока = НужнаяСтрока.Строки.Найти(ЗначениеЭлемента, "Ссылка");
		КонецЕсли;
		Если НужнаяСтрока = Неопределено Тогда
			НужнаяСтрока = ДеревоЗаявокКандидатов.Строки.Найти(ЗначениеЭлемента, "Ссылка", Истина);
		КонецЕсли;
	КонецЕсли;
	Если НужнаяСтрока <> Неопределено Тогда
		ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущаяСтрока = НужнаяСтрока;
	КонецЕсли;
	
	ЭлементыФормы.НадписьКоличествоАктивныхЗаявок.Заголовок = " Активные кандидаты ("+Выборка.Количество()+")";
	
	УстановитьТекстПустогоПисьмаПоЗаявке();
	
КонецПроцедуры

Процедура ОбновитьПодменюУказатьИсточникИнформации()
	
	Кнопки = ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов.Кнопки.ПодменюЗаявки.Кнопки.УказатьИсточникИнформации.Кнопки;
	Кнопки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ " + мКоличествоЭлементовВПодменю + "
	|	ИсточникиИнформации.Ссылка,
	|	ИсточникиИнформации.Наименование
	|ИЗ
	|	Справочник.ИсточникиИнформации КАК ИсточникиИнформации
	|ГДЕ
	|	(НЕ ИсточникиИнформации.ПометкаУдаления)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоСостояний = 1;
	Пока Выборка.Следующий() Цикл
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "Кнопка"+СтрЗаменить(Выборка.Ссылка.УникальныйИдентификатор(), "-", "_");
		НоваяКнопка.Текст		= Выборка.Наименование;
		НоваяКнопка.Действие	= Новый Действие("УказатьИсточникИнформации");
		
		Если КоличествоСостояний = мКоличествоЭлементовВПодменю-1 Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоСостояний = КоличествоСостояний + 1;
	КонецЦикла;
	
	Если (Выборка.Количество() = 0) ИЛИ (Выборка.Количество() > мКоличествоЭлементовВПодменю - 1) Тогда
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Разделитель;
		
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "ВсеИсточники";
		НоваяКнопка.Текст		= "Открыть список всех источников информации";
		НоваяКнопка.Действие	= Новый Действие("ОткрытьСписокВсехИсточников");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПодменюИзменитьСостояние()
	
	Кнопки = ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов.Кнопки.ПодменюЗаявки.Кнопки.ИзменитьСостояние.Кнопки;
	Кнопки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ " + мКоличествоЭлементовВПодменю + "
	|	СостоянияЗаявокКандидатов.Ссылка,
	|	СостоянияЗаявокКандидатов.Наименование
	|ИЗ
	|	Справочник.СостоянияЗаявокКандидатов КАК СостоянияЗаявокКандидатов
	|ГДЕ
	|	(НЕ СостоянияЗаявокКандидатов.ПометкаУдаления)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоСостояний = 1;
	Пока Выборка.Следующий() Цикл
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "Кнопка"+СтрЗаменить(Выборка.Ссылка.УникальныйИдентификатор(), "-", "_");
		НоваяКнопка.Текст		= Выборка.Наименование;
		НоваяКнопка.Действие	= Новый Действие("ИзменитьСостояние");
		
		Если КоличествоСостояний = мКоличествоЭлементовВПодменю-1 Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоСостояний = КоличествоСостояний + 1;
	КонецЦикла;
	
	Если (Выборка.Количество() = 0) ИЛИ (Выборка.Количество() > мКоличествоЭлементовВПодменю - 1) Тогда
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Разделитель;
		
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "ВсеСостояния";
		НоваяКнопка.Текст		= "Открыть список всех состояний";
		НоваяКнопка.Действие	= Новый Действие("ОткрытьСписокВсехСостояний");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПодменюНазначитьВакансию()
	
	Кнопки = ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов.Кнопки.ПодменюЗаявки.Кнопки.НазначитьВакансию.Кнопки;
	Кнопки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ " + мКоличествоЭлементовВПодменю + "
	|	Вакансии.Ссылка,
	|	Вакансии.Наименование
	|ИЗ
	|	Справочник.Вакансии КАК Вакансии
	|ГДЕ
	|	(НЕ Вакансии.ПометкаУдаления)
	|	И (НЕ Вакансии.Закрыта)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоСостояний = 1;
	Пока Выборка.Следующий() Цикл
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "Кнопка"+СтрЗаменить(Выборка.Ссылка.УникальныйИдентификатор(), "-", "_");
		НоваяКнопка.Текст		= Выборка.Наименование;
		НоваяКнопка.Действие	= Новый Действие("НазначитьВакансию");
		
		Если КоличествоСостояний = мКоличествоЭлементовВПодменю-1 Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоСостояний = КоличествоСостояний + 1;
	КонецЦикла;
	
	Если (Выборка.Количество() = 0) ИЛИ (Выборка.Количество() > мКоличествоЭлементовВПодменю - 1) Тогда
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Разделитель;
		
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "ВсеВакансии";
		НоваяКнопка.Текст		= "Открыть список всех открытых вакансий";
		НоваяКнопка.Действие	= Новый Действие("ОткрытьСписокВсехВакансий");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПодменюИзменитьГруппуЗаявки()
	
	Кнопки = ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов.Кнопки.ПодменюЗаявки.Кнопки.ИзменитьГруппуЗаявки.Кнопки;
	Кнопки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ " + мКоличествоЭлементовВПодменю + "
	|	ГруппыЗаявокКандидатов.Ссылка,
	|	ГруппыЗаявокКандидатов.Наименование
	|ИЗ
	|	Справочник.ГруппыЗаявокКандидатов КАК ГруппыЗаявокКандидатов
	|ГДЕ
	|	(НЕ ГруппыЗаявокКандидатов.ПометкаУдаления)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоСостояний = 1;
	Пока Выборка.Следующий() Цикл
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "Кнопка"+СтрЗаменить(Выборка.Ссылка.УникальныйИдентификатор(), "-", "_");
		НоваяКнопка.Текст		= Выборка.Наименование;
		НоваяКнопка.Действие	= Новый Действие("ИзменитьГруппуЗаявок");
		
		Если КоличествоСостояний = мКоличествоЭлементовВПодменю-1 Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоСостояний = КоличествоСостояний + 1;
	КонецЦикла;
	
	Если (Выборка.Количество() = 0) ИЛИ (Выборка.Количество() > мКоличествоЭлементовВПодменю - 1) Тогда
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Разделитель;
		
		НоваяКнопка = Кнопки.Добавить();
		НоваяКнопка.ТипКнопки	= ТипКнопкиКоманднойПанели.Действие;
		НоваяКнопка.Имя			= "ВсеГруппыЗаявок";
		НоваяКнопка.Текст		= "Открыть список всех групп заявок";
		НоваяКнопка.Действие	= Новый Действие("ОткрытьСписокВсехГруппЗаявок");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиЗаявкуКандидата(ДанныеСтроки, НовыйРодитель)
	
	СтарыйРодитель	= ДанныеСтроки.Родитель;
	Если НовыйРодитель <> Неопределено Тогда
		НоваяСтрока = НовыйРодитель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
		
		СтарыйРодитель.Строки.Удалить(ДанныеСтроки);
		Если СтарыйРодитель.Строки.Количество() = 0 Тогда
			ДеревоЗаявокКандидатов.Строки.Удалить(СтарыйРодитель);
		Иначе
			СтарыйРодитель.Количество = СтарыйРодитель.Строки.Количество();
		КонецЕсли;
		
		НовыйРодитель.Количество = НовыйРодитель.Строки.Количество();
		
		ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущаяСтрока = НоваяСтрока;
	КонецЕсли;
	
	ОбновитьОписаниеЗаявки();
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитЗаявкиПоГруппировке(ДанныеСтроки, Объект)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.Родитель = Неопределено Тогда
		ЗначениеГруппировки = ДанныеСтроки.ЗначениеГруппировки;
	Иначе
		ЗначениеГруппировки = ДанныеСтроки.Родитель.ЗначениеГруппировки;
	КонецЕсли;
	
	Если		ТипЗнч(ЗначениеГруппировки) = Тип("СправочникСсылка.Вакансии") Тогда
		Объект.Вакансия		= ЗначениеГруппировки;
		
	ИначеЕсли	ТипЗнч(ЗначениеГруппировки) = Тип("СправочникСсылка.ГруппыЗаявокКандидатов") Тогда
		Объект.ГруппаЗаявок		= ЗначениеГруппировки;
		
	ИначеЕсли	ТипЗнч(ЗначениеГруппировки) = Тип("СправочникСсылка.Пользователи") Тогда
		Объект.Ответственный	= ЗначениеГруппировки;
		
	Иначе 
		НаборПерсоналаПереопределяемый.ПроверитьДополнительныеУсловияЗначенияГруппировки(ЗначениеГруппировки, Объект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПеретаскиваниеОбъектов(Объект, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	
	Ссылка = Объект.Ссылка;
	
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка =	НЕ (ПолеДляУпорядочиванияЗаявок = "ДатаСобытия" ИЛИ ПолеДляУпорядочиванияЗаявок = "Наименование" ИЛИ
							((ПолеДляУпорядочиванияЗаявок = "Подразделение" ИЛИ ПолеДляУпорядочиванияЗаявок = "Должность") И Не Ссылка.Вакансия.Пустая()));
	
КонецПроцедуры

Процедура ПроверитьПеретаскиваниеОбъектов(Объект, СтандартнаяОбработка, Строка, Колонка)
	
	СтандартнаяОбработка = Истина;
	
	Если ТипЗнч(Объект) = Тип("СтрокаДереваЗначений") Тогда
		Ссылка = Объект.Ссылка;
		
		Если Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЗаявкиКандидатов") Тогда
			
			СтандартнаяОбработка =  НаборПерсоналаПереопределяемый.СтандартнаяОбработкаПоДополнительномуУсловию(Строка, ПолеДляУпорядочиванияЗаявок, Ссылка);
			
		КонецЕсли;
		
	Иначе
		СтандартнаяОбработка = ТипЗнч(Объект) <> Тип("ДокументСсылка.ЭлектронноеПисьмо");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеретаскиваниеОбъектов(Элемент, ЗначениеПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	Если ТипЗнч(ЗначениеПеретаскивания) = Тип("СтрокаДереваЗначений") Тогда
		ОбъектПеретаскивания = ЗначениеПеретаскивания.Ссылка;
		
		Если ТипЗнч(ОбъектПеретаскивания) = Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
			Если Строка <> Неопределено И Не Строка.Ссылка.Пустая() Тогда
				ЗаявкаКандидата	= Строка.Ссылка;
				
				СтандартнаяОбработка = Ложь;
				
				ОбъектПисьмо = ОбъектПеретаскивания.ПолучитьОбъект();
				ОбъектПисьмо.ЗаявкаКандидата = ЗаявкаКандидата;
				ОбъектПисьмо.Записать();
				
				УдалитьНеРазобранноеПисьмо(ЗначениеПеретаскивания);
				
				ПерепискаПоЗаявке.Обновить();
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ОбъектПеретаскивания) = Тип("СправочникСсылка.ЗаявкиКандидатов") Тогда
			Если Строка.ЗначениеГруппировки = Неопределено Тогда
				НужнаяСтрока = Строка.Родитель;
			Иначе
				НужнаяСтрока = Строка;
			КонецЕсли;
			
			СтандартнаяОбработка = Ложь;
			
			Если НЕ НаборПерсоналаПереопределяемый.ИзмененСоставГруппировкиПоДополнительномуУсловию(НужнаяСтрока, ОбъектПеретаскивания) Тогда
				
				ОбъектЗаявка = ОбъектПеретаскивания.ПолучитьОбъект();
				ЗаполнитьРеквизитЗаявкиПоГруппировке(НужнаяСтрока, ОбъектЗаявка);
				ОбъектЗаявка.Записать();
				
			КонецЕсли;
			
			ПеренестиЗаявкуКандидата(ЗначениеПеретаскивания, НужнаяСтрока);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗначениеПеретаскивания) = Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
		ЗаявкаКандидата = Строка.Ссылка;
		
		Если ЗаявкаКандидата.Пустая() Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		Если НЕ НаборПерсоналаПереопределяемый.ИзмененСоставГруппировкиПоДополнительномуУсловию(НужнаяСтрока, ОбъектПеретаскивания) Тогда
			
			ОбъектПисьмо = ЗначениеПеретаскивания.ПолучитьОбъект();
			ОбъектПисьмо.ЗаявкаКандидата = ЗаявкаКандидата;
			ОбъектПисьмо.Записать();
			
		КонецЕсли;
		
		ПерепискаПоЗаявке.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОткрытьФормуВыбораИсточникаИнформации(Значение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;

	Форма = Справочники.ИсточникиИнформации.ПолучитьФормуВыбора(, ЭтаФорма, ЭтотОбъект);
	Форма.СправочникСписок.Отбор.ПометкаУдаления.Установить(Ложь);
	
	ТипСправочника = Тип("СправочникСсылка.ИсточникиИнформации");
	Если ТипЗнч(Значение) = ТипСправочника Тогда
		Форма.НачальноеЗначениеВыбора = Значение;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		Попытка
			Форма.НачальноеЗначениеВыбора = XMLЗначение(ТипСправочника, Значение);
		Исключение КонецПопытки;
		
	КонецЕсли;
	
	Форма.ВладелецФормы			= ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов;
	Форма.Открыть();
	
КонецПроцедуры

Процедура ЗаписатьИсточникИнформации(ВыбранноеЗначение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено или ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки) Цикл
		Если ВыделеннаяСтрока.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		ЗаявкаОбъект = ВыделеннаяСтрока.Ссылка.ПолучитьОбъект();
		ЗаявкаОбъект.ИсточникИнформации = ВыбранноеЗначение;
		ЗаявкаОбъект.Записать();
	КонецЦикла;
	
	ОбновитьОписаниеЗаявки();
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораСостоянияЗаявки(Значение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;

	Форма = Справочники.СостоянияЗаявокКандидатов.ПолучитьФормуВыбора(, ЭтаФорма, ЭтотОбъект);
	Форма.СправочникСписок.Отбор.ПометкаУдаления.Установить(Ложь);
	
	ТипСправочника = Тип("СправочникСсылка.СостоянияЗаявокКандидатов");
	Если ТипЗнч(Значение) = ТипСправочника Тогда
		Форма.НачальноеЗначениеВыбора = Значение;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		Попытка
			Форма.НачальноеЗначениеВыбора = XMLЗначение(ТипСправочника, Значение);
		Исключение КонецПопытки;
		
	КонецЕсли;
	
	Форма.ВладелецФормы			= ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов;
	Форма.Открыть();
	
КонецПроцедуры

Процедура ЗаписатьСостояниеЗаявки(ВыбранноеЗначение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено или ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ТекущаяРаботаПоЗаявкамКандидатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ОбщегоНазначения.ПолучитьРабочуюДату());
	
	Для Каждого ВыделеннаяСтрока Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки) Цикл
		Если ВыделеннаяСтрока.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Отбор.ЗаявкаКандидата.Установить(ВыделеннаяСтрока.Ссылка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			Строка = НаборЗаписей.Добавить();
			Строка.Период			= ОбщегоНазначения.ПолучитьРабочуюДату();
			Строка.ЗаявкаКандидата	= ВыделеннаяСтрока.Ссылка;
		Иначе
			Строка = НаборЗаписей[0];
		КонецЕсли;
		
		Строка.Состояние			= ВыбранноеЗначение;
		
		НаборЗаписей.Записать();
	КонецЦикла;
	
	Если ПолеДляУпорядочиванияЗаявок = "Состояние" Тогда
		ОбновитьСписокЗаявок();
	КонецЕсли;
	
	ОбновитьОписаниеЗаявки();
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораВакансии(Значение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОткрытьФорму("Справочник.Вакансии.ФормаВыбора", , ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов);
	
КонецПроцедуры

Процедура ЗаписатьВакансию(ВыбранноеЗначение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено или ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки) Цикл
		Если ВыделеннаяСтрока.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		ЗаявкаОбъект = ВыделеннаяСтрока.Ссылка.ПолучитьОбъект();
		ЗаявкаОбъект.Вакансия = ВыбранноеЗначение;
		РеквизитыВакансии = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВыбранноеЗначение, "Должность,Подразделение,Организация");
		ЗаполнитьЗначенияСвойств(ЗаявкаОбъект, РеквизитыВакансии);
		ЗаявкаОбъект.Записать();
		Оповестить("ЗаписанаЗаявкаКандидата", Новый Структура("Физлицо, Ссылка", Неопределено, ЗаявкаОбъект.Ссылка));
	КонецЦикла;
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораГруппыЗаявок(Значение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;

	Форма = Справочники.ГруппыЗаявокКандидатов.ПолучитьФормуВыбора(, ЭтаФорма, ЭтотОбъект);
	Форма.СправочникСписок.Отбор.ПометкаУдаления.Установить(Ложь);
	
	ТипСправочника = Тип("СправочникСсылка.ГруппыЗаявокКандидатов");
	Если ТипЗнч(Значение) = ТипСправочника Тогда
		Форма.НачальноеЗначениеВыбора = Значение;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		Попытка
			Форма.НачальноеЗначениеВыбора = XMLЗначение(ТипСправочника, Значение);
		Исключение КонецПопытки;
		
	КонецЕсли;
	
	Форма.ВладелецФормы			= ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов;
	Форма.Открыть();
	
КонецПроцедуры

Процедура ЗаписатьГруппуЗаявки(ВыбранноеЗначение)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено или ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки) Цикл
		Если ВыделеннаяСтрока.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		ЗаявкаОбъект = ВыделеннаяСтрока.Ссылка.ПолучитьОбъект();
		ЗаявкаОбъект.ГруппаЗаявок = ВыбранноеЗначение;
		ЗаявкаОбъект.Записать();
	КонецЦикла;
	
	Если ПолеДляУпорядочиванияЗаявок = "ГруппаЗаявок" Тогда
		ОбновитьСписокЗаявок();
	КонецЕсли;
	
	ОбновитьОписаниеЗаявки();
	
КонецПроцедуры


Процедура УстановитьТекстПустогоПисьмаПоЗаявке()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	ЗаявкаНеВыбрана = ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая();
		
	Если ЗаявкаНеВыбрана Тогда
		ТекстСообщения = "Не выбран кандидат";
	Иначе
		Если ЕстьПисьмаПоЗаявке Тогда
			ТекстСообщения = "Письмо не выбрано. Текст письма не может быть показан";
		Иначе
			ТекстСообщения = "Нет писем по кандидату";
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыФормы.ПолеHTMLДокументаПерепискаПоЗаявке.УстановитьТекст("<BODY scroll=auto rightMargin=0><FONT face=""MS Sans Serif"" size=1><P align=center>"+ТекстСообщения+"</P></FONT></BODY>");
	
КонецПроцедуры

Процедура ПерепискаПоЗаявкеПриАктивизацииСтроки()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		УстановитьТекстПустогоПисьмаПоЗаявке();
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",	ДанныеСтроки.УчетнаяЗапись);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.АвтоматическаяУстановкаПометкиРассмотрено,
	|	УчетныеЗаписиЭлектроннойПочты.ИнтервалАвтоматическойУстановкиОтметкиРассмотрено
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка = &УчетнаяЗапись";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Ошибка получения реквизитов учетной записи: " + ДанныеСтроки.УчетнаяЗапись);
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.HTMLСКартинками Тогда
		НайденноеСоответствие = глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем").Получить(ДанныеСтроки.Ссылка);
		Если НайденноеСоответствие = Неопределено Тогда
			КопияТекстаПисьма = ДанныеСтроки.ТекстПисьма;
			УправлениеЭлектроннойПочтой.ПропарситьHTMLИДВ_ТекстКартинки(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), ТекущийПользователь, ДанныеСтроки.Ссылка, КопияТекстаПисьма);
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "</BODY>", "</FONT></BODY>");
			КопияТекстаПисьма = СтрЗаменить(КопияТекстаПисьма, "<BODY", "<BODY scroll=auto");
			ЭлементыФормы.ПолеHTMLДокументаПерепискаПоЗаявке.УстановитьТекст(КопияТекстаПисьма);
			
		Иначе
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "</BODY>", "</FONT></BODY>");
			НайденноеСоответствие = СтрЗаменить(НайденноеСоответствие, "<BODY", "<BODY scroll=auto");
			ЭлементыФормы.ПолеHTMLДокументаПерепискаПоЗаявке.УстановитьТекст(НайденноеСоответствие);
			
		КонецЕсли;
		
	ИначеЕсли ДанныеСтроки.ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.Текст Тогда
		ЭлементыФормы.ПолеHTMLДокументаПерепискаПоЗаявке.УстановитьТекст(УправлениеЭлектроннойПочтой.ВернутьТекстПисьмаВФорматеHTML(ДанныеСтроки.ТекстПисьма));
		
	Иначе
		ТекстПисьма = ДанныеСтроки.ТекстПисьма;
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "<BODY>", "<BODY><FONT face=""MS Sans Serif"" size=1>");
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "</BODY>", "</FONT></BODY>");
		ТекстПисьма = СтрЗаменить(ТекстПисьма, "<BODY", "<BODY scroll=auto");
		ЭлементыФормы.ПолеHTMLДокументаПерепискаПоЗаявке.УстановитьТекст(ТекстПисьма);
		
	КонецЕсли;
	
	Если ДанныеСтроки.НеРассмотрено И Выборка.АвтоматическаяУстановкаПометкиРассмотрено Тогда
		ПисьмоКРассмотрению = ДанныеСтроки.Ссылка;
		ПодключитьОбработчикОжидания("АвтоустановкаРассмотренностиПисьма", Выборка.ИнтервалАвтоматическойУстановкиОтметкиРассмотрено, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработчика ожидания, которая контролирует автоматическую установку флага прочитанности
// эл.писем, настройка устанавливается в параметрах учетной записи.
// (подключается через обработчик ожидания)
Процедура АвтоустановкаРассмотренностиПисьма()
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка = ПисьмоКРассмотрению Тогда		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ Ответственный КАК Ответственный ИЗ Документ.ЭлектронноеПисьмо ГДЕ Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДанныеСтроки.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураРеквизитовКИзменению = Новый Структура("Ответственный, НеРассмотрено");
			СтруктураРеквизитовКИзменению.Ответственный = Выборка.Ответственный;
			Если СтруктураРеквизитовКИзменению.Ответственный.Пустая() Тогда
				СтруктураРеквизитовКИзменению.Ответственный = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
			КонецЕсли;
			Если СтруктураРеквизитовКИзменению.Ответственный.Пустая() Тогда
				СтруктураРеквизитовКИзменению.Ответственный	= ТекущийПользователь;
			КонецЕсли;
			СтруктураРеквизитовКИзменению.НеРассмотрено = Ложь;
			ПолныеПраваЗК.УстановитьРеквизитЭлектронногоПисьма(ДанныеСтроки.Ссылка, СтруктураРеквизитовКИзменению);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ВыполнитьКомандуФормы(Команда, Значение) Экспорт
	
	Если Команда = "ChangeStatus" Тогда
		ОткрытьФормуВыбораСостоянияЗаявки(Значение);
	
	ИначеЕсли Команда = "ChangeInformationSource" Тогда
		ОткрытьФормуВыбораИсточникаИнформации(Значение);
		
	ИначеЕсли Команда = "ChangeVacancy" Тогда
		ОткрытьФормуВыбораВакансии(Значение);
		
	ИначеЕсли Команда = "ChangeApplicationGroup" Тогда
		ОткрытьФормуВыбораГруппыЗаявок(Значение);
		
	ИначеЕсли Команда = "OpenVacancy" Тогда
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
	
		Если Не ПустаяСтрока(Значение) Тогда
			ВыбранныйСправочник	= XMLЗначение(Тип("СправочникСсылка.Вакансии"), Значение);
			ВыбранныйСправочник.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
		КонецЕсли;
		
	ИначеЕсли Команда = "OpenOrganizationDivision" ИЛИ Команда = "OpenDivision" Тогда
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
	
		Если Не ПустаяСтрока(Значение) Тогда
			ВыбранныйСправочник	= XMLЗначение(Тип("СправочникСсылка." + НаборПерсоналаПереопределяемый.ПолучитьНаименованиеСправочникаПодразделения()), Значение);
			ВыбранныйСправочник.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
		КонецЕсли;
		
	ИначеЕсли Команда = "OpenPosition" Тогда
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
	
		Если Не ПустаяСтрока(Значение) Тогда
			ВыбранныйСправочник	= XMLЗначение(Тип("СправочникСсылка." + НаборПерсоналаПереопределяемый.ПолучитьНаименованиеСправочникаДолжности()), Значение);
			ВыбранныйСправочник.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть()
		КонецЕсли;
		
	ИначеЕсли Команда = "OpenFile" Тогда
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
	
		Если Не ПустаяСтрока(Значение) Тогда
			ВыбранныйСправочник	= XMLЗначение(Тип("СправочникСсылка.ХранилищеДополнительнойИнформации"), Значение);
			ОткрытьСохранитьВложение(ВыбранныйСправочник);
		КонецЕсли;
		
	ИначеЕсли Команда = "mailto:" Тогда // Адреса электронной почты
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураНовогоПисьма = Новый Структура;
		СтруктураНовогоПисьма.Вставить("ЗаявкаКандидата",	ДанныеСтроки.ЗаявкаКандидата);
		СтруктураНовогоПисьма.Вставить("Тема",				ДанныеСтроки.Тема);
		
		СписокКому = Новый СписокЗначений;
		СписокКому.Добавить(ВернутьСтрокуСоВсемиСимволами(Значение), "");
		СтруктураНовогоПисьма.Вставить("Кому", СписокКому);
		
		УправлениеЭлектроннойПочтой.НаписатьПисьмо(ТекущийПользователь, СтруктураНовогоПисьма,,,,, ЭтаФорма);
		
	ИначеЕсли Команда = "NewAccount" Тогда
		Справочники.УчетныеЗаписиЭлектроннойПочты.ПолучитьФормуНовогоЭлемента(, ЭтаФорма).Открыть();
		
	ИначеЕсли Команда = "HideUnreadMail" Тогда
		ПоказыватьНеРазобранныеПисьма = Ложь;
		УстановитьВидимостьНеРазобранныхПисем();
		
	Иначе
		
		НаборПерсоналаПереопределяемый.ПроверитьДополнительныеУсловияКомандыФормы(Команда, ЭлементыФормы, Значение, ЭтаФорма, ЭтотОбъект);
		
		ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		УправлениеЭлектроннойПочтой.ПерейтиПоСсылкеИзХТМЛПоля(Значение, ТекущийПользователь, ЭтаФорма, ДанныеСтроки.УчетнаяЗапись);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьУпорядочиваниеЗаявок() Экспорт
	
	СпособыУпорядочивания = Новый СписокЗначений;
	СпособыУпорядочивания.Добавить("ДатаСобытия",	"По дате события");
	СпособыУпорядочивания.Добавить("Состояние",		"По состоянию");
	СпособыУпорядочивания.Добавить("Наименование",	"По наименованию");
	СпособыУпорядочивания.Добавить("Вакансия",		"По вакансии");
	СпособыУпорядочивания.Добавить("Ответственный",	"По ответственному");
	СпособыУпорядочивания.Добавить("Подразделение",	"По подразделению");
	СпособыУпорядочивания.Добавить("Должность",		"По должности");
	СпособыУпорядочивания.Добавить("ГруппаЗаявок",	"По группе заявок");
	ВыбранныйСпособ = СпособыУпорядочивания.ВыбратьЭлемент("Выберите способ упорядочивания", СпособыУпорядочивания.НайтиПоЗначению(ПолеДляУпорядочиванияЗаявок));
	Если ВыбранныйСпособ <> Неопределено Тогда
		ПолеДляУпорядочиванияЗаявок = ВыбранныйСпособ.Значение;
		ОбновитьСписокЗаявок();
		УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
		
		ОбновитьОписаниеЗаявки();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПометкуПодменюУпорядочить(КоманднаяПанель, ПолеДляУпорядочивания)
	
	ПодменюУпорядочить = КоманднаяПанель.Кнопки.ПодменюУпорядочить;
	Для Каждого ЭлементПодменю Из ПодменюУпорядочить.Кнопки Цикл
		ЭлементПодменю.Пометка = ЭлементПодменю.Имя = ПолеДляУпорядочивания;
	КонецЦикла;
	
КонецПроцедуры

Функция ФорматДаты(Дата, СообщениеДляПустойДаты)
	
	ДатаСобытия = НачалоДня(Дата);
	
	Сегодня			= НачалоДня(ОбщегоНазначения.ПолучитьРабочуюДату());
	Вчера			= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -1);
	Позавчера		= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -2);
	ДнейНазад3		= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -3);
	ДнейНазад4		= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -4);
	ДнейНазад5		= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -5);
	ДнейНазад6		= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.День, -6);
	НедельНазад1	= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.Неделя, -1);
	НедельНазад2	= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.Неделя, -2);
	НедельНазад3	= ОбщегоНазначения.ДобавитьИнтервал(Сегодня, Перечисления.Периодичность.Неделя, -3);
	МесяцНазад1		= ДобавитьМесяц(Сегодня, -1);
	МесяцНазад2		= ДобавитьМесяц(Сегодня, -2);
	
	Если Не ЗначениеЗаполнено(ДатаСобытия) Тогда
		Возврат СообщениеДляПустойДаты;
	ИначеЕсли ДатаСобытия = Сегодня Тогда
		Возврат "Сегодня, " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = Вчера Тогда
		Возврат "Вчера, " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = Позавчера Тогда
		Возврат "Позавчера, " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = ДнейНазад3 Тогда
		Возврат ""+Перечисления.ДниНедели[ДеньНедели(ДатаСобытия)-1]+", " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = ДнейНазад4 Тогда
		Возврат ""+Перечисления.ДниНедели[ДеньНедели(ДатаСобытия)-1]+", " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = ДнейНазад5 Тогда
		Возврат ""+Перечисления.ДниНедели[ДеньНедели(ДатаСобытия)-1]+", " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли ДатаСобытия = ДнейНазад6 Тогда
		Возврат ""+Перечисления.ДниНедели[ДеньНедели(ДатаСобытия)-1]+", " + Формат(ДатаСобытия, "ДФ='д ММММ'");
	ИначеЕсли НедельНазад1 >= ДатаСобытия И ДатаСобытия > НедельНазад2 Тогда
		Возврат "Неделю назад";
	ИначеЕсли НедельНазад2 >= ДатаСобытия И ДатаСобытия > НедельНазад3 Тогда
		Возврат "2 недели назад";
	ИначеЕсли НедельНазад3 >= ДатаСобытия И ДатаСобытия > МесяцНазад1 Тогда
		Возврат "3 недели назад";
	ИначеЕсли МесяцНазад1 >= ДатаСобытия И ДатаСобытия > МесяцНазад2 Тогда
		Возврат "Месяц назад";
	ИначеЕсли ДатаСобытия > Сегодня Тогда
		Возврат "В будущем";
	Иначе
		Возврат "Совсем давно";
	КонецЕсли;
	
КонецФункции

Процедура ОткрытьСохранитьВложение(ВыбранныйЭлемент)
	
	ОткликФормы = ПолучитьФорму("ФормаОткрытияВложений").ОткрытьМодально();
	
	Если ОткликФормы = Неопределено ИЛИ ТипЗнч(ОткликФормы) <> Тип("Булево") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОткликФормы Тогда
		РаботаСФайлами.ОткрытьФайлы(ВыбранныйЭлемент, , Ложь);
	Иначе
		
		СохраненноеИмяКаталога = ВосстановитьЗначение("ИмяКаталогаСохраненияФайлов");
		Если СохраненноеИмяКаталога = Неопределено Тогда
			ИмяКаталога = РаботаСФайлами.ПолучитьИмяКаталога();
		Иначе
			ИмяКаталога = СохраненноеИмяКаталога;
		КонецЕсли;
		
		ТолькоЧтение = Ложь;
		
		ФормаСохраненияФайлов = Справочники.ХранилищеДополнительнойИнформации.ПолучитьФорму("ФормаСохраненияФайлов");
		ФормаСохраненияФайлов.ИмяКаталога		= ИмяКаталога;
		ФормаСохраненияФайлов.ТолькоЧтение		= ТолькоЧтение;
		ФормаСохраненияФайлов.ОткрытьКаталог	= Ложь;
		СтруктураПараметров = ФормаСохраненияФайлов.ОткрытьМодально();
		
		Если СтруктураПараметров = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Не РаботаСФайлами.ПроверитьСуществованиеКаталога(СтруктураПараметров.ИмяКаталога) Тогда
			Возврат;
		КонецЕсли;
		
		СохранитьЗначение("ИмяКаталогаСохраненияФайлов", СтруктураПараметров.ИмяКаталога);
		
		СпособПерезаписи = "";
		
		ИмяФайла = РаботаСФайлами.ПолучитьИмяФайла(СтруктураПараметров.ИмяКаталога, РаботаСФайлами.УдалитьЗапрещенныеСимволыИмени(ВыбранныйЭлемент.ИмяФайла));
		Состояние("Сохраняется файл: " + ИмяФайла);
		РаботаСФайлами.СохранитьФайлНаДиске(ВыбранныйЭлемент.Хранилище, ИмяФайла, СтруктураПараметров.ТолькоЧтение, СпособПерезаписи);
		
		Если СтруктураПараметров.ОткрытьКаталог Тогда
			ЗапуститьПриложение(СтруктураПараметров.ИмяКаталога);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Функция ВернутьСтрокуСоВсемиСимволами(Знач Источник)
	
	Пока Найти(Источник, "%") > 0 Цикл
		
		ТекПозиция = Найти(Источник, "%");
		
		КодСимвола = ОбщегоНазначенияЗК.ШестнадцатиричноеВДесятичное(Сред(Источник, ТекПозиция+1, 2));
		
		Источник = СтрЗаменить(Источник, Сред(Источник, ТекПозиция, 3), Символ(КодСимвола));
		
	КонецЦикла;
	
	Возврат Источник;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Не разобранные письма
	
	ПоказыватьНеРазобранныеПисьма = ВосстановитьЗначение("НаборПерсоналаПоказыватьНеРазобранныеПисьма");
	Если ПоказыватьНеРазобранныеПисьма = Неопределено Тогда
		ПоказыватьНеРазобранныеПисьма = Истина;
	КонецЕсли;
	УстановитьВидимостьНеРазобранныхПисем();
	
	ПолеДляУпорядочиванияНеРазобранныхПисем = ВосстановитьЗначение("НаборПерсоналаПолеДляУпорядочиванияНеРазобранныхПисем");
	Если ПолеДляУпорядочиванияНеРазобранныхПисем = Неопределено Тогда
		ПолеДляУпорядочиванияНеРазобранныхПисем = "ДатаПолучения";
	КонецЕсли;
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельНеРазобранныеПисьма, ПолеДляУпорядочиванияНеРазобранныхПисем);
	
	ОбновитьСписокНеРазобранныхПисем();
	
	// Кандидаты
	
	ПерепискаПоЗаявке.Отбор.ЗаявкаКандидата.Использование = Истина;
	
	ПерепискаПоЗаявке.Колонки.Добавить("ВидТекстаПисьма");
	ПерепискаПоЗаявке.Колонки.Добавить("ТекстПисьма");
	ПерепискаПоЗаявке.Колонки.Добавить("НеРассмотрено");
	ПерепискаПоЗаявке.Колонки.Добавить("УчетнаяЗапись");
	ПерепискаПоЗаявке.Колонки.Добавить("РассмотретьПосле");
	ПерепискаПоЗаявке.Колонки.Добавить("СтатусПисьма");
	ПерепискаПоЗаявке.Колонки.Добавить("ЗаявкаКандидата");
	ПерепискаПоЗаявке.Колонки.Добавить("Тема");
	
	ПерепискаПоЗаявке.Порядок.Установить("ДатаОтправления УБЫВ");
	
	ПолеДляУпорядочиванияЗаявок = ВосстановитьЗначение("НаборПерсоналаПолеДляУпорядочиванияЗаявок");
	Если ПустаяСтрока(ПолеДляУпорядочиванияЗаявок) Тогда
		ПолеДляУпорядочиванияЗаявок = "Состояние";
	КонецЕсли;
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
	ПерепискаПоЗаявке.Отбор.ЗаявкаКандидата.Значение = Справочники.ЗаявкиКандидатов.ПолучитьСсылку();
	ЭлементыФормы.ПолеHTMLДокументаОписаниеЗаявки.УстановитьТекст("<BODY scroll=auto rightMargin=0></BODY>");
	УстановитьТекстПустогоПисьмаПоЗаявке();
	
	ОбновитьСписокЗаявок();
	
	ОбновитьПодменюУказатьИсточникИнформации();
	ОбновитьПодменюИзменитьСостояние();
	ОбновитьПодменюНазначитьВакансию();
	ОбновитьПодменюИзменитьГруппуЗаявки();
	
	// Справка формы
	ОтображатьСправкуФормы = ВосстановитьЗначение("НаборПерсоналаСправкаФормы");
	Если ОтображатьСправкуФормы = Неопределено Тогда
		ОтображатьСправкуФормы = Истина;
	КонецЕсли;
	
	ЕстьДоступныеУчетныеЗаписи = мДоступныеУчетныеЗаписиДляЗаписи.Количество() > 0;
	
	ПодготовитьСправкуФормы();
	
	НаборПерсоналаПереопределяемый.ДополнитьКонтекстныеМеню(ЭлементыФормы, Новый Действие("ДополнительныеДействия"));
	
	НаборПерсоналаПереопределяемый.ФормаПередОткрытиемДополнительно(ЭтаФорма, Новый Действие("ДополнительныеДействия"));
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УстановитьВидимостьСправкиФормы(Ложь);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СохранитьЗначение("НаборПерсоналаСправкаФормы",	ОтображатьСправкуФормы);
	
	СохранитьЗначение("НаборПерсоналаПоказыватьНеРазобранныеПисьма",			ПоказыватьНеРазобранныеПисьма);
	СохранитьЗначение("НаборПерсоналаПолеДляУпорядочиванияНеРазобранныхПисем",	ПолеДляУпорядочиванияНеРазобранныхПисем);
	
	СохранитьЗначение("НаборПерсоналаПолеДляУпорядочиванияЗаявок",	ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписаноЭлектронноеПисьмо" Тогда
		СтрокаТабличнойЧасти = ДеревоНеРазобранныхПисем.Строки.Найти(Параметр, "Ссылка", Истина);
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			Запрос = Новый Запрос;
			
			Запрос.УстановитьПараметр("Ссылка",	Параметр);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭлектронноеПисьмо.НеРассмотрено,
			|	ЭлектронноеПисьмо.РассмотретьПосле,
			|	ЭлектронноеПисьмо.СтатусПисьма,
			|	ЭлектронноеПисьмо.Тема,
			|	ВЫБОР
			|		КОГДА ЭлектронноеПисьмо.НеРассмотрено
			|				И ЭлектронноеПисьмо.ЗаявкаКандидата = ЗНАЧЕНИЕ(Справочник.ЗаявкиКандидатов.ПустаяСсылка)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК УдалитьПисьмо
			|ИЗ
			|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
			|ГДЕ
			|	ЭлектронноеПисьмо.Ссылка = &Ссылка";
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			
				Если Выборка.УдалитьПисьмо Тогда
					УдалитьНеРазобранноеПисьмо(СтрокаТабличнойЧасти);
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			ОбновитьСписокНеРазобранныхПисем();
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.ЭлектронноеПисьмо) Тогда
			Запрос = Новый Запрос;
			
			Запрос.УстановитьПараметр("ЭлектронноеПисьмо",	Параметр);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СУММА(1) КАК КоличествоНеразобранныхПисем,
			|	ЭлектронноеПисьмо.ЗаявкаКандидата
			|ИЗ
			|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
			|ГДЕ
			|	ЭлектронноеПисьмо.НеРассмотрено
			|	И ЭлектронноеПисьмо.ЗаявкаКандидата В
			|			(ВЫБРАТЬ
			|				ЭлектронноеПисьмо.ЗаявкаКандидата
			|			ИЗ
			|				Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
			|			ГДЕ
			|				ЭлектронноеПисьмо.Ссылка = &ЭлектронноеПисьмо)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЭлектронноеПисьмо.ЗаявкаКандидата";
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ДанныеСтроки = ДеревоЗаявокКандидатов.Строки.Найти(Выборка.ЗаявкаКандидата, "Ссылка", Истина);
				
				Если ДанныеСтроки <> Неопределено Тогда
					ДанныеСтроки.Количество = Выборка.КоличествоНеразобранныхПисем;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗаписанаЗаявкаКандидата" Тогда
		ОбновитьСписокЗаявок();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ИмяСобытия = "ЗаписанаВакансия" Тогда
		ОбновитьПодменюНазначитьВакансию();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ИмяСобытия = "ЗаписанИсточникИнформации" Тогда
		ОбновитьПодменюУказатьИсточникИнформации();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ИмяСобытия = "ЗаписаноСостояниеЗаявки" Тогда
		ОбновитьПодменюИзменитьСостояние();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ИмяСобытия = "ЗаписанаГруппаЗаявок" Тогда
		ОбновитьПодменюИзменитьГруппуЗаявки();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ИмяСобытия = "ЗаписанаВстреча" Тогда
		Если ПолеДляУпорядочиванияЗаявок = "ДатаСобытия" Тогда
			ОбновитьСписокЗаявок();
		КонецЕсли;
		
		ОбновитьОписаниеЗаявки();
	
	ИначеЕсли ИмяСобытия = "РоботПолученияПисем" Тогда
		ОбновитьСписокНеРазобранныхПисем();
		ОбновитьСписокЗаявок();
	
	ИначеЕсли ИмяСобытия = "ОбновитьФорму" Тогда
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли НаборПерсоналаПереопределяемый.ОбновитьСписокЗаявокПоДополнительномуСобытию(ИмяСобытия, ПолеДляУпорядочиванияЗаявок) Тогда
		ОбновитьСписокЗаявок();
		
	ИначеЕсли НаборПерсоналаПереопределяемый.ОбновитьОписаниеЗаявкиПоДополнительномуСобытию(ИмяСобытия) Тогда
		ОбновитьОписаниеЗаявки();
		
	КонецЕсли;
	
	НаборПерсоналаПереопределяемый.ФормаОбработкаОповещенияДополнительно(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

Процедура ОбработкаЗаписиНовогоОбъекта(Объект, Источник)
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.ЗаявкиКандидатов")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.ЗаявкиКандидатов") Тогда
		СписокПисемКУдалению = Новый Массив;
		
		Если ПисьмаДляЗаявки <> Неопределено Тогда
			Для Каждого ТекущееПисьмо Из ПисьмаДляЗаявки Цикл
				ОбъектПисьмо = ТекущееПисьмо.Ссылка.ПолучитьОбъект();
				ОбъектПисьмо.ЗаявкаКандидата		= Объект.Ссылка;
				Если ОбъектПисьмо.Ответственный.Пустая() Тогда
					ОбъектПисьмо.Ответственный		= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
				КонецЕсли;
				Если ОбъектПисьмо.Ответственный.Пустая() Тогда
					ОбъектПисьмо.Ответственный	= ТекущийПользователь;
				КонецЕсли;
				ОбъектПисьмо.НеРассмотрено			= Ложь;
				ОбъектПисьмо.Записать();
				
				СписокПисемКУдалению.Добавить(ТекущееПисьмо);
			КонецЦикла;
			
			Для Каждого ТекущееПисьмо Из СписокПисемКУдалению Цикл
				УдалитьНеРазобранноеПисьмо(ТекущееПисьмо);
			КонецЦикла;
		КонецЕсли;
		
		ОбновитьСписокЗаявок();
		ОбновитьОписаниеЗаявки();
		
		ПисьмаДляЗаявки = Неопределено;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.СотрудникиОрганизаций")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		Если ЗаявкаДляСотрудника = Неопределено ИЛИ Источник.ВладелецФормы <> ЭтаФорма Тогда
			Возврат;
		КонецЕсли;
		
		ОбъектЗаявка = ЗаявкаДляСотрудника.Ссылка.ПолучитьОбъект();
		ОбъектЗаявка.Физлицо	= Объект.Физлицо;
		ОбъектЗаявка.Записать();
		
		ЗаявкаДляСотрудника = Неопределено;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Вакансии")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.Вакансии") Тогда
		ОбновитьПодменюНазначитьВакансию();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.СостоянияЗаявокКандидатов")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.СостоянияЗаявокКандидатов") Тогда
		ОбновитьПодменюИзменитьСостояние();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ИсточникиИнформации")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.ИсточникиИнформации") Тогда
		ОбновитьПодменюУказатьИсточникИнформации();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ГруппыЗаявокКандидатов")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.ГруппыЗаявокКандидатов") Тогда
		ОбновитьПодменюИзменитьГруппуЗаявки();
		ОбновитьОписаниеЗаявки();
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.УчетныеЗаписиЭлектроннойПочты")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
		ПолучитьДоступныеУчетныеЗаписи();
		ЕстьДоступныеУчетныеЗаписи = мДоступныеУчетныеЗаписиДляЗаписи.Количество() > 0;
		ОбновитьСправкуФормы();
		
	ИначеЕсли НаборПерсоналаПереопределяемый.ЯвляетсяОбъектомДополнительногоТипа(Объект) Тогда
		ОбновитьОписаниеЗаявки();
	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Верхняя командная панель

Процедура ДействияФормыОтчетПоЗаявкамКандидатов(Кнопка)
	
	Отчет = Отчеты.ОтчетПоЗаявкамКандидатов.Создать();
	Форма = Отчет.ПолучитьФорму();
	Форма.Открыть();
	Форма.ОбновитьОтчет();
	
КонецПроцедуры

Процедура ДействияФормыЭффективностьЗатратНаПривлечение(Кнопка)
	
	Отчет = Отчеты.ЭффективностьЗатратНаПривлечение.Создать();
	Форма = Отчет.ПолучитьФорму();
	Форма.Открыть();
	Форма.ОбновитьОтчет();
	
КонецПроцедуры

Процедура ОткрытьКадровоеПланирование(Кнопка)
	
	Обработки.КадровоеПланирование.ПолучитьФорму().Открыть();
	
КонецПроцедуры

Процедура ПерейтиКВакансиям(Кнопка)
	
	Справочники.Вакансии.ПолучитьФормуСписка().Открыть();
	
КонецПроцедуры

Процедура ПерейтиКИсточникам(Кнопка)
	
	Справочники.ИсточникиИнформации.ПолучитьФормуСписка().Открыть();
	
КонецПроцедуры

Процедура ПерейтиКСостояниям(Кнопка)
	
	Справочники.СостоянияЗаявокКандидатов.ПолучитьФормуСписка().Открыть();
	
КонецПроцедуры

Процедура ПереключитьВидимостьСправкиФормы(Кнопка)
	
	ОтображатьСправкуФормы = НЕ ОтображатьСправкуФормы;
	УстановитьВидимостьСправкиФормы();
	
КонецПроцедуры

// Не разобранные письма

Процедура ИзменитьВидимостьСпискаПисем(Элемент)
	
	ПоказыватьНеРазобранныеПисьма = Не ПоказыватьНеРазобранныеПисьма;
	
	УстановитьВидимостьНеРазобранныхПисем();
	
КонецПроцедуры

Процедура ОткрытьПисьмо(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.Ссылка.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
	
КонецПроцедуры

Процедура ПринятьКакЗаявку(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Справочники.ЗаявкиКандидатов.ПолучитьФормуНовогоЭлемента(, ЭтаФорма, ЭтотОбъект);
	Форма.Наименование	= ДанныеСтроки.Тема;
	Форма.Открыть();
	
	ПисьмаДляЗаявки = ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ВыделенныеСтроки);
	
КонецПроцедуры

Процедура ПривязатьКСуществующейЗаявке(Кнопка)
	
	ДанныеСтроки		= ЭлементыФормы.ТабличноеПолеНеразобранныеПисьма.ТекущиеДанные;
	ДанныеСтрокиЗаявки	= ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтрокиЗаявки = Неопределено ИЛИ ДанныеСтрокиЗаявки.ЗначениеГруппировки <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектПисьмо = ДанныеСтроки.Ссылка.ПолучитьОбъект();
	ОбъектПисьмо.ЗаявкаКандидата = ДанныеСтрокиЗаявки.Ссылка;
	ОбъектПисьмо.Записать();
	
	УдалитьНеРазобранноеПисьмо(ДанныеСтроки);
	
	ПерепискаПоЗаявке.Обновить();
	
КонецПроцедуры

Процедура ПометитьКакПрочтенное(Кнопка)
	
	СписокПисемКУдалению = Новый Массив;
	
	Для Каждого ВыбранноеПисьмо Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ВыделенныеСтроки) Цикл
		Если ВыбранноеПисьмо.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ Ответственный КАК Ответственный ИЗ Документ.ЭлектронноеПисьмо ГДЕ Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ВыбранноеПисьмо.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураРеквизитовКИзменению = Новый Структура("Ответственный, НеРассмотрено");
			СтруктураРеквизитовКИзменению.Ответственный = Выборка.Ответственный;
			Если СтруктураРеквизитовКИзменению.Ответственный.Пустая() Тогда
				СтруктураРеквизитовКИзменению.Ответственный = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
			КонецЕсли;
			Если СтруктураРеквизитовКИзменению.Ответственный.Пустая() Тогда
				СтруктураРеквизитовКИзменению.Ответственный	= ТекущийПользователь;
			КонецЕсли;
			СтруктураРеквизитовКИзменению.НеРассмотрено = Ложь;
			ПолныеПраваЗК.УстановитьРеквизитЭлектронногоПисьма(ВыбранноеПисьмо.Ссылка, СтруктураРеквизитовКИзменению);
		КонецЕсли;		
		СписокПисемКУдалению.Добавить(ВыбранноеПисьмо);
		
	КонецЦикла;
	
	Для Каждого ВыбранноеПисьмо Из СписокПисемКУдалению Цикл
		УдалитьНеРазобранноеПисьмо(ВыбранноеПисьмо);
	КонецЦикла;
	
	ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ВыделенныеСтроки.Очистить();
	
КонецПроцедуры

Процедура ПереместитьВНежелательнуюПочту(Кнопка)
	
	СписокПисемКУдалению = Новый Массив;
	
	Для Каждого ВыбранноеПисьмо Из ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ВыделенныеСтроки) Цикл
		Если ВыбранноеПисьмо.Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		УчетнаяЗапись = ВыбранноеПисьмо.Ссылка.УчетнаяЗапись;
		Если УчетнаяЗапись.Пустая() Тогда
			Сообщить("В письме не указана учетная запись. Перемещение письма в нежелательную почту невозможно!");
			Продолжить;
		КонецЕсли;
		
		ПапкаНежелательнаяПочта = УчетнаяЗапись.ГруппаНежелательные;
		Если ПапкаНежелательнаяПочта.Пустая() Тогда
			ТекстВопроса =	"Для учетной записи " + УчетнаяЗапись + " не указана папка, в которую необходимо помещать нежелательную почту.
							|Хотите открыть учетную запись для настройки папки нежелательных писем?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				УчетнаяЗапись.ПолучитьФорму().Открыть();
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		ЭлектронноеПисьмоОбъект = ВыбранноеПисьмо.Ссылка.ПолучитьОбъект();
		Если ЭлектронноеПисьмоОбъект.Ответственный.Пустая() Тогда
			ЭлектронноеПисьмоОбъект.Ответственный	= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
		КонецЕсли;
		Если ЭлектронноеПисьмоОбъект.Ответственный.Пустая() Тогда
			ЭлектронноеПисьмоОбъект.Ответственный	= ТекущийПользователь;
		КонецЕсли;
		ЭлектронноеПисьмоОбъект.НеРассмотрено		= Ложь;
		ЭлектронноеПисьмоОбъект.ГруппаУчетнойЗаписи	= ПапкаНежелательнаяПочта;
		ЭлектронноеПисьмоОбъект.Записать();
		
		СписокПисемКУдалению.Добавить(ВыбранноеПисьмо);
	КонецЦикла;
	
	Для Каждого ВыбранноеПисьмо Из СписокПисемКУдалению Цикл
		УдалитьНеРазобранноеПисьмо(ВыбранноеПисьмо);
	КонецЦикла;
	
	ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ВыделенныеСтроки.Очистить();
	
КонецПроцедуры

Процедура ПринятьПочту(Кнопка)
	
	ПолучениеПисем();
	
КонецПроцедуры

Процедура ПоДате(Кнопка)
	
	ПолеДляУпорядочиванияНеРазобранныхПисем = "ДатаПолучения";
	ОбновитьСписокНеРазобранныхПисем();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельНеРазобранныеПисьма, ПолеДляУпорядочиванияНеРазобранныхПисем);
	
КонецПроцедуры

Процедура ПоАдресуОтправителя(Кнопка)
	
	ПолеДляУпорядочиванияНеРазобранныхПисем = "Отправитель";
	ОбновитьСписокНеРазобранныхПисем();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельНеРазобранныеПисьма, ПолеДляУпорядочиванияНеРазобранныхПисем);
	
КонецПроцедуры

Процедура ВсеПисьма(Кнопка)
	
	Если Не ПравоДоступа("Использование", Метаданные.Обработки.МенеджерКонтактов) Тогда
		Предупреждение("У вас нет прав для работы с электронной почтой");
		Возврат;
	КонецЕсли;
	
	Форма = Обработки.МенеджерКонтактов.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект);
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено И НЕ ДанныеСтроки.Ссылка.Пустая() Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДанныеСтроки.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭлектронноеПисьмо.УчетнаяЗапись,
		|	ЭлектронноеПисьмо.ГруппаУчетнойЗаписи
		|ИЗ
		|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
		|ГДЕ
		|	ЭлектронноеПисьмо.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Форма.УчетнаяЗапись											= Выборка.УчетнаяЗапись;
			Форма.ЭлементыФормы.ГруппыПисемДерево.ТекущаяСтрока			= Выборка.ГруппаУчетнойЗаписи;
			Форма.ЭлементыФормы.ЭлектронныеПисьмаСписок.ТекущаяСтрока	= ДанныеСтроки.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.Открыть();
	
КонецПроцедуры

// Активные заявки кандидатов

Процедура ДобавитьЗаявку(Кнопка)
	
	Форма = Справочники.ЗаявкиКандидатов.ПолучитьФормуНовогоЭлемента(, ЭтаФорма, ЭтотОбъект);
	ЗаполнитьРеквизитЗаявкиПоГруппировке(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные, Форма);
	Форма.Открыть();
	
КонецПроцедуры

Процедура ОткрытьЗаявку(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.Ссылка.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
	
КонецПроцедуры

Процедура УказатьИсточникИнформации(Кнопка)
	
	ВыбранноеЗначение = Справочники.ИсточникиИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрЗаменить(Сред(Кнопка.Имя, 7), "_", "-")));
	
	ЗаписатьИсточникИнформации(ВыбранноеЗначение);
	
КонецПроцедуры

Процедура ОткрытьСписокВсехИсточников(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораИсточникаИнформации(ДанныеСтроки.Ссылка.ИсточникИнформации);
	
КонецПроцедуры

Процедура ИзменитьСостояние(Кнопка)
	
	ВыбранноеЗначение = Справочники.СостоянияЗаявокКандидатов.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрЗаменить(Сред(Кнопка.Имя, 7), "_", "-")));
	
	ЗаписатьСостояниеЗаявки(ВыбранноеЗначение);
	
КонецПроцедуры

Процедура ОткрытьСписокВсехСостояний(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораСостоянияЗаявки(ДанныеСтроки.Ссылка.Состояние);
	
КонецПроцедуры

Процедура НазначитьВакансию(Кнопка)
	
	ВыбранноеЗначение = Справочники.Вакансии.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрЗаменить(Сред(Кнопка.Имя, 7), "_", "-")));
	
	ЗаписатьВакансию(ВыбранноеЗначение);
	
КонецПроцедуры

Процедура ОткрытьСписокВсехВакансий(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораВакансии(ДанныеСтроки.Ссылка.Вакансия);
	
КонецПроцедуры

Процедура ИзменитьГруппуЗаявок(Кнопка)
	
	ВыбранноеЗначение = Справочники.ГруппыЗаявокКандидатов.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрЗаменить(Сред(Кнопка.Имя, 7), "_", "-")));
	
	ЗаписатьГруппуЗаявки(ВыбранноеЗначение);
	
КонецПроцедуры

Процедура ОткрытьСписокВсехГруппЗаявок(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораГруппыЗаявок(ДанныеСтроки.Ссылка.Состояние);
	
КонецПроцедуры

Процедура ДобавитьФайлы(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;

	СтруктураДляСпискаИзображений = Новый Структура("ОтборОбъектИспользование, ОтборОбъектЗначение, ДоступностьОтбораОбъекта, ВидимостьКолонкиОбъекта", Истина, ДанныеСтроки.Ссылка, Ложь, Ложь);
	СтруктураДляСпискаДополнительныхФайлов = Новый Структура("ОтборОбъектИспользование, ОтборОбъектЗначение, ДоступностьОтбораОбъекта, ВидимостьКолонкиОбъекта", Истина, ДанныеСтроки.Ссылка, Ложь, Ложь);
	ОбязательныеОтборы = Новый Структура("Объект", ДанныеСтроки.Ссылка);
	
	РаботаСФайлами.ОткрытьФормуСпискаФайловИИзображений(СтруктураДляСпискаИзображений, СтруктураДляСпискаДополнительныхФайлов, ОбязательныеОтборы, ЭтаФорма);
	
КонецПроцедуры

Процедура ОбновитьЗаявки(Кнопка)
	
	ОбновитьСписокЗаявок();
	
КонецПроцедуры

Процедура ПоДатеСобытия(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "ДатаСобытия";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоСостоянию(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Состояние";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоАлфавиту(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Наименование";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоВакансии(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Вакансия";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоОтветственному(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Ответственный";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоПодразделению(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Подразделение";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоДолжности(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "Должность";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ПоГруппеЗаявок(Кнопка)
	
	ПолеДляУпорядочиванияЗаявок = "ГруппаЗаявок";
	ОбновитьСписокЗаявок();
	УстановитьПометкуПодменюУпорядочить(ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов, ПолеДляУпорядочиванияЗаявок);
	
КонецПроцедуры

Процедура ВсеЗаявки(Кнопка)
	
	Справочники.ЗаявкиКандидатов.ПолучитьФормуСписка(, ЭтаФорма, ЭтотОбъект).Открыть();
	
КонецПроцедуры

// Письма по кандидату

Процедура НовоеПисьмо(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНовогоПисьма = Новый Структура;
	СтруктураНовогоПисьма.Вставить("ЗаявкаКандидата",	ДанныеСтроки.Ссылка);
	СтруктураНовогоПисьма.Вставить("Тема",				ДанныеСтроки.Наименование);
	
	СписокКому = Новый СписокЗначений;
	
	АдресПолучателя = "";
	Если Не ДанныеСтроки.Ссылка.Физлицо.Пустая() Тогда
		АдресПолучателя = УправлениеЭлектроннойПочтой.ОпределитьАдресПолучателя(ДанныеСтроки.Ссылка.Физлицо);
		Если Не ПустаяСтрока(АдресПолучателя) Тогда
			СписокКому.Добавить(АдресПолучателя, АдресПолучателя);
		КонецЕсли;
	КонецЕсли;
		
	Если ПустаяСтрока(АдресПолучателя) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ЗаявкаКандидата",	ДанныеСтроки.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ЭлектронноеПисьмо.ОтправительИмя КАК Имя,
		|	ЭлектронноеПисьмо.ОтправительАдресЭлектроннойПочты КАК АдресЭлектроннойПочты
		|ИЗ
		|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
		|ГДЕ
		|	ЭлектронноеПисьмо.ЗаявкаКандидата = &ЗаявкаКандидата
		|	И ЭлектронноеПисьмо.СтатусПисьма = ЗНАЧЕНИЕ(Перечисление.СтатусыПисем.Полученное)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектронноеПисьмо.ДатаОтправления УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СписокКому.Добавить(Выборка.АдресЭлектроннойПочты, Выборка.Имя);
		КонецЕсли;
	КонецЕсли;
	
	СтруктураНовогоПисьма.Вставить("Кому", СписокКому);
	
	УправлениеЭлектроннойПочтой.НаписатьПисьмо(ТекущийПользователь, СтруктураНовогоПисьма,,,,, ЭтаФорма);
	
КонецПроцедуры

Процедура СкопироватьПисьмо(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыйОбъект = ДанныеСтроки.Ссылка.Скопировать();
	НовыйОбъект.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
	
КонецПроцедуры

Процедура ИзменитьПисьмо(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличноеПолеПерепискаПоЗаявкеПередНачаломИзменения(ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке, Ложь);
	
КонецПроцедуры

Процедура УдалитьПисьмо(Кнопка)
	
	Элемент = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке;
	
	Если Элемент.ВыделенныеСтроки.Количество() > 0 Тогда
		
		Если Элемент.ВыделенныеСтроки.Количество() = 1 Тогда
			Если Элемент.ТекущиеДанные.ПометкаУдаления Тогда
				СтрокаВопроса = "Снять с объекта пометку на удаление?";
			Иначе
				СтрокаВопроса = "Пометить объект на удаление?";
			КонецЕсли;
		Иначе
			ФлагУдаления = Ложь;
			ФлагНеУдаления = Ложь;
			Для каждого Строка Из Элемент.ВыделенныеСтроки Цикл
				Если Строка.ПометкаУдаления Тогда
					ФлагУдаления = Истина;
				Иначе
					ФлагНеУдаления = Истина;
				КонецЕсли;
				Если ФлагНеУдаления И ФлагУдаления Тогда
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
			Если ФлагНеУдаления И ФлагУдаления Тогда
				СтрокаВопроса = "Изменить у объектов пометку на удаление?";
			ИначеЕсли ФлагУдаления Тогда
				СтрокаВопроса = "Снять с объекта пометку на удаление?";
			Иначе
				СтрокаВопроса = "Пометить объект на удаление?";
			КонецЕсли;
		КонецЕсли; 
		
		ОтветНаВопрос = Вопрос(СтрокаВопроса, РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		ВсегоПисем = Элемент.ВыделенныеСтроки.Количество();
		ТекСтрока = 0;
		Для каждого ВыбраннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
			ТекСтрока = ТекСтрока + 1;
			Состояние("Обработно " + Строка(ТекСтрока) + " из " + Строка(ВсегоПисем));
			Объект = ВыбраннаяСтрока.ПолучитьОбъект();
			Попытка
				Объект.УстановитьПометкуУдаления(НЕ Объект.ПометкаУдаления);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ПометкаУдаления Тогда
		ЭлементыФормы.КонтекстноеМенюПерепискаПоЗаявке.Кнопки.УдалитьПисьмо.Текст = "Установить пометку удаления";
	Иначе
		ЭлементыФормы.КонтекстноеМенюПерепискаПоЗаявке.Кнопки.УдалитьПисьмо.Текст = "Снять пометку удаления";
	КонецЕсли;
	
КонецПроцедуры

Процедура Ответить(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеЭлектроннойПочтой.ОтветитьНаПисьмо(ТекущийПользователь, ДанныеСтроки.Ссылка, ЭтаФорма);
	
КонецПроцедуры

Процедура ОтветитьВсем(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеЭлектроннойПочтой.ОтветитьВсемНаПисьмо(ТекущийПользователь, ДанныеСтроки.Ссылка, ЭтаФорма);
	
КонецПроцедуры

Процедура Переслать(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеЭлектроннойПочтой.ПереадресоватьПисьмо(ТекущийПользователь, ДанныеСтроки.Ссылка, , ЭтаФорма);
	
КонецПроцедуры

Процедура УстановитьРассмотрено(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронноеПисьмоОбъект = ДанныеСтроки.Ссылка.ПолучитьОбъект();
	Если ЭлектронноеПисьмоОбъект.Ответственный.Пустая() Тогда
		ЭлектронноеПисьмоОбъект.Ответственный	= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
	КонецЕсли;
	Если ЭлектронноеПисьмоОбъект.Ответственный.Пустая() Тогда
		ЭлектронноеПисьмоОбъект.Ответственный	= ТекущийПользователь;
	КонецЕсли;
	ЭлектронноеПисьмоОбъект.НеРассмотрено		= Ложь;
	ЭлектронноеПисьмоОбъект.Записать();
	
КонецПроцедуры

Процедура УстановитьНеРассмотрено(Кнопка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронноеПисьмоОбъект = ДанныеСтроки.Ссылка.ПолучитьОбъект();
	ЭлектронноеПисьмоОбъект.НеРассмотрено	= Истина;
	ЭлектронноеПисьмоОбъект.Записать();
	
КонецПроцедуры

// Действия дополнительных команд

Процедура ДополнительныеДействия(Кнопка)
	
	НаборПерсоналаПереопределяемый.ВыполнитьДополнительныеДействия(Кнопка, ЭлементыФормы, ЭтаФорма, ЭтотОбъект, ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ПолеHTMLДокументаНеРазобранныеПисьмаonclick(Элемент, pEvtObj)
	
	УправлениеЭлектроннойПочтой.ОбработкаСобытияOnClickПоляHTML(ТекущийПользователь, Элемент, pEvtObj, , , ЭтаФорма);
	
КонецПроцедуры

Процедура ПолеHTMLДокументаОписаниеЗаявкиonclick(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnClick(Элемент, pEvtObj, ЭтаФорма);
	
КонецПроцедуры

Процедура ПолеHTMLДокументаОписаниеЗаявкиonmouseover(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOver(Элемент, pEvtObj);
	
КонецПроцедуры

Процедура ПолеHTMLДокументаОписаниеЗаявкиonmouseout(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOut(Элемент, pEvtObj);
	
КонецПроцедуры

Процедура ПолеHTMLДокументаПерепискаПоЗаявкеonclick(Элемент, pEvtObj)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		УправлениеЭлектроннойПочтой.ОбработкаСобытияOnClickПоляHTML(ТекущийПользователь, Элемент, pEvtObj, , , ЭтаФорма);
	Иначе
		УправлениеЭлектроннойПочтой.ОбработкаСобытияOnClickПоляHTML(ТекущийПользователь, Элемент, pEvtObj, ДанныеСтроки.УчетнаяЗапись, ДанныеСтроки.Ссылка.ГруппаУчетнойЗаписи, ЭтаФорма, ДанныеСтроки.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВстроеннаяСправкаonclick(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnClick(Элемент, pEvtObj, ЭтаФорма);
	
КонецПроцедуры

Процедура ВстроеннаяСправкаonmouseout(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOut(Элемент, pEvtObj);
	
КонецПроцедуры

Процедура ВстроеннаяСправкаonmouseover(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOver(Элемент, pEvtObj);
	
КонецПроцедуры

Процедура НадписьОткрытьСообщенияНажатие(Элемент)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеразобранныеПисьма.ТекущиеДанные;
	
	Форма = Документы.ЭлектронноеПисьмо.ПолучитьФормуСписка();
	Форма.Отбор.ОтправительАдресЭлектроннойПочты.Установить(ДанныеСтроки.ОтправительАдресЭлектроннойПочты);
	
	Форма.Открыть();
	
КонецПроцедуры

Процедура НадписьОткрытьЗаявкуНажатие(Элемент)
	
	Если мНайденнаяЗаявка = Неопределено ИЛИ мНайденнаяЗаявка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	мНайденнаяЗаявка.ПолучитьФорму().Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ НеРазобранныеПисьма

Процедура ТабличноеПолеНеРазобранныеПисьмаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("НеРазобранныеПисьмаПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

Процедура ТабличноеПолеНеРазобранныеПисьмаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеНеРазобранныеПисьма.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки.Ссылка.ПолучитьФорму(, Элемент, ЭтотОбъект).Открыть();
	
КонецПроцедуры

Процедура ТабличноеПолеНеРазобранныеПисьмаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.СтатусПисьма = Перечисления.СтатусыПисем.Сохраненное Тогда
		ОформлениеСтроки.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
		
	ИначеЕсли ДанныеСтроки.СтатусПисьма = Перечисления.СтатусыПисем.Исходящее Тогда
		ОформлениеСтроки.ЦветФона = WebЦвета.ТусклоРозовый;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка.Пустая() Тогда
		ОформлениеСтроки.Шрифт		= ШрифтыСтиля.ШрифтГруппыДерева;
		ОформлениеСтроки.ЦветФона	= ЦветаСтиля.ЦветГруппыДерева;
		ОформлениеСтроки.ЦветТекста	= ЦветаСтиля.ЦветШрифтаГруппыДерева;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ЗаявкиКандидатов

Процедура ТабличноеПолеЗаявкиКандидатовПриАктивизацииСтроки(Элемент)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	КомандыЗаявкиДоступны = ДанныеСтроки <> Неопределено И НЕ ДанныеСтроки.Ссылка.Пустая();
	
	НеДоступнаГрупповаяОбработка = ВернутьВыделенныеСтроки(ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ВыделенныеСтроки).Количество() < 2;
	
	КнопкиПодменю = ЭлементыФормы.КоманднаяПанельЗаявкиКандидатов.Кнопки.ПодменюЗаявки.Кнопки;
	
	КнопкиПодменю.НаписатьКандидату.Доступность			= КомандыЗаявкиДоступны;
	КнопкиПодменю.ОткрытьЗаявку.Доступность				= КомандыЗаявкиДоступны;
	КнопкиПодменю.ДобавитьФайлы.Доступность				= КомандыЗаявкиДоступны И НеДоступнаГрупповаяОбработка;
	
	НаборПерсоналаПереопределяемый.УстановитьДоступностьДополнительныхКнопок(КнопкиПодменю, КомандыЗаявкиДоступны, НеДоступнаГрупповаяОбработка, ДанныеСтроки);
	
	Для Каждого Кнопка Из КнопкиПодменю.ИзменитьСостояние.Кнопки Цикл
		Кнопка.Доступность = КомандыЗаявкиДоступны;
	КонецЦикла;
	Для Каждого Кнопка Из КнопкиПодменю.УказатьИсточникИнформации.Кнопки Цикл
		Кнопка.Доступность = КомандыЗаявкиДоступны;
	КонецЦикла;
	Для Каждого Кнопка Из КнопкиПодменю.НазначитьВакансию.Кнопки Цикл
		Кнопка.Доступность = КомандыЗаявкиДоступны;
	КонецЦикла;
	Для Каждого Кнопка Из КнопкиПодменю.ИзменитьГруппуЗаявки.Кнопки Цикл
		Кнопка.Доступность = КомандыЗаявкиДоступны;
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("ЗаявкиКандидатовПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

Процедура ТабличноеПолеЗаявкиКандидатовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ИсточникиИнформации") Тогда
		СтандартнаяОбработка = Ложь;
		
		ЗаписатьИсточникИнформации(ВыбранноеЗначение);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СостоянияЗаявокКандидатов") Тогда
		СтандартнаяОбработка = Ложь;
		
		ЗаписатьСостояниеЗаявки(ВыбранноеЗначение);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Вакансии") Тогда
		СтандартнаяОбработка = Ложь;
		
		ЗаписатьВакансию(ВыбранноеЗначение);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ГруппыЗаявокКандидатов") Тогда
		СтандартнаяОбработка = Ложь;
		
		ЗаписатьГруппуЗаявки(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьСтрокуКандидатов(Элемент, ОформлениеСтроки)
	
	ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
	
	ОформлениеСтроки.Ячейки.Представление.Текст = ДанныеСтроки.Наименование;
	Если ДанныеСтроки.Количество > 0 Тогда
		ОформлениеСтроки.Ячейки.Представление.Текст = ДанныеСтроки.Наименование + " (" + ДанныеСтроки.Количество + ")";
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка.Пустая() Тогда
		ОформлениеСтроки.Шрифт		= ШрифтыСтиля.ШрифтГруппыДерева;
		ОформлениеСтроки.ЦветФона	= ЦветаСтиля.ЦветГруппыДерева;
		ОформлениеСтроки.ЦветТекста	= ЦветаСтиля.ЦветШрифтаГруппыДерева;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеЗаявкиКандидатовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
			НачатьПеретаскиваниеОбъектов(ЭлементМассива, СтандартнаяОбработка);
			Если СтандартнаяОбработка Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		НачатьПеретаскиваниеОбъектов(ПараметрыПеретаскивания.Значение, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеЗаявкиКандидатовПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
			ПроверитьПеретаскиваниеОбъектов(ЭлементМассива, СтандартнаяОбработка, Строка, Колонка);
			Если СтандартнаяОбработка Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		ПроверитьПеретаскиваниеОбъектов(ПараметрыПеретаскивания.Значение, СтандартнаяОбработка, Строка, Колонка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеЗаявкиКандидатовПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СтрокаДереваЗначений") Тогда
			Если ТипЗнч(ПараметрыПеретаскивания.Значение[0].Ссылка) = Тип("СправочникСсылка.ЗаявкиКандидатов") Тогда
				СоздаватьНовуюЗаявку = Ложь;
				
			ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение[0].Ссылка) = Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
				Если Строка = Неопределено ИЛИ Строка.Ссылка.Пустая() Тогда
					Если ПараметрыПеретаскивания.Значение.Количество() > 1 Тогда
						ТекстВопроса = "Создать нового кандидата по письмам?";
					Иначе
						ТекстВопроса = "Создать нового кандидата по письму?";
					КонецЕсли;
					Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
					Если Ответ = КодВозвратаДиалога.Нет Тогда
						Возврат;
					КонецЕсли;
					
					СоздаватьНовуюЗаявку = Истина;
					
				Иначе
					Форма = ПолучитьОбщуюФорму("ДиалогВопрос");
					Форма.Заголовок	= "Действие над электронным письмом";
					Если ПараметрыПеретаскивания.Значение.Количество() = 1 Тогда
						Форма.ЭлементыФормы.НадписьТекстВопроса.Заголовок	=
						"Создать нового кандидата на основании электронного письма " + ПараметрыПеретаскивания.Значение[0].Тема + " или
						|
						|Присоединить это письмо к переписке по кандидату " + Строка.Наименование;
					Иначе
						Форма.ЭлементыФормы.НадписьТекстВопроса.Заголовок	=
						"Создать нового кандидата на основании выбранных электронных писем или
						|
						|Присоединить выбранные письма к переписке по кандидату " + Строка.Наименование;
					КонецЕсли;
					Форма.ЭлементыФормы.КнопкаДействие1.Заголовок		= "Создать нового кандидата";
					Форма.ЭлементыФормы.КнопкаДействие2.Заголовок		= "Присоединить к переписке";
					Результат = Форма.ОткрытьМодально();
					
					Если ПустаяСтрока(Результат) Тогда
						Возврат;
					Иначе
						СоздаватьНовуюЗаявку = Результат = "1";
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		
		ИначеЕсли Строка = Неопределено ИЛИ Строка.Ссылка.Пустая() Тогда
			Если ПараметрыПеретаскивания.Значение.Количество() > 1 Тогда
				ТекстВопроса = "Создать нового кандидата по письмам?";
			Иначе
				ТекстВопроса = "Создать нового кандидата по письму?";
			КонецЕсли;
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
			
			СоздаватьНовуюЗаявку = Истина;
			
		Иначе
			СоздаватьНовуюЗаявку = Ложь;
			
		КонецЕсли;
		
		Если СоздаватьНовуюЗаявку Тогда
			СтандартнаяОбработка = Ложь;
			
			Форма = Справочники.ЗаявкиКандидатов.ПолучитьФормуНовогоЭлемента(, ЭтаФорма, ЭтотОбъект);
			Если (Строка <> Неопределено) Тогда
				ЗаполнитьРеквизитЗаявкиПоГруппировке(Строка, Форма);
			КонецЕсли;
			Форма.Наименование	= ПараметрыПеретаскивания.Значение[0].Тема;
			Форма.Открыть();
			
			ПисьмаДляЗаявки = ПараметрыПеретаскивания.Значение;
			
		Иначе
			Для Каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
				ПеретаскиваниеОбъектов(Элемент, ЭлементМассива, СтандартнаяОбработка, Строка, Колонка);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		Если	ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СтрокаДереваЗначений") И
				ТипЗнч(ПараметрыПеретаскивания.Значение.Ссылка) = Тип("ДокументСсылка.ЭлектронноеПисьмо") И
				(Строка = Неопределено ИЛИ Строка.Ссылка.Пустая()) Тогда
					ТекстВопроса = "Создать нового кандидата по письму?";
					Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
					Если Ответ = КодВозвратаДиалога.Нет Тогда
						Возврат;
					КонецЕсли;
			
			СтандартнаяОбработка = Ложь;
			
			Форма = Справочники.ЗаявкиКандидатов.ПолучитьФормуНовогоЭлемента(, ЭтаФорма, ЭтотОбъект);
			Если (Строка <> Неопределено) Тогда
				ЗаполнитьРеквизитЗаявкиПоГруппировке(Строка, Форма);
			КонецЕсли;
			Форма.Наименование	= ПараметрыПеретаскивания.Значение.Тема;
			Форма.Открыть();
			
			ПисьмаДляЗаявки = ПараметрыПеретаскивания.Значение;
			
		Иначе
			ПеретаскиваниеОбъектов(Элемент, ПараметрыПеретаскивания.Значение, СтандартнаяОбработка, Строка, Колонка);
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеЗаявкиКандидатовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформитьСтрокуКандидатов(Элемент, ОформлениеСтроки);
	КонецЦикла;
	
	НаборПерсоналаПереопределяемый.СписокКандидатовПриПолученииДанныхДополнительно(ЭтаФорма, Элемент, ОформленияСтрок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ПерепискаПоЗаявке

Процедура ТабличноеПолеЗаявкиКандидатовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки.Ссылка.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПриАктивизацииСтроки(Элемент)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.ПометкаУдаления Тогда
		ЭлементыФормы.КонтекстноеМенюПерепискаПоЗаявке.Кнопки.УдалитьПисьмо.Текст = "Снять пометку удаления";
	Иначе
		ЭлементыФормы.КонтекстноеМенюПерепискаПоЗаявке.Кнопки.УдалитьПисьмо.Текст = "Установить пометку удаления";
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПерепискаПоЗаявкеПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПриПолученииДанных(Элемент, ОформленияСтрок)
	
	НоваяЕстьПисьмаПоЗаявке = ОформленияСтрок.Количество() > 0;
	
	Если ЕстьПисьмаПоЗаявке = НоваяЕстьПисьмаПоЗаявке Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПисьмаПоЗаявке = НоваяЕстьПисьмаПоЗаявке;
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеПерепискаПоЗаявке.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		УстановитьТекстПустогоПисьмаПоЗаявке();
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПередНачаломИзменения(Элемент, Отказ)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",	ДанныеСтроки.УчетнаяЗапись);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.АвтоматическаяУстановкаПометкиРассмотрено
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка = &УчетнаяЗапись";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Ошибка получения реквизитов учетной записи: " + ДанныеСтроки.УчетнаяЗапись);
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.НеРассмотрено И Выборка.АвтоматическаяУстановкаПометкиРассмотрено Тогда
		Объект = ДанныеСтроки.Ссылка.ПолучитьОбъект();
		Если Объект.Ответственный.Пустая() Тогда
			Объект.Ответственный	= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
		КонецЕсли;
		Если Объект.Ответственный.Пустая() Тогда
			Объект.Ответственный	= ТекущийПользователь;
		КонецЕсли;
		Объект.НеРассмотрено		= Ложь;
		Объект.Записать();
	КонецЕсли;
	
	// Перехватываем для того, чтобы установить владельца формы
	ДанныеСтроки.Ссылка.ПолучитьФорму(, ЭтаФорма, ЭтотОбъект).Открыть();
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПередУстановкойПометкиУдаления(Элемент, Отказ)
	
	Отказ = Истина;
	
	УдалитьПисьмо(Элемент);
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	УправлениеЭлектроннойПочтой.ПриВыводеСтрокиЭлектронногоПисьма(Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущаяСтрока;
	
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.ЗначениеГруппировки <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
			ПроверитьПеретаскиваниеОбъектов(ЭлементМассива, СтандартнаяОбработка, Строка, Колонка);
			Если СтандартнаяОбработка Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		ПроверитьПеретаскиваниеОбъектов(ПараметрыПеретаскивания.Значение, СтандартнаяОбработка, Строка, Колонка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеПерепискаПоЗаявкеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	ДанныеСтроки = ЭлементыФормы.ТабличноеПолеЗаявкиКандидатов.ТекущаяСтрока;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
			ПеретаскиваниеОбъектов(Элемент, ЭлементМассива, СтандартнаяОбработка, ДанныеСтроки, Колонка);
		КонецЦикла;
		
	Иначе
		ПеретаскиваниеОбъектов(Элемент, ПараметрыПеретаскивания.Значение, СтандартнаяОбработка, ДанныеСтроки, Колонка);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

КоличествоНеРазобранныхПисем	= 0;
ЕстьПисьмаПоЗаявке				= Ложь;

мКоличествоЭлементовВПодменю = 8;