Функция ПолучитьГраницуПоследовательности(Орг, ИмяПослед)
	
	Возврат Последовательности[ИмяПослед].ПолучитьГраницу(Новый Структура("Организация", Орг));
	
КонецФункции // ПолучитьГраницуПоследовательности()

Процедура КнопкаВыполнитьНажатие(Элемент)
	
	ВыбранныеОрганизации = ТабОрганизаций.НайтиСтроки(Новый Структура("Пометка", Истина));
	КолОрг = ВыбранныеОрганизации.Количество();
	
	Если КолОрг = 0 Тогда
	    Сообщить("Не выбрано ни одной организации. Восстановление состояния расчетов не производится.");
		Возврат;
	КонецЕсли; 
	
	ТекОрг = 0;
	Для Каждого СтрокаТаб Из ВыбранныеОрганизации Цикл
		
		Организация = СтрокаТаб.Организация;
		ТекОрг      = ТекОрг + 1;
		
		ЭлементыФормы.НадписьХодПроцесса.Заголовок = "Организация: " + Организация + " (" + ТекОрг + "/" + КолОрг + ")";
		
		ВосстановитьПоследовательностиПоРасчетам();
		
		Граница = ПолучитьГраницуПоследовательности( СтрокаТаб.Организация, "РасчетыПоПриобретениюОрганизации").Дата;
		СтрокаТаб.ГраницаПриобретений = ?(Граница = Дата('00010101000000'), "<Не установлена>", Граница);
		Граница = ПолучитьГраницуПоследовательности( СтрокаТаб.Организация, "РасчетыПоРеализацииОрганизации").Дата;
		СтрокаТаб.ГраницаРеализаций   = ?(Граница = Дата('00010101000000'), "<Не установлена>", Граница);
		
		Если ПереоценкаВалютныхОстатков Тогда
			СтрокаТаб.ДатаНачалаПереоценки = ПереоценитьВалютныеОстатки(СтрокаТаб.ДатаНачалаПереоценки);
		КонецЕсли;
		
	КонецЦикла;
	
	ЭлементыФормы.НадписьХодПроцесса.Заголовок = "Восстановление состояния расчетов завершено.";
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если НЕ ЗначениеЗаполнено(ДатаАктуализации) Тогда
	     ДатаАктуализации = КонецДня(РабочаяДата);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
	    Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	ПолучитьТекущиеГраницыПоследовательностей();
	
КонецПроцедуры

Процедура ОбновитьГраницы(Кнопка)
	
	ПолучитьТекущиеГраницыПоследовательностей();
	
КонецПроцедуры

Процедура ПолучитьТекущиеГраницыПоследовательностей()

	ТекущееСостояниеПометки = ТабОрганизаций.Выгрузить();
	ТабОрганизаций.Очистить();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Обход = Запрос.Выполнить().Выбрать();
	Пока Обход.Следующий() Цикл
		НоваяСтрока = ТабОрганизаций.Добавить();
		НоваяСтрока.Организация = Обход.Ссылка;
		СтараяСтрока = ТекущееСостояниеПометки.Найти(НоваяСтрока.Организация);
		Если СтараяСтрока = неопределено Тогда
			НоваяСтрока.Пометка     = Ложь;
		Иначе
			НоваяСтрока.Пометка = СтараяСтрока.Пометка;
		КонецЕсли;
		
		ГраницаПриобретений             = ПолучитьГраницуПоследовательности( Обход.Ссылка, "РасчетыПоПриобретениюОрганизации").Дата;
		НоваяСтрока.ГраницаПриобретений = ?(ГраницаПриобретений = Дата('00010101'), "<Не установлена>", ГраницаПриобретений);
		ГраницаРеализаций               = ПолучитьГраницуПоследовательности( Обход.Ссылка, "РасчетыПоРеализацииОрганизации").Дата;
		НоваяСтрока.ГраницаРеализаций   = ?(ГраницаРеализаций = Дата('00010101'), "<Не установлена>", ГраницаРеализаций);
		ДатаНачалаПереоценки            = Мин(
			?(ГраницаПриобретений = Дата('00010101'), ДатаАктуализации, ГраницаПриобретений), 
			?(ГраницаРеализаций   = Дата('00010101'), ДатаАктуализации, ГраницаРеализаций));
		Если ДатаНачалаПереоценки = Дата('00010101') Тогда
			ДатаНачалаПереоценки = РабочаяДата;
		КонецЕсли;
		НоваяСтрока.ДатаНачалаПереоценки = НачалоМесяца(ДатаНачалаПереоценки);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельТабОрганизацийОтметитьВсе(Кнопка)
	
	СтрокиБезОтметки = ТабОрганизаций.НайтиСтроки(Новый Структура("Пометка",Ложь));
	Для каждого СтрокаБезОтметки Из СтрокиБезОтметки Цикл
		СтрокаБезОтметки.Пометка = Истина;
	КонецЦикла; 
	
КонецПроцедуры

Процедура КоманднаяПанельТабОрганизацийСнятьОтметку(Кнопка)
	
	СтрокиСОтметкой = ТабОрганизаций.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого ТекущаяСтрока Из СтрокиСОтметкой Цикл
		ТекущаяСтрока.Пометка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры