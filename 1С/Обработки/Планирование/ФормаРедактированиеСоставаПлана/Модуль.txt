Перем мСтрокаДерева Экспорт;
Перем мТекущийОбъект Экспорт;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если мСтрокаДерева.План = Неопределено ИЛИ мСтрокаДерева.План.Пустая() ИЛИ (ТипЗнч(мСтрокаДерева.План) <> Тип("ДокументСсылка.ПланПродаж") И ТипЗнч(мСтрокаДерева.План) <> Тип("ДокументСсылка.ПланПроизводства") И ТипЗнч(мСтрокаДерева.План) <> Тип("ДокументСсылка.ПланЗакупок")) Тогда
		
		Отказ = Истина;
		Предупреждение(НСтр("ru='Не выбран план для редактирования.'"));
		Возврат;
		
	КонецЕсли;
	
	Попытка
		
		мТекущийОбъект = мСтрокаДерева.План.ПолучитьОбъект();
		мТекущийОбъект.Заблокировать();
		
	Исключение
		
		Отказ = Истина;
		Предупреждение(НСтр("ru='Невозможно заблокировать документ.'"));
		Возврат;
		
	КонецПопытки;
	
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()
	
	ВывестиСоставПлана(мСтрокаДерева, СоставПлана, ЭлементыФормы.СоставПлана, Истина, Ложь, Истина);
	
	Если ТипЗнч(мСтрокаДерева.План) = Тип("ДокументСсылка.ПланПроизводства") Тогда
		
		ЭлементыФормы.Панель.Страницы.ПроизводственнаяПрограмма.Доступность = Истина;
		ВывестиПроизводственнуюПрограмму(мСтрокаДерева, ПроизводственнаяПрограмма);
		
	Иначе
		
		ЭлементыФормы.Панель.Страницы.ПроизводственнаяПрограмма.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Модифицированность Тогда
		
		Отчет = Вопрос(НСтр("ru='Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
		
		Если Отчет = КодВозвратаДиалога.Да Тогда
			
			СохранитьИзменения();
			
		ИначеЕсли Отчет = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	мТекущийОбъект.Разблокировать();
	
КонецПроцедуры // ПередЗакрытием()

Процедура КоманднаяПанельПроизводственнаяПрограммаЗаполнитьПоСоставуПлана(Кнопка)
	
	ЗаполнитьПроизводственнуюПрограмму(мСтрокаДерева, ПроизводственнаяПрограмма);
	
КонецПроцедуры // КоманднаяПанельПроизводственнаяПрограммаЗаполнитьПоСоставуПлана()

Процедура ОсновныеДействияФормыОсновныеДействияОК(Кнопка)
	
	СохранитьИзменения();
	Закрыть();
	
КонецПроцедуры // ОсновныеДействияФормыОсновныеДействияОК()

Процедура ОсновныеДействияФормыЗаписать(Кнопка)
	
	СохранитьИзменения();
	
КонецПроцедуры // ОсновныеДействияФормыЗаписать()

Процедура СохранитьИзменения()
	
	Если ТипЗнч(мТекущийОбъект) = Тип("ДокументОбъект.ПланПроизводства") Тогда
		
		мТекущийОбъект.ПроизводственнаяПрограмма.Загрузить(ПроизводственнаяПрограмма);
		мТекущийОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		ВывестиПроизводственнуюПрограмму(мСтрокаДерева, ПроизводственнаяПрограмма);
		
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Ложь;
	
	ОповеститьОЗаписиНовогоОбъекта(мТекущийОбъект.Ссылка);
	
КонецПроцедуры // СохранитьИзменения()

Процедура ПроизводственнаяПрограммаПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки из ОформленияСтрок Цикл
		
		УстановитьОформлениеСтрокиПроизводственнаяПрограмма(ОформлениеСтроки);
		
	КонецЦикла;
	
КонецПроцедуры // ПроизводственнаяПрограммаПриПолученииДанных()

Процедура ПроизводственнаяПрограммаНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ПроизводственнаяПрограмма.ТекущиеДанные;
	
	Если ТекущиеДанные.ХарактеристикаНоменклатуры <> Неопределено И ТекущиеДанные.Номенклатура <> ТекущиеДанные.ХарактеристикаНоменклатуры.Владелец Тогда
		
		ТекущиеДанные.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		
	КонецЕсли;

КонецПроцедуры // ПроизводственнаяПрограммаНоменклатураПриИзменении()