////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента; 

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

Перем мСписокТипов;

Перем мТекущийВидСчетаФактуры;

Перем ВерсияПостановления;

// Содержит значение функциональной опции "ИспользоватьОбменЭД"
Перем мИспользоватьОбменЭД;

// Процедура устанавливает видимость для реквизитов документа 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	УказанДокументОснование = ДокументыОснования.Количество() > 0 
		И ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование);
		
	АвансКомитента	  = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента;	
	НаРеализацию      = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
	НаАванс           = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс ИЛИ АвансКомитента;
	НаСуммовуюРазницу = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу;
	НалоговыйАгент    = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент;
	Корректировочный  = ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
	
	ПоОтчетуКомиссионера = ДокументыОснования.Количество() > 0 И ТипЗнч(ДокументыОснования[0].ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах");
	
	ЭлементыФормы.Организация.ТолькоПросмотр		= УказанДокументОснование;
	ЭлементыФормы.Контрагент.ТолькоПросмотр			= УказанДокументОснование;
	
	ЭлементыФормы.ДоговорКонтрагента.ТолькоПросмотр	= (УказанДокументОснование И НаРеализацию) ИЛИ
													  (НаСуммовуюРазницу) ИЛИ
													  (ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс И ПоОтчетуКомиссионера);
	ЭлементыФормы.ДокументыОснования.ТолькоПросмотр	= ПоОтчетуКомиссионера;
	
	ЭлементыФормы.ДоговорКонтрагента.АвтоОтметкаНезаполненного = (НаАванс ИЛИ НалоговыйАгент) И НЕ ПоОтчетуКомиссионера;
	
	Если НЕ ЭлементыФормы.ДоговорКонтрагента.АвтоОтметкаНезаполненного Тогда
		ЭлементыФормы.ДоговорКонтрагента.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	Если НаРеализацию Тогда
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.НаРеализацию;
	ИначеЕсли НаАванс Тогда
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.НаАванс;
	ИначеЕсли НаСуммовуюРазницу Тогда
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.НаСуммовуюРазницу;
	ИначеЕсли НалоговыйАгент Тогда
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.НалоговыйАгент;
	ИначеЕсли Корректировочный Тогда
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.Корректировочный;
	Иначе
		ЭлементыФормы.ДанныеСчетаФактуры.ТекущаяСтраница = ЭлементыФормы.ДанныеСчетаФактуры.Страницы.НаРеализацию;
	КонецЕсли;
	
	ЭлементыФормы.Комитент.АвтоОтметкаНезаполненного = АвансКомитента;	
	ЭлементыФормы.ПолеКомитент.АвтоОтметкаНезаполненного = АвансКомитента;	
	Если НЕ ЭлементыФормы.Комитент.АвтоОтметкаНезаполненного Тогда
		ЭлементыФормы.Комитент.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	Если НЕ ЭлементыФормы.ПолеКомитент.АвтоОтметкаНезаполненного Тогда
		ЭлементыФормы.ПолеКомитент.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	ЭлементыФормы.Комитент.Видимость = АвансКомитента;
	ЭлементыФормы.ПолеКомитент.Видимость = АвансКомитента;
	ЭлементыФормы.НадписьКомитент.Видимость = АвансКомитента;
	ЭлементыФормы.НадписьПолеВводаКомитент.Видимость = АвансКомитента;
	
	ЭлементыФормы.НадписьСуммаНДСДокумента.Видимость = ВерсияПостановления = 2;
	ЭлементыФормы.СуммаНДСДокумента.Видимость        = ВерсияПостановления = 2;
	
	ЭлементыФормы.ПанельИсправление.Видимость = ВерсияПостановления = 2 И НЕ НаСуммовуюРазницу;
		
	ПанельСведенияОСчетеФактуреРежимСвертки = РежимСверткиЭлементаУправления.Низ;
		
	Если Дата < '20111001' Тогда
		ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаОбычный;
	ИначеЕсли ВерсияПостановления = 1 Тогда			
		Если Корректировочный Тогда
			ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаКорректировочный;
		Иначе
			ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаОбычный;
		КонецЕсли;			
	Иначе // ВерсияПостановления = 2
		Если Корректировочный Тогда
			ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаКорректировочныйИсправленный;
		ИначеЕсли Исправление Тогда
			ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаИсправленный;			
		Иначе				
			ЭлементыФормы.ПанельНомераДаты.ТекущаяСтраница = ЭлементыФормы.ПанельНомераДаты.Страницы.СтраницаОбычный;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыФормы.СчетФактураБезНДС.Видимость = ВерсияПостановления = 2;
	
	// Эти элементы только для документов с 2012 г.	
	Если ВерсияПостановления = 2 Тогда
		
		ЭлементыФормы.ДатаВыставления.АвтоОтметкаНезаполненного = Выставлен;
		Если НЕ ЭлементыФормы.ДатаВыставления.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.ДатаВыставления.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		ПанельСведенияОСчетеФактуреРежимСвертки = РежимСверткиЭлементаУправления.Нет;
		
		ЭлементыФормы.НомерИсправления.Доступность 		         = Исправление;			
		ЭлементыФормы.НомерИсправления.АвтоОтметкаНезаполненного = Исправление;
		Если НЕ ЭлементыФормы.НомерИсправления.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.НомерИсправления.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		ЭлементыФормы.НомерИсправленияИсходногоДокумента.Доступность = УчитыватьИсправлениеИсходногоДокумента;
		ЭлементыФормы.ДатаИсправленияИсходногоДокумента.Доступность  = УчитыватьИсправлениеИсходногоДокумента;			
		ЭлементыФормы.НомерИсправленияИсходногоДокумента.АвтоОтметкаНезаполненного = УчитыватьИсправлениеИсходногоДокумента;
		Если НЕ ЭлементыФормы.НомерИсправленияИсходногоДокумента.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.НомерИсправленияИсходногоДокумента.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		ЭлементыФормы.ДатаИсправленияИсходногоДокумента.АвтоОтметкаНезаполненного  = УчитыватьИсправлениеИсходногоДокумента;
		Если НЕ ЭлементыФормы.ДатаИсправленияИсходногоДокумента.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.ДатаИсправленияИсходногоДокумента.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		ЭлементыФормы.НомерИсправляемогоКорректировочногоДокумента.Доступность = Исправление;
		ЭлементыФормы.ДатаИсправляемогоКорректировочногоДокумента.Доступность  = Исправление;
		ЭлементыФормы.НомерИсправляемогоКорректировочногоДокумента.АвтоОтметкаНезаполненного = Исправление;
		Если НЕ ЭлементыФормы.НомерИсправляемогоКорректировочногоДокумента.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.НомерИсправляемогоКорректировочногоДокумента.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		ЭлементыФормы.ДатаИсправляемогоКорректировочногоДокумента.АвтоОтметкаНезаполненного  = Исправление;
		Если НЕ ЭлементыФормы.ДатаИсправляемогоКорректировочногоДокумента.АвтоОтметкаНезаполненного Тогда
			ЭлементыФормы.ДатаИсправляемогоКорректировочногоДокумента.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		Если Корректировочный Тогда
			ЭлементыФормы.ПанельИтоги.ТекущаяСтраница = ЭлементыФормы.ПанельИтоги.Страницы.СтраницаУвеличениеУменьшение;
		Иначе
			ЭлементыФормы.ПанельИтоги.ТекущаяСтраница = ЭлементыФормы.ПанельИтоги.Страницы.СтраницаВсего;
		КонецЕсли;
		
		ЭлементыФормы.Выставлен.Видимость          = НЕ НалоговыйАгент И НЕ НаСуммовуюРазницу;
		ЭлементыФормы.ДатаВыставления.Видимость    = НЕ НалоговыйАгент И НЕ НаСуммовуюРазницу;
		ЭлементыФормы.НаБумажномНосителе.Видимость = НЕ НалоговыйАгент И НЕ НаСуммовуюРазницу;
		ЭлементыФормы.ВЭлектронномВиде.Видимость   = НЕ НалоговыйАгент И НЕ НаСуммовуюРазницу;
		
		ЭлементыФормы.СчетФактураНеВыставляется.Видимость   = НЕ НаАванс;
		ЭлементыФормы.СчетФактураНеВыставляется.Доступность = НЕ НалоговыйАгент И НЕ НаСуммовуюРазницу;
		
		ЭлементыФормы.Выставлен.Доступность          = НЕ СчетФактураНеВыставляется;
		ЭлементыФормы.ДатаВыставления.Доступность    = НЕ СчетФактураНеВыставляется И Выставлен;
		ЭлементыФормы.НаБумажномНосителе.Доступность = НЕ СчетФактураНеВыставляется;
		ЭлементыФормы.ВЭлектронномВиде.Доступность   = НЕ СчетФактураНеВыставляется;
		
	ИначеЕсли Дата >= '20111001' Тогда			
		Если Корректировочный Тогда
			ЭлементыФормы.ПанельИтоги.ТекущаяСтраница = ЭлементыФормы.ПанельИтоги.Страницы.СтраницаУвеличениеУменьшение;
		Иначе
			ЭлементыФормы.ПанельИтоги.ТекущаяСтраница = ЭлементыФормы.ПанельИтоги.Страницы.СтраницаВсего;
		КонецЕсли;
	Иначе
		ЭлементыФормы.ПанельИтоги.ТекущаяСтраница = ЭлементыФормы.ПанельИтоги.Страницы.СтраницаВсего;
	КонецЕсли;
		
	ЭлементыФормы.ПанельСведенияОСчетеФактуре.Свертка = ПанельСведенияОСчетеФактуреРежимСвертки;
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	ЭлементыФормы.Руководитель.Видимость = (ВерсияПостановления = 2);
	ЭлементыФормы.ГлавныйБухгалтер.Видимость = (ВерсияПостановления = 2);
	ЭлементыФормы.НадписьРуководитель.Видимость = (ВерсияПостановления = 2);
	ЭлементыФормы.НадписьГлавныйБухгалтер.Видимость = (ВерсияПостановления = 2);
			
КонецПроцедуры // УстановитьВидимость()

Процедура ПроверитьЗаполнениеСчетаФактуры()
	
	Если ЭтоНовый() ИЛИ ДокументыОснования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СообщениеПользователю = "";
	Если ПроверитьДатуСчетаФактуры(СообщениеПользователю) Тогда
		ТекстВопроса = СообщениеПользователю 
			+ Символы.ПС + "Изменить дату счета-фактуры на дату документа-основания?";
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Дата = ДокументыОснования[0].ДокументОснование.Дата;
		КонецЕсли;
		
	ИначеЕсли СообщениеПользователю <> "" Тогда
		Предупреждение(СообщениеПользователю 
			+ Символы.ПС + "Внесите изменения в счет-фактуру вручную.");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьСуммуДокумента()
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс 
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента 
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		Сумма    = Авансы.Итог("Сумма");
		СуммаНДС = Авансы.Итог("СуммаНДС");
	КонецЕсли;
	
	Если ВалютаДокумента <> мВалютаРегламентированногоУчета Тогда
		СтруктураКурсаВзаиморасчетов = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
		СуммаДокумента = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Сумма,
			мВалютаРегламентированногоУчета, ВалютаДокумента, 
			1, СтруктураКурсаВзаиморасчетов.Курс, 
			1, СтруктураКурсаВзаиморасчетов.Кратность);
		СуммаНДСДокумента = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаНДС,
			мВалютаРегламентированногоУчета, ВалютаДокумента, 
			1, СтруктураКурсаВзаиморасчетов.Курс, 
			1, СтруктураКурсаВзаиморасчетов.Кратность);
	Иначе
		СуммаДокумента    = Сумма;
		СуммаНДСДокумента = СуммаНДС;
	КонецЕсли;
	
КонецПроцедуры

Функция МожноИспользоватьДоговорКонтрагента(ВыбранныйДоговорКонтрагента)
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
		ИЛИ Исправление Тогда
		
		Если ВыбранныйДоговорКонтрагента.РасчетыВУсловныхЕдиницах 
			И ВыбранныйДоговорКонтрагента.ВалютаВзаиморасчетов <> мВалютаРегламентированногоУчета Тогда
			
			Если Исправление Тогда
				УточнениеСообщения = "исправленных";				
			Иначе
				УточнениеСообщения = "корректировочных"; 
			КонецЕсли;
			
			Предупреждение("Для договоров в условных единицах выставление " 
				+ УточнениеСообщения + " счетов-фактур, оформленных в валюте, не поддерживается.");
			
			Возврат Ложь;			
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка, ВыбратьТекущуюКолонку = Ложь)
	
	СтандартнаяОбработка = Ложь;
	
	СписокТипов = ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры, Исправление);
	
	Отбор = Новый Структура();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Отбор.Вставить("Контрагент", Контрагент);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Отбор.Вставить("ДокументыПоДоговоруКонтрагента", ДоговорКонтрагента);
	КонецЕсли;
	
	Отбор.Вставить("ПоставщикуВыставляетсяСчетФактураНаВозврат", Истина);
	Отбор.Вставить("ПокупателемВыставляетсяСчетФактураНаВозврат", Ложь);
	
	Если Исправление Тогда
		Отбор.Вставить("ВидОперации", Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки);
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Отбор.Вставить("ВидОперации", Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение);
	КонецЕсли;
	
	Если ВыбратьТекущуюКолонку Тогда
		ЭлементыФормы.ДокументыОснования.ТекущаяКолонка = ЭлементыФормы.ДокументыОснования.Колонки.ДокументОснование;
	КонецЕсли; 
	
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ДокументыОснования", СписокТипов);
	
КонецПроцедуры

Процедура УстановитьРуководителяИГлавногоБухгалтера()
	
	Организация = ЭтотОбъект.Организация;
	Если Организация = Справочники.Организации.ПустаяСсылка() Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеУполномоченногоЛица = ОбщегоНазначения.ПолучитьДанныеУполномоченногоЛица(Организация, Ответственный);
	ОтветственныеЛица = ОбщегоНазначения.ОтветственныеЛица(Организация, Дата);
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	Если ВерсияПостановления = 1 Тогда
		ЭтотОбъект.Руководитель 	= Справочники.ФизическиеЛица.ПустаяСсылка();
		ЭтотОбъект.ГлавныйБухгалтер = Справочники.ФизическиеЛица.ПустаяСсылка();
	Иначе
		Если ДанныеУполномоченногоЛица = Неопределено Тогда 
			ЭтотОбъект.Руководитель = ОтветственныеЛица.Руководитель;
			ЭтотОбъект.ГлавныйБухгалтер = ОтветственныеЛица.ГлавныйБухгалтер;
		Иначе
			ЭтотОбъект.Руководитель = ?(ДанныеУполномоченногоЛица.Руководитель=Неопределено,ОтветственныеЛица.Руководитель,ДанныеУполномоченногоЛица.Руководитель);
			ЭтотОбъект.ГлавныйБухгалтер = ?(ДанныеУполномоченногоЛица.ГлавныйБухгалтер=Неопределено,ОтветственныеЛица.ГлавныйБухгалтер,ДанныеУполномоченногоЛица.ГлавныйБухгалтер);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();

	Если ДокументыОснования.Количество() > 0 Тогда
		НачислятьНДСПоОтгрузке = Неопределено;
		
		Для Каждого СтрДокОснования Из ДокументыОснования Цикл
			
			Если ЗначениеЗаполнено(СтрДокОснования.ДокументОснование) Тогда
				ДолжноБытьНачислятьНДСПоОтгрузке = Неопределено;
				Если СтрДокОснования.ДокументОснование.Метаданные().Имя = "РеализацияТоваровУслуг" Тогда
					Если СтрДокОснования.ДокументОснование.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
						Отказ = Истина;
						Предупреждение("При передаче на комиссию выписка счета-фактуры не требуется.");
					ИначеЕсли ЭтоНовый() И СтрДокОснования.ДокументОснование.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
						ДолжноБытьНачислятьНДСПоОтгрузке = Истина;
					КонецЕсли;
				ИначеЕсли ЭтоНовый() И СтрДокОснования.ДокументОснование.Метаданные().Имя = "ПринятиеКУчетуОС" Тогда	
					Док = СтрДокОснования.ДокументОснование;
					Если Док.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства Тогда
						Если Док.СтоимостьБУХоз = 0 Тогда
							Предупреждение("Счет-фактуру можно вводить только на основании документа принятия к учету объектов строительства, построенных хоз. способом.");
							Отказ = Истина;
						КонецЕсли;
					Иначе
						Предупреждение("Счет-фактуру можно вводить только на основании документа принятия к учету объектов строительства, построенных хоз. способом.");
						Отказ = Истина;
					КонецЕсли;
				ИначеЕсли ЭтоНовый() И СтрДокОснования.ДокументОснование.Метаданные().Имя = "РеализацияОтгруженныхТоваров" Тогда	
					ДолжноБытьНачислятьНДСПоОтгрузке = Ложь;
					Если СтрДокОснования.ДокументОснование.ДокументОтгрузки.Метаданные().Имя = "ПередачаОС" Тогда
						УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация, Истина);
						НачислятьНДСПоОтгрузке = ?(УчетнаяПолитика = Неопределено, Ложь, УчетнаяПолитика.НачислятьНДСПриПередачеНедвижимости);
					КонецЕсли;
				КонецЕсли;
				
				Если ДолжноБытьНачислятьНДСПоОтгрузке <> Неопределено Тогда
					Если НачислятьНДСПоОтгрузке = Неопределено Тогда
						УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация, Истина);
						НачислятьНДСПоОтгрузке = ?(УчетнаяПолитика = Неопределено, Ложь, УчетнаяПолитика.НачислятьНДСПоОтгрузке);
					КонецЕсли;
					Если НачислятьНДСПоОтгрузке <> ДолжноБытьНачислятьНДСПоОтгрузке Тогда
						Отказ = Истина;
						Предупреждение("Для документа """+СокрЛП(СтрДокОснования.ДокументОснование)+""" счет-фактура не требуется");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// ЭлектронныеДокументы
	РаботаСДиалогами.УдалитьКнопкуЭД(ЭлементыФормы.ДействияФормы.Кнопки, мИспользоватьОбменЭД);
	// Конец ЭлектронныеДокументы
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если Не ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);
		Если ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда 
			УстановитьРуководителяИГлавногоБухгалтера();
		КонецЕсли;
	КонецЕсли;
	
	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;

	мСписокТипов = ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры);
	
	Если ДокументыОснования.Количество() = 0 Тогда
		ДокументыОснования.Добавить();
	КонецЕсли;
	ЭлементыФормы.ДокументыОснования.ТекущаяСтрока = ДокументыОснования[0];
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс 
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
		Или ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		Если Авансы.Количество() = 0 Тогда
			Авансы.Добавить();
		КонецЕсли;
		ЭлементыФормы.Авансы.ТекущаяСтрока = Авансы[0];
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Если ДатаНомерДокументовОплаты.Количество() = 0 Тогда
			ДатаНомерДокументовОплаты.Добавить();
		КонецЕсли;
		ЭлементыФормы.ДатаНомерДокументовОплаты.ТекущаяСтрока = ДатаНомерДокументовОплаты[0];
	КонецЕсли;
	
	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
	УстановитьДоступныеВидыСчетаФактуры();
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);	
	
	УчетНДС.ЗаполнитьСписокКодовВидовОпераций(Перечисления.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры, ЭлементыФормы.КодВидаОперации.СписокВыбора);
	
	УстановитьВидимость();
	
	// ЭлектронныеДокументы
	ТекстСостоянияЭД = ЭлектронныеДокументыКлиент.ПолучитьТекстСостоянияЭД(Ссылка, ЭтаФорма);
	// Конец ЭлектронныеДокументы
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПередЗаписью" формы.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ДокументыОснования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаСообщения = ПроверитьВозможностьЗаписиСФ(Отказ);
	
	Если Отказ Тогда
		Предупреждение(СтрокаСообщения);
	КонецЕсли; 
	
	Если НЕ Отказ И НЕ СформированПриВводеНачальныхОстатковНДС тогда
		ПроверитьЗаполнениеСчетаФактуры();
		РассчитатьСуммуДокумента();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	// Если открыли данную форму из формы документа, то там надо поменять текст
	Если не ВладелецФормы = Неопределено Тогда

		Если ТипЗнч(ВладелецФормы) = Тип("Форма") тогда

			// Надо поменять текст про счет-фактуру в форме-владельце
			ВладелецФормы.ЗаполнитьТекстПроСчетФактуру();
		КонецЕсли;

	КонецЕсли; 
	
	// ЭлектронныеДокументы
	ТекстСостоянияЭД = ЭлектронныеДокументыКлиент.ПолучитьТекстСостоянияЭД(Ссылка, ЭтаФорма);
	// Конец ЭлектронныеДокументы
	
КонецПроцедуры // ПослеЗаписи()

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ЭлектронныеДокументы
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		ТекстСостоянияЭД = ЭлектронныеДокументыКлиент.ПолучитьТекстСостоянияЭД(Ссылка, ЭтаФорма);
	ИначеЕсли ИмяСобытия = "ОбновитьДокументИБПослеЗаполнения" Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	// Конец ЭлектронныеДокументы
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	СоответствиеТЧ = Новый Соответствие;
	
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДокументыОснования,ЭлементыФормы.КоманднаяПанельДокументыОснованияНаРеализацию.Кнопки.ПодменюЗаполнить);
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДатаНомерДокументовОплаты,ЭлементыФормы.КоманднаяПанельДатаНомерДокументовОплаты.Кнопки.ПодменюЗаполнить);
	СоответствиеТЧ.Вставить(ЭлементыФормы.Авансы,ЭлементыФормы.КоманднаяПанельАвансы.Кнопки.Заполнить);
	СоответствиеТЧ.Вставить(ЭлементыФормы.НалоговыйАгент,ЭлементыФормы.КоманднаяПанельНалоговыйАгент.Кнопки.Заполнить);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
			
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

// Процедура открывает журнал проводок БУ с отбором по текущему регистратору
//
Процедура ДействияФормыПроводкиДтКт(Кнопка)

	БухгалтерскийУчет.ОткрытьЖурналПроводок(Ссылка);

КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

// Процедура вызывается при нажатии кнопки "ВыгрузитьВФорматеCommerceMLФайлПанель"
// подменю "ВыгрузитьВФорматеCommerceMLПанель" командной панели формы
//
Процедура ДействияФормыВыгрузитьВФорматеCommerceMLФайлПанель(Кнопка)

	ЗаполнениеДокументов.ВыгрузитьДокументВФайлCommerceML(Ссылка);
    	
КонецПроцедуры

// Процедура вызывается при нажатии кнопки "ВыгрузитьВФорматеCommerceMLЭлПочтаПанель"
// подменю "ВыгрузитьВФорматеCommerceMLПанель" командной панели формы
//
Процедура ДействияФормыВыгрузитьВФорматеCommerceMLЭлПочтаПанель(Кнопка)

	ЗаполнениеДокументов.ОтправитьДокументПоПочтеCommerceML(Ссылка);

КонецПроцедуры

// Процедура открывает список ЭД по текущему документу
//
Процедура ДействияФормыСписокЭлектронныхДокументов(Кнопка)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Уникальность", 	Ссылка.УникальныйИдентификатор());
	ПараметрыОткрытия.Вставить("Источник", 		ЭтаФорма);
	
	ЭлектронныеДокументыКлиент.ОткрытьДеревоЭД(Ссылка, ПараметрыОткрытия);
	
КонецПроцедуры

Процедура ДействияФормыПодписатьИОтправить(Кнопка)
	
	Если НЕ ЭлектронныеДокументыКлиент.ОбъектМодифицирован(ЭтотОбъект, ЭтаФорма) Тогда
		ЭлектронныеДокументыКлиент.СформироватьПодписатьОтправитьЭД(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыСформироватьНовый(Кнопка)
	
	Если НЕ ЭлектронныеДокументыКлиент.ОбъектМодифицирован(ЭтотОбъект, ЭтаФорма) Тогда
		ЭлектронныеДокументыКлиент.СформироватьНовыйЭД(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыОткрытьАктуальныйЭД(Кнопка)
	
	ЭлектронныеДокументыКлиент.ОткрытьАктуальныйЭД(Ссылка, ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельАвансыПоОснованию(Кнопка)
	
	Авансы.Очистить();
	ОпределениеПараметровСчетаФактурыНаАванс(Неопределено);
		
КонецПроцедуры

Процедура КоманднаяПанельАвансыПоСчетуНаОплату(Кнопка)
	
	ФормаВыбора = Документы.СчетНаОплатуПокупателю.ПолучитьФормуВыбора();
	ФормаВыбора.Отбор.Организация.Установить(Организация);
	ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	ФормаВыбора.Отбор.ДоговорКонтрагента.Установить(ДоговорКонтрагента);
	СчетНаОплату = ФормаВыбора.ОткрытьМодально();
	
	ЗаполнитьПоСчетуНаОплату(СчетНаОплату, Ложь);
	
КонецПроцедуры

Процедура КоманднаяПанельАвансыПоЗаказу(Кнопка)
	
	ФормаВыбора = Документы.ЗаказПокупателя.ПолучитьФормуВыбора();
	ФормаВыбора.Отбор.Организация.Установить(Организация);
	ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	ФормаВыбора.Отбор.ДоговорКонтрагента.Установить(ДоговорКонтрагента);
	СчетНаОплату = ФормаВыбора.ОткрытьМодально();
	
	ЗаполнитьПоСчетуНаОплату(СчетНаОплату, Ложь);
		
КонецПроцедуры

Процедура КоманднаяПанельАвансыПоСчетуНаОплатуДобавить(Кнопка)
	
	ФормаВыбора = Документы.СчетНаОплатуПокупателю.ПолучитьФормуВыбора();
	ФормаВыбора.Отбор.Организация.Установить(
	Организация);
	ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	ФормаВыбора.Отбор.ДоговорКонтрагента.Установить(ДоговорКонтрагента);
	СчетНаОплату = ФормаВыбора.ОткрытьМодально();
	
	ЗаполнитьПоСчетуНаОплату(СчетНаОплату, Истина);
		
	УстановитьВидимость();
	
КонецПроцедуры

Процедура КоманднаяПанельАвансыПоЗаказуДобавить(Кнопка)
	
	ФормаВыбора = Документы.ЗаказПокупателя.ПолучитьФормуВыбора();
	ФормаВыбора.Отбор.Организация.Установить(	Организация);
	ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	ФормаВыбора.Отбор.ДоговорКонтрагента.Установить(ДоговорКонтрагента);
	СчетНаОплату = ФормаВыбора.ОткрытьМодально();
	
	ЗаполнитьПоСчетуНаОплату(СчетНаОплату, Истина);
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ЗаполнитьПоСчетуНаОплату(СчетНаОплату, РежимДобавления = Ложь)
	
	Если СчетНаОплату <> Неопределено Тогда
	
		Если НЕ РежимДобавления Тогда
			Авансы.Свернуть("СтавкаНДС", "Сумма, СуммаНДС");
		КонецЕсли;
		ТаблицаАвансов = Авансы.ВыгрузитьКолонки();
		
		СтрокиКУдаления = Новый Массив;
		
		Для Каждого СтрокаАванса Из Авансы Цикл
			Если НЕ РежимДобавления
				ИЛИ НЕ ЗначениеЗаполнено(СтрокаАванса.Номенклатура) Тогда
				ТаблицуАвансовПоСчету = ПолучитьТаблицуАвансовПоСчетуНаОплату(
					СтрокаАванса.Сумма, СтрокаАванса.СуммаНДС, СтрокаАванса.СтавкаНДС, СчетНаОплату);
				ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицуАвансовПоСчету, ТаблицаАвансов);
				Если РежимДобавления Тогда
					СтрокиКУдаления.Добавить(СтрокаАванса);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если РежимДобавления Тогда
			Для Каждого СтрокаКУдалению Из СтрокиКУдаления Цикл
				Авансы.Удалить(СтрокаКУдалению);
			КонецЦикла;
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаАвансов, Авансы);
		Иначе
			Авансы.Загрузить(ТаблицаАвансов);
		КонецЕсли;
		
		Авансы.Свернуть("Номенклатура, Содержание, СтавкаНДС", "Сумма, СуммаНДС"); 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельНалоговыйАгентПоОснованию(Кнопка)
	
	Авансы.Очистить();
	ОпределениеПараметровСчетаФактурыНалоговыйАгент(Неопределено);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура УстановитьДоступныеВидыСчетаФактуры()

	ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Очистить();
	ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаАванс);
	ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент);
	ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу);

	Если Дата >= '20111001' Тогда
		ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	КонецЕсли;
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	Если ВерсияПостановления = 2 Тогда
		ЭлементыФормы.ВидСчетаФактуры.ДоступныеЗначения.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента);	
	КонецЕсли;

КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	
	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	мТекущаяДатаДокумента = Дата; // Запомним текущую дату документа для контроля номера документа
	
	Если Дата < '20111001'
		И ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		
		ВидСчетаФактуры = Неопределено;
		
	КонецЕсли;
	
	РассчитатьСуммуДокумента();
	
	УстановитьДоступныеВидыСчетаФактуры();
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	Если ВерсияПостановления = 1 Тогда
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента Тогда
			ВидСчетаФактуры = Неопределено;
		КонецЕсли;
		Исправление      = Ложь;
		НомерИсправления = "";
		НомерИсходногоДокумента = "";
		ДатаИсходногоДокумента  = '00010101';
		НомерИсправленияИсходногоДокумента = "";
		ДатаИсправленияИсходногоДокумента  = '00010101';
		НомерИсправляемогоКорректировочногоДокумента = "";
		ДатаИсправляемогоКорректировочногоДокумента  = '00010101';
		Выставлен       = Ложь;
		ДатаВыставления = '00010101';
	КонецЕсли;	
	
	УстановитьПризнакВыставлен();
	УстановитьВидимость();
	УстановитьРуководителяИГлавногоБухгалтера();
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	Если НЕ ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(
			ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
	
	КонтрагентПриИзменении(Элемент);
	УстановитьПризнакВыставлен();
	УстановитьРуководителяИГлавногоБухгалтера();
	
КонецПроцедуры

Процедура КонтрагентПриИзменении(Элемент)
	
	// Выполняем общие действия для всех документов при изменении Контрагент.
	ЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(ЭтотОбъект);
		
    ПриИзмененииДоговора();
	УстановитьПризнакВыставлен();
	
КонецПроцедуры

Процедура ПриИзмененииДоговора()
		
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу 
		И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		
		ВалютаДокумента = ?(ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, 
			мВалютаРегламентированногоУчета, 
			ДоговорКонтрагента.ВалютаВзаиморасчетов);
		РассчитатьСуммуДокумента();

	КонецЕсли;
	
	УстановитьКодВидаОперации();
	
КонецПроцедуры

Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СписокВидовДоговоров = Новый СписокЗначений;
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	Иначе		
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	КонецЕсли;
	
	РаботаСДиалогами.НачалоВыбораЗначенияДоговораКонтрагента(
		ЭтотОбъект, ЭтаФорма, Элемент, Контрагент, ДоговорКонтрагента, СписокВидовДоговоров, СтандартнаяОбработка);

КонецПроцедуры

Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ МожноИспользоватьДоговорКонтрагента(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ПриИзмененииДоговора();
	
КонецПроцедуры

Процедура ВидСчетаФактурыПриИзменении(Элемент, ЗначениеКорректировки = Неопределено)
	
	СписокТипов = ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры, Исправление);
	
	Если ЗначениеКорректировки = Неопределено Тогда
		ЗначениеКорректировки = Ложь;
		ПроверитьТипыДокументов = Ложь;
	Иначе
		ПроверитьТипыДокументов = Истина;
	КонецЕсли;
		
	Если (мТекущийВидСчетаФактуры <> ВидСчетаФактуры ИЛИ ПроверитьТипыДокументов)
		И ДокументыОснования.Количество() <> 0
		И ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование)
		И СписокТипов.Найти(ТипЗнч(ДокументыОснования[0].ДокументОснование)) = Неопределено Тогда
		
		ТекстВопроса = "Выбранный документа-основание не соответствует виду счета-фактуры. 
			|Для продолжения требуется очистить документ-основание. Продолжить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ВидСчетаФактуры = мТекущийВидСчетаФактуры;
			ЗначениеКорректировки = НЕ ЗначениеКорректировки;
			Возврат;
		Иначе
			ДокументыОснования.Очистить();
			Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
				ДокументыОснования.Добавить();
				ЭлементыФормы.ДокументыОснования.ТекущаяСтрока = ДокументыОснования[0];
			КонецЕсли;
			ДатаНомерДокументовОплаты.Очистить();
		КонецЕсли;
			
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию 
		И ДокументыОснования.Количество() > 1 Тогда
		
		ТекстВопроса = "Несколько документов-оснований может быть указано только для счета-фактуры на реализацию. 
			|Для продолжения требуется очистить все документы-основания, кроме первого. Продолжить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ЗначениеКорректировки = НЕ ЗначениеКорректировки;
			ВидСчетаФактуры = мТекущийВидСчетаФактуры;
			Возврат;
		Иначе
			ДокументОснованиеПервый = ДокументыОснования[0].ДокументОснование;
			ДокументыОснования.Очистить();
			ДокументыОснования.Добавить();
			ДокументыОснования[0].ДокументОснование = ДокументОснованиеПервый;
			ЭлементыФормы.ДокументыОснования.ТекущаяСтрока = ДокументыОснования[0];
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДокументыОснования.Количество() = 0 Тогда
		ДокументыОснования.Добавить();
		ЭлементыФормы.ДокументыОснования.ТекущаяСтрока = ДокументыОснования[0];
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Если ДатаНомерДокументовОплаты.Количество() = 0 Тогда
			ДатаНомерДокументовОплаты.Добавить();
		КонецЕсли;
		ЭлементыФормы.ДатаНомерДокументовОплаты.ТекущаяСтрока = ДатаНомерДокументовОплаты[0];
	Иначе
		ДатаНомерДокументовОплаты.Очистить();
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаАванс И
		ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента И
		ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		Авансы.Очистить();
	КонецЕсли;
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу Тогда
		СчетФактураНеВыставляется = Истина;
		Выставлен = Ложь;
		ДатаВыставления = '00010101';
	Иначе
		СчетФактураНеВыставляется = Ложь;
	КонецЕсли;	
	
	мСписокТипов = ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры, Исправление);
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу Тогда 
		Если ВалютаДокумента <> мВалютаРегламентированногоУчета Тогда
			ВалютаДокумента = мВалютаРегламентированногоУчета;
			РассчитатьСуммуДокумента();
		КонецЕсли;
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			ДоговорКонтрагента = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		НомерИсправляемогоКорректировочногоДокумента = "";
		ДатаИсправляемогоКорректировочногоДокумента  = '00010101';
		Если НЕ Исправление Тогда
			НомерИсходногоДокумента = "";
			ДатаИсходногоДокумента  = '00010101';
		КонецЕсли;
	КонецЕсли;
		
	РассчитатьСуммуДокумента();
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию 
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		ОпределениеПараметровСчетаФактуры();
	КонецЕсли;
	
	УстановитьКодВидаОперации();
	УстановитьПризнакВыставлен();		
	УстановитьВидимость();

КонецПроцедуры

Процедура ВидСчетаФактурыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	мТекущийВидСчетаФактуры = ВидСчетаФактуры;
	
КонецПроцедуры

Процедура ВидСчетаФактурыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ МожноИспользоватьДоговорКонтрагента(ДоговорКонтрагента) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;	
	
	Если ВыбранноеЗначение <> Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу И ВыбранноеЗначение <> ВидСчетаФактуры Тогда
		СписокВидовДоговоров = Новый СписокЗначений;
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		УправлениеВзаиморасчетами.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент,Организация, СписокВидовДоговоров);
	КонецЕсли;
	
	Если НЕ Исправление Тогда
		БылНаАванс  = (ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс) ИЛИ (ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента);
		СталНаАванс = (ВыбранноеЗначение = Перечисления.ВидСчетаФактурыВыставленного.НаАванс) ИЛИ (ВыбранноеЗначение = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента);
		Если БылНаАванс <> СталНаАванс 
			И НЕ ПустаяСтрока(Номер) Тогда
			МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(
				ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИсправлениеПриИзменении(Элемент)
	
	Если Исправление И НЕ МожноИспользоватьДоговорКонтрагента(ДоговорКонтрагента) Тогда
		Исправление = Ложь;
		Возврат;
	КонецЕсли;
	
	мТекущийВидСчетаФактуры = ВидСчетаФактуры;
	ВидСчетаФактурыПриИзменении(Элемент, Исправление);
	
	Если НЕ Исправление Тогда
		НомерИсправления = "";
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
			НомерИсправляемогоКорректировочногоДокумента = "";
			ДатаИсправляемогоКорректировочногоДокумента  = '00010101';
		Иначе
			НомерИсходногоДокумента = "";
			ДатаИсходногоДокумента  = '00010101';
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(
			ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
	
	УстановитьКодВидаОперации();	
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура УчитыватьИсправлениеИсходногоДокументаПриИзменении(Элемент)
	
	Если НЕ УчитыватьИсправлениеИсходногоДокумента Тогда
		НомерИсправленияИсходногоДокумента = "";
		ДатаИсправленияИсходногоДокумента  = '00010101';
	КонецЕсли;
	
	УстановитьВидимость();
		
КонецПроцедуры

Процедура СчетФактураНеВыставляетсяПриИзменении(Элемент)
	
	Если СчетФактураНеВыставляется Тогда		
		Выставлен = Ложь;
		ДатаВыставления = '00010101';
		КодСпособаВыставления = 1;
	КонецЕсли;	
	
	УстановитьКодВидаОперации();
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ВыставленПриИзменении(Элемент)
	
	Если НЕ Выставлен Тогда
		ДатаВыставления = '00010101';
	Иначе
		ДатаВыставления = Дата;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура КодВидаОперацииНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(КодВидаОперации);
	ВыбранныйКод = ВыбратьИзСписка(Элемент.СписокВыбора, Элемент, ТекущийКод);
	Если ВыбранныйКод <> Неопределено Тогда
		КодВидаОперации = ВыбранныйКод.Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ТекстСостоянияЭДНажатие(Элемент)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Уникальность", 	Ссылка.УникальныйИдентификатор());
	ПараметрыОткрытия.Вставить("Источник", 		ЭтаФорма);
	
	ЭлектронныеДокументыКлиент.ОткрытьДеревоЭД(Ссылка, ПараметрыОткрытия);
	
КонецПроцедуры

Процедура НаБумажномНосителеПриИзменении(Элемент)
	
	Если КодСпособаВыставления = 2 Тогда
		Выставлен = Ложь;
		ДатаВыставления = '00010101';
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДЛЯ ВИДА СЧЕТА-ФАКТУРЫ НаРеализацию

Процедура ДокументыОснованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		Элемент.ТекущаяСтрока.ДокументОснование = Неопределено;
	КонецЕсли;

КонецПроцедуры

Процедура ДокументыОснованияПередУдалением(Элемент, Отказ)
	
	Отказ = ДокументыОснования.Количество() = 1;
	
КонецПроцедуры

Процедура ДокументыОснованияПослеУдаления(Элемент)
	
	ОпределениеПараметровСчетаФактуры();

КонецПроцедуры

Процедура ДокументыОснованияДокументОснованиеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ДокументыОснованияДокументОснованиеПриИзменении(Элемент)
	
	ТекущееЗначение = Элемент.Значение;
	
	Если Не ЗначениеЗаполнено(ТекущееЗначение) Тогда
	    Возврат;
	КонецЕсли;
	
	Если Исправление И НЕ МожноИспользоватьДоговорКонтрагента(ТекущееЗначение.ДоговорКонтрагента) Тогда
		Элемент.Значение = Неопределено;
		Возврат;
	КонецЕсли;
	
	СтрокаДокумента = ДокументыОснования.Найти(ТекущееЗначение, "ДокументОснование");
	УжеВыбран = Ложь;
	
	Если СтрокаДокумента <> ЭлементыФормы.ДокументыОснования.ТекущаяСтрока Тогда
		
		Сообщить("Данный документ уже выбран в качестве документа-основания.
			|Повторный выбор не допускается.");
		Элемент.Значение = Неопределено;
		
	Иначе
		
		ОпределениеПараметровСчетаФактуры();
		
	КонецЕсли;
	
	Ответ = Вопрос("Перезаполнить данные о платежно-расчетных документах?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ДатаНомерДокументовОплаты.Очистить();
		ЗаполнитьДатуИНомерВходящегоДокумента();
		
	КонецЕсли;  
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДЛЯ ВИДА СЧЕТА-ФАКТУРЫ НаАванс

Процедура ДокументОснованиеНаАвансНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

Процедура ДокументОснованиеНаАвансПриИзменении(Элемент)
	
	ТекОснование = ДокументыОснования[0].ДокументОснование;
	
	Если ЗначениеЗаполнено(ТекОснование) Тогда 
	
		ДокументЗаполнен = Ложь;
		Авансы.Очистить();
		ДатаНомерДокументовОплаты.Очистить();
		ОпределениеПараметровСчетаФактурыНаАванс(ДокументЗаполнен);
		
		Если НЕ ДокументЗаполнен Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные для регистрации счета-фактуры на предварительную оплату.");
			Возврат;
		КонецЕсли; 
		
		ЗаполнитьДатуИНомерВходящегоДокумента();
		
	КонецЕсли;

	УстановитьВидимость();
	
КонецПроцедуры

Процедура АвансыПослеУдаления(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

Процедура АвансыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

Процедура АвансыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.Авансы.ТекущиеДанные;

	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) 
		И ТекущиеДанные.Номенклатура.Услуга Тогда
		ОбработкаТабличныхЧастей.ЗаполнитьСодержаниеТабЧасти(ТекущиеДанные, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура АвансыСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.Авансы.ТекущиеДанные;
	ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

Процедура АвансыСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.Авансы.ТекущиеДанные;
	ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДЛЯ ВИДА СЧЕТА-ФАКТУРЫ НалоговыйАгент

Процедура ДокументОснованиеНалоговыйАгентНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

Процедура ДокументОснованиеНалоговыйАгентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование) Тогда 
		
		ДокументЗаполнен = Ложь;
		Авансы.Очистить();
		ДатаНомерДокументовОплаты.Очистить();
		ОпределениеПараметровСчетаФактурыНалоговыйАгент(ДокументЗаполнен);
		
		Если НЕ ДокументЗаполнен Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные для регистрации счета-фактуры налогового агента.");
			Возврат;
		КонецЕсли; 
		
		ЗаполнитьДатуИНомерВходящегоДокумента();
		
	КонецЕсли;

	УстановитьВидимость();
	
КонецПроцедуры

Процедура НалоговыйАгентПослеУдаления(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

Процедура НалоговыйАгентПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

Процедура НалоговыйАгентНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.НалоговыйАгент.ТекущиеДанные;

	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) 
		И ТекущиеДанные.Номенклатура.Услуга Тогда
		ОбработкаТабличныхЧастей.ЗаполнитьСодержаниеТабЧасти(ТекущиеДанные, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура НалоговыйАгентСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.НалоговыйАгент.ТекущиеДанные;
	ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

Процедура НалоговыйАгентСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.НалоговыйАгент.ТекущиеДанные;
	ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДЛЯ ВИДА СЧЕТА-ФАКТУРЫ НаСуммовуюРазницу

Процедура ДокументОснованиеНаСуммовуюРазницуНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

Процедура ДокументОснованиеНаСуммовуюРазницуПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование) Тогда 
		
		ДокументЗаполнен = Ложь;
		Авансы.Очистить();
		ДатаНомерДокументовОплаты.Очистить();
		ОпределениеПараметровСчетаФактурыНаАванс(ДокументЗаполнен);
		
		Если НЕ ДокументЗаполнен Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные для регистрации счета-фактуры yf суммовую разницу.");
			Возврат;
		КонецЕсли; 
		
		ЗаполнитьДатуИНомерВходящегоДокумента();
		
	КонецЕсли;

	УстановитьВидимость();
	
КонецПроцедуры

Процедура СуммаПриИзменении(Элемент)
	
	СуммаНДС = УчетНДС.РассчитатьСуммуНДС(Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(СтавкаНДС));
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

Процедура СтавкаНДСПриИзменении(Элемент)
	
	СуммаНДС = УчетНДС.РассчитатьСуммуНДС(Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(СтавкаНДС));
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДЛЯ ВИДА СЧЕТА-ФАКТУРЫ Корректировочный

Процедура ДокументыОснованияКорректировкаДокументОснованиеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	//НачалоВыбораДокументаОснования(Элемент, СтандартнаяОбработка);
	СтандартнаяОбработка = Ложь;
	
	СписокТипов = ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры, Исправление);
	
	Отбор = Новый Структура();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Отбор.Вставить("Контрагент", Контрагент);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Отбор.Вставить("ДокументыПоДоговоруКонтрагента", ДоговорКонтрагента);
	КонецЕсли;
	Если Исправление Тогда
		Отбор.Вставить("ВидОперации", Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки);
	КонецЕсли;
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Отбор.Вставить("ВидОперации", Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение);
	КонецЕсли;	
	
	ЭлементыФормы.ДокументыОснованияКорректировка.ТекущаяКолонка = ЭлементыФормы.ДокументыОснования.Колонки.ДокументОснование;
	
	// В качестве владельца формы выбора устанавливаем данный элемент,
	// чтобы выбранное значение было присвоено стандартно.
	ФормаВыбора = Документы.КорректировкаРеализации.ПолучитьФормуВыбора(,Элемент,);

	Попытка
		// Отфильтруем список документов.
		Для каждого ЭлементСтруктуры Из Отбор Цикл
            Если не ФормаВыбора.Отбор.Найти(ЭлементСтруктуры.Ключ) = неопределено тогда
				Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("СписокЗначений") Тогда
					ФормаВыбора.Отбор[ЭлементСтруктуры.Ключ].ВидСравнения = ВидСравнения.ВСписке;
				Иначе
					Если НЕ ФормаВыбора.Отбор[ЭлементСтруктуры.Ключ].ТипЗначения.СодержитТип(ТипЗнч(ЭлементСтруктуры.Значение)) Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;

				ФормаВыбора.Отбор[ЭлементСтруктуры.Ключ].Значение      = ЭлементСтруктуры.Значение;
				ФормаВыбора.Отбор[ЭлементСтруктуры.Ключ].Использование = Истина;
        	КонецЕсли;
		КонецЦикла;

	Исключение
		// нет таких отборов как заявлено в структуре.
	КонецПопытки;

	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		ФормаВыбора.ПараметрТекущаяСтрока = Элемент.Значение;
	КонецЕсли;

	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура ДокументыОснованияКорректировкаДокументОснованиеПриИзменении(Элемент)
	
	ТекущееЗначение = Элемент.Значение;
	
	Если НЕ ЗначениеЗаполнено(ТекущееЗначение) Тогда
	    Возврат;
	КонецЕсли;
	
	Если НЕ МожноИспользоватьДоговорКонтрагента(ТекущееЗначение.ДоговорКонтрагента) Тогда
		Элемент.Значение = Неопределено;
		Возврат;
	КонецЕсли;
	
	СтрокаДокумента = ДокументыОснования.Найти(ТекущееЗначение, "ДокументОснование");
	УжеВыбран = Ложь;
	
	Если СтрокаДокумента <> ЭлементыФормы.ДокументыОснованияКорректировка.ТекущаяСтрока Тогда
		
		Сообщить("Данный документ уже выбран в качестве документа-основания.
			|Повторный выбор не допускается.");
		Элемент.Значение = Неопределено;
		
	Иначе
		
		ОпределениеПараметровСчетаФактуры();
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ОтветственныйПриИзменении(Элемент)
	
	УстановитьРуководителяИГлавногоБухгалтера();
	
КонецПроцедуры

мВалютаРегламентированногоУчета   = Константы.ВалютаРегламентированногоУчета.Получить();
мИспользоватьОбменЭД = ЭлектронныеДокументыПереопределяемый.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД");