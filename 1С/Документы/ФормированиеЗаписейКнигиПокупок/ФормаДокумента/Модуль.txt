// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧПоПриобретеннымЦенностям;
Перем мКнопкиЗаполненияТЧПоАвансам;
Перем мКнопкиЗаполненияТЧПоАвансамВыданным;
Перем мКнопкиЗаполненияТЧПоНалоговомуАгенту;

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
КонецПроцедуры

// Процедура открывает журнал проводок БУ с отбором по текущему регистратору
//
Процедура ДействияФормыПроводкиДтКт(Кнопка)

	БухгалтерскийУчет.ОткрытьЖурналПроводок(Ссылка);

КонецПроцедуры

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

Процедура ДействияФормыЗаполнитьДокумент(Кнопка)
	
	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВычетПоПриобретеннымЦенностям.Количество() > 0 
		Или НДСсАвансов.Количество() > 0
		Или НДСсАвансовВыданных.Количество() > 0
		Или ВычетНДСПоНалоговомуАгенту.Количество() > 0
		Тогда
		ОтветНаВопрос = Вопрос("Заполненные ранее табличные поля документа будут очищены. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьДокумент(, , , Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура вызывается при нажатии на кнопку "Запонить" в диалоге документа
// Реализует алгоритм автоматического заполнения табличной части ВычетПоПриобретеннымЦенностям.
//
Процедура КоманднаяПанельВычетПоПриобретеннымЦенностямЗаполнить(Кнопка)

	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВычетПоПриобретеннымЦенностям.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Табличное поле ""Вычет НДС по приобретенным ценностям"" будет очищено. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям(Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // КоманднаяПанельВычетПоПриобретеннымЦенностямЗаполнить()

// Процедура вызывается при нажатии на кнопку "Запонить" в диалоге документа
// Реализует алгоритм автоматического заполнения табличной части НДСсАвансов.
//
Процедура КоманднаяПанельНДСсАвансовЗаполнить(Кнопка)
	
	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НДСсАвансов.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Табличное поле ""Вычет НДС с полученных авансов"" будет очищено. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_НДСсАвансов(Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // КоманднаяПанельНДСсАвансовЗаполнить()

Процедура КоманднаяПанельНДСсАвансовВыданныхЗаполнить(Кнопка)

	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НДСсАвансовВыданных.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Табличное поле ""Вычет НДС с выданных авансов"" будет очищено. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_НДСсАвансовВыданных(Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

// Процедура устанавливает видимость для тех колонок в табличной части 
// "ВычетПоПриобретеннымЦенностям", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()
	
	ЭлементыФормы.НДСПоВидамНачисления.Страницы.НДССАвансовВыданных.Видимость = НЕ ПредъявленНДСКВычету0;
	
	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.Колонки.ДокументОтгрузки, ПредъявленНДСКВычету0);
	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.Колонки.Состояние, ПредъявленНДСКВычету0);

	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.НДСсАвансов.Колонки.ДокументОтгрузки, ПредъявленНДСКВычету0);
	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.НДСсАвансов.Колонки.Состояние, ПредъявленНДСКВычету0);
	
	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.Колонки.ДокументОтгрузки, ПредъявленНДСКВычету0);
	ОбработкаТабличныхЧастей.УстановитьВидимостьКолонкиТабЧасти(ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.Колонки.Состояние,	ПредъявленНДСКВычету0);
	
	ЭлементыФормы.ИмеетсяРешениеОВозмещенииНДС.Видимость = ПредъявленНДСКВычету0;
	
	Законодательство2012 = УчетНДС.ПолучитьВерсиюПостановления(Дата) = 2; 
	
	ЭлементыФормы.НДСсАвансовВыданных.Колонки.ИсправленныйСчетФактура.Видимость = Законодательство2012;
	ЭлементыФормы.НДСсАвансов.Колонки.ИсправленныйСчетФактура.Видимость = Законодательство2012;
	ЭлементыФормы.ВычетПоПриобретеннымЦенностям.Колонки.ИсправленныйСчетФактура.Видимость = Законодательство2012;
	ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.Колонки.ИсправленныйСчетФактура.Видимость = Законодательство2012;
	ЭлементыФормы.НДСПоВидамНачисления.Страницы.ВычетПриИзмененииСтоимости.Видимость = НЕ ПредъявленНДСКВычету0 И Дата >= '20111001';
	ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.Колонки.ИсправленныйСчетФактура.Видимость = Законодательство2012;
		
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать() 

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПредъявленНДСКВычету0ПриИзменении(Элемент)

	Если ВычетПоПриобретеннымЦенностям.Количество() <> 0 
		Или НДСсАвансов.Количество() <> 0 
		Или НДСсАвансовВыданных.Количество() <> 0 
		Или ВычетНДСПоНалоговомуАгенту.Количество() <> 0 Тогда

		Если Вопрос("Необходимо очистить табличные части. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ВычетПоПриобретеннымЦенностям.Очистить();
			НДСсАвансов.Очистить();
			НДСсАвансовВыданных.Очистить();
			ВычетНДСПоНалоговомуАгенту.Очистить();
		Иначе
			ПредъявленНДСКВычету0 = НЕ ПредъявленНДСКВычету0;
		КонецЕсли;

	КонецЕсли;

	УстановитьВидимость();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	КонецЕсли;

КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода ставки НДС
// в строке табличной части "Товары".
//
Процедура СтавкаНДСПриИзменении(Элемент)

	// Рассчитать реквизиты табличной части.
	ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.СтавкаНДС));

КонецПроцедуры // ТоварыСтавкаНДСПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля дата 
// в диалоге документа.
//
Процедура ДатаПриИзменении(Элемент)

	УстановитьВремя(РежимАвтоВремя.Последним);
	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	
	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
    УстановитьВидимость();
	
КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямСчетУчетаНДСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)

КонецПроцедуры

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	мКнопкиЗаполненияТЧПоПриобретеннымЦенностям = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоПриобретеннымЦенностям"));
	мКнопкиЗаполненияТЧПоАвансам = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоАвансам"));
	мКнопкиЗаполненияТЧПоАвансамВыданным = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоАвансамВыданным"));
	мКнопкиЗаполненияТЧПоНалоговомуАгенту = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоНалоговомуАгенту"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ВычетПоПриобретеннымЦенностям, ЭлементыФормы.КоманднаяПанельВычетПоПриобретеннымЦенностям.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧПоПриобретеннымЦенностям,СоответствиеТЧ);
	
	СоответствиеТЧ.Очистить();
	СоответствиеТЧ.Вставить(ЭлементыФормы.НДСсАвансов, ЭлементыФормы.КоманднаяПанельНДСсАвансов.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧПоАвансам,СоответствиеТЧ);
	
	СоответствиеТЧ.Очистить();
	СоответствиеТЧ.Вставить(ЭлементыФормы.НДСсАвансовВыданных, ЭлементыФормы.КоманднаяПанельНДСсАвансовВыданных.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧПоАвансамВыданным,СоответствиеТЧ);
	
	СоответствиеТЧ.Очистить();
	СоответствиеТЧ.Вставить(ЭлементыФормы.ВычетНДСПоНалоговомуАгенту, ЭлементыФормы.КоманднаяПанельВычетНДСПоНалоговомуАгенту.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧПоНалоговомуАгенту,СоответствиеТЧ);
	
КонецПроцедуры // УстановитьКнопкиПодменюЗаполненияТЧ()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();

КонецПроцедуры

Процедура ПриОткрытии()

	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, "Продажа");

		УстановитьВремя(РежимАвтоВремя.Последним);

		НачалоПериода = НачалоМесяца(Дата); 
		КонецПериода  = КонецМесяца(Дата);

	КонецЕсли;

	мВестиУчетНДС = УчетНДС.ПроводитьПоРазделуУчетаНДС(Дата);
	мТекущаяДатаДокумента        = Дата;
	
	// Параметры для выбора организации
	УчетПоВсемОрганизациям = Истина; // Для совместимости с БП
	
	// Проверка ведения однофирменности
	ЭлементыФормы.Организация.ТолькоПросмотр = НЕ УчетПоВсемОрганизациям;

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

	УстановитьВидимость();
	
	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю1);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если НЕ ЗначениеЗаполнено(Организация) Тогда

		Предупреждение("Не указана организация. Операция не записана.");

		Отказ = Истина;

		ТекущийЭлемент = ЭлементыФормы.Организация;

		Возврат;

	КонецЕсли;

КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямДокументОплатыПриИзменении(Элемент)
	
	ТД = ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТД.ДокументОплаты) Тогда
		ТД.ДатаОплаты = ТД.ДокументОплаты.Дата;
	КонецЕсли;
	
КонецПроцедуры

Процедура СуммаБезНДСПриИзменении(Элемент)
	
	ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные.СтавкаНДС));

КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	СтраницаПанели  = ЭлементыФормы.НДСПоВидамНачисления.Страницы;
	
	СтраницаПанели.ВычетПоПриобретеннымЦенностям.Заголовок = "Вычет НДС по приобретенным ценностям ("   + ВычетПоПриобретеннымЦенностям.Количество() + " поз.)";
	СтраницаПанели.НДСсАвансов.Заголовок = "Вычет НДС с полученных авансов (" + НДСсАвансов.Количество() + " поз.)";
	СтраницаПанели.НДССАвансовВыданных.Заголовок = "Вычет НДС с выданных авансов (" + НДСсАвансовВыданных.Количество() + " поз.)";
	СтраницаПанели.ВычетНДСПоНалоговомуАгенту.Заголовок = "Вычет НДС по налоговому агенту (" + ВычетНДСПоНалоговомуАгенту.Количество() + " поз.)";
	СтраницаПанели.ВычетПриИзмененииСтоимости.Заголовок = "Вычет НДС по уменьшению стоимости реализации (" + ВычетПриИзмененииСтоимостиВСторонуУменьшения.Количество() + " поз.)";

КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТД = ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные;
	Если ТД = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация, Контрагент, ПокупателемВыставляетсяСчетФактураНаВозврат", Организация, ТД.Поставщик, Истина);
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ВычетПоПриобретеннымЦенностям");

КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямДокументОплатыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТД = ЭлементыФормы.ВычетПоПриобретеннымЦенностям.ТекущиеДанные;
	Если ТД = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация,Контрагент", Организация,ТД.Поставщик);
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ВычетПоПриобретеннымЦенностям");

КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямСостояниеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	СписокВыбора.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	
	РаботаСДиалогами.УстановитьСписокПоляВыбора(Элемент, СписокВыбора);
	
КонецПроцедуры

Процедура ВычетПоПриобретеннымЦенностямПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.КорректируемыйПериод.ОтметкаНезаполненного = (ДанныеСтроки.ЗаписьДополнительногоЛиста и ДанныеСтроки.КорректируемыйПериод = '00010101000000');

КонецПроцедуры

Процедура НДСсАвансовПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.КорректируемыйПериод.ОтметкаНезаполненного = (ДанныеСтроки.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(ДанныеСтроки.КорректируемыйПериод));
	
	Если ТипЗнч(ДанныеСтроки.СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")
		И НЕ ЗначениеЗаполнено(ДанныеСтроки.ДоговорКонтрагента) Тогда
		ОформлениеСтроки.Ячейки.ДоговорКонтрагента.ОтметкаНезаполненного = Ложь;
		ОформлениеСтроки.Ячейки.ДоговорКонтрагента.Текст = "<Комиссия>";
	КонецЕсли;

КонецПроцедуры

Процедура НДСсАвансовСтавкаНДСПриИзменении(Элемент)
	
	ЭлементыФормы.НДСсАвансов.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.НДСсАвансов.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.НДСсАвансов.ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

Процедура НДСсАвансовСуммаБезНДСПриИзменении(Элемент)
	
	ЭлементыФормы.НДСсАвансов.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.НДСсАвансов.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.НДСсАвансов.ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоПриобретеннымЦенностям(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧПоПриобретеннымЦенностям.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоАвансам(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧПоАвансам.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоАвансамВыданным(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧПоАвансамВыданным.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

Процедура НДСсАвансовСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	ТекущиеДанные = ЭлементыФормы.НДСсАвансов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	//СписокДоговоров = Новый СписокЗначений;
	//СписокДоговоров.Добавить(ТекущиеДанные.ДоговорКонтрагента);
	//СписокДоговоров.Добавить(Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	Отбор = Новый Структура("Организация, Контрагент, ДокументыПоДоговоруКонтрагента", Организация, ТекущиеДанные.Покупатель, ТекущиеДанные.ДоговорКонтрагента);
	
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "НДСсАвансов");
КонецПроцедуры

Процедура НДСсАвансовВыданныхПоставщикПриИзменении(Элемент)
		
	СтрокаТабличнойЧасти = ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные;
	УправлениеВзаиморасчетами.УстановитьДоговорКонтрагента(СтрокаТабличнойЧасти.ДоговорКонтрагента, СтрокаТабличнойЧасти.Поставщик, Организация);

КонецПроцедуры

Процедура НДСсАвансовВыданныхДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;

	
	РаботаСДиалогами.НачалоВыбораЗначенияДоговораКонтрагента(ЭтотОбъект, ЭтаФорма, Элемент, ТекущиеДанные.Поставщик, ТекущиеДанные.ДоговорКонтрагента,
	                                        Неопределено, СтандартнаяОбработка);

КонецПроцедуры

Процедура НДСсАвансовВыданныхСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	СписокДоговоров = Новый СписокЗначений;
	СписокДоговоров.Добавить(ТекущиеДанные.ДоговорКонтрагента);
	СписокДоговоров.Добавить(Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	Отбор = Новый Структура("Организация, Контрагент, ДокументыПоДоговоруКонтрагента", Организация, ТекущиеДанные.Поставщик, СписокДоговоров);
	
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "НДСсАвансовВыданных");

КонецПроцедуры

Процедура НДСсАвансовВыданныхСтавкаНДСПриИзменении(Элемент)
	
	ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.СтавкаНДС));

КонецПроцедуры

Процедура НДСсАвансовВыданныхСуммаБезНДСПриИзменении(Элемент)
	
	ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.НДСсАвансовВыданных.ТекущиеДанные.СтавкаНДС));
	
КонецПроцедуры

Процедура НДСсАвансовВыданныхПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.КорректируемыйПериод.ОтметкаНезаполненного = (ДанныеСтроки.ЗаписьДополнительногоЛиста И Не ЗначениеЗаполнено(ДанныеСтроки.КорректируемыйПериод));

КонецПроцедуры

// ВЫЧЕТ НДС ПО НАЛОГОВОМУ АГЕНТУ

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧПоНалоговомуАгенту(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧПоНалоговомуАгенту.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

Процедура КоманднаяПанельВычетНДСПоНалоговомуАгентуЗаполнить(Кнопка)
	
	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВычетНДСПоНалоговомуАгенту.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Табличное поле ""Вычет НДС по налоговому агенту"" будет очищено. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_ВычетНДСПоНалоговомуАгенту(Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТД = ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.ТекущиеДанные;
	Если ТД = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация,Контрагент", Организация,ТД.Поставщик);
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ВычетНДСПоНалоговомуАгенту");
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуДокументОплатыНачалоВыбора(Элемент, СтандартнаяОбработка)
		
	ТекущиеДанные = ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация,Контрагент", Организация, ТекущиеДанные.Поставщик);
	
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ВычетНДСПоНалоговомуАгенту");

КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуДокументОплатыПриИзменении(Элемент)
	
	ТД = ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТД.ДокументОплаты) Тогда
		ТД.ДатаОплаты = ТД.ДокументОплаты.Дата;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуСостояниеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	СписокВыбора.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	
	РаботаСДиалогами.УстановитьСписокПоляВыбора(Элемент, СписокВыбора);
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуСуммаБезНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) Тогда
		ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ВычетНДСПоНалоговомуАгенту.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) Тогда
		ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ТекущиеДанные.СуммаБезНДС, Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.СчетУчетаНДС.ОтметкаНезаполненного = (ДанныеСтроки.НДС <> 0 И Не ЗначениеЗаполнено(ДанныеСтроки.СчетУчетаНДС));
	ОформлениеСтроки.Ячейки.КорректируемыйПериод.ОтметкаНезаполненного = (ДанныеСтроки.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(ДанныеСтроки.КорректируемыйПериод));
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуВидЦенностиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	СписокВыбора.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	СписокВыбора.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	
	РаботаСДиалогами.УстановитьСписокПоляВыбора(Элемент, СписокВыбора);
	
КонецПроцедуры

Процедура ВычетНДСПоНалоговомуАгентуСчетУчетаНДСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)
	
КонецПроцедуры

// ВЫЧЕТ НДС ПО УМЕНЬШЕНИЮ СТОИМОСТИ РЕАЛИЗАЦИИ

Процедура КоманднаяПанельВычетНДСПоУменьшениюСтоимостиРеализацииЗаполнить(Кнопка)
	
	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВычетПриИзмененииСтоимостиВСторонуУменьшения.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Табличное поле ""Вычет при изменении стоимости в сторону уменьшения"" будет очищено. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_ВычетПриИзмененииСтоимостиВСторонуУменьшения(Истина);

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ВычетПриИзмененииСтоимостиВСторонуУменьшенияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.СчетУчетаНДС.ОтметкаНезаполненного = (ДанныеСтроки.НДС <> 0 И НЕ ЗначениеЗаполнено(ДанныеСтроки.СчетУчетаНДС));
	
КонецПроцедуры

Процедура ВычетПриИзмененииСтоимостиВСторонуУменьшенияСтавкаНДСПриИзменении(Элемент)
	
	ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.НДС = 
		УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.СуммаБезНДС, Истина, Ложь, 
			УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.СтавкаНДС));
    	
КонецПроцедуры

Процедура ВычетПриИзмененииСтоимостиВСторонуУменьшенияСуммаБезНДСПриИзменении(Элемент)
	
	ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.НДС = 
		УчетНДС.РассчитатьСуммуНДС(ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.СуммаБезНДС, 
			Истина, Ложь, УчетНДС.ПолучитьСтавкуНДС(ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные.СтавкаНДС));
    	
КонецПроцедуры

Процедура ВычетПриИзмененииСтоимостиВСторонуУменьшенияСчетУчетаНДСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);
	
КонецПроцедуры

Процедура ВычетПриИзмененииСтоимостиВСторонуУменьшенияСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТД = ЭлементыФормы.ВычетПриИзмененииСтоимостиВСторонуУменьшения.ТекущиеДанные;
	Если ТД = Неопределено тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация,Контрагент", Организация, ТД.Поставщик);
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ВычетПриИзмененииСтоимостиВСторонуУменьшения");
    	
КонецПроцедуры