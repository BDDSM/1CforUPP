////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента;

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ 

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДенежныеДокументы,ЭлементыФормы.КоманднаяПанельДенежныеДокументы);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(
		Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(
		Ссылка, ПолучитьСтруктуруПечатныхФорм(), 
		Новый Действие("ОсновныеДействияФормыПечать"), 
		Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати(
		мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов);
	
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(
		мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,
		Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ЗаполнитьПринятоОтДаннымиФизЛица(ФизЛицо)

	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, ФизическиеЛица.Наименование) КАК Фамилия,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Имя, """") КАК Имя,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Отчество, """") КАК Отчество
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО ФизическиеЛица.Ссылка = ФИОФизЛицСрезПоследних.ФизЛицо
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &ФизЛицо";
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПринятоОт = Выборка.Фамилия+" "+Выборка.Имя+" "+Выборка.Отчество;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриИзмененииВалютыДокумента()

	СтруктураКурса = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	КурсВзаиморасчетов      = СтруктураКурса.Курс;
	КратностьВзаиморасчетов = СтруктураКурса.Кратность;

КонецПроцедуры

Процедура ПриИзмененииВидаОперации()

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		Контрагент = Справочники.ФизическиеЛица.ПустаяСсылка();
	Иначе // .ПрочееПоступление
		Контрагент = Неопределено;
	КонецЕсли;
	
	ДоговорКонтрагента = Неопределено;
	ПринятоОт          = "";
	
	СчетУчетаРасчетовСКонтрагентом = Неопределено;
	
	РаботаСДиалогами.ПриВыбореСчетаВТабличномПоле(СчетУчетаРасчетовСКонтрагентом,
		СубконтоКт1, ЭлементыФормы.СубконтоКт1,
		СубконтоКт2, ЭлементыФормы.СубконтоКт2,
		СубконтоКт3, ЭлементыФормы.СубконтоКт3);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

Процедура УстановитьВидимость()
	
	ЭлементыФормы.ДействияФормы.Кнопки.НастройкаУСН.Доступность = НалоговыйУчетУСН.ПрименениеУСН(Организация, Дата);
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
		ЭлементыФормы.ПанельВидаОперации.ТекущаяСтраница = ЭлементыФормы.ПанельВидаОперации.Страницы.ОтКонтрагента;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		ЭлементыФормы.ПанельВидаОперации.ТекущаяСтраница = ЭлементыФормы.ПанельВидаОперации.Страницы.ОтПодотчетногоЛица;
		ЭлементыФормы.ДействияФормы.Кнопки.НастройкаУСН.Доступность = НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата);
	Иначе
		ЭлементыФормы.ПанельВидаОперации.ТекущаяСтраница = ЭлементыФормы.ПанельВидаОперации.Страницы.Прочее;
	КонецЕсли;
	
	ЭлементыФормы.ВалютаДокумента.Видимость        = СчетУчетаДенежныхДокументов.Валютный;
	ЭлементыФормы.НадписьВалютаДокумента.Видимость = СчетУчетаДенежныхДокументов.Валютный;
	
	УстановитьВидимостьСубконто();

КонецПроцедуры // УстановитьВидимость()

Процедура УстановитьВидимостьСубконто()

	Для Ном = 1 по 3 Цикл

		Если ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) 
			И (Ном <= СчетУчетаРасчетовСКонтрагентом.ВидыСубконто.Количество()) Тогда
			
			ЭлементыФормы["НадписьСубконтоКт"+Ном].Заголовок = 
				Строка(СчетУчетаРасчетовСКонтрагентом.ВидыСубконто[Ном-1].ВидСубконто);
			ЭлементыФормы["НадписьСубконтоКт"+Ном].Видимость = Истина;
			ЭлементыФормы["СубконтоКт"+Ном].Видимость        = Истина;
			
		Иначе
			
			ЭлементыФормы["НадписьСубконтоКт"+Ном].Видимость = Ложь;
			ЭлементыФормы["СубконтоКт"+Ном].Видимость        = Ложь;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // УстановитьВидимостьСубконто()

// Процедура формирует текст в информационной надписи об итогах документа.
//
// Параметры:
//  Нет.
//
Процедура ОбновитьПодвал()

	// При изменении данных обновим суммы в подвале.
	ЭлементыФормы.Всего.Значение    = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект));

	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (<>):";
	Иначе
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (" + СокрЛП(ДокументОбъект.ВалютаДокумента) +"):";
	КонецЕсли;

КонецПроцедуры // ОбновитьПодвал()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, , ПараметрОбъектКопирования);
			
		ПриИзмененииВалютыДокумента();
			
		Если НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда
			
			СчетУчетаДенежныхДокументов = ПланыСчетов.Хозрасчетный.ДенежныеДокументы;
			
			ПриИзмененииВидаОперации();
			
		КонецЕсли;

	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю1);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(
		Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1,ЭлементыФормы.Номер);
            
	// Заполняем подменю, вызываемое нажатием кнопки "Операция" командной панели 
	// формы, значениями перечисления "Вид операции" данного вида документа.
	// В качестве обработки выбора вида операции назначается процедура 
	// ДействияФормыДействиеУстановитьОперацию модуля формы.
	РаботаСДиалогами.УстановитьПодменюВыбораВидаОперации(
		ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации,
		ВидОперации.Метаданные().ЗначенияПеречисления,
		Новый Действие("ДействияФормыДействиеУстановитьОперацию"));

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента        = Дата;
	// Установить видимость реквизитов и заголовков колонок.
	УстановитьВидимость();
	
	// Установить активный реквизит.
	СтруктураРеквизитов = Новый Структура("Дата,Организация,ПодразделениеОрганизации,СчетУчетаДенежныхДокументов");
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
		СтруктураРеквизитов.Вставить("Контрагент");
		СтруктураРеквизитов.Вставить("ДоговорКонтрагента");
		СтруктураРеквизитов.Вставить("СчетУчетаРасчетовСКонтрагентом");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		СтруктураРеквизитов.Вставить("ПодотчетноеЛицо");
	Иначе //.ПрочееПоступление
		СтруктураРеквизитов.Вставить("СчетУчетаРасчетовСКонтрагентомПрочее");
	КонецЕсли;
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма, СтруктураРеквизитов);

    // Установить доступность формы с учетом даты запрета редактирования	
	РаботаСДиалогами.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ПриОткрытии()

Процедура ОбновлениеОтображения()

	ОбновитьПодвал();

	// Подсчитаем количество строк в табличных частях.
	ЭлементыФормы.ОсновнаяПанель.Страницы.ДенежныеДокументы.Заголовок = 
		"Денежные документы (" + ДокументОбъект.ДенежныеДокументы.Количество() + " поз.)";
	
КонецПроцедуры // ОбновлениеОтображения()

Процедура ПослеЗаписи()

	УстановитьКнопкиПечати();

	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(
		ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);

КонецПроцедуры // ПослеЗаписи()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "ПодменюВидаОперации" командной панели формы.
// Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ДействияФормыДействиеУстановитьОперацию(Кнопка)

	ПредыдущийВидОперации = ВидОперации;
	Если Кнопка <> Неопределено Тогда
		ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов[Кнопка.Имя];
	КонецЕсли;
	Если ПредыдущийВидОперации = ВидОперации Тогда 
		Возврат;
	КонецЕсли;
	
	ПриИзмененииВидаОперации();
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	УстановитьВидимость();
	НалоговыйУчетУСН.ЗаполнитьНастройкуКУДиР(ЭтотОбъект, Истина);

КонецПроцедуры // ДействияФормыДействиеУстановитьОперацию()

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура разрешения/запрещения редактирования номера документа
//
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(
		ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
			
КонецПроцедуры

// Процедура открывает журнал проводок БУ с отбором по текущему регистратору
//
Процедура ДействияФормыПроводкиДтКт(Кнопка)

	БухгалтерскийУчет.ОткрытьЖурналПроводок(Ссылка);

КонецПроцедуры

Процедура ДействияФормыНастройкаУСН(Кнопка)

	НалоговыйУчетУСН.ЗаполнитьНастройкуКУДиР(ЭтотОбъект);
	
	ФормаНастройки = ПолучитьОбщуюФорму("ФормаНастройкиПлатежаУСН");

	ФормаНастройки.Графа4       = Графа4_УСН;
	ФормаНастройки.Графа5       = Графа5_УСН;
	ФормаНастройки.Графа6       = Графа6_УСН;
	ФормаНастройки.Графа7       = Графа7_УСН;
	ФормаНастройки.НДС          = НДС_УСН;
	ФормаНастройки.ДоходЕНВД    = ДоходыЕНВД_УСН;
	ФормаНастройки.РасходЕНВД   = РасходыЕНВД_УСН;
	ФормаНастройки.Содержание   = Содержание_УСН;
	ФормаНастройки.ТолькоДоходы = НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата);

	СруктураПараметров = ФормаНастройки.ОткрытьМодально();

	Если СруктураПараметров = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Графа4_УСН      <> СруктураПараметров.Графа4
	 ИЛИ Графа5_УСН      <> СруктураПараметров.Графа5
	 ИЛИ Графа6_УСН      <> СруктураПараметров.Графа6
	 ИЛИ Графа7_УСН      <> СруктураПараметров.Графа7
	 ИЛИ НДС_УСН         <> СруктураПараметров.НДС
	 ИЛИ ДоходыЕНВД_УСН  <> СруктураПараметров.ДоходыЕНВД
	 ИЛИ РасходыЕНВД_УСН <> СруктураПараметров.РасходыЕНВД
	 ИЛИ Содержание_УСН  <> СруктураПараметров.Содержание Тогда

		РучнаяНастройка_УСН = Истина;

	КонецЕсли;

	Графа4_УСН      = СруктураПараметров.Графа4;
	Графа5_УСН      = СруктураПараметров.Графа5;
	Графа6_УСН      = СруктураПараметров.Графа6;
	Графа7_УСН      = СруктураПараметров.Графа7;
	НДС_УСН         = СруктураПараметров.НДС;
	ДоходыЕНВД_УСН  = СруктураПараметров.ДоходыЕНВД;
	РасходыЕНВД_УСН = СруктураПараметров.РасходыЕНВД;
	Содержание_УСН  = СруктураПараметров.Содержание;

КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(
		мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина), ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		
		УстановитьКнопкиПечати();
		
	КонецЕсли; 
	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(
		ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	РаботаСДиалогами.ПриИзмененииЗначенияДатыДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета);

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	
	УстановитьВидимость();
	
КонецПроцедуры // ДатаПриИзменении()

Процедура ОрганизацияПриИзменении(Элемент)

	ПриИзмененииОрганизации();
	
КонецПроцедуры // ОрганизацияПриИзменении()

Процедура КонтрагентПриИзменении(Элемент)
	
	ПриИзмененииКонтрагента();
	
КонецПроцедуры // КонтрагентПриИзменении()

Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтруктураДопПараметров = Новый Структура;
	СтруктураОтбора = Новый Структура("ЗначениеОтбора", ВалютаДокумента);
	СтруктураДопПараметров.Вставить("ВалютаВзаиморасчетов", СтруктураОтбора);
	РаботаСДиалогами.НачалоВыбораЗначенияДоговораКонтрагента(
		ЭтотОбъект, ЭтаФорма, Элемент, 
		Контрагент, ДоговорКонтрагента, Перечисления.ВидыДоговоровКонтрагентов.Прочее, 
		СтандартнаяОбработка, СтруктураДопПараметров);

КонецПроцедуры // ДоговорКонтрагентаНачалоВыбора()

Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	ПриИзмененииДоговора();

КонецПроцедуры // ДоговорКонтрагентаПриИзменении()

Процедура ПриИзмененииОрганизации()
	
	Если НЕ ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(
			ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	КонецЕсли;
	
	мСтруктураПараметровДляПолученияДоговора.Вставить("ВалютаВзаиморасчетовДоговора",   ВалютаДокумента);
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект, мСтруктураПараметровДляПолученияДоговора);
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриИзмененииОрганизации()

Процедура ПриИзмененииКонтрагента()
	
	мСтруктураПараметровДляПолученияДоговора.Вставить("ВалютаВзаиморасчетовДоговора",   ВалютаДокумента);
	ЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(ЭтотОбъект, мСтруктураПараметровДляПолученияДоговора);

	СведенияОКонтрагенте = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Контрагент, Дата);
	ПринятоОт = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКонтрагенте, "ПолноеНаименование,");
	
КонецПроцедуры // ПриИзмененииКонтрагента

Процедура ПриИзмененииДоговора()
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ДоговорКонтрагента.Организация;
		ПриИзмененииОрганизации();
		
	КонецЕсли;
	
	Если Контрагент.Пустая() Тогда
		Контрагент=ДоговорКонтрагента.Владелец;
		ПриИзмененииКонтрагента();
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриИзмененииДоговора()


Процедура СчетУчетаРасчетовСКонтрагентомОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

Процедура СчетУчетаРасчетовСКонтрагентомПрочееПриИзменении(Элемент)
	
	РаботаСДиалогами.ПриВыбореСчетаВТабличномПоле(Элемент.Значение,
		СубконтоКт1, ЭлементыФормы.СубконтоКт1,
		СубконтоКт2, ЭлементыФормы.СубконтоКт2,
		СубконтоКт3, ЭлементыФормы.СубконтоКт3);
		
	УстановитьВидимостьСубконто();
	
КонецПроцедуры

Процедура СчетУчетаДенежныхДокументовПриИзменении(Элемент)
	
	Если НЕ СчетУчетаДенежныхДокументов.Валютный Тогда
		ВалютаДокумента = мВалютаРегламентированногоУчета;
		ПриИзмененииВалютыДокумента();
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ВалютаДокументаПриИзменении(Элемент)
	
	ПриИзмененииВалютыДокумента();
	
	Если (ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика)
		И ЗначениеЗаполнено(ДоговорКонтрагента)
		И (ДоговорКонтрагента.ВалютаВзаиморасчетов <> ВалютаДокумента) Тогда
	
		ДоговорКонтрагента = Неопределено;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодотчетноеЛицоПриИзменении(Элемент)
	
	ЗаполнитьПринятоОтДаннымиФизЛица(Контрагент);
	
КонецПроцедуры

Процедура РасчетныйДокументНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	// Надо отфильтровать список документов по организации, физ. лицу и валюте взаиморасчетов.
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Предупреждение("Не выбрана организация!");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Предупреждение("Не выбрано подотчетное лицо!");
		Возврат;
	КонецЕсли;
	
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить("ВыдачаДенежныхДокументов",              "Выдача денежных документов");
	СписокТипов.Добавить("РасходныйКассовыйОрдер",                "Расходный кассовый ордер");
	СписокТипов.Добавить("ПлатежноеПоручениеИсходящее",           "Платежное поручение исходящее");
	СписокТипов.Добавить("ПлатежныйОрдерСписаниеДенежныхСредств", "Платежный ордер на списание денежных средств");
	
	ВыбранныйЭлемент = ВыбратьИзСписка(СписокТипов, Элемент);
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйТип = ВыбранныйЭлемент.Значение;
	ФормаВыбора  = Документы[ВыбранныйТип].ПолучитьФормуВыбора(, Элемент);

	ФормаВыбора.Отбор.Организация.Установить(Организация);
	Если ВыбранныйТип = "РасходныйКассовыйОрдер" Тогда
		ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	ИначеЕсли ВыбранныйТип = "ВыдачаДенежныхДокументов" Тогда
		ФормаВыбора.Отбор.Контрагент.Установить(Контрагент);
	Иначе
		ФормаВыбора.Отбор.ФизЛицо.Установить(Контрагент);
	КонецЕсли;
	
	Если ВыбранныйТип = "ВыдачаДенежныхДокументов" Тогда
		ФормаВыбора.Отбор.ВалютаДокумента.Установить(ВалютаДокумента);
	Иначе
		ФормаВыбора.Отбор.ВалютаВзаиморасчетовРаботника.Установить(ВалютаДокумента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.Значение) 
		И ТипЗнч(Элемент.Значение) = Тип("ДокументСсылка." + ВыбранныйТип) 
		Тогда
		ФормаВыбора.ПараметрТекущаяСтрока = Элемент.Значение;
	КонецЕсли;

	ФормаВыбора.Открыть();

КонецПроцедуры

Процедура ПринятоОтПрочееНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = Справочники.ФизическиеЛица.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура ПринятоОтПрочееОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ЗаполнитьПринятоОтДаннымиФизЛица(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ДЕНЕЖНЫЕ ДОКУМЕНТЫ

Процедура ДенежныеДокументыДенежныйДокументПриИзменении(Элемент)

	СтрокаТЧ = ЭлементыФормы.ДенежныеДокументы.ТекущиеДанные;
	Если СтрокаТЧ.Количество = 0 Тогда
		СтрокаТЧ.Количество = 1;
	КонецЕсли;
	
	СтрокаТЧ.Сумма = СтрокаТЧ.ДенежныйДокумент.Стоимость * СтрокаТЧ.Количество;
	
КонецПроцедуры

Процедура ДенежныеДокументыКоличествоПриИзменении(Элемент)

	СтрокаТЧ = ЭлементыФормы.ДенежныеДокументы.ТекущиеДанные;
	
	СтрокаТЧ.Сумма = СтрокаТЧ.ДенежныйДокумент.Стоимость * СтрокаТЧ.Количество;

КонецПроцедуры

Процедура СчетУчетаДенежныхДокументовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);
	
КонецПроцедуры