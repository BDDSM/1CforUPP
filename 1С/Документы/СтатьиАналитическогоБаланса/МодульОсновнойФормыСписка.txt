Процедура ЗаполнитьСтатьиБаланса()

	СправСтатьиБаланса= Справочники.СтатьиАналитическогоБаланса;
	
	МакетСтатьи=СправСтатьиБаланса.ПолучитьМакет("СтруктураБаланса");
	ОбластьСтатьи = МакетСтатьи.ПолучитьОбласть("Список");
	ОбластьСтатьиВысота = ОбластьСтатьи.ВысотаТаблицы;
	
	НачатьТранзакцию();
	
	Для НСтр = 1 По ОбластьСтатьиВысота Цикл
		
		Код = СокрЛП(ОбластьСтатьи.Область(НСтр, 3).Текст);
		
		Если СокрЛП(ОбластьСтатьи.Область(НСтр, 2).Текст)="1" тогда
			НовыйОбъект=СправСтатьиБаланса.СоздатьГруппу();
		Иначе
			НовыйОбъект=СправСтатьиБаланса.СоздатьЭлемент();
			НовыйОбъект.ОбластьОтчета=СокрЛП(ОбластьСтатьи.Область(НСтр, 5).Текст);
		КонецЕсли;
		
		НовыйОбъект.Владелец=ПараметрОтборПоВладельцу;
		
		Если СокрЛП(ОбластьСтатьи.Область(НСтр, 4).Текст)<>"" тогда
			Родитель=СправСтатьиБаланса.НайтиПоКоду(СокрЛП(ОбластьСтатьи.Область(НСтр, 4).Текст),,,ПараметрОтборПоВладельцу);
			НовыйОбъект.Родитель=Родитель;
		КонецЕсли;	
		
		НовыйОбъект.Код=Код;
		НовыйОбъект.Наименование=СокрЛП(ОбластьСтатьи.Область(НСтр, 1).Текст);
		НовыйОбъект.Записать();
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	
КонецПроцедуры // ЗаполнитьСтатьиБаланса()


Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если  ПараметрОтборПоВладельцу=Неопределено Тогда
		
		Сообщить("Справочник относится к подсистеме ""Бюджетирование"" и не предназначен для открытия из меню ""Операции"".",СтатусСообщения.Внимание);
		Отказ=Истина;
		
		Возврат;
		
	КонецЕсли;
		
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	СтатьиАналитическогоБаланса.Ссылка
	|ИЗ
	|	Справочник.СтатьиАналитическогоБаланса КАК СтатьиАналитическогоБаланса
	|
	|ГДЕ
	|	СтатьиАналитическогоБаланса.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец",ПараметрОтборПоВладельцу); 
	
	Если Запрос.Выполнить().Пустой() Тогда
		ЗаполнитьСтатьиБаланса()
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Не ПараметрОтборПоВладельцу =Неопределено Тогда
		
		Заголовок=ПараметрОтборПоВладельцу.Наименование+": статьи аналитического баланса.";
		Порядок.Установить("Код");
		
	КонецЕсли;
	
КонецПроцедуры