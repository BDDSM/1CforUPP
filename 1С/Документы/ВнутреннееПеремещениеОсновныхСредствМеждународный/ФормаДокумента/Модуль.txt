// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;



// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ОсновныеСредства,ЭлементыФормы.КоманднаяПанельОС.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры


Процедура КоманднаяПанельОСЗаполнить(Кнопка)
	ОсновныеСредства.Очистить();
	Счета = ПланыСчетов.Международный;
	
	МассивСчетов = Новый Массив();
	МассивСчетов.Добавить(Счета.Земля); //1011
	МассивСчетов.Добавить(Счета.Здания); //10121
	МассивСчетов.Добавить(Счета.СооруженияИОборудование); //10131
	МассивСчетов.Добавить(Счета.МашиныИИнструменты); //10141
	МассивСчетов.Добавить(Счета.АвтотранспортныеСредства); //10151
	МассивСчетов.Добавить(Счета.ВложенияВЛизинговоеОборудование); //10161
	МассивСчетов.Добавить(Счета.ПрочиеОсновныеСредства); //10171
	МассивСчетов.Добавить(Счета.ИнвестиционнаяСобственность); //1021

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МеждународныйОбороты.Счет,
	               |	МеждународныйОбороты.Субконто1 КАК ОС,
	               |	СУММА(МеждународныйОбороты.СуммаОборотДт) КАК СуммаОборотДт,
	               |	СУММА(МеждународныйОбороты.СуммаОборотКт) КАК СуммаОборотКт
	               |ИЗ
	               |	РегистрБухгалтерии.Международный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В ИЕРАРХИИ (&Счет), &ОС, Организация = &Организация, КорСчет В ИЕРАРХИИ (&Счет), &ОС) КАК МеждународныйОбороты
	               |
	               |ГДЕ
	               |	(МеждународныйОбороты.Субконто1 = МеждународныйОбороты.КорСубконто1) И
	               |	(МеждународныйОбороты.Счет = МеждународныйОбороты.КорСчет)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	МеждународныйОбороты.Счет,
	               |	МеждународныйОбороты.Субконто1";
	
	Запрос.УстановитьПараметр("НачалоПериода", ПериодНачало);
	Запрос.УстановитьПараметр("КонецПериода", ПериодКонец);
	Запрос.УстановитьПараметр("Счет", МассивСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОС", ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СрезСведенийБУ = РегистрыСведений.МестонахождениеОСБухгалтерскийУчет.ПолучитьПоследнее(ПериодКонец, Новый Структура("ОсновноеСредство", Выборка.ОС));
		СрезСведенийМежд = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.ПолучитьПоследнее(ПериодКонец, Новый Структура("ОсновноеСредство", Выборка.ОС));

		СтрокаТП = ОсновныеСредства.Добавить();
		СтрокаТП.ОсновноеСредство = Выборка.ОС;
		
		СтрокаТП.МестонахождениеОбъекта = СрезСведенийМежд.МестонахождениеОбъекта;
		СтрокаТП.МОЛ = СрезСведенийМежд.МОЛ;
		СтрокаТП.СчетЗатрат = СрезСведенийМежд.СчетЗатрат;
		СтрокаТП.Субконто1 = СрезСведенийМежд.Субконто1;
		СтрокаТП.Субконто2 = СрезСведенийМежд.Субконто2;
		СтрокаТП.Субконто3 = СрезСведенийМежд.Субконто3;
		
		СтрокаТП.МестонахождениеОбъектаНов = СрезСведенийБУ.Местонахождение;
		СтрокаТП.МОЛНов = СрезСведенийБУ.МОЛ;
		СтрокаТП.СчетЗатратНов = СрезСведенийМежд.СчетЗатрат;
		СтрокаТП.Субконто1Нов = СрезСведенийМежд.Субконто1;
		СтрокаТП.Субконто2Нов = СрезСведенийМежд.Субконто2;
		СтрокаТП.Субконто3Нов = СрезСведенийМежд.Субконто3;
	КонецЦикла;
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(ПериодНачало, ?(ПериодКонец='0001-01-01', ПериодКонец, КонецДня(ПериодКонец)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.Редактировать();
	ПериодНачало = НастройкаПериода.ПолучитьДатуНачала();
	ПериодКонец = НастройкаПериода.ПолучитьДатуОкончания();
КонецПроцедуры

Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()

	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, "Продажа");
	КонецЕсли;

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ОсновныеСредстваОсновноеСредствоПриИзменении(Элемент)
	СтрокаТП = ЭлементыФормы.ОсновныеСредства.ТекущаяСтрока;
	
	СрезСведенийБУ = РегистрыСведений.МестонахождениеОСБухгалтерскийУчет.ПолучитьПоследнее(ПериодКонец, Новый Структура("ОсновноеСредство", СтрокаТП.ОсновноеСредство));
	СрезСведенийМежд = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.ПолучитьПоследнее(ПериодКонец, Новый Структура("ОсновноеСредство", СтрокаТП.ОсновноеСредство));

	СтрокаТП.МестонахождениеОбъекта = СрезСведенийМежд.МестонахождениеОбъекта;
	СтрокаТП.МОЛ = СрезСведенийМежд.МОЛ;
	СтрокаТП.СчетЗатрат = СрезСведенийМежд.СчетЗатрат;
	СтрокаТП.Субконто1 = СрезСведенийМежд.Субконто1;
	СтрокаТП.Субконто2 = СрезСведенийМежд.Субконто2;
	СтрокаТП.Субконто3 = СрезСведенийМежд.Субконто3;
	
	СтрокаТП.МестонахождениеОбъектаНов = СрезСведенийБУ.Местонахождение;
	СтрокаТП.МОЛНов = СрезСведенийБУ.МОЛ;
	СтрокаТП.СчетЗатратНов = СрезСведенийМежд.СчетЗатрат;
	СтрокаТП.Субконто1Нов = СрезСведенийМежд.Субконто1;
	СтрокаТП.Субконто2Нов = СрезСведенийМежд.Субконто2;
	СтрокаТП.Субконто3Нов = СрезСведенийМежд.Субконто3;
КонецПроцедуры

Процедура ОсновныеСредстваСубконто1НовНачалоВыбора(Элемент, СтандартнаяОбработка)
	Для Ном = 1 по 3 Цикл
		Если (ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СчетЗатратНов.ВидыСубконто.Количество() > (Ном-1)) и (НЕ ЗначениеЗаполнено(ЭлементыФормы.ОсновныеСредства.ТекущиеДанные["Субконто"+Ном+"Нов"])) Тогда
			Типы = ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СчетЗатратНов.ВидыСубконто[Ном-1].ВидСубконто.ТипЗначения.Типы();
			ЭлементыФормы.ОсновныеСредства.ТекущиеДанные["Субконто"+Ном+"Нов"] = Новый(Типы[0]);
		КонецЕсли;
	КонецЦикла
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры
// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

Процедура ОрганизацияПриИзменении(Элемент)
	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
КонецПроцедуры

Процедура ОсновныеСредстваСчетЗатратНовПриИзменении(Элемент)
	ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто1Нов = Неопределено;
	ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто2Нов = Неопределено;
	ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто3Нов = Неопределено;
КонецПроцедуры

// Процедура вызова структуры подчиненности документа
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать() 

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры