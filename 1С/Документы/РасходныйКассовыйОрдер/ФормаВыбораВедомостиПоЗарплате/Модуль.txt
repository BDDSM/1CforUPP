////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПолучитьСтруктуруВыбора()

	Если ЭлементыФормы.СписокДокументов.ТекущаяСтрока <> Неопределено Тогда
		
		ТД = ЭлементыФормы.СписокДокументов.ТекущиеДанные;
		
		РезультатФункции = Новый Структура;
		Для каждого Колонка Из СписокДокументов.Колонки Цикл
			РезультатФункции.Вставить(Колонка.Имя, ТД[Колонка.Имя]);
		КонецЦикла;
		
	Иначе
		РезультатФункции = Неопределено;
	КонецЕсли;
	
	Возврат РезультатФункции;

КонецФункции // ПолучитьСтруктуруВыбора()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка) КАК СсылкаПредставление,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Дата КАК Дата,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Номер,
	|	СУММА(ЗарплатаКВыплатеОрганизацийЗарплата.Сумма + ЗарплатаКВыплатеОрганизацийЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК Сумма,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация,
	|	ПРЕДСТАВЛЕНИЕ(ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация) КАК ОрганизацияПредставление,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо,
	|	ПРЕДСТАВЛЕНИЕ(ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо) КАК ФизлицоПредставление,
	|	ВЫРАЗИТЬ(ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Комментарий КАК СТРОКА(200)) КАК Комментарий
	|ИЗ
	|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = РанееВыплаченныеСуммы.Ведомость
	|			И ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо
	|			И (РанееВыплаченныеСуммы.Регистратор <> &Ссылка)
	|ГДЕ
	|	ЗарплатаКВыплатеОрганизацийЗарплата.ВыплаченностьЗарплаты = &ВыплаченностьЗарплаты
	|	И (НЕ ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.ПометкаУдаления)
	|	И ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.СпособВыплаты = &СпособВыплаты
	|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|	//ГДЕ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Дата,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Номер,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация,
	|	ВЫРАЗИТЬ(ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Комментарий КАК СТРОКА(200))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ВыплаченностьЗарплаты", Перечисления.ВыплаченностьЗарплаты.Выплачено);
	Запрос.УстановитьПараметр("СпособВыплаты", Перечисления.СпособыВыплатыЗарплаты.ЧерезКассу);
	Запрос.УстановитьПараметр("Ссылка", РКО);
	
	ДопУсловия = "";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.УстановитьПараметр("Организация", Организация);
		ДопУсловия = ДопУсловия + " И ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация = &Организация";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		Запрос.УстановитьПараметр("Физлицо", Контрагент);
		ДопУсловия = ДопУсловия + " И ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = &Физлицо";
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ГДЕ", ДопУсловия);
	
	Результат = Запрос.Выполнить();
	СписокДокументов = Результат.Выгрузить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыВыбрать(Кнопка)
	
	Закрыть(ПолучитьСтруктуруВыбора());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Закрыть(ПолучитьСтруктуруВыбора());
	
КонецПроцедуры