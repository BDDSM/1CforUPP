////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ТаблицаРаспределенияКонтрагентов,ЭлементыФормы.КоманднаяПанельТаблицаРаспределенияКонтрагентов.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры

// Функция определяет параметр ABC_КлассКлиента контрагента
//
// Параметры
//  ABC_КлассКлиентаСсылка - ПеречислениеСсылка, для которой необходимо
//  определить значение параметра
//
// Возвращаемое значение:
//   ЗначениеПараметраABC_КлассКлиента - Число
//
Функция ПолучитьЗначениеПараметраABC_КлассКлиента(ABC_КлассКлиентаСсылка)

	Если ABC_КлассКлиентаСсылка = Перечисления.ABCКлассификация.AКласс Тогда
		Возврат ПроцентAКласса;
	ИначеЕсли ABC_КлассКлиентаСсылка = Перечисления.ABCКлассификация.BКласс Тогда
		Возврат ПроцентBКласса;
	ИначеЕсли ABC_КлассКлиентаСсылка = Перечисления.ABCКлассификация.CКласс Тогда
		Возврат ПроцентCКласса;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции // ПолучитьЗначениеПараметраABC_КлассКлиента()

// Процедура сортирует табличную часть
//
Процедура СортироватьТЧ()

	ТаблицаРаспределенияКонтрагентов.Сортировать("ABCКлассификация ВОЗР, ЗначениеПараметра УБЫВ");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	
	Если ЭтоНовый() Тогда
	
		ВыборкаДокументов = Документы.ABCКлассификацияПокупателей.Выбрать(, Дата, , "Дата УБЫВ");
		Если ВыборкаДокументов.Следующий() Тогда
			ПроцентAКласса = ВыборкаДокументов.Ссылка.ПроцентAКласса;
			ПроцентBКласса = ВыборкаДокументов.Ссылка.ПроцентBКласса;
			ПроцентCКласса = ВыборкаДокументов.Ссылка.ПроцентCКласса;
			ДатаНачала = НачалоДня(ВыборкаДокументов.Ссылка.ДатаОкончания + 24*60*60);
			ДатаОкончания = КонецМесяца(ДатаНачала);
		Иначе
			ПроцентAКласса = 70;
			ПроцентBКласса = 20;
			ПроцентCКласса = 10;
		КонецЕсли; 
		
		Если ДатаНачала = '00010101000000' Тогда
			ДатаНачала = НачалоМесяца(ТекущаяДата());
		КонецЕсли; 
		Если ДатаОкончания = '00010101000000' Тогда
			ДатаОкончания = КонецМесяца(ТекущаяДата());
		КонецЕсли; 
		
	КонецЕсли;
	
	СортироватьТЧ();
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()
	
	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ
		
		Ответственный = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновнойОтветственный");
		
	КонецЕсли;
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "После записи"
//
Процедура ПослеЗаписи()
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
КонецПроцедуры // ПослеЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ОБРАБОТЧИКОВ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события Нажатие элемента формы КоманднаяПанельТаблицаРаспределенияКонтрагентов.Заполнить.
//
Процедура КоманднаяПанельТаблицаРаспределенияКонтрагентовЗаполнить(Кнопка)
	
	Если (ПроцентAКласса + ПроцентBКласса + ПроцентCКласса) <> 100 Тогда
		Предупреждение("Сумма значений критериев для параметра распределения, указанных на закладке ""Параметры распределения"", должна быть равна 100%.");
		Возврат;
	КонецЕсли; 
	
	Если ТаблицаРаспределенияКонтрагентов.Количество() > 0 Тогда
		ОтветНаВопрос = Вопрос("Таблица будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	
	СтруктураЗначенийРегистра = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(ТекущаяДата());
	Если НЕ ЗначениеЗаполнено(СтруктураЗначенийРегистра) Тогда
		Возврат;
	КонецЕсли; 
	
	ПараметрАВСРаспределения = Неопределено;
	СтруктураЗначенийРегистра.Свойство("ПараметрABCКлассификацииПокупателей", ПараметрАВСРаспределения);
	
	Если ПараметрАВСРаспределения = Неопределено ИЛИ (ТипЗнч(ПараметрАВСРаспределения) = Тип("ПеречислениеСсылка.ПараметрыABCКлассификацииПокупателей") И ПараметрАВСРаспределения.Пустая()) Тогда
		Предупреждение("В учетной политике предприятия не указан параметр для АВС-классификации покупателей. Заполнение невозможно.");
		Возврат;
	КонецЕсли; 
	
	Если ПараметрАВСРаспределения = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВаловойПрибыли Тогда
		СтрокаЗапроса = "
		|	ВЫБОР КОГДА ЦенаСебестоимостиНоменклатуры.ЦенаСебестоимости ЕСТЬ NULL ТОГДА
		|		ПродажиКомпанииОбороты.СтоимостьОборот
		|	ИНАЧЕ
		|		ПродажиКомпанииОбороты.СтоимостьОборот - (ПродажиКомпанииОбороты.КоличествоОборот * ЦенаСебестоимостиНоменклатуры.ЦенаСебестоимости)
		|	КОНЕЦ
		|";
	ИначеЕсли ПараметрАВСРаспределения = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВыручки Тогда
		СтрокаЗапроса = "
		|		ПродажиКомпанииОбороты.СтоимостьОборот";
	Иначе
		Предупреждение("В учетной политике предприятия не указан параметр для АВС-классификации покупателей. Заполнение невозможно.");
		Возврат;
	КонецЕсли; 

	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫБОР
	               |		КОГДА ABCКлассификацияПокупателейСрезПоследних.ABCКлассПокупателя ЕСТЬ NULL 
	               |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ABCКлассификация.ПустаяСсылка)
	               |		ИНАЧЕ ABCКлассификацияПокупателейСрезПоследних.ABCКлассПокупателя
	               |	КОНЕЦ КАК ABCКлассПокупателя,
	               |	ВЫБОР
	               |		КОГДА СтадииВзаимоотношенийСПокупателямиСрезПоследних.Стадия ЕСТЬ NULL 
	               |			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтадииВзаимоотношенийСПокупателями.ПустаяСсылка)
	               |		ИНАЧЕ СтадииВзаимоотношенийСПокупателямиСрезПоследних.Стадия
	               |	КОНЕЦ КАК СтадияПокупателя,
	               |	ВложенныйЗапрос.Контрагент КАК Контрагент,
	               |	ВложенныйЗапрос.Контрагент.ОсновнойМенеджерПокупателя КАК Менеджер,
	               |	ВложенныйЗапрос.Сумма КАК Сумма
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ПродажиОбороты.Контрагент КАК Контрагент,
	               |		ВЫБОР
	               |			КОГДА &НеВключатьНДСВСтоимостьПартий
	               |				ТОГДА СУММА(ПродажиОбороты.СтоимостьОборот - ПродажиОбороты.НДСОборот)
	               |			ИНАЧЕ СУММА(ПродажиОбороты.СтоимостьОборот)
	               |		КОНЕЦ";
				   
	Если СтруктураЗначенийРегистра.ПараметрABCКлассификацииПокупателей = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВаловойПрибыли Тогда
		Запрос.Текст = Запрос.Текст + " - СУММА(ЕСТЬNULL(ТаблицаРегистраПродажиСебестоимость.СтоимостьОборот, 0))";
	КонецЕсли;				   
	
	Запрос.Текст = Запрос.Текст + " КАК Сумма
	               |	ИЗ
	               |		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ) КАК ПродажиОбороты";
				   
	Если СтруктураЗначенийРегистра.ПараметрABCКлассификацииПокупателей = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВаловойПрибыли Тогда
		Запрос.Текст = Запрос.Текст + "
				   |			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				   |				ПродажиСебестоимость.Номенклатура КАК Номенклатура,
				   |				ПродажиСебестоимость.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				   |				ВЫБОР
				   |					КОГДА ПродажиСебестоимость.ДокументДвижения <> НЕОПРЕДЕЛЕНО
				   |						ТОГДА ПродажиСебестоимость.ДокументДвижения
				   |					ИНАЧЕ ПродажиСебестоимость.Регистратор
				   |				КОНЕЦ КАК Регистратор,
				   |				СУММА(ПродажиСебестоимость.Стоимость) КАК СтоимостьОборот
				   |			ИЗ
				   |				РегистрНакопления.ПродажиСебестоимость КАК ПродажиСебестоимость
				   |			ГДЕ
				   |				ПродажиСебестоимость.Период МЕЖДУ &ДатаНачала И &ДатаКонца
				   |
				   |			СГРУППИРОВАТЬ ПО
				   |				ПродажиСебестоимость.Номенклатура,
				   |				ПродажиСебестоимость.ХарактеристикаНоменклатуры,
				   |				ВЫБОР
				   |					КОГДА ПродажиСебестоимость.ДокументДвижения <> НЕОПРЕДЕЛЕНО
				   |						ТОГДА ПродажиСебестоимость.ДокументДвижения
				   |					ИНАЧЕ ПродажиСебестоимость.Регистратор
				   |				КОНЕЦ) КАК ТаблицаРегистраПродажиСебестоимость
				   |			ПО (ТаблицаРегистраПродажиСебестоимость.Номенклатура = ПродажиОбороты.Номенклатура)
				   |				И (ТаблицаРегистраПродажиСебестоимость.ХарактеристикаНоменклатуры = ПродажиОбороты.ХарактеристикаНоменклатуры)
				   |				И (ТаблицаРегистраПродажиСебестоимость.Регистратор = ПродажиОбороты.Регистратор)";
	КонецЕсли;				   
	
	Запрос.Текст = Запрос.Текст + "
				   |	ГДЕ
	               |		ПродажиОбороты.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ПродажиОбороты.Контрагент
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		Контрагенты.Ссылка,
	               |		0
	               |	ИЗ
	               |		Справочник.Контрагенты КАК Контрагенты
	               |	ГДЕ
	               |		(НЕ Контрагенты.Ссылка В
	               |					(ВЫБРАТЬ
	               |						ПродажиКомпанииОбороты.Контрагент
	               |					ИЗ
	               |						РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца) КАК ПродажиКомпанииОбороты))
	               |		И Контрагенты.Покупатель = ИСТИНА) КАК ВложенныйЗапрос
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтадииВзаимоотношенийСПокупателями.СрезПоследних(&ДатаКонца, ) КАК СтадииВзаимоотношенийСПокупателямиСрезПоследних
	               |		ПО ВложенныйЗапрос.Контрагент = СтадииВзаимоотношенийСПокупателямиСрезПоследних.Контрагент
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ABCКлассификацияПокупателей.СрезПоследних(&ДатаКонца, ) КАК ABCКлассификацияПокупателейСрезПоследних
	               |		ПО ВложенныйЗапрос.Контрагент = ABCКлассификацияПокупателейСрезПоследних.Контрагент";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("НеВключатьНДСВСтоимостьПартий", СтруктураЗначенийРегистра.НеВключатьНДСВСтоимостьПартий);

	ТаблицаРаспределенияКонтрагентов.Очистить();

	ТаблицаКонтрагентов = Запрос.Выполнить().Выгрузить();
	ТаблицаКонтрагентов.Индексы.Добавить("ABCКлассПокупателя, СтадияПокупателя");
	
	// Сначала уберем все строки, у которых стадия - потерянный, а АВС-класс - пустая ссылка
	ПотерянныеСтроки = ТаблицаКонтрагентов.НайтиСтроки(Новый Структура("ABCКлассПокупателя, СтадияПокупателя", Перечисления.ABCКлассификация.ПустаяСсылка(), Перечисления.СтадииВзаимоотношенийСПокупателями.ПотерянныйПокупатель));
	Для каждого ПотеряннаяСтрока Из ПотерянныеСтроки Цикл
		ТаблицаКонтрагентов.Удалить(ПотеряннаяСтрока);
	КонецЦикла; 
	
	ТаблицаКонтрагентов.Сортировать("Сумма ВОЗР");

	ВсегоСумма = ТаблицаКонтрагентов.Итог("Сумма");

	СуммаВысокая = Окр((ВсегоСумма*ПолучитьЗначениеПараметраABC_КлассКлиента(Перечисления.ABCКлассификация.AКласс)/100),2);
	СуммаСредняя = Окр((ВсегоСумма*ПолучитьЗначениеПараметраABC_КлассКлиента(Перечисления.ABCКлассификация.BКласс)/100),2);
	СуммаНизкая = ВсегоСумма - СуммаВысокая - СуммаСредняя;

	СуммаНакопления = 0;
	Для каждого Строки Из ТаблицаКонтрагентов Цикл
		
		СуммаНакопления = СуммаНакопления + Строки.Сумма;
		
		НоваяСтрока = ТаблицаРаспределенияКонтрагентов.Добавить();
		
		Если СуммаНакопления <= СуммаНизкая Тогда
			НоваяСтрока.ABCКлассификация = Перечисления.ABCКлассификация.CКласс;
		ИначеЕсли СуммаНакопления <= (СуммаНизкая + СуммаСредняя) Тогда
			НоваяСтрока.ABCКлассификация = Перечисления.ABCКлассификация.BКласс;
		Иначе
			НоваяСтрока.ABCКлассификация = Перечисления.ABCКлассификация.AКласс;
		КонецЕсли;
		
		// Обнулим АВС-класс, для контрагентов, у которых стадия - потерянный
		Если Строки.СтадияПокупателя = Перечисления.СтадииВзаимоотношенийСПокупателями.ПотерянныйПокупатель Тогда
			НоваяСтрока.ABCКлассификация = Перечисления.ABCКлассификация.ПустаяСсылка();
		КонецЕсли; 
		
		НоваяСтрока.Контрагент = Строки.Контрагент;
		НоваяСтрока.ABCКлассификацияСтарая = Строки.ABCКлассПокупателя;
		НоваяСтрока.ЗначениеПараметра = Строки.Сумма;
		НоваяСтрока.МенеджерКонтрагента = Строки.Менеджер;
		
	КонецЦикла;

	СортироватьТЧ();
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанельТаблицаРаспределенияКонтрагентов.Перераспределить.
//
Процедура КоманднаяПанельТаблицаРаспределенияКонтрагентовПерераспределить(Кнопка)
	
	Если (ПроцентAКласса + ПроцентBКласса + ПроцентCКласса) <> 100 Тогда
		Предупреждение("Сумма значений критериев для параметра распределения, указанных на закладке ""Параметры распределения"", должна быть равна 100%.");
		Возврат;
	КонецЕсли; 
	
	ТаблицаКонтрагентов = ТаблицаРаспределенияКонтрагентов.Выгрузить();
	ТаблицаКонтрагентов.Колонки.Добавить("ИндексКоличества");
	Для каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл
		СтрокаТаблицы.ИндексКоличества = 1;
	КонецЦикла; 
	
	ТекстСообщения = "";
	ТаблицаКонтрагентов.Свернуть("Контрагент", "ИндексКоличества");
	Для каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл
		Если СтрокаТаблицы.ИндексКоличества > 1 Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + СтрокаТаблицы.Контрагент;
		КонецЕсли; 
	КонецЦикла; 
	
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		Сообщить("Найдены строки с повторяющимися контрагентами, корректное перераспределение невозможно." + ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	ОтветНаВопрос = Вопрос("Будут перераспределены классы покупателей в зависимости от значений параметра. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаКонтрагентов = ТаблицаРаспределенияКонтрагентов.Выгрузить();
	
	ТаблицаКонтрагентов.Сортировать("ЗначениеПараметра ВОЗР");

	ВсегоСумма = ТаблицаКонтрагентов.Итог("ЗначениеПараметра");

	СуммаВысокая = Окр((ВсегоСумма*ПолучитьЗначениеПараметраABC_КлассКлиента(Перечисления.ABCКлассификация.AКласс)/100),2);
	СуммаСредняя = Окр((ВсегоСумма*ПолучитьЗначениеПараметраABC_КлассКлиента(Перечисления.ABCКлассификация.BКласс)/100),2);
	СуммаНизкая = ВсегоСумма - СуммаВысокая - СуммаСредняя;

	СуммаНакопления = 0;
	Для каждого Строки Из ТаблицаКонтрагентов Цикл
		
		СуммаНакопления = СуммаНакопления + Строки.ЗначениеПараметра;
		
		Если СуммаНакопления <= СуммаНизкая Тогда
			ABCКлассификация = Перечисления.ABCКлассификация.CКласс;
		ИначеЕсли СуммаНакопления <= (СуммаНизкая + СуммаСредняя) Тогда
			ABCКлассификация = Перечисления.ABCКлассификация.BКласс;
		Иначе
			ABCКлассификация = Перечисления.ABCКлассификация.AКласс;
		КонецЕсли;
		
		СтрокаКонтрагента = ТаблицаРаспределенияКонтрагентов.Найти(Строки.Контрагент, "Контрагент");
		Если СтрокаКонтрагента <> Неопределено Тогда
			СтрокаКонтрагента.ABCКлассификация = ABCКлассификация;
		КонецЕсли; 
		
	КонецЦикла;

	СортироватьТЧ();
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанельТаблицаРаспределенияКонтрагентов.История.
//
Процедура КоманднаяПанельТаблицаРаспределенияКонтрагентовИстория(Кнопка)
	
	Если ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные <> Неопределено Тогда
	
		ФормаРегистра = РегистрыСведений.ABCКлассификацияПокупателей.ПолучитьФормуСписка(, ЭтаФорма);
		ФормаРегистра.Отбор.Контрагент.Значение      = ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.Контрагент;
		ФормаРегистра.Отбор.Контрагент.Использование = Истина;
		ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НастройкаОтбора.Контрагент.Доступность = Ложь;
		ФормаРегистра.Открыть();
		
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПослеУдаления элемента формы ТаблицаРаспределенияКонтрагентов.
//
Процедура ТаблицаРаспределенияКонтрагентовПослеУдаления(Элемент)
	
	СортироватьТЧ();
	
КонецПроцедуры

// Обработчик события ПриОкончанииРедактирования элемента формы ТаблицаРаспределенияКонтрагентов.
//
Процедура ТаблицаРаспределенияКонтрагентовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	СортироватьТЧ();
	
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ТаблицаРаспределенияКонтрагентов.Контрагент.
//
Процедура ТаблицаРаспределенияКонтрагентовКонтрагентПриИзменении(Элемент)
	
	Если Элемент.Значение.Пустая() Тогда
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификация       = Перечисления.ABCКлассификация.ПустаяСсылка();
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = Перечисления.ABCКлассификация.ПустаяСсылка();
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ЗначениеПараметра       = 0;
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.МенеджерКонтрагента     = Справочники.Пользователи.ПустаяСсылка();
		Возврат;
	Иначе
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.МенеджерКонтрагента     = Элемент.Значение.ОсновнойМенеджерПокупателя;
	КонецЕсли; 
	
	СтруктураЗначенийРегистра = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(ТекущаяДата());
	Если НЕ ЗначениеЗаполнено(СтруктураЗначенийРегистра) Тогда
		Возврат;
	КонецЕсли; 
	
	ПараметрАВСРаспределения = Неопределено;
	СтруктураЗначенийРегистра.Свойство("ПараметрABCКлассификацииПокупателей", ПараметрАВСРаспределения);
	
	Если ПараметрАВСРаспределения = Неопределено ИЛИ (ТипЗнч(ПараметрАВСРаспределения) = Тип("ПеречислениеСсылка.ПараметрыABCКлассификацииПокупателей") И ПараметрАВСРаспределения.Пустая()) Тогда
		СтруктураЗначенийРегистраКлассов = РегистрыСведений.ABCКлассификацияПокупателей.ПолучитьПоследнее(ТекущаяДата(), Новый Структура("Контрагент", Элемент.Значение));
		Попытка
			ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = СтруктураЗначенийРегистраКлассов.ABCКлассПокупателя;
		Исключение
			ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = Перечисления.ABCКлассификация.ПустаяСсылка();
		КонецПопытки;
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ЗначениеПараметра       = 0;
		Возврат;
	КонецЕсли; 
	
	Если ПараметрАВСРаспределения = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВаловойПрибыли Тогда
		СтрокаЗапроса = "
		|	ВЫБОР КОГДА ЦенаСебестоимостиНоменклатуры.ЦенаСебестоимости ЕСТЬ NULL ТОГДА
		|		ПродажиСумма.Сумма
		|	ИНАЧЕ
		|		ПродажиСумма.Сумма - (ПродажиСумма.Количество * ЦенаСебестоимостиНоменклатуры.ЦенаСебестоимости)
		|	КОНЕЦ
		|";
	ИначеЕсли ПараметрАВСРаспределения = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВыручки Тогда
		СтрокаЗапроса = "
		|		ПродажиСумма.Сумма ";
	Иначе
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = Перечисления.ABCКлассификация.ПустаяСсылка();
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ЗначениеПараметра       = 0;
		Возврат;
	КонецЕсли; 

	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|
	|	ВЫБОР КОГДА ABCКлассификацияПокупателей.ABCКлассПокупателя ЕСТЬ NULL ТОГДА
	|		&ПустойABCКласс
	|	ИНАЧЕ
	|		ABCКлассификацияПокупателей.ABCКлассПокупателя
	|	КОНЕЦ                           КАК ABCКлассПокупателя,
	|	ТаблицаКонтрагентов.Контрагент  КАК Контрагент,
	|	СУММА(ТаблицаКонтрагентов.Сумма)КАК Сумма
	|
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|	
	|		ПродажиСумма.Контрагент КАК Контрагент,
	|		" + СтрокаЗапроса + "   КАК Сумма
	|	
	|	ИЗ
	|		(
	|	
	|		ВЫБРАТЬ
	|			ПродажиКомпанииОбороты.Контрагент					  КАК Контрагент,
	|			ПродажиКомпанииОбороты.Номенклатура                   КАК Номенклатура,
	|			ПродажиКомпанииОбороты.СтоимостьОборот                КАК Сумма,
	|			ПродажиКомпанииОбороты.КоличествоОборот               КАК Количество
	|			
	|
	|		ИЗ
	|			РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, , ДоговорКонтрагента <> &ПустойДоговор)
	|		                          КАК ПродажиКомпанииОбороты
	|		ГДЕ
	|			ПродажиКомпанииОбороты.Контрагент = &ВыбКонтрагент
	|
	|		) КАК ПродажиСумма
	|	
	|";
	
	Если ПараметрАВСРаспределения = Перечисления.ПараметрыABCКлассификацииПокупателей.СуммаВаловойПрибыли Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		(
		|		ВЫБРАТЬ
		|			СебестоимостьНоменклатуры.Номенклатура КАК Номенклатура,
		|			ВЫБОР КОГДА СебестоимостьНоменклатуры.Количество ЕСТЬ NULL ИЛИ СебестоимостьНоменклатуры.Количество = 0 ТОГДА
		|				0
		|			ИНАЧЕ
		|				СебестоимостьНоменклатуры.СуммаСебестоимости/СебестоимостьНоменклатуры.Количество
		|			КОНЕЦ КАК ЦенаСебестоимости
		|		ИЗ
		|		
		|			(
		|			ВЫБРАТЬ
		|				ПродажиСебестоимость.Номенклатура            КАК Номенклатура,
		|				СУММА(ПродажиСебестоимость.СтоимостьОборот)  КАК СуммаСебестоимости,
		|				СУММА(ПродажиСебестоимость.КоличествоОборот) КАК Количество
		|			ИЗ
		|				РегистрНакопления.ПродажиСебестоимость.Обороты(&ДатаНачала, &ДатаКонца) КАК ПродажиСебестоимость
		|				
		|			СГРУППИРОВАТЬ ПО
		|				ПродажиСебестоимость.Номенклатура
		|						
		|			) КАК СебестоимостьНоменклатуры
		|			
		|		) КАК ЦенаСебестоимостиНоменклатуры
		|		
		|	ПО 
		|		ЦенаСебестоимостиНоменклатуры.Номенклатура = ПродажиСумма.Номенклатура
		|";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|   ) КАК ТаблицаКонтрагентов
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ABCКлассификацияПокупателей.СрезПоследних(&ДатаКонца) КАК ABCКлассификацияПокупателей
	|
	|ПО
	|	ABCКлассификацияПокупателей.Контрагент = ТаблицаКонтрагентов.Контрагент
	|	
	|СГРУППИРОВАТЬ ПО
	|
	|	ВЫБОР КОГДА ABCКлассификацияПокупателей.ABCКлассПокупателя ЕСТЬ NULL ТОГДА
	|		&ПустойABCКласс
	|	ИНАЧЕ
	|		ABCКлассификацияПокупателей.ABCКлассПокупателя
	|	КОНЕЦ,
	|	ТаблицаКонтрагентов.Контрагент
	|";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("ПустойДоговор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойABCКласс", Перечисления.ABCКлассификация.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВыбКонтрагент", Элемент.Значение);
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаЗапроса.Количество() > 0 Тогда
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = ТаблицаЗапроса[0].ABCКлассПокупателя;
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ЗначениеПараметра       = ТаблицаЗапроса[0].Сумма;
	Иначе
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ABCКлассификацияСтарая = Перечисления.ABCКлассификация.ПустаяСсылка();
		ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.ЗначениеПараметра       = 0;
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПриВыводеСтроки элемента формы ТаблицаРаспределенияКонтрагентов.
//
Процедура ТаблицаРаспределенияКонтрагентовПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	СуммаВсего = ТаблицаРаспределенияКонтрагентов.Итог("ЗначениеПараметра");
	СтрокиСПустыКлассом = ТаблицаРаспределенияКонтрагентов.НайтиСтроки(Новый Структура("ABCКлассификация", Перечисления.ABCКлассификация.ПустаяСсылка()));
	Для каждого СтрокаСПустымКлассом Из СтрокиСПустыКлассом Цикл
		СуммаВсего = СуммаВсего - СтрокаСПустымКлассом.ЗначениеПараметра;
	КонецЦикла; 
	
	Если ДанныеСтроки.ABCКлассификация.Пустая() Тогда
		ОформлениеСтроки.Ячейки.ABCКлассификация.ОтображатьТекст = Истина;
		ОформлениеСтроки.Ячейки.ABCКлассификация.Текст = "Не указан";
		ОформлениеСтроки.Ячейки.ПроцентПараметра.ОтображатьТекст = Ложь;
		ОформлениеСтроки.ЦветТекста = WebЦвета.Серый;
	Иначе
		ОформлениеСтроки.Ячейки.ПроцентПараметра.ОтображатьТекст = Истина;
		Если СуммаВсего = 0 Тогда
			ОформлениеСтроки.Ячейки.ПроцентПараметра.Текст = "";
		Иначе
			ОформлениеСтроки.Ячейки.ПроцентПараметра.Текст = Формат((ДанныеСтроки.ЗначениеПараметра*100/СуммаВсего), "ЧЦ=4; ЧДЦ=2");
		КонецЕсли; 
		Если ДанныеСтроки.ABCКлассификация = Перечисления.ABCКлассификация.AКласс Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.Кирпичный;
		ИначеЕсли ДанныеСтроки.ABCКлассификация = Перечисления.ABCКлассификация.BКласс Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.ТемноЗеленый;
		ИначеЕсли ДанныеСтроки.ABCКлассификация = Перечисления.ABCКлассификация.CКласс Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.ТемноСиний;
		КонецЕсли;
	КонецЕсли; 
	
	ОформлениеСтроки.Ячейки.Контрагент.ОтображатьКартинку = Истина;
	Если ДанныеСтроки.ABCКлассификацияСтарая.Пустая() Тогда
		ОформлениеСтроки.Ячейки.Контрагент.ИндексКартинки = 2;
	ИначеЕсли ДанныеСтроки.ABCКлассификация.Пустая() Тогда
		ОформлениеСтроки.Ячейки.Контрагент.ОтображатьКартинку = Ложь;
	Иначе
		Если Перечисления.ABCКлассификация.Индекс(ДанныеСтроки.ABCКлассификация) = Перечисления.ABCКлассификация.Индекс(ДанныеСтроки.ABCКлассификацияСтарая) Тогда
			ОформлениеСтроки.Ячейки.Контрагент.ИндексКартинки = 0;
		ИначеЕсли Перечисления.ABCКлассификация.Индекс(ДанныеСтроки.ABCКлассификация) < Перечисления.ABCКлассификация.Индекс(ДанныеСтроки.ABCКлассификацияСтарая) Тогда
			ОформлениеСтроки.Ячейки.Контрагент.ИндексКартинки = 2;
		Иначе
			ОформлениеСтроки.Ячейки.Контрагент.ИндексКартинки = 1;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПриСменеСтраницы элемента формы ПанельДокумента.
//
Процедура ПанельДокументаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.ТекущаяСтраница.Имя = "ПараметрыРаспределения" Тогда
	
		СтруктураЗначенийРегистра = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(ТекущаяДата());
		Если НЕ ЗначениеЗаполнено(СтруктураЗначенийРегистра) Тогда
			Возврат;
		КонецЕсли; 
		
		ПараметрАВСРаспределения = Неопределено;
		СтруктураЗначенийРегистра.Свойство("ПараметрABCКлассификацииПокупателей", ПараметрАВСРаспределения);
		
		Если ПараметрАВСРаспределения = Неопределено ИЛИ (ТипЗнч(ПараметрАВСРаспределения) = Тип("ПеречислениеСсылка.ПараметрыABCКлассификацииПокупателей") И ПараметрАВСРаспределения.Пустая()) Тогда
			ЭлементыФормы.НадписьПараметраРаспределения.Заголовок = "Параметр в учетной политике не указан."
		Иначе
			ЭлементыФормы.НадписьПараметраРаспределения.Заголовок = "" + ПараметрАВСРаспределения + ", в валюте упр.учета (" + СокрЛП(глЗначениеПеременной("ВалютаУправленческогоУчета").Наименование) + ")";
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КнопкаВыбораПериода.
//
Процедура КнопкаВыбораПериодаНажатие(Элемент)
	
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(ДатаНачала, ?(ДатаОкончания='0001-01-01', ДатаОкончания, КонецДня(ДатаОкончания)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Интервал;
	НастройкаПериода.Редактировать();
	ДатаНачала    = НастройкаПериода.ДатаНачала;
	ДатаОкончания = НастройкаПериода.ДатаОкончания;
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанельТаблицаРаспределенияКонтрагентов.ДокументыПоКонтрагенту.
//
Процедура КоманднаяПанельТаблицаРаспределенияКонтрагентовДокументыПоКонтрагенту(Кнопка)
	
	Если ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	ФормаЖурнала = ЖурналыДокументов.ДокументыКонтрагентов.ПолучитьФорму();
	ФормаЖурнала.Отбор.ДокументыПоКонтрагенту.Значение      = ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.Контрагент;
	ФормаЖурнала.Отбор.ДокументыПоКонтрагенту.Использование = Истина;
	
	ФормаЖурнала.Отбор.Дата.Использование = Истина;
	ФормаЖурнала.Отбор.Дата.ВидСравнения  = ВидСравнения.ИнтервалВключаяГраницы;
	ФормаЖурнала.Отбор.Дата.ЗначениеС  = НачалоДня(ДатаНачала);
	ФормаЖурнала.Отбор.Дата.ЗначениеПо = КонецДня(ДатаОкончания);
	
	ФормаЖурнала.Открыть();
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанельТаблицаРаспределенияКонтрагентов.Взаиморасчеты.
//
Процедура КоманднаяПанельТаблицаРаспределенияКонтрагентовВзаиморасчеты(Кнопка)
	
	Если ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетКонтрагента = Отчеты.ВедомостьВзаиморасчетыСКонтрагентами.Создать();
	
	ОтчетКонтрагента.УстановитьНачальныеНастройки();
	
	ОтчетКонтрагента.УниверсальныйОтчет.ДатаКон = КонецДня(ДатаОкончания);
	ОтчетКонтрагента.УниверсальныйОтчет.ДатаНач = НачалоДня(ДатаНачала);
	ОтчетКонтрагента.УниверсальныйОтчет.ВыводитьДетальныеЗаписи = Истина;
	
	Построитель = ОтчетКонтрагента.УниверсальныйОтчет.ПостроительОтчета;
	
	// Удалить группировки отчета по умолчанию
	ОтчетКонтрагента.УниверсальныйОтчет.ОчиститьНастройкиПостроителя();
	
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ДоговорКонтрагента");
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Сделка");
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьДополнительноеПоле("Период");
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьДополнительноеПоле("Регистратор");
	
	ТекКонтрагент = ЭлементыФормы.ТаблицаРаспределенияКонтрагентов.ТекущиеДанные.Контрагент;
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьОтбор("Контрагент", Истина, ВидСравнения.Равно, ТекКонтрагент);
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьПорядок("Период");
	ОтчетКонтрагента.УниверсальныйОтчет.ДобавитьПорядок("Регистратор");
		
	ОтчетКонтрагента.УниверсальныйОтчет.мВосстанавливатьНастройкиПриОткрытии = Ложь;
	
	ФормаОтчета = ОтчетКонтрагента.ПолучитьФорму();
	ФормаОтчета.ОбновитьОтчет();
	ФормаОтчета.Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// ***НЕ СУЩЕСТВУЕТ ОБЪЕКТ ПРОЦЕДУРЫ*** 
//Процедура выполняет изменение возможности редактирования номера документа
//
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
КонецПроцедуры // ДействияФормыРедактироватьНомер()

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры