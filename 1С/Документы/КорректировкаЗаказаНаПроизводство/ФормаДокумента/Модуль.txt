Перем мТекущаяДатаДокумента; // Хранит последнюю установленную дату документа - для проверки перехода документа в другой период

Перем мКолонкиПродукция, мКолонкиМатериалы;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

Перем мИспользоватьТолькоСборочныеСпецификации Экспорт;

Перем мФормаВводПараметровВыпуска, мТекущаяСтрокаПродукцияПараметры;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура вызывается при выборе пункта подменю "ПодменюВидаОперации" командной панели
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ДействияФормыДействиеУстановитьОперацию(Кнопка)

	Если Кнопка <> Неопределено Тогда // найти новое значение вида операции
		ВидОперации = Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство[Кнопка.Имя];
	КонецЕсли;

	// Отобразить в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);
	
	УстановитьВидимость();
    УстановитьДоступностьКнопкиЗаполнитьИПровести();
	
КонецПроцедуры // ДействияФормыДействиеУстановитьОперацию()


// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.Продукция,ЭлементыФормы.КоманднаяПанельПродукция.Кнопки.ПодменюЗаполнить);
	СоответствиеТЧ.Вставить(ЭлементыФормы.Материалы,ЭлементыФормы.КоманднаяПанельМатериалы.Кнопки.ПодменюЗаполнить);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры // УстановитьКнопкиПодменюЗаполненияТЧ()

// Функция формирует список запросов для передачи в форму подбора.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, для подбора в которую формируется список запросов.
//
// Возвращаемое значение:
//  Список значений - список запросов.
//
Функция СформироватьСписокЗапросовДляПодбора(ТабличнаяЧасть)

	СписокЗапросов      = Новый СписокЗначений();
	СписокЗапросов.Добавить(,"По справочнику");
	Если ТабличнаяЧасть = Материалы Тогда
		СписокЗапросов.Добавить("ОстаткиНоменклатуры", 	"По остаткам номенклатуры");
		СписокЗапросов.Добавить("Спецификации", 		"По спецификациям");
	КонецЕсли;

	Возврат СписокЗапросов;

КонецФункции // СформироватьСписокЗапросовДляПодбора()

// Процедура обновляет параметры в форме подбора, если она открыта.
//
// Параметры:
//  Реквизит - измененный реквизит.
//
Процедура ОбновитьФормуПодбора(Реквизит)

	РаботаСДиалогами.ОбновитьПараметрыИФормуПодбора(ЭтотОбъект, ЭтаФорма, Реквизит);

КонецПроцедуры // ОбновитьФормуПодбора()

// Процедура вызывает сервисный механизм для подбора номеклатуры в табличную часть.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, в которую осуществляется подбор.
//
Процедура ДействиеПодбор(ТабличнаяЧасть)

	Перем Команда, Валюта;

	ЕстьСерия               = Ложь;
	ПодбиратьУслуги         = Ложь;
	ОтборУслугПоСправочнику = Истина;

	Если ТабличнаяЧасть = Продукция Тогда
		Команда         = "ПодборВТабличнуюЧастьПродукция";
		ИмяТабличнойЧасти       = "Продукция";
		ПодбиратьУслуги         = Истина;
		ОтборУслугПоСправочнику = Ложь;
	ИначеЕсли ТабличнаяЧасть = Материалы Тогда
		Команда           = "ПодборВТабличнуюЧастьМатериалы";
		ИмяТабличнойЧасти = "Материалы";
	КонецЕсли;

	СписокВидовПодбора = СформироватьСписокЗапросовДляПодбора(ТабличнаяЧасть);
	ПредставлениеДок = Метаданные().Представление();

	СтруктураПараметровПодбора = Новый Структура();
	СтруктураПараметровПодбора.Вставить("Команда"            , Команда);
	СтруктураПараметровПодбора.Вставить("СписокВидовПодбора" , СписокВидовПодбора);

	// Параметры запросов.
	ВременнаяДатаРасчетов = ?(НачалоДня(Дата) = НачалоДня(ТекущаяДата()), Неопределено, Дата);
	СтруктураПараметровПодбора.Вставить("ДатаРасчетов"           , ВременнаяДатаРасчетов);
	СтруктураПараметровПодбора.Вставить("Склад"                  , Неопределено);
	СтруктураПараметровПодбора.Вставить("Организация"            , Организация);
	СтруктураПараметровПодбора.Вставить("ЕстьЦена"               , Ложь);
	СтруктураПараметровПодбора.Вставить("ЕстьСерия"              , ЕстьСерия);
	СтруктураПараметровПодбора.Вставить("ВалютаДокумента"        , мВалютаРегламентированногоУчета);
	СтруктураПараметровПодбора.Вставить("ПодбиратьУслуги"        , ПодбиратьУслуги);
	СтруктураПараметровПодбора.Вставить("ОтборУслугПоСправочнику", ОтборУслугПоСправочнику);
	СтруктураПараметровПодбора.Вставить("Заголовок", "Подбор номенклатуры в документ " + 
	                                    ПредставлениеДок + " № " + Номер + " (" + ИмяТабличнойЧасти + ")");

	РаботаСДиалогами.ОткрытьПодборНоменклатуры(ЭтаФорма, СтруктураПараметровПодбора, Метаданные());

КонецПроцедуры // ДействиеПодбор()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
//  ТабличнаяЧасть    - табличная часть, в которую надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(ТабличнаяЧасть, ЗначениеВыбора) Экспорт

	Перем СпособЗаполненияЦен, ВалютаЦены;
	Перем Номенклатура, ЕдиницаИзмерения, Количество, КоличествоМест, Цена, Характеристика, Серия;
	Перем Спецификация, текПродукция, ХарактеристикаПродукции, КоличествоПродукции, ВидВоспроизводства;


	// Получим параметры подбора из структуры подбора.
	ЗначениеВыбора.Свойство("СпособЗаполненияЦен", СпособЗаполненияЦен);
	ЗначениеВыбора.Свойство("ВалютаЦены"         , ВалютаЦены);

	ЗначениеВыбора.Свойство("Номенклатура"    , Номенклатура);
	ЗначениеВыбора.Свойство("ЕдиницаИзмерения", ЕдиницаИзмерения);
	ЗначениеВыбора.Свойство("Количество"      , Количество);
	ЗначениеВыбора.Свойство("КоличествоМест"  , КоличествоМест);
	ЗначениеВыбора.Свойство("Цена"            , Цена);
	ЗначениеВыбора.Свойство("Характеристика"  , Характеристика);
    ЗначениеВыбора.Свойство("Спецификация"    , Спецификация);
	ЗначениеВыбора.Свойство("Продукция"       , текПродукция);
	ЗначениеВыбора.Свойство("ХарактеристикаПродукции"  , ХарактеристикаПродукции);
    ЗначениеВыбора.Свойство("КоличествоПродукции"  , КоличествоПродукции);
    ЗначениеВыбора.Свойство("ВидВоспроизводства"  , ВидВоспроизводства);

	// Ищем выбранную позицию в таблице подобранной номенклатуры.
	//  Если найдем - увеличим количество; не найдем - добавим новую строку.
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Характеристика);
    Если  ТабличнаяЧасть = Материалы и ЗначениеЗаполнено(Спецификация) Тогда
		СтруктураОтбора.Вставить("ХарактеристикаПродукции", ХарактеристикаПродукции);
		СтруктураОтбора.Вставить("Продукция", Продукция);
        СтруктураОтбора.Вставить("Спецификация", Спецификация);
		СтруктураОтбора.Вставить("ВидВоспроизводства", ВидВоспроизводства);
	КонецЕсли;

	СтрокаТабличнойЧасти = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(ТабличнаяЧасть, СтруктураОтбора);
	Если СтрокаТабличнойЧасти <> Неопределено Тогда

		// Нашли, увеличиваем количество в первой найденной строке.
		СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Количество;
			
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
		
	Иначе

		// Не нашли - добавляем новую строку.
		СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Количество;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент      = СтрокаТабличнойЧасти.ЕдиницаИзмерения.Коэффициент;
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Характеристика;

		// Рассчитываем реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
		
		Если ТабличнаяЧасть = Продукция Тогда
			СтрокаТабличнойЧасти.Спецификация 	= УправлениеПроизводством.ОпределитьСпецификациюПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, Дата, Подразделение);
		КонецЕсли;
		Если ТабличнаяЧасть = Материалы и ЗначениеЗаполнено(Спецификация) Тогда 
			  СтрокаТабличнойЧасти.Спецификация = Спецификация;
			  СтрокаТабличнойЧасти.Продукция = текПродукция;
			  СтрокаТабличнойЧасти.ХарактеристикаПродукции = ХарактеристикаПродукции;
			  СтрокаТабличнойЧасти.ВидВоспроизводства = ВидВоспроизводства;

			  //Если такой продукции нет в ТЧ Продукция - добавим
			  СтруктураОтбораПродукция = новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Спецификация",текПродукция,ХарактеристикаПродукции,Спецификация);
			  Если Продукция.НайтиСтроки(СтруктураОтбораПродукция).Количество()=0 и ЗначениеЗаполнено(текПродукция) Тогда
				  СтрокаТабЧастиПродукция = Продукция.Добавить();
				  СтрокаТабЧастиПродукция.Номенклатура = текПродукция;
				  СтрокаТабЧастиПродукция.ХарактеристикаНоменклатуры = ХарактеристикаПродукции;
				  СтрокаТабЧастиПродукция.Спецификация = Спецификация;
				  СтрокаТабЧастиПродукция.Коэффициент = 1;
				  СтрокаТабЧастиПродукция.ЕдиницаИзмерения = текПродукция.ЕдиницаХраненияОстатков;
				  СтрокаТабЧастиПродукция.Количество = КоличествоПродукции;
				  СтрокаТабЧастиПродукция.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;

			  КонецЕсли;
			  
		КонецЕсли;
		Если ТабличнаяЧасть = Продукция Тогда
			СтрокаТабличнойЧасти.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;
		ИначеЕсли не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВидВоспроизводства)	 Тогда
			СтрокаТабличнойЧасти.ВидВоспроизводства = СтрокаТабличнойЧасти.Номенклатура.ВидВоспроизводства;
		КонецЕсли;	
	КонецЕсли;

	Если ТабличнаяЧасть = Продукция Тогда
		ИмяТабличнойЧасти = "Продукция";
	ИначеЕсли ТабличнаяЧасть = Материалы Тогда
		ИмяТабличнойЧасти = "Материалы";
		УстановитьДоступностьКнопкиЗаполнитьИПровести();
	КонецЕсли;
	
	ЭлементыФормы[ИмяТабличнойЧасти].ТекущаяСтрока  = СтрокаТабличнойЧасти;
	ЭлементыФормы[ИмяТабличнойЧасти].ТекущаяКолонка = ЭлементыФормы[ИмяТабличнойЧасти].Колонки["Количество"];

КонецПроцедуры // ОбработкаПодбора()

// Производит заполнение и установку необходимых полей при изменении характеристики товара в табличной части.
//
Процедура ПриИзмененииХарактеристикиНоменклатурыТоваров(СтрокаТабличнойЧасти)

	// Заполняем реквизиты табличной части.
	ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 

КонецПроцедуры // ПриИзмененииХарактеристикиНоменклатурыТоваров()

// Процедура - обработчик события "ОбработкаВыбора" поля Исполнитель
//
// Параметры:
//  Элемент - элемент формы, который отображает физическое лицо
//
Процедура ИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		Элемент.Значение = ВыбранноеЗначение.Физлицо;
	Иначе
		Элемент.Значение = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры
 
// Процедура - обработчик события "ПриИзменении" поля ввода Исполнитель 
//
// Параметры:
//  Элемент - элемент формы, который отображает физическое лицо
//
Процедура ИсполнительНачалоВыбора(Элемент, СтандартнаяОбработка)
	// переопеределим выбор физлица на выбор из списка сотрудников
	ПроцедурыУправленияПерсоналомПереопределяемый.ОткрытьФормуВыбораСотрудника(Элемент, Ссылка, Истина, Дата, СтандартнаяОбработка, Элемент.Значение);
КонецПроцедуры

// Процедура - обработчик события "АвтоПодборТекста" поля ввода Исполнитель
// переопеределим выбор физлица на выбор из списка регистра сведений
//
Процедура ИсполнительАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	ТекстАвтоПодбора = ПроцедурыУправленияПерсоналомДополнительный.ПодобратьФИОСотрудника(СтандартнаяОбработка, "Работники", Текст);
КонецПроцедуры

// Процедура - обработчик события "ОкончаниеВводаТекста" поля ввода Исполнитель
// переопеределим выбор физлица на выбор из списка регистра сведений
//
Процедура ИсполнительОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	Значение = ПроцедурыУправленияПерсоналомДополнительный.ПодобратьСписокСотрудников(СтандартнаяОбработка, "Работники", Текст, Элемент.Значение, , Истина);
	
КонецПроцедуры

// Процедура заполняет табличную часть "Материалы" по спецификациям выпуска,
// указанным в табличной части на закладке "Продукция.
//
Процедура ЗаполнитьМатериалыПоСпецификации(ТабличнаяЧасть, ИмяТабличнойЧасти)
	
	Если ТабличнаяЧасть.Количество() > 0 Тогда
		Ответ = Вопрос("Табличная часть """ + ИмяТабличнойЧасти + """ уже содержит строки."
					  + Символы.ПС + "При заполнении они будут удалены!" + Символы.ПС,
					   РежимДиалогаВопрос.ДаНет);
		Если Не Ответ = КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		ТабличнаяЧасть.Очистить();
	КонецЕсли;
	
	ТаблицаИсходныеКомплектующие = ТабличнаяЧасть.Выгрузить();
	
	Параметры = Новый Структура("ПараметрыВыпуска, КоличествоУровнейРазузлования, ДатаСпецификации");
	
	Для Каждого СтрокаТабличнойЧасти Из Продукция Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатРазузлования = Новый Структура("ИсходныеКомплектующие");
		
		СтруктураИсточник = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент, Количество, Спецификация");
		ЗаполнитьЗначенияСвойств(СтруктураИсточник, СтрокаТабличнойЧасти);
		
		ПараметрыВыпуска = Новый Соответствие;
		Для Каждого ПараметрВыпуска из ПараметрыВыпускаПродукции Цикл
			
			Если ПараметрВыпуска.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи Тогда
				ПараметрыВыпуска.Вставить(ПараметрВыпуска.ВидПараметра.Наименование, ПараметрВыпуска.Значение);
			КонецЕсли;
			
		КонецЦикла;

		Параметры.ПараметрыВыпуска = ПараметрыВыпуска;
		Параметры.КоличествоУровнейРазузлования = 1;
		Параметры.ДатаСпецификации = Дата;
		
		МассивОшибок = РазузлованиеНоменклатуры.РазузловатьНоменклатуру(СтруктураИсточник, РезультатРазузлования, Параметры);
		
		Если МассивОшибок.Количество() > 0 Тогда
			
			Для каждого Ошибка из МассивОшибок Цикл
				
				ОбщегоНазначения.Сообщение("Ошибка: " + Ошибка.Причина, Ошибка.СтатусОшибки);
				ОбщегоНазначения.Сообщение(" Спецификация: " + Ошибка.Спецификация);
				ОбщегоНазначения.Сообщение(" Номер строки: " + Ошибка.НомерСтроки);
				ОбщегоНазначения.Сообщение(" Описание ошибки: " + Ошибка.ОписаниеОшибки);
			
			КонецЦикла;
			
		КонецЕсли;
		
		Если РезультатРазузлования.ИсходныеКомплектующие = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИсходныеКомплектующие = РезультатРазузлования.ИсходныеКомплектующие;
		
		ИсходныеКомплектующие.Колонки.Добавить("Заказ");
		ИсходныеКомплектующие.ЗаполнитьЗначения(СтрокаТабличнойЧасти.Заказ, "Заказ");
		ИсходныеКомплектующие.ЗаполнитьЗначения(Подразделение, "Подразделение");
		//заполним подразделение по основной спецификации полуфабрикатов
		Для каждого Строка из ИсходныеКомплектующие цикл
			ПодразделениеНоменклатуры = ЗаказыНаПроизводствоИПереработку.ПолучитьПодразделениеИзСпецификации(Строка.Спецификация,Строка.Номенклатура,Строка.ХарактеристикаНоменклатуры);
			Если ЗначениеЗаполнено(ПодразделениеНоменклатуры) Тогда
				Строка.Подразделение = ПодразделениеНоменклатуры;
			КонецЕсли;
		КонецЦикла;

		ИсходныеКомплектующие.Колонки.Добавить("Продукция");
		ИсходныеКомплектующие.Колонки.Добавить("ХарактеристикаПродукции");
		
		ИсходныеКомплектующие.ЗаполнитьЗначения(СтрокаТабличнойЧасти.Номенклатура, "Продукция");
		ИсходныеКомплектующие.ЗаполнитьЗначения(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, "ХарактеристикаПродукции");
		ИсходныеКомплектующие.ЗаполнитьЗначения(СтрокаТабличнойЧасти.Спецификация, "Спецификация");
		
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ИсходныеКомплектующие, ТаблицаИсходныеКомплектующие);
		
	КонецЦикла;
	
	ТаблицаИсходныеКомплектующие.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент, ВидВоспроизводства, Спецификация, Подразделение, Продукция, ХарактеристикаПродукции", "Количество");
	
	ТабличнаяЧасть.Загрузить(ТаблицаИсходныеКомплектующие);
	
	Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуМестТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьМатериалыПоСпецификации()

// Заполняет переданную табличную часть по остаткам
//
// Параметры:
//  ТабличнаяЧасть  - табличная часть документа.
//	ЗаказНаПроизводство - заказ по которому производится заполнение
// 	Очистить		- очистить предварительно ТЧ или добавить в конец
//
Процедура ЗаполнитьТабличнуюЧастьПоЗаказуНаПроизводство(ТабличнаяЧасть, ВыбЗаказНаПроизводство = Неопределено, Очистить = Истина, ТипДокумента = Неопределено)

	Если НЕ ЗначениеЗаполнено(ВыбЗаказНаПроизводство) Тогда
		ФормаЗаказы = Документы[ТипДокумента].ПолучитьФормуВыбора();
		ФормаЗаказы.Заголовок = "Выберите заказ на производство для заполнения заказа на производство";
		ФормаЗаказы.РежимВыбора = Истина;
		ВыбЗаказНаПроизводство = ФормаЗаказы.ОткрытьМодально();
	КонецЕсли; 

	Если НЕ ЗначениеЗаполнено(ВыбЗаказНаПроизводство) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЗаказНаПроизводство) Тогда
		ЗаказНаПроизводство = ВыбЗаказНаПроизводство;
		ЗаказНаПроизводствоПриИзменении(ЗаказНаПроизводство);
	КонецЕсли;

	Если Очистить И ТабличнаяЧасть.Количество() > 0 Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да,);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 
		
		ТабличнаяЧасть.Очистить();
		
	КонецЕсли;

	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтрицательноеКоличество", Истина);
	
	ДатаОстатков = ОбщегоНазначения.ПолучитьДатуОстатков(ЭтотОбъект);
	
	ЗаказыНаПроизводствоИПереработку.ЗаполнитьТабличнуюЧастьПродукцияПоОстаткамЗаказовНаПроизводство(ТабличнаяЧасть, ВыбЗаказНаПроизводство, ДатаОстатков, ДопПараметры);

КонецПроцедуры // ЗаполнитьТабличнуюЧастьПоЗаказуПокупателю()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
//  ТабличнаяЧасть    - табличная часть, в которую надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбораПоСпецификации(ТабличнаяЧасть, ЗначениеВыбора)
	
	Перем Спецификация, Количество;
	
	ЗначениеВыбора.Свойство("Спецификация", Спецификация);
	ЗначениеВыбора.Свойство("Количество", 	Количество);
	
	Если ТипЗнч(Спецификация) <> Тип("СправочникСсылка.СпецификацииНоменклатуры") Тогда
		Возврат;
	КонецЕсли;

	Отбор = Новый Структура();
	
	ИсходныеКомплектующие = УправлениеПроизводством.ПолучитьМатериалыПоСпецификации(Спецификация, Количество, Отбор, Дата, мИспользоватьТолькоСборочныеСпецификации);
	
	Если ИсходныеКомплектующие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ИсходныеКомплектующие Цикл
	
		СтруктураПодбора = Новый Структура();
		СтруктураПодбора.Вставить("Номенклатура",       Строка.Номенклатура);
		СтруктураПодбора.Вставить("ЕдиницаИзмерения",   Строка.ЕдиницаИзмерения);
		СтруктураПодбора.Вставить("Характеристика",     Строка.ХарактеристикаНоменклатуры);
		СтруктураПодбора.Вставить("Серия",     			Справочники.СерииНоменклатуры.ПустаяСсылка());
		СтруктураПодбора.Вставить("Количество",     	Строка.Количество);
		СтруктураПодбора.Вставить("СтатьяЗатрат",     	Строка.СтатьяЗатрат);
		СтруктураПодбора.Вставить("ВидВоспроизводства", Строка.ВидВоспроизводства);

		СтруктураПодбора.Вставить("Спецификация",       Спецификация);
		СтруктураПодбора.Вставить("КоличествоПродукции", Количество);
		
		Если ЗначениеЗаполнено(Спецификация)
	   		И Спецификация.ВидСпецификации = Перечисления.ВидыСпецификаций.Сборочная Тогда
        	СтруктураПодбора.Вставить("Продукция", 					Спецификация.ВыходныеИзделия[0].Номенклатура);
			СтруктураПодбора.Вставить("ХарактеристикаПродукции", 	Спецификация.ВыходныеИзделия[0].ХарактеристикаНоменклатуры);
        КонецЕсли;

		ОбработкаПодбора(ТабличнаяЧасть, СтруктураПодбора);
		
	КонецЦикла;
	
КонецПроцедуры // ОбработкаПодбораПоСпецификации()

// Процедура очищает колонку "Размещение" табличной части.
//
Процедура ОчиститьРазмещениеВТабличнойЧасти()

	Материалы.Свернуть(
		// Поля (исключаем Размещение)
		"ВидВоспроизводства,
		|ЕдиницаИзмерения,
		|ЕдиницаИзмеренияМест,
		|Заказ,
		|Коэффициент,
		|Номенклатура,
		|НомерПередела,
		|Подразделение,
		|Продукция,
		|Спецификация,
		|ХарактеристикаНоменклатуры,
		|ХарактеристикаПродукции",
		// Количество
		"Количество,
		|КоличествоМест");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура устанавливает видимость для тех колонок в табличной части 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()
	
	Колонки      = ЭлементыФормы.Продукция.Колонки;
	КолонкиМатериалы = ЭлементыФормы.Материалы.Колонки;
	
	// Управление доступностью элементов формы в зависимости от вида операции
	КнопкиКоманднойПанели = ЭлементыФормы.ДействияФормы.Кнопки;
	
	КнопкиКоманднойПанели.ДействиеАнализ.Доступность = Истина;
	ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации.Кнопки.ЗакрытиеПотребностейЗаказаНаПроизводство.Доступность = истина;
	Если мСпособЗакрытияПотребностейЗаказовНаПроизводство<>Перечисления.СпособыЗакрытияПотребностейЗаказовНаПроизводство.Явно Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации.Кнопки.ЗакрытиеПотребностейЗаказаНаПроизводство.Доступность = ложь;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство.ЗакрытиеПотребностейЗаказаНаПроизводство Тогда
		
		КолонкиМатериалы.Размещение.Видимость = ложь;
		КолонкиМатериалы.Размещение.ИзменятьВидимость = ложь;
		ЭлементыФормы.ОсновнаяПанель.Страницы.Продукция.Видимость = ложь;
		ЭлементыФормы.КоманднаяПанельМатериалы.Кнопки.ПодменюЗаполнить.Кнопки.ЗаполнитьПоСпецификации.Доступность = Ложь;
	Иначе
		КолонкиМатериалы.Размещение.Видимость = истина;
		КолонкиМатериалы.Размещение.ИзменятьВидимость = истина;
		ЭлементыФормы.ОсновнаяПанель.Страницы.Продукция.Видимость = истина;
		ЭлементыФормы.КоманднаяПанельМатериалы.Кнопки.ПодменюЗаполнить.Кнопки.ЗаполнитьПоСпецификации.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимость()

// Процедура устанавливает доступность кнопки "Заполнить и провести". Кнопка доступна,
// если документ - внешний заказ, его дата совпадает с текущей и установлен хотя бы один
// из флагов Авторазмещение или Авторезервирование.
//
// Параметры:
//	Нет.
//
Процедура УстановитьДоступностьКнопкиЗаполнитьИПровести()

	ЭлементыФормы.ДействияФормы.Кнопки.ДействиеЗаполнитьИПровести.Доступность =
		НачалоДня(Дата) = НачалоДня(ТекущаяДата())
		И Материалы.Количество() > 0 и ВидОперации<>Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство.ЗакрытиеПотребностейЗаказаНаПроизводство;

КонецПроцедуры // УстановитьДоступностьКнопкиЗаполнитьИПровести

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	Отказ = Не УправлениеЗаказами.ИспользоватьЗаказыНаПроизводство(Истина);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПараметрОснование) Тогда
		ТипОснования = ТипЗнч(ПараметрОснование);
		Если ТипОснования = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг")
			ИЛИ ТипОснования = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену")
			ИЛИ ТипОснования = Тип("ДокументСсылка.ТребованиеНакладная")
			ИЛИ ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
			
			Если НЕ мИспользоватьПотребностиЗаказовНаПроизводство
				ИЛИ мСпособЗакрытияПотребностейЗаказовНаПроизводство <> Перечисления.СпособыЗакрытияПотребностейЗаказовНаПроизводство.Явно Тогда
				
				Предупреждение("Корректировку заказа на производство
				|можно вводить на основании документа """ + СокрЛП(ПараметрОснование.Метаданные().Синоним)+"""
				|если ведется учет потребностей заказов на производство
				|и указан явный способ закрытия потребностей!");
				
				Отказ = Истина;
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		        Если НЕ (ПараметрОснование.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.ВыпускПродукции
					И ПараметрОснование.ВидКомплектации = Перечисления.ВидыКомплектации.Сборка
					И ЗначениеЗаполнено(ПараметрОснование.ЗаказВыпуска) И ТипЗнч(ПараметрОснование.ЗаказВыпуска)=Тип("ДокументСсылка.ЗаказНаПроизводство")) Тогда
					
					Предупреждение("Корректировку заказа на производство
					|можно вводить на основании документа """ + СокрЛП(ПараметрОснование.Метаданные().Синоним)+"""
					|если в документе-основании указан вид операции 'Выпуск продукции', вид комплектации 'Сборка', 
					|и в реквизите 'Заказ-выпуск' указан заказ на производство!");

					Возврат;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, , ПараметрОбъектКопирования);
		
		Если ЗначениеЗаполнено(ЗаказНаПроизводство) Тогда
			Организация = ЗаказНаПроизводство.Организация;
			Подразделение = ЗаказНаПроизводство.Подразделение;
		Иначе
			Организация = неопределено;
			Подразделение = неопределено;
		КонецЕсли;
	КонецЕсли;
	
		// Заполняем подменю, вызываемое нажатием кнопки "Операция" командной панели 
	// формы, значениями перечисления "Вид операции" данного вида документа.
	// В качестве обработки выбора вида операции назначается процедура 
	// ДействияФормыДействиеУстановитьОперацию модуля формы.
	РаботаСДиалогами.УстановитьПодменюВыбораВидаОперации(ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации,
	                                      ВидОперации.Метаданные().ЗначенияПеречисления,
	                                      Новый Действие("ДействияФормыДействиеУстановитьОперацию"));



	СтруктураКолонок = Новый Структура();

	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("ЕдиницаХранения");
	
	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Продукция.Колонки, 	СтруктураКолонок);
	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Материалы.Колонки, 	СтруктураКолонок);

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;

	// Установить видимость колонок "ХарактеристикаНоменклатуры"
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(мКолонкиПродукция);
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(мКолонкиМатериалы);

	// Установить видимость реквизитов и заголовков колонок.
	УстановитьДоступностьКнопкиЗаполнитьИПровести();
	УстановитьВидимость();

	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма,новый Структура("Дата,Организация,ЗаказНаПроизводство"));
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю1);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1,ЭлементыФормы.Номер);
	
	Если ВидОперации = Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство.ЗакрытиеПотребностейЗаказаНаПроизводство Тогда
		ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы["Материалы"];
	КонецЕсли; 
	
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура ОбновлениеОтображения()

	// Подсчитаем количество строк в табличных частях.
	СтраницыПанели = ЭлементыФормы.ОсновнаяПанель.Страницы;
	
	СтраницыПанели.Продукция.Заголовок 		= "Продукция и услуги (" + ДокументОбъект.Продукция .Количество() + " поз.)";
	СтраницыПанели.Материалы.Заголовок 		= "Материалы (" + ДокументОбъект.Материалы.Количество() + " поз.)";
	
КонецПроцедуры // ОбновлениеОтображения()

// Процедура - обработчик события "ОбработкаВыбора" формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	Перем Команда, Спецификация;

	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		
		ЗначениеВыбора.Свойство("Команда", Команда);

		Если Команда = "ПодборВТабличнуюЧастьПродукция" Тогда
			ОбработкаПодбора(Продукция, ЗначениеВыбора);
		ИначеЕсли Команда = "ПодборВТабличнуюЧастьМатериалы" Тогда
			ЗначениеВыбора.Свойство("Спецификация", Спецификация);
			Если Спецификация <> Неопределено Тогда
				ОбработкаПодбораПоСпецификации(Материалы, ЗначениеВыбора);
			Иначе
				ОбработкаПодбора(Материалы, ЗначениеВыбора);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ОбработкаВыбора()

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	
КонецПроцедуры // ПослеЗаписи()

// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()
	
	мФормаВводПараметровВыпуска = неопределено;

КонецПроцедуры // ПриЗакрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
КонецПроцедуры
// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ДействияФормыДействиеОткрытьСвойства()

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ДействияФормыДействиеОткрытьКатегории()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Подбор" командной панели
// табличного поля "Продукция", вызывает сервисный механизм для
// подбора номеклатуры в табличную часть "Продукция".
//
Процедура КоманднаяПанельПродукцияДействиеПодбор(Кнопка)

	ДействиеПодбор(Продукция);

КонецПроцедуры // КоманднаяПанельПродукцияДействиеПодбор()

// Процедура вызывается при нажатии кнопки "Параметры" командной панели 
// табличного поля "Продукция".
//
Процедура КоманднаяПанельПродукцияПараметрыВыпуска(Кнопка)
	
	СтрокаТабличнойЧасти = ЭлементыФормы.Продукция.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мТекущаяСтрокаПродукцияПараметры = СтрокаТабличнойЧасти;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Номенклатура", 				СтрокаТабличнойЧасти.Номенклатура);
	СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", 	СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
	СтруктураПараметров.Вставить("Количество", 					СтрокаТабличнойЧасти.Количество);
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", 			СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("ДокументОбъект", 				ЭтотОбъект);
	СтруктураПараметров.Вставить("ИмяТабличнойЧасти", 			"Продукция");
	СтруктураПараметров.Вставить("НомерСтрокиТабличнойЧасти", 	СтрокаТабличнойЧасти.НомерСтроки);
	СтруктураПараметров.Вставить("СтрокаТабличнойЧасти", 		СтрокаТабличнойЧасти);
	СтруктураПараметров.Вставить("ПараметрыСвязиСтрокТЧ", 		мПараметрыСвязиСтрокТЧ);
	
	// Открываем форму подбора.
	мФормаВводПараметровВыпуска = Обработки.ВводПараметровВыпускаПродукции.ПолучитьФорму("ОсновнаяФорма", ЭтаФорма, ЭтаФорма);
	мФормаВводПараметровВыпуска.СтруктураИсходныхПараметров = СтруктураПараметров;
	мФормаВводПараметровВыпуска.Открыть();
	
КонецПроцедуры // КоманднаяПанельПродукцияПараметрыВыпуска()

// Процедура вызывается при нажатии кнопки "Подбор" командной панели
// табличного поля "Материалы", вызывает сервисный механизм для
// подбора номеклатуры в табличную часть "Материалы".
//
Процедура КоманднаяПанельМатериалыДействиеПодбор(Кнопка)
	
	ДействиеПодбор(Материалы);
	
КонецПроцедуры // КоманднаяПанельМатериалыДействиеПодбор()

// Процедура вызывается при нажатии кнопки "Анализ" командной панели формы,
// вызывает анализ текущего состояния заказа.
//
Процедура ДействияФормыДействиеАнализ(Кнопка)
	ЗаказыНаПроизводствоИПереработку.СформироватьОтчетАнализЗаказаНаПроизводство(ЗаказНаПроизводство,истина, истина);
КонецПроцедуры // ДействияФормыДействиеАнализ()

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура вызывается при нажатии кнопки "Изменить" командной панели 
// табличного поля "Продукция", вызывает сервисный механизм для изменения табличной части.
//
Процедура КоманднаяПанельПродукцияДействиеИзменить(Кнопка)

	// Получим контекст обработки
	ИзменениеТабличнойЧасти      = Обработки.ОбработкаТабличнойЧастиТовары.Создать();
	ФормаИзменениеТабличнойЧасти = ИзменениеТабличнойЧасти.ПолучитьФорму(,ЭтаФорма);

	//Установим реквизиты и переменные формы.
	ФормаИзменениеТабличнойЧасти.ДокументОбъект             = ЭтотОбъект;
	ФормаИзменениеТабличнойЧасти.мФормаДокумента            = ЭтаФорма;
	ФормаИзменениеТабличнойЧасти.мЕстьНДС                   = Ложь; 
	ФормаИзменениеТабличнойЧасти.мЕстьЦенаВРознице          = Ложь;
	ФормаИзменениеТабличнойЧасти.мСпособЗаполненияЦен       = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатуры;
	ФормаИзменениеТабличнойЧасти.мИмяТабличнойЧастиДокумента = "Материалы";

	//Перенесем табличную часть
	ИзменениеТабличнойЧасти.Товары.Загрузить(Материалы.Выгрузить());

	// Открываем форму обработки
	ФормаИзменениеТабличнойЧасти.Открыть();

КонецПроцедуры // КоманднаяПанельПродукцияДействиеИзменить()

// Процедура вызывается при нажатии кнопки "Заполнить по заказу на производство" командной панели 
// табличного поля "Продукция".
//
Процедура КоманднаяПанельПродукцияЗаполнитьПоЗаказуНаПроизводство(Кнопка)
	
	ЗаполнитьТабличнуюЧастьПоЗаказуНаПроизводство(Продукция, ЗаказНаПроизводство, , "ЗаказНаПроизводство");
	
КонецПроцедуры // КоманднаяПанельПродукцияЗаполнитьПоЗаказуНаПроизводство()

// Процедура вызывается при нажатии кнопки "Добавить по заказу на производство" командной панели 
// табличного поля "Продукция".
//
Процедура КоманднаяПанельПродукцияДобавитьПоЗаказуНаПроизводство(Кнопка)
	
	ЗаполнитьТабличнуюЧастьПоЗаказуНаПроизводство(Продукция, , Ложь, "ЗаказНаПроизводство");
	
КонецПроцедуры // КоманднаяПанельПродукцияДобавитьПоЗаказуНаПроизводство()

// Процедура вызывается при нажатии кнопки "Заполнить по спецификации" командной панели 
// табличного поля "Материалы", вызывает сервисный механизм для изменения табличной части.
//
Процедура КоманднаяПанельМатериалыЗаполнитьПоСпецификации(Кнопка)
	
	ЗаполнитьМатериалыПоСпецификации(Материалы, "Материалы");
	УстановитьДоступностьКнопкиЗаполнитьИПровести();

КонецПроцедуры // КоманднаяПанельМатериалыЗаполнитьПоСпецификации()

// Процедура вызывается при нажатии кнопки "Заполнить по заказу на производство" командной панели 
// табличного поля "Материалы", вызывает сервисный механизм для изменения табличной части.
//
Процедура КоманднаяПанельМатериалыЗаполнитьПоЗаказуНаПроизводство(Кнопка)

	Если НЕ ЗначениеЗаполнено(ЗаказНаПроизводство) Тогда
		Предупреждение("Не выбран заказ на производство!");
		Возврат;
	КонецЕсли;
	Если Материалы.Количество() > 0 Тогда
		Ответ = Вопрос("В табличной части уже присутствуют строки. При заполнении они будут удалены!" + Символы.ПС + "Продолжить", РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		Материалы.Очистить();
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтрицательноеКоличество", ВидОперации = Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство.КорректировкаЗаказаНаПроизводство);
	
	ЗаказыНаПроизводствоИПереработку.ЗаполнитьТабличнуюЧастьМатериалыПоОстаткам(Материалы, ЗаказНаПроизводство, ДопПараметры);
    УстановитьДоступностьКнопкиЗаполнитьИПровести();

КонецПроцедуры // КоманднаяПанельМатериалыЗаполнитьПоЗаказуНаПроизводство()

// Процедура вызывается при нажатии кнопки "Заполнить и провести" командной панели формы,
// вызывает заполнение документа с проведением.
//
Процедура ДействияФормыДействиеЗаполнитьИПровести(Кнопка)

	// Заполнять с проведением можно документы с текущую датой
	Если НачалоДня(Дата) = НачалоДня(ТекущаяДата()) И ВидОперации <> Перечисления.ВидыОперацийКорректировкаЗаказаНаПроизводство.ЗакрытиеПотребностейЗаказаНаПроизводство Тогда
		Если НЕ ЭтоНовый() Тогда
			// Если документ ранее был записан, то он будет переноситься в конец дня из-за оперативного проведения, о чем следует предупредить.
			Ответ = Вопрос("В режиме заполнения с проведением документ будет проводиться оперативно. " + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет);
			Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		Возврат;
	КонецЕсли; 

	// Заполнение документа
	Форма = ПолучитьОбщуюФорму("ФормаВыбораПараметровАвторезервирования", ЭтаФорма, );

	//Установим реквизиты и переменные формы.
	Форма.ФормаДокумента = ЭтаФорма;
	Провести = Форма.ОткрытьМодально();
	//если форму закрыли просто крестом, 
	Если Провести=неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ОчищатьРазмещениеПередЗаполнением Тогда
		ОчиститьРазмещениеВТабличнойЧасти();
		Записать(РежимЗаписиДокумента.Запись); // Иначе не сможем корректно заполнить таб. часть "Материалы"
	КонецЕсли;

	// Очистим записи документа по регистрам остатков и заказов покупателей, если он был ранее проведен
	Если Проведен Тогда
		НачатьТранзакцию();

		СтруктРегистры = Новый Структура;
		СтруктРегистры.Вставить("ЗаказыНаПроизводство");
		СтруктРегистры.Вставить("ПотребностиЗаказовНаПроизводство");
		СтруктРегистры.Вставить("РазмещениеЗаказовПокупателей");
		СтруктРегистры.Вставить("ТоварыВРезервеНаСкладах");
		
		Для Каждого Регистр Из СтруктРегистры Цикл
			НаборЗаписей = РегистрыНакопления[Регистр.Ключ].СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Ссылка);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;

	// Заполнение документа
	СтруктПараметров = Новый Структура;
	СтруктПараметров.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
	СтруктПараметров.Вставить("Авторезервирование",  Авторезервирование);
	СтруктПараметров.Вставить("Авторазмещение",      Авторазмещение);
	СтруктПараметров.Вставить("СтратегияАвторезервированияПоЗаказам", СтратегияАвторезервированияПоЗаказам);

	ЗаполнитьТабличныеЧастиПередПроведениемУпр(СтруктПараметров);

	Если Проведен Тогда
		ОтменитьТранзакцию();
	КонецЕсли;

	РаботаСДиалогами.ПровестиДокументВФормеОперативно(ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	РаботаСДиалогами.ПриИзмененииЗначенияДатыДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета);
	УстановитьДоступностьКнопкиЗаполнитьИПровести();

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	КонецЕсли;

	// Выполняем общие действия для всех документов при изменении Организация.
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект);

	УстановитьВидимость();
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля заказа на производство.
//
Процедура ЗаказНаПроизводствоПриИзменении(Элемент)
	
	ИзменяетсяОрганизация = НЕ Организация = ЗаказНаПроизводство.Организация;
	Если  ИзменяетсяОрганизация Тогда
		Если Не ПустаяСтрока(Номер) Тогда
			МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
		КонецЕсли;
		// Выполняем общие действия для всех документов при изменении Организация.
		ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект);
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗаказНаПроизводство) Тогда
		Организация = ЗаказНаПроизводство.Организация;
		Подразделение = ЗаказНаПроизводство.Подразделение;
	Иначе
		Организация = неопределено;
		Подразделение = неопределено;
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры // ЗаказНаПроизводствоПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ПРОДУКЦИЯ

// Процедура - обработчик события "ПриВыводеСтроки" табличной части "Продукция".
//
Процедура ПродукцияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	РаботаСДиалогами.ПоказатьКоэффициентМест(мКолонкиПродукция, ОформлениеСтроки.Ячейки, ДанныеСтроки.ЕдиницаИзмеренияМест);

	РаботаСДиалогами.ПоказатьКодАртикул(мКолонкиПродукция, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);
	
КонецПроцедуры // ПродукцияПриВыводеСтроки()

// Процедура - обработчик события "ПриНачалеРедактирования" табличной части "Продукция".
//
Процедура ПродукцияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти.КлючСвязи = 0;
		СтрокаТабличнойЧасти.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;
	КонецЕсли;
	
КонецПроцедуры // ПродукцияПриНачалеРедактирования()

// Процедура - обработчик события "ПроверкаПеретаскивания" табличной части "Продукция".
//
Процедура ПродукцияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	УправлениеКонтактами.ПроверкаПеретаскиванияВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, "Продукция");
	
КонецПроцедуры // ПродукцияПроверкаПеретаскивания()

// Процедура - обработчик события "Перетаскивание" табличной части "Продукция".
//
Процедура ПродукцияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	УправлениеКонтактами.ПеретаскиваниеВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, "Продукция", ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ПродукцияПеретаскивание()

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Продукция".
//
Процедура ПродукцияНоменклатураПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Продукция.ТекущиеДанные;

	// Выполнить общие действия для всех документов при изменении номенклатуры.
	ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

	// Заполняем реквизиты табличной части.
	ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
	
	СтрокаТабличнойЧасти.Спецификация = УправлениеПроизводством.ОпределитьСпецификациюПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, Дата, Подразделение);
	СтрокаТабличнойЧасти.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;
	
КонецПроцедуры // ПродукцияНоменклатураПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода характеристики номенклатуры
// в строке табличной части "Продукция".
//
Процедура ПродукцияХарактеристикаНоменклатурыПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Продукция.ТекущиеДанные;

	ПриИзмененииХарактеристикиНоменклатурыТоваров(СтрокаТабличнойЧасти);
	
	СтрокаТабличнойЧасти.Спецификация = УправлениеПроизводством.ОпределитьСпецификациюПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, Дата, Подразделение);

КонецПроцедуры // ПродукцияХарактеристикаНоменклатурыПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы
// в строке табличной части "Продукция".
//
Процедура ПродукцияЕдиницаПриИзменении(Элемент)

	// Сохраняем текущее значение коэффициента
	СтароеЗначениеКоэффициента = ЭлементыФормы.Продукция.ТекущиеДанные.Коэффициент;

	// Выполнить общие действия для всех документов при изменении Единица.
	ОбработкаТабличныхЧастей.ПриИзмененииЕдиницыТабЧасти(ЭлементыФормы.Продукция.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // ПродукцияЕдиницаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы мест
// в строке табличной части "Продукция".
//
Процедура ПродукцияЕдиницаМестПриИзменении(Элемент)

	// Выполнить общие действия для всех документов при изменении ЕдиницаМест.
	ОбработкаТабличныхЧастей.ПриИзмененииЕдиницыМестТабЧасти(ЭлементыФормы.Продукция.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // ПродукцияЕдиницаМестПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества
// в строке табличной части "Продукция".
//
Процедура ПродукцияКоличествоПриИзменении(Элемент)

	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(ЭлементыФормы.Продукция.ТекущиеДанные, ДокументОбъект);

КонецПроцедуры // ПродукцияКоличествоПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества мест
// в строке табличной части "Продукция".
//
Процедура ПродукцияКоличествоМестПриИзменении(Элемент)

	// Рассчитать новое количество
	ОбработкаТабличныхЧастей.РассчитатьКоличествоТабЧасти(ЭлементыФормы.Продукция.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // ПродукцияКоличествоМестПриИзменении()

// Процедура - обработчик события НачалоВыбора поля ввода Спецификации
// в табличной части "Продукция".
//
Процедура ПродукцияСпецификацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ЭлементыФормы.Продукция.ТекущиеДанные.Номенклатура) Тогда
		РаботаСДиалогами.НачалоВыбораЗначенияСпецификации(ЭлементыФормы.Продукция.ТекущиеДанные.Номенклатура, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры // ПродукцияСпецификацияНачалоВыбора()

// Процедура - обработчик события "НачалоВыбора" поля ввода Заказа
// в табличной части "Продукция".
//
Процедура ПродукцияЗаказНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтруктураОтбора = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;
	

	МассивДоступныеПоляОтбора = Новый Массив;
	МассивДоступныеПоляОтбора.Добавить("Организация");
	ДопПараметры = Новый Структура("ДоступныеПоляОтбора", МассивДоступныеПоляОтбора);

	УправлениеЗаказами.НачалоВыбораДокументаЗаказа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, СтруктураОтбора, "Продукция", ДопПараметры);
	
КонецПроцедуры // ПродукцияЗаказНачалоВыбора()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ МАТЕРИАЛЫ

// Процедура - обработчик события "ПриВыводеСтроки" табличной части "Материалы".
//
Процедура МатериалыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	РаботаСДиалогами.ПоказатьКоэффициентМест(мКолонкиМатериалы, ОформлениеСтроки.Ячейки, ДанныеСтроки.ЕдиницаИзмеренияМест);

	РаботаСДиалогами.ПоказатьКодАртикул(мКолонкиМатериалы, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);
	
КонецПроцедуры // МатериалыПриВыводеСтроки()

// Процедура - обработчик события "ПроверкаПеретаскивания" табличной части "Материалы".
//
Процедура МатериалыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	УправлениеКонтактами.ПроверкаПеретаскиванияВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, "Материалы");
	
КонецПроцедуры // МатериалыПроверкаПеретаскивания()

// Процедура - обработчик события "Перетаскивание" табличной части "Материалы".
//
Процедура МатериалыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	
	УправлениеКонтактами.ПеретаскиваниеВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, "Материалы", ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // МатериалыПеретаскивание()

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Материалы".
//
Процедура МатериалыНоменклатураПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Материалы.ТекущиеДанные;

	// Выполнить общие действия для всех документов при изменении номенклатуры.
	ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

	// Заполняем реквизиты табличной части.
	ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
	
	СтрокаТабличнойЧасти.ВидВоспроизводства = СтрокаТабличнойЧасти.Номенклатура.ВидВоспроизводства;

КонецПроцедуры // МатериалыНоменклатураПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода характеристики номенклатуры
// в строке табличной части "Материалы".
//
Процедура МатериалыХарактеристикаНоменклатурыПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Материалы.ТекущиеДанные;

	ПриИзмененииХарактеристикиНоменклатурыТоваров(СтрокаТабличнойЧасти);
	
КонецПроцедуры // МатериалыХарактеристикаНоменклатурыПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы
// в строке табличной части "Материалы".
//
Процедура МатериалыЕдиницаПриИзменении(Элемент)

	// Сохраняем текущее значение коэффициента
	СтароеЗначениеКоэффициента = ЭлементыФормы.Материалы.ТекущиеДанные.Коэффициент;

	// Выполнить общие действия для всех документов при изменении Единица.
	ОбработкаТабличныхЧастей.ПриИзмененииЕдиницыТабЧасти(ЭлементыФормы.Материалы.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // МатериалыЕдиницаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы мест
// в строке табличной части "Материалы".
//
Процедура МатериалыЕдиницаМестПриИзменении(Элемент)

	// Выполнить общие действия для всех документов при изменении ЕдиницаМест.
	ОбработкаТабличныхЧастей.ПриИзмененииЕдиницыМестТабЧасти(ЭлементыФормы.Материалы.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // МатериалыЕдиницаМестПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества
// в строке табличной части "Материалы".
//
Процедура МатериалыКоличествоПриИзменении(Элемент)

	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(ЭлементыФормы.Материалы.ТекущиеДанные, ДокументОбъект);

КонецПроцедуры // МатериалыКоличествоПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества мест
// в строке табличной части "Материалы".
//
Процедура МатериалыКоличествоМестПриИзменении(Элемент)

	// Рассчитать новое количество
	ОбработкаТабличныхЧастей.РассчитатьКоличествоТабЧасти(ЭлементыФормы.Материалы.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // МатериалыКоличествоМестПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода Вид воспроизводства
// в строке табличной части "Материалы".
//
Процедура МатериалыВидВоспроизводстваПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = ЭлементыФормы.Материалы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Закупка
	 ИЛИ СтрокаТабличнойЧасти.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.ПринятыеВПереработку Тогда
		СтрокаТабличнойЧасти.Спецификация = Неопределено;
	КонецЕсли;
	
КонецПроцедуры // МатериалыВидВоспроизводстваПриИзменении()

// Процедура - обработчик события "ПриНачалеВыбора" поля ввода размещения
// в строке табличной части "Материалы"
//
Процедура МатериалыРазмещениеНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтруктураОтбора = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;

	МассивДоступныеПоляОтбора = Новый Массив;
	МассивДоступныеПоляОтбора.Добавить("Организация");
	ДопПараметры = Новый Структура("ДоступныеПоляОтбора,ИспользоватьЗаказыНаПроизводство",
			МассивДоступныеПоляОтбора,
			Истина);
			
	РаботаСДиалогами.НачалоВыбораЗначенияРазмещения(ЭтаФорма, Элемент, СтандартнаяОбработка, СтруктураОтбора, ДопПараметры);
	
КонецПроцедуры // МатериалыРазмещениеНачалоВыбора()

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры // ДействияФормыСтруктураПодчиненностиДокумента()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Спецификации
// в табличной части "Материалы".
//
Процедура МатериалыСпецификацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ЭлементыФормы.Материалы.ТекущиеДанные.Продукция) Тогда
		РаботаСДиалогами.НачалоВыбораЗначенияСпецификации(ЭлементыФормы.Материалы.ТекущиеДанные.Продукция, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры // МатериалыСпецификацияНачалоВыбора()

Процедура ПродукцияПередУдалением(Элемент, Отказ)
	ТекДанные = Элемент.ТекущиеДанные;
	Если текДанные = мТекущаяСтрокаПродукцияПараметры И мФормаВводПараметровВыпуска.Открыта() Тогда
		мФормаВводПараметровВыпуска.Закрыть();
	КонецЕсли;
	//удаление строк на закладке Материалы, связанных с продукцией
	струОтбор = новый Структура("Продукция, ХарактеристикаПродукции, Спецификация", текДанные.Номенклатура, текДанные.ХарактеристикаНоменклатуры, текДанные.Спецификация);
	масСтроки = Материалы.НайтиСтроки(струОтбор);
	Если масСтроки.Количество()>0 Тогда
		Ответ = Вопрос("На закладке Материалы содержатся строки, связанные с удаляемой строкой. Удалить эти строки с закладки Материалы автоматически?",РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = истина;
			Возврат;
		ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
			Для каждого элемент из МасСтроки цикл
				Материалы.Удалить(элемент);
			КонецЦикла;
			
        КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПродукцияСпецификацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекДанные = ЭлементыФормы.Продукция.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		Если ВыбранноеЗначение.ВидСпецификации = Перечисления.ВидыСпецификаций.Сборочная
			И ВыбранноеЗначение.ВыходныеИзделия.Количество() > 0 Тогда
			ТекДанные.Номенклатура = ВыбранноеЗначение.ВыходныеИзделия[0].Номенклатура;
			ТекДанные.ХарактеристикаНоменклатуры = ВыбранноеЗначение.ВыходныеИзделия[0].ХарактеристикаНоменклатуры;
			ТекДанные.ЕдиницаИзмерения = ТекДанные.Номенклатура.ЕдиницаХраненияОстатков;
			ТекДанные.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;
			ТекДанные.Коэффициент = 1;
        КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура МатериалыСпецификацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекДанные = ЭлементыФормы.Материалы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекДанные.Продукция) Тогда
		Если ВыбранноеЗначение.ВидСпецификации = Перечисления.ВидыСпецификаций.Сборочная
			И ВыбранноеЗначение.ВыходныеИзделия.Количество() > 0 Тогда
			ТекДанные.Продукция = ВыбранноеЗначение.ВыходныеИзделия[0].Номенклатура;
			ТекДанные.ХарактеристикаПродукции = ВыбранноеЗначение.ВыходныеИзделия[0].ХарактеристикаНоменклатуры;
        КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура МатериалыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Если НоваяСтрока Тогда // Чтобы установить доступность кнопки "Заполнить и провести".
		УстановитьДоступностьКнопкиЗаполнитьИПровести();
	КонецЕсли;
	
КонецПроцедуры // МатериалыПриОкончанииРедактирования()

Процедура МатериалыПослеУдаления(Элемент)

	УстановитьДоступностьКнопкиЗаполнитьИПровести();
	
КонецПроцедуры // МатериалыПослеУдаления()

Процедура МатериалыПодразделениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	ТаблицаОтбора = РаботаСДиалогами.СоздатьТаблицуДляОтбора();
	
	СписокВидовПодразделений = Новый СписокЗначений();
	СписокВидовПодразделений.Добавить(Перечисления.ВидыПодразделений.ОсновноеПроизводство);
	СписокВидовПодразделений.Добавить(Перечисления.ВидыПодразделений.ВспомогательноеПроизводство);
	
	НоваяСтрока = ТаблицаОтбора.Добавить();
	НоваяСтрока.Имя 		 = "ВидПодразделения";
	НоваяСтрока.ВидСравнения = ВидСравнения.ВСписке;
	НоваяСтрока.Значение 	 = СписокВидовПодразделений;
	
	РаботаСДиалогами.НачалоВыбораПодразделения(ТаблицаОтбора, Элемент, СтандартнаяОбработка);	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

мКолонкиПродукция 		= ЭлементыФормы.Продукция.Колонки;
мКолонкиМатериалы 		= ЭлементыФормы.Материалы.Колонки;

мИспользоватьТолькоСборочныеСпецификации = глЗначениеПеременной("ИспользоватьТолькоСборочныеСпецификации");