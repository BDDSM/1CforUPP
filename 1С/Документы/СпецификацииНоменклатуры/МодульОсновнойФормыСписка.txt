////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура создает новую версию спецификации на основании текущей.
//
Процедура СоздатьНовуюВерсию()
	
	Спецификация = ЭлементыФормы.СправочникСписок.ТекущаяСтрока;
	Если Спецификация = Неопределено ИЛИ Спецификация.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСпецификация = Справочники.СпецификацииНоменклатуры.СоздатьЭлемент();
	НоваяСпецификация.Заполнить(Спецификация);
	ФормаСпецификации = НоваяСпецификация.ПолучитьФорму();
	ФормаСпецификации.Открыть();
		
КонецПроцедуры // СоздатьНовуюВерсию()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	ИспользоватьВерсииСпецификацийНоменклатуры = Константы.ИспользоватьВерсииСпецификацийНоменклатуры.Получить();
	ИспользоватьТолькоСборочныеСпецификации = глЗначениеПеременной("ИспользоватьТолькоСборочныеСпецификации");
	
	ЭлементыФормы.СправочникСписок.Колонки.КодВерсии.Видимость 		   = ИспользоватьВерсииСпецификацийНоменклатуры;
	ЭлементыФормы.СправочникСписок.Колонки.КодВерсии.ИзменятьВидимость = ИспользоватьВерсииСпецификацийНоменклатуры;
	
	ЭлементыФормы.СправочникСписок.Колонки.ВидСпецификации.Видимость = УправлениеПроизводством.ИспользоватьВидСпецификации(ИспользоватьТолькоСборочныеСпецификации);
		
	Если Не ИспользоватьВерсииСпецификацийНоменклатуры Тогда
		
		ИндексКнопкиСоздатьВерсию = ЭлементыФормы.ДействияФормы.Кнопки.Индекс(ЭлементыФормы.ДействияФормы.Кнопки.СоздатьВерсию);
		Если ИндексКнопкиСоздатьВерсию > -1 Тогда
			ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ИндексКнопкиСоздатьВерсию);
		КонецЕсли;
		
		МенюПерейти = ЭлементыФормы.ДействияФормы.Кнопки.Подменю2;
		ИндексКнопкиВерсии = МенюПерейти.Кнопки.Индекс(МенюПерейти.Кнопки.ВерсииСпецификации);
		Если ИндексКнопкиСоздатьВерсию > -1 Тогда
			МенюПерейти.Кнопки.Удалить(ИндексКнопкиВерсии);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта "Версии спецификации" меню "Перейти"
// командной панели формы.
//
Процедура ДействияФормыВерсииСпецификации(Кнопка)
	
	Спецификация = ЭлементыФормы.СправочникСписок.ТекущаяСтрока;
	Если Спецификация = Неопределено ИЛИ Спецификация.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ФормаСписка = Справочники.СпецификацииНоменклатуры.ПолучитьФормуСписка(, ЭтаФорма);
	
	ФормаСписка.Отбор.Код.ВидСравнения  = ВидСравнения.Равно;
	ФормаСписка.Отбор.Код.Значение      = Спецификация.Код;
	ФормаСписка.Отбор.Код.Использование = Истина;
	
	ФормаСписка.Открыть();
	
КонецПроцедуры // ДействияФормыВерсииСпецификации()

// Процедура вызывается при выборе пункта "Создать версию" командной панели формы.
//
Процедура ДействияФормыСоздатьВерсию(Кнопка)
	
	СоздатьНовуюВерсию();
	
КонецПроцедуры // ДействияФормыСоздатьВерсию()

//  Процедура печатает выбранный элемент 
// Печатается та форма, которая была отпечатана при нажатии в документе кнопки
// печати по умолчанию
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭтаФорма.ЭлементыФормы.СправочникСписок.ТекущаяСтрока = Неопределено тогда
		Возврат
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокументИзФормыСписка(ЭтаФорма.ЭлементыФормы.СправочникСписок.ТекущаяСтрока.ПолучитьОбъект());

КонецПроцедуры // ДействиеПечать()