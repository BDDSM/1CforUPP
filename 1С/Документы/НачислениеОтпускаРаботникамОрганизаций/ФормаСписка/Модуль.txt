////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	
	ЭлементыФормы.ДокументСписок.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭтаФорма.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Начисления отпуска сотрудникам организаций");
	ЭлементыФормы.ОрганизацияФлаг.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;

	РаботаСДиалогами.НапечататьДвиженияДокумента(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура вызывается при выборе пункта подменю "Структура подчиненности" меню "Перейти".
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);
	
КонецПроцедуры // ДействияФормыСтруктураПодчиненностиДокумента()

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущаяСтрока <> Неопределено Тогда

		Попытка
			УниверсальныеМеханизмы.НапечататьДокументИзФормыСписка(ЭлементыФормы.ДокументСписок.ТекущаяСтрока.ПолучитьОбъект())
		Исключение
		КонецПопытки
			
	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	УстановитьЗаголовкиОрганизацийВФорме();
	
	РаботаСДиалогамиЗК.НастроитьОтборПоОрганизации(ЭтаФорма, "ДокументСписок");
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РаботаСДиалогамиЗК.ОбработкаОповещенияСпискаДокументов(ИмяСобытия, Параметр, Источник, ЭтаФорма, "ДокументСписок");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОрганизацияПриИзменении(Элемент)
	
	РаботаСДиалогамиЗК.ОбработкаИзмененияБыстрогоОтбораПоОрганизации(ЭтаФорма, Элемент, "ДокументСписок");

КонецПроцедуры

Процедура ДокументСписокПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	РаботаСДиалогамиЗК.СписокДокументовПередНачаломДобавления(ЭтаФорма, Отказ, Копирование, Отбор.Организация.Значение, "НачислениеОтпускаРаботникамОрганизаций");

КонецПроцедуры

Процедура ДокументСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ПоказыватьДатаНачала = Элемент.Колонки.ДатаНачала.Видимость;
	ПоказыватьДатаОкончания = Элемент.Колонки.ДатаОкончания.Видимость;
	Если Не ПоказыватьДатаНачала И не ПоказыватьДатаОкончания Тогда
		Возврат
	КонецЕсли;
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		Ячейки = ОформлениеСтроки.Ячейки;
		Если ПоказыватьДатаНачала Тогда
			ДатаНачала = ?(ДанныеСтроки.ОсновнойОтпуск,ДанныеСтроки.ДатаНачалаОсновногоОтпуска,?(ДанныеСтроки.ДополнительныйОтпуск,ДанныеСтроки.ДатаНачалаДополнительногоОтпуска,ДанныеСтроки.ДатаНачалаОсновногоОтпуска));
			Ячейки.ДатаНачала.УстановитьТекст(Формат(ДатаНачала,"ДФ=dd.MM.yyyy"));
		КонецЕсли;
		Если ПоказыватьДатаОкончания Тогда
			ДатаОкончания = ?(ДанныеСтроки.ДополнительныйОтпуск,ДанныеСтроки.ДатаОкончанияДополнительногоОтпуска,ДанныеСтроки.ДатаОкончанияОсновногоОтпуска);
			Ячейки.ДатаОкончания.УстановитьТекст(Формат(ДатаОкончания,"ДФ=dd.MM.yyyy"));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

ДокументСписок.Колонки.Добавить("ДатаНачалаДополнительногоОтпуска");
ДокументСписок.Колонки.Добавить("ДатаОкончанияДополнительногоОтпуска");
ДокументСписок.Колонки.Добавить("ОсновнойОтпуск");
ДокументСписок.Колонки.Добавить("ДополнительныйОтпуск");