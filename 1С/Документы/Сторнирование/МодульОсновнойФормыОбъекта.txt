////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента; 

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;



////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	СоответствиеТЧ = Новый Соответствие;
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Ссылка, Неопределено, Новый Действие("ОсновныеДействияФормыПечать"), Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати    (мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов);
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры


// Формирует закладки формы с текущими движениями сторнируемого документа.
//
Процедура ОбновитьДвиженияДокумента(ВзятьДвиженияИзДокументаОснования = Истина)

	// удаляем все страницы панели движений
	ЭлементыФормы.ДвиженияДокумента.Видимость = Ложь;
	ЭлементыФормы.ДвиженияДокумента.Страницы.Очистить();

	Если НЕ ЗначениеЗаполнено(СторнируемыйДокумент) Тогда

		// закладки не показываем
 		ЭлементыФормы.ДвиженияДокумента.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать; 
		
		Надпись = ЭлементыФормы.Добавить(Тип("Надпись"), "Надпись", Истина, ЭлементыФормы.ДвиженияДокумента);

		Надпись.Верх   = 0;
		Надпись.Лево   = 0;
		Надпись.Ширина = ЭлементыФормы.ДвиженияДокумента.Ширина;
		Надпись.Высота = ЭлементыФормы.ДвиженияДокумента.Высота;

		// установка привязок
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Низ,   ЭлементыФормы.ДвиженияДокумента, ГраницаЭлементаУправления.Низ);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭлементыФормы.ДвиженияДокумента, ГраницаЭлементаУправления.Право);

		Надпись.Значение = "Необходимо выбрать документ для сторнирования. Для этого заполните реквизит Документ";
        Надпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
		Надпись.ВертикальноеПоложение = ВертикальноеПоложение.Верх;

		ЭлементыФормы.ДвиженияДокумента.Видимость = Истина;

		Для каждого Движение Из Движения Цикл
		    Движение.Очистить()
		КонецЦикла; 
		
		Возврат;

	КонецЕсли;

	МетаданныеДокумент = СторнируемыйДокумент.Метаданные();

	// закладки показываем
	ЭлементыФормы.ДвиженияДокумента.ОтображениеЗакладок = ОтображениеЗакладок.Сверху;

	Для Каждого МетаданныеРегистр Из МетаданныеДокумент.Движения Цикл

		// если документ-сторно не может иметь таких движений - это не сторнируемый регистр
		Если не Метаданные().Движения.Содержит(МетаданныеРегистр) Тогда
			Продолжить;
		КонецЕсли;

		НазваниеДвижений = МетаданныеРегистр.Представление();
		ИмяДвижений      = МетаданныеРегистр.Имя;

		// новая страница
		СтраницаДвижений = ЭлементыФормы.ДвиженияДокумента.Страницы.Добавить("Страница"+ИмяДвижений, НазваниеДвижений);

		// новое табличное поле
		ЭлементыФормы.ДвиженияДокумента.ТекущаяСтраница = СтраницаДвижений;

		ПолеДвижений = ЭлементыФормы.Добавить(Тип("ТабличноеПоле"), ИмяДвижений, Истина, ЭлементыФормы.ДвиженияДокумента);

		ПолеДвижений.Верх   = 6;
		ПолеДвижений.Лево   = 6;
		ПолеДвижений.Ширина = ЭлементыФормы.ДвиженияДокумента.Ширина - 12 - 2;
		ПолеДвижений.Высота = ЭлементыФормы.ДвиженияДокумента.Высота - 12 - 20;

		// установка привязок
		ПолеДвижений.УстановитьПривязку(ГраницаЭлементаУправления.Низ,   ЭлементыФормы.ДвиженияДокумента, ГраницаЭлементаУправления.Низ);
		ПолеДвижений.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭлементыФормы.ДвиженияДокумента, ГраницаЭлементаУправления.Право);

		ПолеДвижений.Данные = "Движения." + ИмяДвижений;

		Если ВзятьДвиженияИзДокументаОснования Тогда
			Движения[ИмяДвижений].Очистить();
			ЗаполнитьНаборЗаписей(Движения[ИмяДвижений], МетаданныеРегистр);
		Иначе	
			Движения[ИмяДвижений].Прочитать()
		КонецЕсли;
		
		СторнированиеКлиент.НастроитьСвойстваДвижения(ЭтотОбъект, ИмяДвижений);

		ПолеДвижений.СоздатьКолонки();

	КонецЦикла;

	ЭлементыФормы.ДвиженияДокумента.ТекущаяСтраница = ЭлементыФормы.ДвиженияДокумента.Страницы[0];

	ЭлементыФормы.ДвиженияДокумента.Страницы.Удалить(0);
	ЭлементыФормы.ДвиженияДокумента.Видимость = Истина;

КонецПроцедуры

// обновление периода регистрации для тех движений, которые его поддерживают
//
Процедура ОбновитьПериодРегистрацииДвижений()
	
	МетаданныеДокумент = СторнируемыйДокумент.Метаданные();
	Для Каждого МетаданныеРегистр Из МетаданныеДокумент.Движения Цикл

		Если не Метаданные().Движения.Содержит(МетаданныеРегистр) ИЛИ МетаданныеРегистр.Имя = "ВнутрисменноеВремяРаботниковОрганизаций" Тогда
			// если документ-сторно не может иметь таких движений - это не сторнируемый регистр
			// не изменяем период у регистра ВнутрисменноеВремяРаботниковОрганизаций
			Продолжить;
		КонецЕсли;
		ИмяДвижений      = МетаданныеРегистр.Имя;
		ИзменитьПериодРегистрацииЗаписей(Движения[ИмяДвижений], МетаданныеРегистр);
		
	КонецЦикла;

КонецПроцедуры

Процедура НазначитьТипДокументу()
	Если ТипЗнч(СторнируемыйДокумент) = ВидДокумента Тогда 
		Возврат;
	КонецЕсли;
    Типы = Новый Массив;
    Типы.Добавить(ВидДокумента);
    ОписаниеТипов = Новый ОписаниеТипов(Типы);
    СторнируемыйДокумент = ОписаниеТипов.ПривестиЗначение(СторнируемыйДокумент);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	Типы = Метаданные().Реквизиты.СторнируемыйДокумент.Тип.Типы();
	Для Каждого Тип Из Типы Цикл
	    Объект = Метаданные.НайтиПоТипу(Тип);
	    Если Объект <> Неопределено Тогда
	        ЭлементыФормы.ВидДокумента.СписокВыбора.Добавить(Тип, Объект.Представление());
	    КонецЕсли;
	КонецЦикла;
	ЭлементыФормы.ВидДокумента.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы.
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументовПереопределяемый.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
		
	Иначе
		
		// Установить доступность формы с учетом даты запрета редактирования	
		РаботаСДиалогамиЗК.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
		
	КонецЕсли;
	
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
    Если СторнируемыйДокумент <> Неопределено Тогда
        Тип = ТипЗнч(СторнируемыйДокумент);
    Иначе
		Тип = Тип("ДокументСсылка.НачислениеЗарплатыРаботникамОрганизаций");
	КонецЕсли;
    ВидДокумента = Тип;
    НазначитьТипДокументу();

	// Вывести в заголовке формы статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

	// Заполним реквизит формы МесяцСтрока.
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	
	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;

	ОбновитьДвиженияДокумента(ЭтоНовый());

КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
Процедура ПослеЗаписи()
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Вывести в заголовке формы статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	// оповестим отменяемые документы
	Оповестить("ОбновитьФорму", Новый Структура("Команда, Объект","ДоступностьОтмененногоДокумента", ?(ЗначениеЗаполнено(СторнируемыйДокумент),СторнируемыйДокумент,Неопределено)), Ссылка);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа
//
Процедура ДатаПриИзменении(Элемент = Неопределено)

	РаботаСДиалогами.ПроверитьНомерДокумента(ДокументОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	мТекущаяДатаДокумента = Дата;
	
	ОбновитьПериодРегистрацииДвижений();

КонецПроцедуры // ДатаПриИзменении

// Процедура - обработчик события "ПриИзменении" поля ввода Сторнируемый документ
//
Процедура СторнируемыйДокументПриИзменении(Элемент)
    ОбновитьДвиженияДокумента();
КонецПроцедуры

// Процедура - обработчик события "Очистка" поля ввода Сторнируемый документ
//
Процедура СторнируемыйДокументОчистка(Элемент, СтандартнаяОбработка)
	ОбновитьДвиженияДокумента();
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" элемента выбора "ВидДокумента"
//
Процедура ВидДокументаПриИзменении(Элемент)
    НазначитьТипДокументу();
	ОбновитьДвиженияДокумента();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обслуживание ЭУ Период регистрации
//

// Процедура - обработчик события "ПриИзменении" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииПриИзменении(Элемент)
	РаботаСДиалогами.ДатаКакМесяцПодобратьДатуПоТексту(Элемент.Значение, ПериодРегистрации);
    ОбновитьПериодРегистрацииДвижений();
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	РаботаСДиалогами.НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, ПериодРегистрации, ЭтаФорма);
    ОбновитьПериодРегистрацииДвижений();
КонецПроцедуры

// Процедура - обработчик события "Регулирование" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации, Направление);
    ОбновитьПериодРегистрацииДвижений();
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	
КонецПроцедуры

// Процедура - обработчик события "АвтоПодборТекста" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	РаботаСДиалогами.ДатаКакМесяцАвтоПодборТекста(Текст, ТекстАвтоПодбора, СтандартнаяОбработка);
КонецПроцедуры

// Процедура - обработчик события "ОкончаниеВводаТекста" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	РаботаСДиалогами.ДатаКакМесяцОкончаниеВводаТекста(Текст, Значение, СтандартнаяОбработка);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()
                    
// Процедура вызова структуры подчиненности документа
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		
		УстановитьКнопкиПечати();
		
	КонецЕсли; 
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
			
КонецПроцедуры