// Проверяет дату, выбранную как период планирования при переносе транзакций
// и, при необходимости, выравнивает ее по началу периода планирования и(или)
// блокирует установку периода в интервал актуализации.
// 
// Параметры
//  ВыбраннаяДата  – Дата, выбранный период планирования
//
// Возвращаемое значение:
//   ПериодПланирования   – скорректированное значение периода планирования.
//
Функция ПроверкаПериодаДляПереноса(ВыбраннаяДата)

	ПериодПланирования=ОбщегоНазначения.ДатаНачалаПериода(ВыбраннаяДата,Сценарий.Периодичность);
	
	Если ПериодПланирования<=ДатаКонца Тогда
		ПериодПланирования=ОбщегоНазначения.ДатаНачалаПериода(ОбщегоНазначения.ДобавитьИнтервал(ДатаКонца,Сценарий.Периодичность,1),Сценарий.Периодичность);
	КонецЕсли;
	
	Возврат ПериодПланирования;	

КонецФункции // ПроверкаПериодаДляПереноса()
 

Процедура КоманднаяПанельТранзакцииУстановитьВсе(Кнопка)
	
	Для каждого Транзакция Из НастройкаПереносаТранзакций Цикл
	
		Транзакция.ПереноситьТранзакцию=Истина;
	
	КонецЦикла; 
	
КонецПроцедуры

Процедура КоманднаяПанельТранзакцииСнятьВсе(Кнопка)
	
	Для каждого Транзакция Из НастройкаПереносаТранзакций Цикл
	
		Транзакция.ПереноситьТранзакцию=Ложь;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельТранзакцииЗаполнить(Кнопка)
	
	Если НастройкаПереносаТранзакций.Количество() > 0 Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да,);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 
		
		НастройкаПереносаТранзакций.Очистить();
		
	КонецЕсли;
	
	ТабКорректировок=СформироватьЗапросПоОборотам();
	ТабКорректировок.Свернуть("Валюта,СтатьяОборотов,ЦФО,Проект,Контрагент,Номенклатура","Количество,ВалютнаяСумма");
	
	Периодичность=Сценарий.Периодичность;
	
	ДатаНачалаПереноса=ОбщегоНазначения.ДатаНачалаПериода(ОбщегоНазначения.ДобавитьИнтервал(ДатаКонца,Периодичность,1),Периодичность);
	
	Для каждого Транзакция Из ТабКорректировок Цикл
	
		ТранзакцияКПереносу=НастройкаПереносаТранзакций.Добавить();
		ТранзакцияКПереносу.Валюта=Транзакция.Валюта;
		ТранзакцияКПереносу.СтатьяОборотов=Транзакция.СтатьяОборотов;
		ТранзакцияКПереносу.ЦФО=Транзакция.ЦФО;
		ТранзакцияКПереносу.Проект=Транзакция.Проект;
		ТранзакцияКПереносу.Контрагент=Транзакция.Контрагент;
		ТранзакцияКПереносу.Номенклатура=Транзакция.Номенклатура;
		ТранзакцияКПереносу.Количество=Транзакция.Количество*(-1);
		ТранзакцияКПереносу.ВалютнаяСумма=Транзакция.ВалютнаяСумма*(-1);
		ТранзакцияКПереносу.УстановкаПериода=Перечисления.ВидыПереносаОтклоненийПланФакт.РучноеУказаниеПериода;
		
		ТранзакцияКПереносу.Период=ДатаНачалаПереноса;
		ТранзакцияКПереносу.ПереноситьТранзакцию=Истина;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура НастройкаПереносаТранзакцийПериодПриИзменении(Элемент)
	
	Если НЕ ТипЗнч(Элемент.Значение)=Тип("СправочникСсылка.ПрофилиИзмененияПлановПоПериодам") Тогда
	
		Элемент.Значение=ПроверкаПериодаДляПереноса(Элемент.Значение);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаПереносаТранзакцийУстановкаПериодаПриИзменении(Элемент)
	
	Если Элемент.Значение=Перечисления.ВидыПереносаОтклоненийПланФакт.ПоПрофилю Тогда
		ЭлементыФормы.НастройкаПереносаТранзакций.ТекущаяСтрока.Период=Новый(Тип("СправочникСсылка.ПрофилиИзмененияПлановПоПериодам"));
		ЭлементыФормы.НастройкаПереносаТранзакций.Колонки.Период.ЭлементУправления.КнопкаРегулирования=Ложь;
	Иначе
		Периодичность=Сценарий.Периодичность;
		ЭлементыФормы.НастройкаПереносаТранзакций.ТекущаяСтрока.Период=ОбщегоНазначения.ДатаНачалаПериода(ОбщегоНазначения.ДобавитьИнтервал(ДатаКонца,Периодичность,1),Периодичность);
		ЭлементыФормы.НастройкаПереносаТранзакций.Колонки.Период.ЭлементУправления.КнопкаРегулирования=Истина;
	КонецЕсли;	
		
КонецПроцедуры

Процедура НастройкаПереносаТранзакцийПриАктивизацииСтроки(Элемент)
	
	Если Не Элемент.ТекущиеДанные=Неопределено Тогда
		
		Если Элемент.ТекущиеДанные.УстановкаПериода=Перечисления.ВидыПереносаОтклоненийПланФакт.ПоПрофилю Тогда
			ЭлементыФормы.НастройкаПереносаТранзакций.Колонки.Период.ЭлементУправления.КнопкаРегулирования=Ложь;
		Иначе
			ЭлементыФормы.НастройкаПереносаТранзакций.Колонки.Период.ЭлементУправления.КнопкаРегулирования=Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаПереносаТранзакцийПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	Элемент.Значение=ПроверкаПериодаДляПереноса(ОбщегоНазначения.ДобавитьИнтервал(Элемент.Значение,Сценарий.Периодичность,Направление));
		
КонецПроцедуры

Процедура ЗакрытьФорму()
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры