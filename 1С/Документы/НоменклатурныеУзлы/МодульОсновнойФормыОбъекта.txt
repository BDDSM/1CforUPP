Процедура ПриОткрытии()

	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(ЭлементыФормы.ИсходныеКомплектующие.Колонки);

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
	
КонецПроцедуры // ПриОткрытии()

Процедура ПослеЗаписи()
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ПослеЗаписи()

Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ДействияФормыРедактироватьКод()

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.ДатаУтверждения.Видимость = (Состояние = Перечисления.СостоянияОбъектов.Утвержден);
	ЭлементыФормы.НадписьДатаУтверждения.Видимость = (Состояние = Перечисления.СостоянияОбъектов.Утвержден);
	
КонецПроцедуры // ОбновлениеОтображения()

Процедура СостояниеПриИзменении(Элемент)

	Если Состояние = Перечисления.СостоянияОбъектов.Утвержден Тогда
		
		ДатаУтверждения = РабочаяДата;
		
	Иначе
		
		ДатаУтверждения = Неопределено;
		
	КонецЕсли;
	
	ЭлементыФормы.ДатаУтверждения.Видимость = (Состояние = Перечисления.СостоянияОбъектов.Утвержден);
	ЭлементыФормы.НадписьДатаУтверждения.Видимость = (Состояние = Перечисления.СостоянияОбъектов.Утвержден);
	
КонецПроцедуры // СостояниеПриИзменении()

Процедура ИсходныеКомплектующиеВидНормативаПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	
	Если ТекущиеДанные.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Номенклатура Тогда
		
		ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		ТекущиеДанные.Номенклатура = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.Номенклатура);
		
	ИначеЕсли ТекущиеДанные.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Узел Тогда
		
		ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеУзлы");
		ТекущиеДанные.Номенклатура = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.Номенклатура);
		
	Иначе
		
		ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.Номенклатура, СправочникСсылка.НоменклатурныеУзлы");
		ТекущиеДанные.Номенклатура = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.Номенклатура);
		
	КонецЕсли;
	
	ПриИзмененииНоменклатуры(ТекущиеДанные);
	
КонецПроцедуры // ИсходныеКомплектующиеВидНормативаПриИзменении()

Процедура ИсходныеКомплектующиеНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	ПриИзмененииНоменклатуры(ТекущиеДанные);
	
	Если ТипЗнч(ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ТекущиеДанные.СтатьяЗатрат = ТекущиеДанные.Номенклатура.СтатьяЗатрат;
		
	КонецЕсли;
	
КонецПроцедуры // ИсходныеКомплектующиеНоменклатураПриИзменении()

Процедура ИсходныеКомплектующиеНоменклатураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	
	Если  ТекущиеДанные.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Номенклатура Тогда
		
		Элемент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		Элемент.Значение = Элемент.ОграничениеТипа.ПривестиЗначение(Элемент.Значение);
		Элемент.ВыбиратьТип = Ложь;
		
	ИначеЕсли ТекущиеДанные.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Узел Тогда
		
		Элемент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеУзлы");
		Элемент.Значение = Элемент.ОграничениеТипа.ПривестиЗначение(Элемент.Значение);
		Элемент.ВыбиратьТип = Ложь;
		
	Иначе
		
		Элемент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Номенклатура, СправочникСсылка.НоменклатурныеУзлы");
		Элемент.ВыбиратьТип = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ИсходныеКомплектующиеНоменклатураНачалоВыбора()

Процедура ИсходныеКомплектующиеХарактеристикаНоменклатурыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ВидНорматива = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные.ВидНорматива;
	
	Если ТипЗнч(Элемент.ВыборПоВладельцу) = Тип("СправочникСсылка.НоменклатурныеУзлы") ИЛИ ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Узел Тогда
		
		СтандартнаяОбработка = Ложь;
		Предупреждение("Для номенклатурных узлов не ведется учет по характеристикам!");
		
	КонецЕсли;

КонецПроцедуры // ИсходныеКомплектующиеХарактеристикаНоменклатурыНачалоВыбора()

Процедура ПриИзмененииНоменклатуры(СтрокаТабличнойЧасти)
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВидНорматива) Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			СтрокаТабличнойЧасти.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Номенклатура;
			
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.НоменклатурныеУзлы") Тогда
			
			СтрокаТабличнойЧасти.ВидНорматива = Перечисления.ВидыНормативовНоменклатуры.Узел;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти.Номенклатура <> СтрокаТабличнойЧасти.ЕдиницаИзмерения.Владелец Тогда
		
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = Неопределено;
		
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти.Номенклатура <> СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры.Владелец Тогда
		
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Неопределено;
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЕдиницаИзмерения) Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.Номенклатура.ЕдиницаХраненияОстатков;
			
		КонецЕсли;
		
	КонецЕсли; 

КонецПроцедуры // ПриИзмененииНоменклатуры()

Процедура ИсходныеКомплектующиеПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки из ОформленияСтрок Цикл
		
		Если ТипЗнч(ОформлениеСтроки.ДанныеСтроки.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			ОформлениеСтроки.Ячейки.СтатьяЗатрат.ТолькоПросмотр = Ложь;
			ОформлениеСтроки.Ячейки.СтатьяЗатрат.Видимость = Истина;
			
		Иначе
			
			ОформлениеСтроки.Ячейки.СтатьяЗатрат.ТолькоПросмотр = Истина;
			ОформлениеСтроки.Ячейки.СтатьяЗатрат.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ИсходныеКомплектующиеПриПолученииДанных()