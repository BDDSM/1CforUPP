Перем мСтруктураНастройкиЗаполнения;

// Обработчик события ПередНачаломДобавления элемента СписокКурсыВалют.
//
Процедура СписокКурсыВалютПередНачаломДобавления(Элемент, Отказ, Копирование)

	Если ЭтоНовый() Тогда
		Ответ = Вопрос("Элемент еще не записан. Записать?", РежимДиалогаВопрос.ОКОтмена);

		Если Ответ = КодВозвратаДиалога.ОК Тогда
			Отказ = НЕ ЗаписатьВФорме();
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // СписокКурсыВалютПередНачаломДобавления()

// Обработчик события ПриНачалеРедактирования элемента СписокКурсыВалют.
//
Процедура СписокКурсыВалютПриНачалеРедактирования(Элемент, НоваяСтрока)

	СтруктураОтбора=Новый Структура;
	СтруктураОтбора.Вставить("Валюта", ВалютаКурсов);
	СтруктураОтбора.Вставить("Сценарий", Ссылка);
	
	Если НоваяСтрока Тогда

		СтруктураКурса = РегистрыСведений.КурсыВалютПоСценариям.ПолучитьПоследнее(ТекущаяДата(), СтруктураОтбора);

		Элемент.ТекущиеДанные.Курс      = СтруктураКурса.Курс;
		Элемент.ТекущиеДанные.Кратность = СтруктураКурса.Кратность;
		Элемент.ТекущиеДанные.Валюта	= ВалютаКурсов;
        Элемент.ТекущиеДанные.Сценарий	= Ссылка;

		Элемент.ТекущиеДанные.Период=ОбщегоНазначения.ДатаНачалаПериода(Элемент.ТекущиеДанные.Период,Периодичность); 
		
	КонецЕсли;

КонецПроцедуры // СписокКурсыВалютПриНачалеРедактирования()

Процедура ВалютаКурсовПриИзменении(Элемент)
	
    // Устанавливается отбор курсов валюты.
	СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);
	СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);
	
КонецПроцедуры // ВалютаКурсовПриИзменении()

Процедура ИспользоватьКурсыСценарияПриИзменении(Элемент)
	
   Если Элемент.Значение И ЭтоНовый() Тогда

		Ответ = Вопрос("Элемент еще не записан. Записать?", РежимДиалогаВопрос.ОКОтмена);

		Если Ответ = КодВозвратаДиалога.ОК Тогда
			
			Элемент.Значение = ЗаписатьВФорме();
			
		Иначе
			
			Элемент.Значение = Ложь;
			
		КонецЕсли;

	КонецЕсли;
	
	Если Элемент.Значение Тогда

		ЭлементыФормы.ОсновнаяПанель.Страницы.КурсыВалют.Доступность = Истина;
		ВалютаКурсов = Валюта;
		СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);
		СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);

	Иначе

		ЭлементыФормы.ОсновнаяПанель.Страницы.КурсыВалют.Доступность = Ложь;

	КонецЕсли;
		
КонецПроцедуры // ИспользоватьКурсыСценарияПриИзменении()

Процедура ЗаполнитьНажатие(Элемент)
	
	Если ЭлементыФормы.ВалютаКурсов.Значение.Пустая() Тогда
		Сообщить("Выберите валюту для установки курсов!");
		Возврат;
	КонецЕсли;
		
	Если ЭтоНовый() Тогда
		
		Отказ=Ложь;
		
		Ответ = Вопрос("Элемент еще не записан. Записать?", РежимДиалогаВопрос.ОКОтмена);
		
		Если Ответ = КодВозвратаДиалога.ОК Тогда
			Отказ = НЕ ЗаписатьВФорме();
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		Иначе
			// Устанавливается отбор курсов валюты.
			СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);
			СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);
		КонецЕсли;	
	КонецЕсли;
		
	ФормаЗаполнения = ПолучитьФорму("ФормаАвтозаполненияКурсов", ЭтаФорма,);
	
	Если мСтруктураНастройкиЗаполнения <> Неопределено Тогда
		
		ФормаЗаполнения.НачальноеЗначениеВыбора = мСтруктураНастройкиЗаполнения;
		
	КонецЕсли;
	
	ФормаЗаполнения.РежимВыбора = Истина;
	ФормаЗаполнения.Открыть();
	
КонецПроцедуры // ЗаполнитьНажатие()

Процедура СписокКурсыВалютПериодПриИзменении(Элемент)

	Элемент.Значение = ОбщегоНазначения.ДатаНачалаПериода(Элемент.Значение,Периодичность);
	
КонецПроцедуры // СписокКурсыВалютПериодПриИзменении()

Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры // ДействияФормыРедактироватьКод()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
		
	Если ИспользоватьКурсыСценария Тогда
		
		ЭлементыФормы.ОсновнаяПанель.Страницы.КурсыВалют.Доступность = Истина;
		// Устанавливается отбор курсов валюты.
		СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);
		ВалютаКурсов=Валюта;	
		СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);
		
	Иначе
		
		ЭлементыФормы.ОсновнаяПанель.Страницы.КурсыВалют.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

// Обработчик события ПриОткрытии Формы.
//
Процедура ПриОткрытии()
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);

КонецПроцедуры //ПриОткрытии

// Обработчик события ПослеЗаписи формы.
//
Процедура ПослеЗаписи()

	// Устанавливается отбор курсов валюты.
	СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);
	СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);

КонецПроцедуры // ПослеЗаписи()

// Обработчик события ПриИзмененииДанных формы.
//
Процедура ПриИзмененииДанных()

	// Устанавливается отбор курсов валюты.
	СписокКурсыВалют.Отбор.Сценарий.Установить(Ссылка);
	СписокКурсыВалют.Отбор.Валюта.Установить(ВалютаКурсов);

КонецПроцедуры // ПриИзмененииДанных()

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	мСтруктураНастройкиЗаполнения = ЗначениеВыбора.Настройка;

	РегистрКурсыВалют = РегистрыСведений.КурсыВалютПоСценариям;
	ЗаписьКурсовВалют = РегистрКурсыВалют.СоздатьМенеджерЗаписи();

	Для Каждого СтрокаКурса Из ЗначениеВыбора.Данные Цикл

		ЗаписьКурсовВалют.Валюта   = ЭлементыФормы.ВалютаКурсов.Значение;
		ЗаписьКурсовВалют.Сценарий = Ссылка;
		ЗаписьКурсовВалют.Период   = СтрокаКурса.ДатаКурса;
		ЗаписьКурсовВалют.Прочитать();
		ЗаписьКурсовВалют.Валюта    = ЭлементыФормы.ВалютаКурсов.Значение;
		ЗаписьКурсовВалют.Сценарий 	= Ссылка;
		ЗаписьКурсовВалют.Период    = СтрокаКурса.ДатаКурса;
		ЗаписьКурсовВалют.Курс      = СтрокаКурса.Курс;
		ЗаписьКурсовВалют.Кратность = СтрокаКурса.Кратность;
		ЗаписьКурсовВалют.Записать();

	КонецЦикла;
	
КонецПроцедуры // ОбработкаВыбора()