// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;



// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ОсновныеСредства,ЭлементыФормы.КоманднаяПанельОС);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры


Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
КонецПроцедуры

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()

	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, "Продажа");
	КонецЕсли;

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	
	
КонецПроцедуры

Процедура ОсновныеСредстваОсновноеСредствоПриИзменении(Элемент)
	СтрокаТП = ЭлементыФормы.ОсновныеСредства.ТекущаяСтрока;

	СрезСведенийМежд = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.ПолучитьПоследнее((Дата - 1), Новый Структура("ОсновноеСредство", СтрокаТП.ОсновноеСредство));

	СтрокаТП.СрокПолезногоИспользования = СрезСведенийМежд.СрокПолезногоИспользования;
	СтрокаТП.НачислятьАмортизацию = СрезСведенийМежд.НачислятьАмортизацию;
	СтрокаТП.МетодНачисленияАмортизации = СрезСведенийМежд.МетодНачисленияАмортизации;
	СтрокаТП.ПредполагаемыйОбъемПродукции = СрезСведенийМежд.ПредполагаемыйОбъемПродукции;
	СтрокаТП.КоэффициентУскорения = СрезСведенийМежд.КоэффициентУскорения;
	СтрокаТП.Состояние = СрезСведенийМежд.Состояние;
	СтрокаТП.СчетЗатрат = СрезСведенийМежд.СчетЗатрат;
	СтрокаТП.Субконто1 = СрезСведенийМежд.Субконто1;
	СтрокаТП.Субконто2 = СрезСведенийМежд.Субконто2;
	СтрокаТП.Субконто3 = СрезСведенийМежд.Субконто3;
	
	СтрокаТП.СрокПолезногоИспользованияНов = СрезСведенийМежд.СрокПолезногоИспользования;
	СтрокаТП.НачислятьАмортизациюНов = СрезСведенийМежд.НачислятьАмортизацию;
	СтрокаТП.МетодНачисленияАмортизацииНов = СрезСведенийМежд.МетодНачисленияАмортизации;
	СтрокаТП.ПредполагаемыйОбъемПродукцииНов = СрезСведенийМежд.ПредполагаемыйОбъемПродукции;
	СтрокаТП.КоэффициентУскоренияНов = СрезСведенийМежд.КоэффициентУскорения;
	СтрокаТП.СчетЗатратНов = СрезСведенийМежд.СчетЗатрат;
	СтрокаТП.Субконто1Нов = СрезСведенийМежд.Субконто1;
	СтрокаТП.Субконто2Нов = СрезСведенийМежд.Субконто2;
	СтрокаТП.Субконто3Нов = СрезСведенийМежд.Субконто3;
	СтрокаТП.СостояниеНов = СрезСведенийМежд.Состояние;
КонецПроцедуры

Процедура ОсновныеСредстваСубконто1НовНачалоВыбора(Элемент, СтандартнаяОбработка)
	Для Ном = 1 по 3 Цикл
		Если (ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СчетЗатратНов.ВидыСубконто.Количество() > (Ном-1)) и (НЕ ЗначениеЗаполнено(ЭлементыФормы.ОсновныеСредства.ТекущиеДанные["Субконто"+Ном+"Нов"])) Тогда
			Типы = ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СчетЗатратНов.ВидыСубконто[Ном-1].ВидСубконто.ТипЗначения.Типы();
			ЭлементыФормы.ОсновныеСредства.ТекущиеДанные["Субконто"+Ном+"Нов"] = Новый(Типы[0]);
		КонецЕсли;
	КонецЦикла
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры
// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

Процедура ОсновныеСредстваСостояниеНовПриИзменении(Элемент)
	Если ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СостояниеНов = Перечисления.ВидыСостоянийОС.СнятоСУчета Тогда
		Предупреждение("Операция снятия с учета ОС оформляется специальным документом.");
		ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.СостояниеНов = ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Состояние;
	КонецЕсли;
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
КонецПроцедуры

Процедура ОсновныеСредстваПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	ОформлениеСтроки.Ячейки.ПредполагаемыйОбъемПродукцииНов.ТолькоПросмотр = (ДанныеСтроки.МетодНачисленияАмортизацииНов <> Перечисления.МетодыНачисленияАмортизацииОСМеждународный.ПропорциональноОбъемуПродукции);
	ОформлениеСтроки.Ячейки.КоэффициентУскоренияНов.ТолькоПросмотр = (ДанныеСтроки.МетодНачисленияАмортизацииНов <> Перечисления.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка);
КонецПроцедуры

Процедура ОсновныеСредстваСчетЗатратНовПриИзменении(Элемент)

	РаботаСДиалогами.ПриВыбореСчетаВТабличномПоле(Элемент.Значение,
	                             ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто1Нов,
	                             ЭлементыФормы.ОсновныеСредства.Колонки.Субконто1Нов,
	                             ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто2Нов,
	                             ЭлементыФормы.ОсновныеСредства.Колонки.Субконто2Нов,
	                             ЭлементыФормы.ОсновныеСредства.ТекущиеДанные.Субконто3Нов,
	                             ЭлементыФормы.ОсновныеСредства.Колонки.Субконто3Нов);

КонецПроцедуры // СпособыСчетЗатратПриИзменении()

// Процедура вызова структуры подчиненности документа
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать() 

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры