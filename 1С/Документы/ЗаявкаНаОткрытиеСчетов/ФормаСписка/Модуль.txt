////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	
	ЭлементыФормы.ДокументСписок.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭлементыФормы.ОрганизацияФлаг.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РаботаСДиалогами.НапечататьДвиженияДокумента(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура вызывается при выборе пункта подменю "Структура подчиненности" меню "Перейти".
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);
	
КонецПроцедуры // ДействияФормыСтруктураПодчиненностиДокумента()


Процедура ДокументСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ТаблицаСсылок = Новый ТаблицаЗначений;
	ТаблицаСсылок.Колонки.Добавить("ИндексСтроки", Новый ОписаниеТипов("Число"),Новый КвалификаторыЧисла(10));
	ТаблицаСсылок.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаОткрытиеСчетов"));
	Для К = 0 По ОформленияСтрок.Количество() - 1 Цикл
		СтрокаТаблицы = ТаблицаСсылок.Добавить();
		СтрокаТаблицы.ИндексСтроки = К;
		СтрокаТаблицы.Ссылка = ОформленияСтрок[К].ДанныеСтроки.Ссылка;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСсылок", ТаблицаСсылок);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таблица.ИндексСтроки,
	               |	Таблица.Ссылка
	               |ПОМЕСТИТЬ ВТТаблицаСсылок
	               |ИЗ
	               |	&ТаблицаСсылок КАК Таблица";
				   
	Запрос.Выполнить();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТТаблицаСсылок.ИндексСтроки КАК ИндексСтроки,
	               |	ВЫБОР
	               |		КОГДА ПротоколыОбменаСБанком.Содержание ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ПолученОтвет
	               |ИЗ
	               |	ВТТаблицаСсылок КАК ВТТаблицаСсылок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколыОбменаСБанком КАК ПротоколыОбменаСБанком
	               |		ПО ВТТаблицаСсылок.Ссылка = ПротоколыОбменаСБанком.Документ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ИндексСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОформленияСтрок[Выборка.ИндексСтроки].Ячейки.ОбработаноВБанке.ЗначениеФлажка = Выборка.ПолученОтвет;
		ОформленияСтрок[Выборка.ИндексСтроки].Ячейки.ОбработаноВБанке.ОтображатьФлажок = Истина;
	КонецЦикла;
	

КонецПроцедуры

// Процедура выполняет  открытие формы регистра ЛицевыеСчетаРаботниковОрганизации
// с отбором по данному документу
Процедура ДействияФормыЛицевыеСчетаРаботниковОрганизаций(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ФормаСписка = РегистрыСведений.ЛицевыеСчетаРаботниковОрганизации.ПолучитьФормуСписка();	
	ФормаСписка.Отбор.Документ.Установить(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);
	ФормаСписка.Открыть();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	УстановитьЗаголовкиОрганизацийВФорме();
	
	РаботаСДиалогамиЗК.НастроитьОтборПоОрганизации(ЭтаФорма, "ДокументСписок");
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РаботаСДиалогамиЗК.ОбработкаОповещенияСпискаДокументов(ИмяСобытия, Параметр, Источник, ЭтаФорма, "ДокументСписок");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОрганизацияПриИзменении(Элемент)
	
	РаботаСДиалогамиЗК.ОбработкаИзмененияБыстрогоОтбораПоОрганизации(ЭтаФорма, Элемент, "ДокументСписок");

КонецПроцедуры

Процедура ДокументСписокПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	РаботаСДиалогамиЗК.СписокДокументовПередНачаломДобавления(ЭтаФорма, Отказ, Копирование, Отбор.Организация.Значение, "ЗаявкаНаОткрытиеСчетов");

КонецПроцедуры