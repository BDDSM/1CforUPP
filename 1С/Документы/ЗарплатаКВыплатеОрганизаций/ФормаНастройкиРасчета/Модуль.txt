////////////////////////////////////////////////////////////
/// Управление реквизитами формы

Процедура ЗаполнитьРеквизитыФормыПоДокументу()
	
	формаДнейНеВыплаты					= ДнейНеВыплаты;
	
	Округлять = ЗначениеЗаполнено(формаОкругление);

	Если ЗначениеЗаполнено(Округление) Тогда
		формаОкругление	= Округление;
		Округлять = Истина;
	Иначе
		формаОкругление = Перечисления.СпособыОкругленияЗарплатыКВыплате.Рубль;
		Округлять = Ложь;
	КонецЕсли;
	
	формаПредварительныйУчетУдержаний	= ПредварительныйУчетУдержаний;
	формаПроцентВыплаты					= ПроцентВыплаты;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоРеквизитамФормы()
	
	Если ДнейНеВыплаты <> формаДнейНеВыплаты Тогда
		ДнейНеВыплаты = формаДнейНеВыплаты;
	КонецЕсли;
	
	формаОкругление = ?(Округлять, формаОкругление, Перечисления.СпособыОкругленияЗарплатыКВыплате.ПустаяСсылка()); 
	Если Округление <> формаОкругление Тогда
		Округление = формаОкругление;
	КонецЕсли;
	
	Если ПредварительныйУчетУдержаний <> формаПредварительныйУчетУдержаний Тогда
		ПредварительныйУчетУдержаний = формаПредварительныйУчетУдержаний;
	КонецЕсли;
	
	Если ПроцентВыплаты <> формаПроцентВыплаты Тогда
		ПроцентВыплаты = формаПроцентВыплаты;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////
/// Управление элементами формы

Процедура УстановитьТекстДнейНеВыплаты(НадписьДней, ПолеВводаДней)
	НадписьДней.Заголовок = ОбщегоНазначения.ФормаМножественногоЧисла("день","дня","дней", ?(ПолеВводаДней <> Неопределено, ПолеВводаДней.Значение, ДнейНеВыплаты));
КонецПроцедуры

Процедура УстановитьДоступностьЭлементовФормы()
	
	НастройкиИзменились =
			ПроцентВыплаты <> ЭтаФорма.ПроцентВыплаты
			ИЛИ ПредварительныйУчетУдержаний  <> ЭтаФорма.ПредварительныйУчетУдержаний
			ИЛИ Округление  <> ЭтаФорма.Округление
			ИЛИ ДнейНеВыплаты = ЭтаФорма.ДнейНеВыплаты; 
			
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Пересчитать.Доступность = Истина; // НастройкиИзменились;		
			
КонецПроцедуры

////////////////////////////////////////////////////////////
/// Обработчики событий формы

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = НЕ ЗначениеЗаполнено(ЗарплатаКВыплатеОрганизацийСервис.ИмяСтраницыНастройкиРасчета(ХарактерВыплаты));
	
	ЗаполнитьРеквизитыФормыПоДокументу();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = 
		ЭлементыФормы.ПанельНастроек.Страницы.Найти(
			ЗарплатаКВыплатеОрганизацийСервис.ИмяСтраницыНастройкиРасчета(ХарактерВыплаты));
	
	ОкруглятьПриИзменении(Неопределено);
	ДнейНеВыплатыПриИзменении(Неопределено);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////
/// Обработчики событий командной панели формы

Процедура ОсновныеДействияФормыПересчитать(Кнопка)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Если НЕ РаботаСДиалогами.ЗаписатьДокументОтменивПроведениеПередВыполнениемДействия(ДокументОбъект, ЭтаФорма, "рассчитать") Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли; 
	
	Если НЕ Округлять Тогда
		Округление = Перечисления.СпособыОкругленияЗарплатыКВыплате.ПустаяСсылка();
	КонецЕсли;
	
	ЗаполнитьДокументПоРеквизитамФормы();
	
	Рассчитать();

	ЗарплатаКВыплатеОрганизацийСервис.СохранитьНастройки(ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если НЕ Округлять Тогда
		Округление = Перечисления.СпособыОкругленияЗарплатыКВыплате.ПустаяСсылка();
	КонецЕсли;
	
	ЗаполнитьДокументПоРеквизитамФормы();
	
	ЗарплатаКВыплатеОрганизацийСервис.СохранитьНастройки(ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////
/// Обработчики событий элементов формы

Процедура ОкруглятьПриИзменении(Элемент)
	
	Если ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.АвансЗаПервуюПоловинуМесяца Тогда
		ЭлементыФормы.АвансЗаПервуюПоловинуМесяца_Округление.Доступность = Округлять;
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.Зарплата Тогда
		ЭлементыФормы.Зарплата_Округление.Доступность = Округлять;
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.ПрочиеВыплаты Тогда
		ЭлементыФормы.ПрочиеВыплаты_Округление.Доступность = Округлять;
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.ПрочиеВыплатыБезУдержаний Тогда
		ЭлементыФормы.ПрочиеВыплатыБезУдержаний_Округление.Доступность = Округлять;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы()	
	
КонецПроцедуры

Процедура ДнейНеВыплатыПриИзменении(Элемент)
	
	Если ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.ПлановыйАванс Тогда
		УстановитьТекстДнейНеВыплаты(ЭлементыФормы.ПлановыйАванс_НадписьДней, Элемент);
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.АвансЗаПервуюПоловинуМесяца Тогда
		УстановитьТекстДнейНеВыплаты(ЭлементыФормы.АвансЗаПервуюПоловинуМесяца_НадписьДней, Элемент);
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.Зарплата Тогда
		УстановитьТекстДнейНеВыплаты(ЭлементыФормы.Зарплата_НадписьДней, Элемент);
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.ПрочиеВыплаты Тогда
		УстановитьТекстДнейНеВыплаты(ЭлементыФормы.ПрочиеВыплаты_НадписьДней, Элемент);
	ИначеЕсли ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы.ПрочиеВыплатыБезУдержаний Тогда
		УстановитьТекстДнейНеВыплаты(ЭлементыФормы.ПрочиеВыплатыБезУдержаний_НадписьДней, Элемент);
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы()	
	
КонецПроцедуры

Процедура ОкруглениеПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы()
КонецПроцедуры

Процедура ПредварительныйУчетУдержанийПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы()
КонецПроцедуры

Процедура ПроцентВыплатыПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы()
КонецПроцедуры