////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	РежимИспользованияРасширеннойАналитики = глЗначениеПеременной("РежимИспользованияРасширеннойАналитикиУчетаНоменклатурыИЗатрат");
	
	Если РежимИспользованияРасширеннойАналитики = Перечисления.РежимыИспользованияРасширеннойАналитики.УправленческийИРегламентированныйУчет Тогда
		ЭлементыФормы.СправочникСписок.Колонки.УправленческийУчет.Видимость = Истина;
		ЭлементыФормы.СправочникСписок.Колонки.РегламентированныйУчет.Видимость = Истина;
		ЭлементыФормы.СправочникСписок.Колонки.УправленческийУчет.Доступность = Истина;
		ЭлементыФормы.СправочникСписок.Колонки.РегламентированныйУчет.Доступность = Ложь;
	ИначеЕсли РежимИспользованияРасширеннойАналитики = Перечисления.РежимыИспользованияРасширеннойАналитики.РегламентированныйУчетСДополнительнойАналитикой Тогда
		ЭлементыФормы.СправочникСписок.Колонки.УправленческийУчет.Видимость = Ложь;
		ЭлементыФормы.СправочникСписок.Колонки.РегламентированныйУчет.Видимость = Истина;
		ЭлементыФормы.СправочникСписок.Колонки.РегламентированныйУчет.Доступность = Истина;
	Иначе	
		Отказ = Истина;
		Предупреждение("Установлен режим использования расширенной аналитики учета затрат """ + РежимИспользованияРасширеннойАналитики + """. Настройка детализации аналитики в этом режиме недоступна!");
	КонецЕсли;	
	
КонецПроцедуры // ПередОткрытием()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Заполнить по умолчанию" командной панели.
//
Процедура ДействияФормыЗаполнитьПоУмолчанию(Кнопка)
	
	РасширеннаяАналитикаУчета.ЗаполнитьНастройкиАналитикиПоШаблону();
	
КонецПроцедуры // ДействияФормыЗаполнитьПоУмолчанию()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ


// Процедура - обработчик события "ПриПолученииДанных" списка справочника.
//
Процедура СправочникСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	МассивТолькоПросмотр = РасширеннаяАналитикаУчета.ПолучитьМассивНастроекРАТолькоПросмотр();
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		Ячейки = ОформлениеСтроки.Ячейки;
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		
		Если ДанныеСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТолькоПросмотр = МассивТолькоПросмотр.Найти(ДанныеСтроки.Ссылка);
		
		Если СтрокаТолькоПросмотр <> Неопределено Тогда
			
			Ячейки.УправленческийУчет.ТолькоПросмотр = СтрокаТолькоПросмотр.ТолькоПросмотр ИЛИ (НЕ ЭлементыФормы.СправочникСписок.Колонки.УправленческийУчет.Доступность);
			Ячейки.РегламентированныйУчет.ТолькоПросмотр = СтрокаТолькоПросмотр.ТолькоПросмотрРегл ИЛИ (НЕ ЭлементыФормы.СправочникСписок.Колонки.РегламентированныйУчет.Доступность);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // СправочникСписокПриПолученииДанных()