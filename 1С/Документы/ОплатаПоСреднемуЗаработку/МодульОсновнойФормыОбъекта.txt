////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

// Хранит последнюю установленную дату документа - для проверки перехода документа в другой период
Перем мТекущаяДатаДокумента;
// Текущий период
Перем мТекущийПериод Экспорт; 

Перем мРассчитываемыеТаблицы;

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

// Хранит продолжительность (количество месяцев) расчетного периода текущего вида оплаты 
Перем мПериодРасчетаСреднегоЗаработка;
Перем мСуммированныйУчетРабочегоВремени;

Перем мОбработкаПодбораПоСтроке;
Перем мТекстПодбораПоСтроке;
Перем мПоследнееЗначениеЭлементаПодбораПоСтроке;

// Хранит ссылку на головную организацию
Перем мГоловнаяОрганизация;

Перем мСведенияОВидахРасчетаПоказатели;
Перем мСведенияОВидахРасчета;

// Механизм исправлений
Перем мДокументИсправление;
Перем мДокументСторнирование;
Перем мКнопкаИсправление;
Перем мКнопкаОтменаИсправление;

// Диалог настройки периода
Перем мНастройкаПериода;

// Дополнительные свойства формы
Перем мДополнительныеСвойства Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция формирует структуру параметров для для ввода головной организации по подстроке
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Структура имен и значений параметров.
//
Функция ПолучитьСтруктуруПараметровПодбораПоСтроке(ИмяЭУ = "ВидРасчета")

	Если ИмяЭУ = "ВидРасчета" Тогда
		МассивСпособовРасчета = Новый СписокЗначений;
		МассивСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку);
		МассивСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ДоплатаДоСреднегоЗаработка);
			
		МассивВидовВремени = Новый СписокЗначений;
	    Если СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены Тогда
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ДополнительноОплачиваемоеВПределахНормы);
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ОтработанноеВПределахНормы);
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ОтработанноеСверхНормы);
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЦелодневноеНеотработанное);
		Иначе
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеНеотработанное);
			МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеОтработанноеВПределахНормы);
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура("СпособРасчета,ВидВремени", МассивСпособовРасчета, МассивВидовВремени);
	ИначеЕсли ИмяЭУ = "РасчетСреднего" Тогда
		СтруктураПараметров = Новый Структура("Ссылка", ЭлементыФормы.РасчетСреднего.Колонки.ВидРасчета.ЭлементУправления.СписокВыбора);
	КонецЕсли;

	Возврат СтруктураПараметров;

КонецФункции // ПолучитьСтруктуруПараметровПодбораПоСтроке()

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.РасчетСреднего,ЭлементыФормы.КоманднаяПанельРасчетСреднего);
	СоответствиеТЧ.Вставить(ЭлементыФормы.Начисления,ЭлементыФормы.КоманднаяПанельНачисления);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры // УстановитьКнопкиПодменюЗаполненияТЧ()

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Ссылка, ПолучитьСтруктуруПечатныхФорм(), Новый Действие("ОсновныеДействияФормыПечать"), Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати    (мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов);
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры // УстановитьКнопкиПечати()

// Определяет параметры расчета, используемые при заполнении строк начислений
//
// Параметры:
//	Нет.
//
Процедура ПрочитатьСвойстваВидаОплаты()

	Если ВидРасчета.Пустая() Тогда
		мПериодРасчетаСреднегоЗаработка = 12;
		
	Иначе
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Ссылка",ВидРасчета);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ОсновныеНачисленияОрганизаций.ПорядокОпределенияРасчетногоПериодаСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ПорядокОпределенияРасчетногоПериодаСреднегоЗаработка.ПоКолдоговору)
		|			ТОГДА ОсновныеНачисленияОрганизаций.ПериодРасчетаСреднегоЗаработка
		|		ИНАЧЕ 12
		|	КОНЕЦ КАК ПериодРасчетаСреднегоЗаработка
		|ИЗ
		|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
		|ГДЕ
		|	ОсновныеНачисленияОрганизаций.Ссылка = &Ссылка";
		
		Данные = Запрос.Выполнить().Выбрать();
		Данные.Следующий();
		мПериодРасчетаСреднегоЗаработка = Данные.ПериодРасчетаСреднегоЗаработка;
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьСвойстваВидаОплаты()

// Определяет параметры расчета, используемые при заполнении строк начислений
//
// Параметры:
//	Нет.
//
Процедура ПрочитатьГрафикСотрудника()

	Если Сотрудник.Пустая() Тогда
		мСуммированныйУчетРабочегоВремени = Ложь;
	Иначе
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Сотрудник",Сотрудник);
		Запрос.УстановитьПараметр("ДатаНачалаСобытия",ДатаНачалаСобытия);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаНачалаСобытия
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ГрафикРаботыЗавершения.СуммированныйУчетРабочегоВремени
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ГрафикРаботы.СуммированныйУчетРабочегоВремени
		|	КОНЕЦ КАК СуммированныйУчетРабочегоВремени
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаНачалаСобытия, Сотрудник = &Сотрудник) КАК РаботникиОрганизацииСрезПоследних";
		
		Данные = Запрос.Выполнить().Выбрать();
		Если Данные.Следующий() Тогда
			мСуммированныйУчетРабочегоВремени = Данные.СуммированныйУчетРабочегоВремени;
		Иначе 	
			мСуммированныйУчетРабочегоВремени = Ложь;
		КонецЕсли;;
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьСвойстваВидаОплаты()

// Выполняет авторасчет реквизитов таблицы начислений
//
Процедура ВыполнитьАвторасчетРеквизитовСтрокиНачислений(ДанныеСтроки)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудник",			Сотрудник);
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаНачала);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
	|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, Сотрудник = &Сотрудник) КАК РаботникиОрганизацииСрезПоследних";
	
	// подразделние, график и вид учета времени
	ПрежниеДанные = Запрос.Выполнить().Выбрать();
	Если ПрежниеДанные.Следующий() тогда
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ПодразделениеОрганизации) Тогда
			ДанныеСтроки.ПодразделениеОрганизации =  ПрежниеДанные.ПодразделениеОрганизации;
		КонецЕсли;
	КонецЕсли;

	// Рассчитаем период расчета среднего заработка
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ПериодРасчетаСреднегоЗаработкаНачало) Тогда
		ДанныеСтроки.ПериодРасчетаСреднегоЗаработкаНачало = ПериодРасчетаСреднегоЗаработкаНачало;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ПериодРасчетаСреднегоЗаработкаОкончание) Тогда
		ДанныеСтроки.ПериодРасчетаСреднегоЗаработкаОкончание = ПериодРасчетаСреднегоЗаработкаОкончание;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьАвторасчетРеквизитовСтрокиНачислений()

// Управляет видимостью и доступностью элементов формы
//
Процедура ОбновитьДоступностьЭлементовФормы()
	
	ЭтоИсправление = ЗначениеЗаполнено(ПерерассчитываемыйДокумент);
	ДокументИсправлен = ЗначениеЗаполнено(мДокументИсправление);
	ДокументСторнирован = ЗначениеЗаполнено(мДокументСторнирование);
	
	ТолькоПросмотр = ДокументИсправлен Или ДокументСторнирован;
	РаботаСДиалогамиЗК.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);		
	
	ЭлементыФормы.Сотрудник.ТолькоПросмотр = ТолькоПросмотр Или ЭтоИсправление;
	ЭлементыФормы.Организация.ТолькоПросмотр = ТолькоПросмотр Или ЭтоИсправление;
	
	ЭлементыФормы.ПанельОткрытьИсправление.Свертка = ?(ДокументИсправлен Или ДокументСторнирован,РежимСверткиЭлементаУправления.Нет,РежимСверткиЭлементаУправления.Право);
	ЭлементыФормы.ПанельОткрытьИсходный.Свертка = ?(ЭтоИсправление И Не ДокументСторнирован,РежимСверткиЭлементаУправления.Нет,РежимСверткиЭлементаУправления.Право);
	
	ЭлементыФормы.НадписьОткрытьИсправление.Заголовок = ?(ДокументСторнирован,"Открыть сторно-документ","Открыть исправление");
	
	РаботаСДиалогамиЗК.УстановитьКнопкиИсправленияДокументаОдногоСотрудника(ПериодРегистрации, ДокументИсправлен, ДокументСторнирован, ЭлементыФормы.ДополнительныеДействия.Кнопки, мКнопкаИсправление, мКнопкаОтменаИсправление);
	
КонецПроцедуры // ОбновитьДоступностьЭлементовФормы()

// Определяет флажки-описатели состояния документа, формирует и показывает строку-описание
//
Процедура ОбновитьОписаниеСостоянияДокумента()

	// связанные документы
	ЭтоИсправление = ЗначениеЗаполнено(ПерерассчитываемыйДокумент);
	ДокументИсправлен = ЗначениеЗаполнено(мДокументИсправление);
	Сторнирован = ЗначениеЗаполнено(мДокументСторнирование);
	
	НеЗаполнен = Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Сотрудник) Или Не ЗначениеЗаполнено(ВидРасчета);
	НеЗаполнен = НеЗаполнен Или (Не ЗначениеЗаполнено(ДатаНачалаСобытия) и Не ЗначениеЗаполнено(ДатаНачала));
	
	ЗаполненСОшибками = ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания;
	ЗаполненСОшибками = ЗаполненСОшибками Или (СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены И Не ЗначениеЗаполнено(ДатаОкончания));
	ЗаполненСОшибками = ЗаполненСОшибками Или (СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЧастиСмены И Не ЗначениеЗаполнено(ОплачиватьЧасов));
	ЗаполненСОшибками = ЗаполненСОшибками Или (ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаНачалаСобытия) И ДатаНачалаСобытия > ДатаНачала);
	
	Рассчитан = (Начисления.Количество() > 0);
	
	ОписаниеСостоянияДокумента = РаботаСДиалогамиЗК.ПолучитьОписаниеРасчетногоДокумента(Не НеЗаполнен,ЗаполненСОшибками,Рассчитан,Проведен,ЭтоИсправление,ДокументИсправлен,Сторнирован); // поля структуры описания - ТекстСообщения и ВажностьСообщения
	РаботаСДиалогамиЗК.ПоказатьИнформациюОДокументе(ЭлементыФормы.НадписьПредупреждение,ЭлементыФормы.ПолеКартинкиПредупреждение, ОписаниеСостоянияДокумента.ТекстСообщения, ОписаниеСостоянияДокумента.ВажностьСообщения);

КонецПроцедуры

// Управляет видимость элементов формы, зависящих от Способа регистрации времени
Процедура ОбновитьВидимостьПоСпособуРегистрацииВремени()
	
	ЭтоЦелосменныйНевыход = СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
	ЭлементыФормы.Начисления.Колонки.ДатаОкончания.Видимость = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.Начисления.Колонки.ДатаОкончания.ИзменятьВидимость = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаНачала.ТолькоПросмотр = Не ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаНачала.АвтоОтметкаНезаполненного = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаНачала.ОтметкаНезаполненного = ЭтоЦелосменныйНевыход И Не ЗначениеЗаполнено(ДатаНачала);
	ЭлементыФормы.ДатаОкончания.ТолькоПросмотр = Не ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаОкончания.АвтоОтметкаНезаполненного = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаОкончания.ОтметкаНезаполненного = ЭтоЦелосменныйНевыход И Не ЗначениеЗаполнено(ДатаОкончания);
	ЭлементыФормы.ДатаНачалаВнутрисменная.ТолькоПросмотр = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаНачалаВнутрисменная.АвтоОтметкаНезаполненного = Не ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ДатаНачалаВнутрисменная.ОтметкаНезаполненного = Не ЭтоЦелосменныйНевыход И Не ЗначениеЗаполнено(ДатаНачала);
	ЭлементыФормы.ОплачиватьЧасов.ТолькоПросмотр = ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ОплачиватьЧасов.АвтоОтметкаНезаполненного = Не ЭтоЦелосменныйНевыход;
	ЭлементыФормы.ОплачиватьЧасов.ОтметкаНезаполненного = Не ЭтоЦелосменныйНевыход И Не ЗначениеЗаполнено(ОплачиватьЧасов);
	
КонецПроцедуры // ОбновитьВидимостьПоСпособуРегистрацииВремени()

Процедура ПриИзмененииПериодаРегистрации()
	
	РаботаСДиалогамиЗК.УстановитьКнопкиИсправленияДокументаОдногоСотрудника(ПериодРегистрации, ЗначениеЗаполнено(мДокументИсправление), ЗначениеЗаполнено(мДокументСторнирование), ЭлементыФормы.ДополнительныеДействия.Кнопки, мКнопкаИсправление, мКнопкаОтменаИсправление);
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДополнительныеДействияПриИзмененииПериодаРегистрации(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	ЭлементыФормы.НадписьОрганизация.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	ЭлементыФормы.Организация.Подсказка = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭлементыФормы.Начисления.Колонки.ПодразделениеОрганизации.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Подразделение организации");
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

Процедура ОбновитьИнформационнуюНадписьРазмерНачислено()

	НачисленияДокумента = Новый Соответствие;
	НачисленияДокумента.Вставить(ВидРасчета,"");

	СуммаПоДокументу = 0;
	СуммаСторно  = 0;
	СуммаДоначислено = 0;
	ОплаченоДней = 0;
	НормаДней = 0;
	ИнформационнаяНадпись = "";
	ИнформационнаяНадписьСторно = "";
	ИнформационнаяНадписьДоначислено = "";
	
	Если Начисления.Количество() = 0 и РасчетСреднего.Количество() = 0 Тогда
		ИнформационнаяНадпись = "";
	ИначеЕсли Начисления.Количество() = 0 Тогда	
		ИнформационнаяНадпись = "Сумма начислений не рассчитана"
	Иначе
		// табличные части не пустые, получим сумму и проверим наличие сторнирования
		// начислений прошлых периодов
		Для каждого СтрокаНачислений Из Начисления Цикл
			Если СтрокаНачислений.Сторно Тогда
				ИнформационнаяНадписьСторно = "Сторнированы начисления прошлых периодов";
				СуммаСторно = СуммаСторно + СтрокаНачислений.Результат;
				Продолжить;
			КонецЕсли;
			Если НачисленияДокумента[СтрокаНачислений.ВидРасчета] = Неопределено Тогда
				ИнформационнаяНадписьДоначислено = "Начисления прошлых периодов";
				СуммаДоначислено = СуммаДоначислено + СтрокаНачислений.Результат;
				Продолжить;
			КонецЕсли;
			СуммаПоДокументу = СуммаПоДокументу + СтрокаНачислений.Результат;
			ОплаченоДней = ОплаченоДней + СтрокаНачислений.ОплаченоДнейЧасов;
			НормаДней = НормаДней + СтрокаНачислений.НормаДней;
		КонецЦикла;
	КонецЕсли;	
	
	// информационная надпись на лицевой стороне
	ЭлементыФормы.ЗначениеРазмер.Заголовок = Формат(СуммаПоДокументу, "ЧЦ=12; ЧДЦ=2; ЧН=");
	ЭлементыФормы.ИнформационнаяНадписьРазмер.Заголовок = ""+ ИнформационнаяНадпись + ИнформационнаяНадписьСторно;
	
	Если СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены Тогда
		Если ОплаченоДней > НормаДней * 2 Тогда
			РазмерностьОплаченногоВремени = "часы"
		Иначе
			РазмерностьОплаченногоВремени = "дни"; 
		КонецЕсли;
	Иначе
		РазмерностьОплаченногоВремени = "часы"
	КонецЕсли;
	
	СтрокаРазмер = "Сумма: " + Формат(СуммаПоДокументу, "ЧЦ=12; ЧДЦ=2; ЧН=") + " руб."; 
	СтрокаРазмер = СтрокаРазмер + " Оплачено: " + ОплаченоДней + " " + ?(РазмерностьОплаченногоВремени = "дни",ОбщегоНазначения.ФормаМножественногоЧисла("день","дня","дней",ОплаченоДней),ОбщегоНазначения.ФормаМножественногоЧисла("час","часа","часов",ОплаченоДней));
	СтрокаРазмер = СтрокаРазмер + Символы.ПС + ИнформационнаяНадписьСторно + ?(ПустаяСтрока(ИнформационнаяНадписьСторно),"", ": " + Формат(СуммаСторно, "ЧЦ=12; ЧДЦ=2; ЧН=") + " руб.");
	СтрокаРазмер = СтрокаРазмер + " " + ИнформационнаяНадписьДоначислено + ?(ПустаяСтрока(ИнформационнаяНадписьДоначислено),"", ": " + Формат(СуммаДоначислено, "ЧЦ=12; ЧДЦ=2; ЧН=") + " руб.");
	ЭлементыФормы.НадписьОплата.Заголовок = СтрокаРазмер;

КонецПроцедуры

Процедура ОбновитьИнформационнуюНадписьСреднийЗаработок()
	
	Если мСуммированныйУчетРабочегоВремени Или СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЧастиСмены Тогда
		НадписьСреднийЗаработокТекст = "Средний часовой заработок:";
		ИспользоватьСреднеЧасовойЗаработок = Истина;
	Иначе
		НадписьСреднийЗаработокТекст = "Средний дневной заработок:";
		ИспользоватьСреднеЧасовойЗаработок = Ложь;
	КонецЕсли;
	
	// данные среднего заработка
	
	Если ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		
		ПериодРасчетаСреднегоЗаработкаТекст = "Расчетный период " + Символы.ПС + " с " + Формат(ПериодРасчетаСреднегоЗаработкаНачало,"ДФ=dd.MM.yyyy") +" по "+ Формат(ПериодРасчетаСреднегоЗаработкаОкончание,"ДФ=dd.MM.yyyy");
		ПериодРасчетаСреднегоЗаработкаТекст1 = "Расчетный период с " + Формат(ПериодРасчетаСреднегоЗаработкаНачало,"ДФ=dd.MM.yyyy") +" по "+ Формат(ПериодРасчетаСреднегоЗаработкаОкончание,"ДФ=dd.MM.yyyy");
		
		Если ЗначениеЗаполнено(ВидРасчета) Тогда
			СведениеОВидеРасчета = ПроведениеРасчетов.ПолучитьСведенияОВидеРасчета(мСведенияОВидахРасчета, ВидРасчета);
			МесяцевРасчетногоПериода = 0;
			ОбщегоНазначения.РазобратьРазностьДат(НачалоМесяца(ПериодРасчетаСреднегоЗаработкаОкончание), НачалоМесяца(ПериодРасчетаСреднегоЗаработкаНачало), , МесяцевРасчетногоПериода);
			Если ЗначениеЗаполнено(МесяцевРасчетногоПериода) Тогда
				МесяцевРасчетногоПериода = МесяцевРасчетногоПериода + 1;
			Иначе 
				МесяцевРасчетногоПериода = ?(СведениеОВидеРасчета.ПорядокОпределенияРасчетногоПериодаСреднегоЗаработка = Перечисления.ПорядокОпределенияРасчетногоПериодаСреднегоЗаработка.ПоКолдоговору,СведениеОВидеРасчета.ПериодРасчетаСреднегоЗаработка,12);
			КонецЕсли;
			СреднедневнойЗаработок = Формат(ОплатаПоСреднемуЗаработкуПереопределяемый.РасчетСреднегоЗаработка(ЭтотОбъект, ИспользоватьСреднеЧасовойЗаработок, МесяцевРасчетногоПериода, ПериодРасчетаСреднегоЗаработкаНачало, ПериодРасчетаСреднегоЗаработкаОкончание),  "ЧЦ=12; ЧДЦ=2; ЧН=");
		Иначе
			СреднедневнойЗаработок = "";
		КонецЕсли;
	Иначе
		ПериодРасчетаСреднегоЗаработкаТекст = ""; 
		ПериодРасчетаСреднегоЗаработкаТекст1 = ""; 
		СреднедневнойЗаработок = "";
	КонецЕсли;
	
	ЭлементыФормы.НадписьСреднийЗаработок.Заголовок = НадписьСреднийЗаработокТекст;
	ЭлементыФормы.ЗначениеСреднийЗаработок.Заголовок = СреднедневнойЗаработок;
	ЭлементыФормы.ИнформационнаяНадписьСреднийЗаработок.Заголовок = ПериодРасчетаСреднегоЗаработкаТекст;
	ЭлементыФормы.НадписьРасчетСреднегоЗаработка.Заголовок = НадписьСреднийЗаработокТекст + " " + СреднедневнойЗаработок + " руб." + Символы.ПС + ПериодРасчетаСреднегоЗаработкаТекст1;
	
КонецПроцедуры

Процедура ОчиститьНачисления()
	
	Если Начисления.Количество() > 0 Тогда
		Начисления.Очистить();
	КонецЕсли;
	
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДополнительныеДействияПриУдаленииНачислений(ЭтотОбъект, ЭтаФорма);
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

Процедура ОчиститьВсеРасчеты()

	ОчиститьНачисления();
	
	Если РасчетСреднего.Количество() > 0 Тогда
		РасчетСреднего.Очистить();
	КонецЕсли;

	ОбновитьИнформационнуюНадписьСреднийЗаработок();
	
КонецПроцедуры

Процедура РассчитатьНачисления()

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	МассивТаблиц = ОплатаПоСреднемуЗаработкуПереопределяемый.ПолучитьМассивТабличныхЧастей(ЭтотОбъект, "РасчетНачислений");
	ТекстВопроса1 = "Рассчитать документ можно только после отмены его проведения. Продолжить?";
	ТекстВопроса2 = "Перед расчетом таблица ""Оплата"" будет очищена. Продолжить?";
	Если НЕ РаботаСДиалогами.ЗаписатьДокументОчиститьТаблицыПередВыполнениемДействия(ДокументОбъект, ЭтаФорма, МассивТаблиц, ТекстВопроса1, ТекстВопроса2) Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	Для каждого ТабличнаяЧасть Из МассивТаблиц Цикл
		ТабличнаяЧасть.Очистить();
	КонецЦикла;
	
	мРассчитываемыеТаблицы.Начисления = Истина;
	мРассчитываемыеТаблицы.РасчетСреднего = Ложь;
	Рассчитать(мРассчитываемыеТаблицы);
	ОбработкаКомментариев.ПоказатьСообщения();
	
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДействияПередОткрытиемФормы(ЭтаФорма);
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	Если Не ЭтоНовый() Тогда
		мДокументИсправление = ПроведениеРасчетов.ПолучитьДокументИсправление(Ссылка,"ТаблицаДокумента");
		мДокументСторнирование = ПроведениеРасчетов.ПолучитьДокументСторнирование(Ссылка);
		ОбновитьОписаниеСостоянияДокумента();
	Иначе	
		РаботаСДиалогамиЗК.ПоказатьИнформациюОДокументе(ЭлементыФормы.НадписьПредупреждение,ЭлементыФормы.ПолеКартинкиПредупреждение, "Документ не заполнен", "СообщениеОПроблемах");
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументовПереопределяемый.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
		Если НЕ ЗначениеЗаполнено(ВидРасчета) и ПроцентОплаты = 0 Тогда
			ПроцентОплаты = 100
		КонецЕсли;
		Если СпособРегистрацииВремени.Пустая() Тогда
			СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
		КонецЕсли;
		
	Иначе
		
		// Установить доступность формы с учетом даты запрета редактирования	
		РаботаСДиалогамиЗК.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
		
	КонецЕсли;
	
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1,ЭлементыФормы.Номер);

	// Заполним реквизит формы МесяцСтрока.
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	
	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок = Новый Структура();

	СтруктураКолонок.Вставить("ВидРасчета");
	СтруктураКолонок.Вставить("ДатаНачала");
	СтруктураКолонок.Вставить("Результат");
	СтруктураКолонок.Вставить("НаименованиеПоказатель1");
	СтруктураКолонок.Вставить("НаименованиеПоказатель2");
	СтруктураКолонок.Вставить("НаименованиеПоказатель3");
	СтруктураКолонок.Вставить("НаименованиеПоказатель4");
	СтруктураКолонок.Вставить("НаименованиеПоказатель5");
	СтруктураКолонок.Вставить("НаименованиеПоказатель6");
	СтруктураКолонок.Вставить("Показатель1");
	СтруктураКолонок.Вставить("Показатель2");
	СтруктураКолонок.Вставить("Показатель3");
	СтруктураКолонок.Вставить("Показатель4");
	СтруктураКолонок.Вставить("Показатель5");
	СтруктураКолонок.Вставить("Показатель6");

	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Начисления.Колонки, СтруктураКолонок);
	
	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок = Новый Структура();

	СтруктураКолонок.Вставить("ВидРасчета");
	СтруктураКолонок.Вставить("БазовыйПериодНачало");
	СтруктураКолонок.Вставить("БазовыйПериодКонец");
	СтруктураКолонок.Вставить("КоэффициентИндексации");
	СтруктураКолонок.Вставить("Результат");

	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.РасчетСреднего.Колонки, СтруктураКолонок);

	СтруктураКолонок = Новый Структура();
	
	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;
	// Получим и запомним ссылку на головную организацию
	мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	// Запомнить текущий расчетный период
	мТекущийПериод = ПериодРегистрации;
	
	// запомним реквизиты в.р.
	ПрочитатьСвойстваВидаОплаты();
	ПрочитатьГрафикСотрудника();
	
	// Список видов записей расчета среднего
	// и видимость колонок
	ОплатаПоСреднемуЗаработкуПереопределяемый.НастроитьСпискиВыбораЭлементовУправления(ЭтотОбъект, ЭтаФорма);
	
	// Исправление документов
	мКнопкаИсправление = ЭлементыФормы.ДополнительныеДействия.Кнопки.Исправить;
	мКнопкаОтменаИсправление = ЭлементыФормы.ДополнительныеДействия.Кнопки.ОтменитьИсправление;
	ЭлементыФормы.ДополнительныеДействия.Кнопки.Очистить();
	
	// Установим доступность элементов формы 
	ОбновитьДоступностьЭлементовФормы();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДополнительныеДействияПриОткрытииФормы(ЭтотОбъект, ЭтаФорма);

	// Установим видимость в зависимости от значения способа регистрации времени
	ОбновитьВидимостьПоСпособуРегистрацииВремени();
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОбновитьИнформационнуюНадписьСреднийЗаработок();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);
	
	// Установить активный реквизит.
	Если Не РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма) Тогда
		ТекущийЭлемент = ЭлементыФормы.ДатаНачала;
	КонецЕсли;

	УстановитьЗаголовкиОрганизацийВФорме();

	// Установить настройку периода по умолчанию
	мНастройкаПериода = Новый НастройкаПериода;
	мНастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ОбработкаОповещения" формы
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьФорму" И Не ЭтоНовый() Тогда
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			
			Команда = "";
			Если Параметр.Свойство("Команда",Команда) Тогда  // Источник прислал структуру параметров с командой
				Если Команда = "ДоступностьИсправленногоДокумента" Тогда
					Если Параметр.Объект = Ссылка И мДокументИсправление <> Источник Тогда
						мДокументИсправление = Источник;
						ОбновитьДоступностьЭлементовФормы();
					КонецЕсли;
				ИначеЕсли Команда = "ДоступностьОтмененногоДокумента" Тогда
					Если Параметр.Объект = Ссылка Тогда
						Если мДокументСторнирование <> Источник Тогда
							мДокументСторнирование = Источник;
							ОбновитьДоступностьЭлементовФормы();
						КонецЕсли;
					ИначеЕсли ЗначениеЗаполнено(мДокументСторнирование) Тогда
						НовыйДокументСторнирование = ПроведениеРасчетов.ПолучитьДокументСторнирование(Ссылка);
						Если мДокументСторнирование <> НовыйДокументСторнирование Тогда
							мДокументСторнирование = НовыйДокументСторнирование;
							Если Не ЗначениеЗаполнено(мДокументСторнирование) Тогда
								РаботаСДиалогамиЗК.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
							КонецЕсли;
							ОбновитьДоступностьЭлементовФормы();
						КонецЕсли;
					КонецЕсли;
				Иначе
				КонецЕсли;
			Иначе
			КонецЕсли;
			
		Иначе
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события "ОбновлениеОтображения" формы
//
Процедура ОбновлениеОтображения()
	
	ОбновитьОписаниеСостоянияДокумента();
	
КонецПроцедуры // ОбновлениеОтображения()

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// оповестим исходный, исправляемый документ
	Если ЗначениеЗаполнено(ПерерассчитываемыйДокумент) Тогда
		Оповестить("ОбновитьФорму", Новый Структура("Команда, Объект","ДоступностьИсправленногоДокумента", ПерерассчитываемыйДокумент), Ссылка);
	КонецЕсли;

	// Вывести в заголовке формы статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	
КонецПроцедуры // ПослеЗаписи()

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	РаботаСДиалогамиЗК.ОбработатьЗакрытиеФормы(Отказ, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ДействияФормыДействиеОткрытьСвойства()

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ДействияФормыДействиеОткрытьКатегории()

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры // ДействияФормыСтруктураПодчиненностиДокумента()

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
			
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры // НажатиеНаДополнительнуюКнопкуЗаполненияТЧ()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры // ОсновныеДействияФормыПечатьПоУмолчанию()

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		УстановитьКнопкиПечати();
	КонецЕсли;
	
КонецПроцедуры // ОсновныеДействияФормыУстановитьПечатьПоУмолчанию()

// Процедура ввода документа-исправления
Процедура ДополнительныеДействияИсправить(Кнопка)

	РаботаСДиалогамиЗК.ВвестиДокументИсправление(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

// Процедура отмены исправлений
Процедура ДополнительныеДействияОтменитьИсправление(Кнопка)
	
	РаботаСДиалогамиЗК.ОтменитьИсправлениеДокумента(Ссылка);
	мДокументИсправление = ПроведениеРасчетов.ПолучитьДокументИсправление(Ссылка,"ТаблицаДокумента");
	ОбновитьДоступностьЭлементовФормы();
	ОбновитьОписаниеСостоянияДокумента();
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки настройки периода.
//
Процедура ВыбПериодНажатие(Элемент)
	
	мНастройкаПериода.УстановитьПериод(ПериодРасчетаСреднегоЗаработкаНачало, КонецДня(ПериодРасчетаСреднегоЗаработкаОкончание));
	
	Если мНастройкаПериода.Редактировать() Тогда
		ПериодРасчетаСреднегоЗаработкаНачало	= мНастройкаПериода.ПолучитьДатуНачала();
		ПериодРасчетаСреднегоЗаработкаОкончание	= мНастройкаПериода.ПолучитьДатуОкончания();
		ОчиститьВсеРасчеты();
	КонецЕсли;
	
КонецПроцедуры // ВыбПериодНажатие()

Процедура КнопкаРассчитатьВсеНажатие(Элемент)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();

	МассивТаблиц = ОплатаПоСреднемуЗаработкуПереопределяемый.ПолучитьМассивТабличныхЧастей(ЭтотОбъект, "ПолныйРасчет");
	ТекстВопроса1 = "Рассчитать документ можно только после отмены его проведения. Продолжить?";
	ТекстВопроса2 = "Перед расчетом все таблицы документа будут очищены. Продолжить?";
	Если НЕ РаботаСДиалогами.ЗаписатьДокументОчиститьТаблицыПередВыполнениемДействия(ДокументОбъект, ЭтаФорма, МассивТаблиц, ТекстВопроса1, ТекстВопроса2) Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	Для каждого ТабличнаяЧасть Из МассивТаблиц Цикл
		ТабличнаяЧасть.Очистить();
	КонецЦикла;
	
	мРассчитываемыеТаблицы.Начисления = Истина;
	мРассчитываемыеТаблицы.РасчетСреднего = Истина;
		
	Рассчитать(мРассчитываемыеТаблицы);
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОбновитьИнформационнуюНадписьСреднийЗаработок();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаРассчитатьНачисленияНажатие(Элемент)
	РассчитатьНачисления();
КонецПроцедуры

Процедура КоманднаяПанельНачисленияРассчитатьНачисления(Кнопка)
	РассчитатьНачисления()
КонецПроцедуры

Процедура КоманднаяПанельРасчетСреднегоРассчитатьСреднийЗаработок(Кнопка)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();

	МассивТаблиц = ОплатаПоСреднемуЗаработкуПереопределяемый.ПолучитьМассивТабличныхЧастей(ЭтотОбъект, "РасчетСреднего");
	ТекстВопроса1 = "Рассчитать документ можно только после отмены его проведения. Продолжить?";
	ТекстВопроса2 = "Перед расчетом все таблицы документа будут очищены. Продолжить?";
	Если НЕ РаботаСДиалогами.ЗаписатьДокументОчиститьТаблицыПередВыполнениемДействия(ДокументОбъект, ЭтаФорма, МассивТаблиц, ТекстВопроса1, ТекстВопроса2) Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	Для каждого ТабличнаяЧасть Из МассивТаблиц Цикл
		ТабличнаяЧасть.Очистить();
	КонецЦикла;
	
	мРассчитываемыеТаблицы.Начисления = Ложь;
	мРассчитываемыеТаблицы.РасчетСреднего = Истина;
	Рассчитать(мРассчитываемыеТаблицы);
	ОбработкаКомментариев.ПоказатьСообщения();
	
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОбновитьИнформационнуюНадписьСреднийЗаработок();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);

	мТекущаяДатаДокумента = Дата;

КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
	КонецЕсли;
	
	// Получим и запомним ссылку на головную организацию
	мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);

КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииПриИзменении(Элемент)
	
	РаботаСДиалогами.ДатаКакМесяцПодобратьДатуПоТексту(Элемент.Значение, ПериодРегистрации);
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	ПриИзмененииПериодаРегистрации();
	
КонецПроцедуры // ПериодРегистрацииПриИзменении()

// Процедура - обработчик события "Регулирование" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации, Направление);
	Элемент.Значение = РаботаСДиалогами.ДатаКакМесяцПредставление(ПериодРегистрации);
	ПриИзмененииПериодаРегистрации();
	
КонецПроцедуры // ПериодРегистрацииРегулирование()

// Процедура - обработчик события "Очистка" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // ПериодРегистрацииОчистка()

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	РаботаСДиалогами.НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, ПериодРегистрации, ЭтаФорма);
	ПриИзмененииПериодаРегистрации();
	
КонецПроцедуры // ПериодРегистрацииНачалоВыбораИзСписка()

// Процедура - обработчик события "АвтоПодборТекста" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	РаботаСДиалогами.ДатаКакМесяцАвтоПодборТекста(Текст, ТекстАвтоПодбора, СтандартнаяОбработка);
	
КонецПроцедуры // ПериодРегистрацииАвтоПодборТекста()

// Процедура - обработчик события "ОкончаниеВводаТекста" поля ввода периода регистрации.
//
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	РаботаСДиалогами.ДатаКакМесяцОкончаниеВводаТекста(Текст, Значение, СтандартнаяОбработка);
	
КонецПроцедуры // ПериодРегистрацииОкончаниеВводаТекста()

Процедура СотрудникПриИзменении(Элемент)
	Физлицо = Элемент.Значение.Физлицо;
	ПрочитатьГрафикСотрудника();
    ОчиститьВсеРасчеты();
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля ввода физлица
// переопеределим выбор физлица на выбор из списка регистра сведений
//
Процедура СотрудникНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ПроцедурыУправленияПерсоналом.ОткрытьФормуВыбораСотрудникаОрганизации(Элемент, Ссылка, Истина, Дата, мГоловнаяОрганизация, 2, СтандартнаяОбработка, Элемент.Значение);
	
КонецПроцедуры // СотрудникНачалоВыбора()

// Процедура - обработчик события "АвтоПодборТекста" поля ввода физлица
// переопеределим выбор физлица на выбор из списка регистра сведений
//
Процедура СотрудникАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	ТекстАвтоПодбора = ПроцедурыУправленияПерсоналомПереопределяемый.ПодобратьФИОСотрудника(СтандартнаяОбработка, 2, Текст, Организация);
	
КонецПроцедуры // СотрудникАвтоПодборТекста()

// Процедура - обработчик события "ОкончаниеВводаТекста" поля ввода физлица
// переопеределим выбор физлица на выбор из списка регистра сведений
//
Процедура СотрудникОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	Значение = ПроцедурыУправленияПерсоналомПереопределяемый.ПодобратьСписокСотрудников(СтандартнаяОбработка, 2, Текст, Организация);
	
КонецПроцедуры // СотрудникОкончаниеВводаТекста()

// Процедура - обработчик события "ПриИзменении" поля вида расчета
//
Процедура ВидРасчетаПриИзменении(Элемент)
	
	ПрочитатьСвойстваВидаОплаты();
	Если ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда 
		ПериодРасчетаСреднегоЗаработкаНачало = ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), - мПериодРасчетаСреднегоЗаработка);
		ПериодРасчетаСреднегоЗаработкаОкончание = НачалоМесяца(ДатаНачалаСобытия) - 1;
	КонецЕсли;
	ОчиститьВсеРасчеты();
	
КонецПроцедуры // ВидРасчетаПриИзменении()

// Процедура - обработчик события "НачалоВыбора" поля вида расчета
//
Процедура ВидРасчетаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	//Предложим для выбора список расчетов, имеющих способ расчета, соответствующий указанной форме оплаты
	ФормаВыбораВидаРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПолучитьФормуВыбора("ФормаВыбора", Элемент, "дляДокументаОплатаПоСреднемуЗаработку");
	
	ФормаВыбораВидаРасчета.НачальноеЗначениеВыбора = Элемент.Значение;
	
	МассивСпособовРасчета = Новый СписокЗначений;
	МассивСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку);
	МассивСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ДоплатаДоСреднегоЗаработка);
		
	МассивВидовВремени = Новый СписокЗначений;
    Если СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены Тогда
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ДополнительноОплачиваемоеВПределахНормы);
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ОтработанноеВПределахНормы);
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ОтработанноеСверхНормы);
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЦелодневноеНеотработанное);
	Иначе
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеНеотработанное);
		МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеОтработанноеВПределахНормы);
	КонецЕсли;
	
	МассивВидовРасчета = Новый СписокЗначений;
	МассивВидовРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПростойПоВинеРаботодателя);
	МассивВидовРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПочасовойПростойПоВинеРаботодателя);
	
	ФормаВыбораВидаРасчета.Отбор.СпособРасчета.ВидСравнения = ВидСравнения.ВСписке;
	ФормаВыбораВидаРасчета.Отбор.СпособРасчета.Значение = МассивСпособовРасчета;
	ФормаВыбораВидаРасчета.Отбор.СпособРасчета.Использование = Истина;

	ФормаВыбораВидаРасчета.Отбор.ВидВремени.ВидСравнения = ВидСравнения.ВСписке;
	ФормаВыбораВидаРасчета.Отбор.ВидВремени.Значение = МассивВидовВремени;
	ФормаВыбораВидаРасчета.Отбор.ВидВремени.Использование = Истина;

	ФормаВыбораВидаРасчета.Отбор.Ссылка.ВидСравнения = ВидСравнения.НеВСписке;
	ФормаВыбораВидаРасчета.Отбор.Ссылка.Значение = МассивВидовРасчета;
	ФормаВыбораВидаРасчета.Отбор.Ссылка.Использование = Истина;

	ФормаВыбораВидаРасчета.Открыть();
	
	СтандартнаяОбработка = Ложь
	
КонецПроцедуры // ВидРасчетаНачалоВыбора()

Процедура ВидРасчетаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке(), Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"));
	
КонецПроцедуры // ВидРасчетаАвтоПодборТекста()

Процедура ВидРасчетаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке(), ЭтаФорма, Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"), мОбработкаПодбораПоСтроке, мТекстПодбораПоСтроке, мПоследнееЗначениеЭлементаПодбораПоСтроке, Ложь);
	
КонецПроцедуры // ВидРасчетаОкончаниеВводаТекста()

Процедура ДатаНачалаСобытияПриИзменении(Элемент)
	
	// Список видов записей расчета среднего
	// и видимость колонок
	ОплатаПоСреднемуЗаработкуПереопределяемый.НастроитьСпискиВыбораЭлементовУправления(ЭтотОбъект, ЭтаФорма);
	
	Если ДатаНачала < ДатаНачалаСобытия Тогда
		ДатаНачала = ДатаНачалаСобытия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда 
		ПериодРасчетаСреднегоЗаработкаНачало = ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), - мПериодРасчетаСреднегоЗаработка);
		ПериодРасчетаСреднегоЗаработкаОкончание = НачалоМесяца(ДатаНачалаСобытия) - 1;
	Иначе
		ПериодРасчетаСреднегоЗаработкаНачало = "";
		ПериодРасчетаСреднегоЗаработкаОкончание = ""
	КонецЕсли;
	
	ОчиститьВсеРасчеты();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" даты начала события
//
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ОчиститьНачисления();
	
КонецПроцедуры // ДатаНачалаПриИзменении()

Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ОчиститьНачисления();
	
КонецПроцедуры

Процедура ОплачиватьЧасовПриИзменении(Элемент)
	
	ОчиститьНачисления();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" процента оплаты
//
Процедура ПроцентОплатыПриИзменении(Элемент)
	
	ОчиститьНачисления();
	
КонецПроцедуры // ПроцентОплатыПриИзменении()

Процедура ПроцентОплатыРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Если Направление = 1 Тогда // увеличиваем значение
		Элемент.Значение = Элемент.Значение + 50;
		
	Иначе // = -1 - уменьшаем значение
		Если Элемент.Значение > 50 Тогда
			Элемент.Значение = Элемент.Значение - 50;
			
		Иначе
			Предупреждение("Документом регистрируются только оплачиваемые начисления!");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОчиститьНачисления();
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // ПроцентОплатыРегулирование()

// Процедура - обработчик события "ПриИзменении" перечисления "СпособРегистрацииВремени"
Процедура СпособРегистрацииВремениПриИзменении(Элемент)
	
	ОбновитьВидимостьПоСпособуРегистрацииВремени();
	
КонецПроцедуры

Процедура ПериодРасчетаСреднегоЗаработкаНачалоПриИзменении(Элемент)
    ОчиститьВсеРасчеты();
КонецПроцедуры

Процедура ПериодРасчетаСреднегоЗаработкаОкончаниеПриИзменении(Элемент)
    ОчиститьВсеРасчеты();
КонецПроцедуры


Процедура НадписьРазмерНажатие(Элемент)
	
	ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Начисления;
	ТекущийЭлемент = ЭлементыФормы.Начисления;

КонецПроцедуры

Процедура НадписьСреднийЗаработокНажатие(Элемент)
	
	ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.РасчетСреднего;
	ТекущийЭлемент = ЭлементыФормы.РасчетСреднего;
	
КонецПроцедуры

Процедура ЗаголовокДополнительнойИнформационнойНадписиНажатие(Элемент)
	ОплатаПоСреднемуЗаработкуПереопределяемый.ПоказатьДополнительнуюФорму(ЭтотОбъект, ЭтаФорма)
КонецПроцедуры


Процедура НадписьОткрытьИсправлениеНажатие(Элемент)
	Если ЗначениеЗаполнено(мДокументИсправление) Тогда
		мДокументИсправление.ПолучитьФорму(, ЭтаФорма).Открыть();
	ИначеЕсли ЗначениеЗаполнено(мДокументСторнирование) Тогда
		мДокументСторнирование.ПолучитьФорму(, ЭтаФорма).Открыть();
	КонецЕсли;
КонецПроцедуры

Процедура НадписьОткрытьИсходныйНажатие(Элемент)
	Если ЗначениеЗаполнено(ПерерассчитываемыйДокумент) Тогда
		ПерерассчитываемыйДокумент.ПолучитьФорму(, ЭтаФорма).Открыть();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ Начисления

Процедура НачисленияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияНачисленияПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

Процедура ОбработчикОжиданияНачисленияПриАктивизацииСтроки()
	
	ДанныеСтроки = ЭлементыФормы.Начисления.ТекущаяСтрока;
	Если ДанныеСтроки <> Неопределено Тогда
		ДанныеСтрокиВидРасчета = ДанныеСтроки.ВидРасчета;
		Если ДанныеСтрокиВидРасчета = Неопределено Тогда
			ДанныеСтрокиВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	РаботаСДиалогамиЗК.УстановитьФорматЗначенийПоказателей(ЭлементыФормы.Начисления, мСведенияОВидахРасчетаПоказатели, , Истина);
	
	МожноРедактироватьСтроку = ДанныеСтроки = Неопределено Или Не ДанныеСтроки.Сторно Или ДанныеСтроки.СторнируемыйДокумент <> ПерерассчитываемыйДокумент Или Не ЗначениеЗаполнено(ПерерассчитываемыйДокумент);
	ЭлементыФормы.КоманднаяПанельНачисления.Кнопки.Действие2.Доступность = МожноРедактироватьСтроку;
	ЭлементыФормы.КоманднаяПанельНачисления.Кнопки.Действие1.Доступность = ЭлементыФормы.Начисления.ИзменятьСоставСтрок И МожноРедактироватьСтроку;
	ЭлементыФормы.КонтекстноеМенюНачисления.Кнопки.Действие2.Доступность = МожноРедактироватьСтроку;
	ЭлементыФормы.КонтекстноеМенюНачисления.Кнопки.Действие1.Доступность = ЭлементыФормы.Начисления.ИзменятьСоставСтрок И МожноРедактироватьСтроку;
	
КонецПроцедуры // ОбработчикОжиданияНачисленияПриАктивизацииСтроки

// Процедура - обработчик события "ПередНачаломДобавления" табличного поля "Начисления"
//
Процедура НачисленияПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если Копирование Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		Отказ = ДанныеСтроки.Сторно И ДанныеСтроки.СторнируемыйДокумент = ПерерассчитываемыйДокумент И ЗначениеЗаполнено(ПерерассчитываемыйДокумент);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПередНачаломИзменения" табличного поля "Начисления"
//
Процедура НачисленияПередНачаломИзменения(Элемент, Отказ)
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Отказ = ДанныеСтроки.Сторно И ДанныеСтроки.СторнируемыйДокумент = ПерерассчитываемыйДокумент И ЗначениеЗаполнено(ПерерассчитываемыйДокумент);
КонецПроцедуры

// Процедура - обработчик события "ПриНачалеРедактирования" строки таблицы
//
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока)
	
	Если НоваяСтрока Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		
		ДанныеСтроки.ВидРасчета	= ВидРасчета;
		ДанныеСтроки.Показатель1= ПроцентОплаты;
		
		ИндексСтроки = Начисления.Индекс(ДанныеСтроки);
		Если ИндексСтроки > 0  Тогда
			ДанныеСтроки.ДатаНачала = Начисления[ИндексСтроки-1].ДатаОкончания + мДлинаСуток;
		КонецЕсли;
		
		ДанныеСтроки.Авторасчет = Истина;
		
	КонецЕсли;
	
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДействияПриНачалеРедактированияНачисления(ЭтаФорма, Элемент, НоваяСтрока)
	
КонецПроцедуры // НачисленияПриНачалеРедактирования()

Процедура НачисленияПриПолученииДанных(Элемент, ОформленияСтрок)
	
	РаботаСДиалогамиЗК.ОбработатьОтображениеПоказателейДляРасчета(Элемент, ОформленияСтрок, мСведенияОВидахРасчетаПоказатели);
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		Ячейки = ОформлениеСтроки.Ячейки;
		
		// сторно запись - красным
		Если ДанныеСтроки.Сторно Тогда
			ОформлениеСтроки.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		Иначе 	
			Ячейки.СторнируемыйДокумент.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеСтроки.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ДополнительныеНачисленияОрганизаций") Тогда
			Ячейки.ДатаНачалаСобытия.ТолькоПросмотр = Истина;
			Ячейки.ДатаНачалаСобытия.ОтметкаНезаполненного = Ложь;
		Иначе
			Ячейки.ЧислоМесяцев.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		ЯчейкаСКартинкой = Ячейки.Картинка;
		ЯчейкаСКартинкой.ОтображатьКартинку = Истина;
		ЯчейкаСКартинкой.ИндексКартинки = Число(ДанныеСтроки.Авторасчет);	
		
	КонецЦикла;	
	
КонецПроцедуры

// Процедура - обработчик события "ПриОкончанииРедактирования" строки таблицы
//
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки.Сторно ИЛИ ДанныеСтроки.ВидРасчета <> ВидРасчета Тогда
		Возврат;
	КонецЕсли;
	
	// Авторасчет незаполненных реквизитов строки
	ВыполнитьАвторасчетРеквизитовСтрокиНачислений(ДанныеСтроки);
	
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДействияПослеРедактированияНачисления(ЭтаФорма, ДанныеСтроки);
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);
	
	Если СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЧастиСмены Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСтроки.ДатаНачала) И ЗначениеЗаполнено(ДанныеСтроки.ДатаОкончания) Тогда
		// Разбивка строк на помесячные
		РазницаВМесяцах = (Год(ДанныеСтроки.ДатаОкончания)*12 + Месяц(ДанныеСтроки.ДатаОкончания)) - (Год(ДанныеСтроки.ДатаНачала)*12 + Месяц(ДанныеСтроки.ДатаНачала));
		Если РазницаВМесяцах > 0 Тогда
			ТекстВопроса = "Разбить строку начислений на помесячные записи?";
			Ответ  = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ПроведениеРасчетов.РазбитьСтрокуНачисленийНаПомесячныеЗаписи(ДанныеСтроки, Начисления);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // НачисленияПриОкончанииРедактирования()

Процедура НачисленияПередУдалением(Элемент, Отказ)
	
	ОбновитьИнформационнуюНадписьРазмерНачислено();
	ОплатаПоСреднемуЗаработкуПереопределяемый.ОбновитьДополнительнуюИнформационнуюНадпись(ЭтотОбъект, ЭтаФорма);
	ОплатаПоСреднемуЗаработкуПереопределяемый.ВыполнитьДействияПередУдалениемНачисления(Элемент, ЭтотОбъект, Отказ)
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода даты начала
//
Процедура НачисленияДатаНачалаПриИзменении(Элемент)
	
	Если ТипЗнч(ЭлементыФормы.Начисления.ТекущиеДанные.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ДополнительныеНачисленияОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	ЭлементыФормы.Начисления.ТекущиеДанные.ДатаНачалаСобытия = Элемент.Значение;
	
КонецПроцедуры // НачисленияДатаНачалаПриИзменении()

Процедура НачисленияСторнируемыйДокументНачалоВыбора(Элемент, СтандартнаяОбработка)
		
	Если Организация.Пустая() Тогда
		Предупреждение(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не выбрана организация!"), 10);
		Возврат;
	КонецЕсли;
		
	РаботаСДиалогамиЗК.ОбработкаНачалоВыбораСторнируемогоДокумента(Элемент, ЭтаФорма, Ссылка, Организация, СтандартнаяОбработка, Истина);
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ РасчетСреднего

// Процедура - обработчик события "ПриНачалеРедактирования" строки таблицы
//
Процедура РасчетСреднегоПриНачалеРедактирования(Элемент, НоваяСтрока)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КоэффициентИндексации = 1;
	КонецЕсли;
	
КонецПроцедуры // РасчетСреднегоПриНачалеРедактирования()

Процедура РасчетСреднегоПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Не ОтменаРедактирования Тогда
		ОбновитьИнформационнуюНадписьСреднийЗаработок();
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСреднегоПослеУдаления(Элемент)
	ОбновитьИнформационнуюНадписьСреднийЗаработок();
КонецПроцедуры

Процедура РасчетСреднегоПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ЭлементыФормы.РасчетСреднего.Колонки.КоэффициентИндексации.Видимость Тогда
		СсылкаВР = ДанныеСтроки.ВидРасчета;
		ОформлениеСтроки.Ячейки.КоэффициентИндексации.ТолькоПросмотр = (СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые 
			Или СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремииНеИндексируемые 
			Или СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремииНеИндексируемые);
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСреднегоВидРасчетаПриИзменении(Элемент)
	СсылкаВР = Элемент.Значение;
	Если СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые 
			Или СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремииНеИндексируемые 
			Или СсылкаВР = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремииНеИндексируемые Тогда
		ЭлементыФормы.РасчетСреднего.ТекущиеДанные.КоэффициентИндексации = 1;
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСреднегоВидРасчетаНачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокВыбора = Элемент.СписокВыбора;
	ЭлементСписка = ВыбратьИзСписка(СписокВыбора,Элемент,СписокВыбора.НайтиПоЗначению(Элемент.Значение));
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСреднегоВидРасчетаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке("РасчетСреднего"), Тип("ПланВидовРасчетаСсылка.СреднийЗаработок"));
	
КонецПроцедуры

Процедура РасчетСреднегоВидРасчетаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, ПолучитьСтруктуруПараметровПодбораПоСтроке("РасчетСреднего"), ЭтаФорма, Тип("ПланВидовРасчетаСсылка.СреднийЗаработок"), мОбработкаПодбораПоСтроке, мТекстПодбораПоСтроке, мПоследнееЗначениеЭлементаПодбораПоСтроке, Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток 		= 86400; // в секундах
мРассчитываемыеТаблицы = Новый Структура("Начисления,РасчетСреднего");

мОбработкаПодбораПоСтроке = Ложь;
мТекстПодбораПоСтроке = "";
мПоследнееЗначениеЭлементаПодбораПоСтроке = Неопределено;

мСведенияОВидахРасчетаПоказатели = Новый Соответствие;
мСведенияОВидахРасчета = Новый Соответствие;

мДополнительныеСвойства = Новый Структура()