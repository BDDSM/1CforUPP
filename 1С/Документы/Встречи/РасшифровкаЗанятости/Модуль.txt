////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мРасшифровка Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ОбновитьЗанятость()
	
	ДокументЗанятости = мРасшифровка.Документ;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ",	мРасшифровка.Документ);
	
	Если мРасшифровка.Вид = "Помещение" Тогда
		Заголовок			= "Расшифровка занятости помещения: " + мРасшифровка.Объект;
		Наименование		= "Помещение: " + мРасшифровка.Объект;
		ОписаниеЗанятости	= "Помещение занято с " + мРасшифровка.ДатаНачала + " по " + мРасшифровка.ДатаОкончания;
		
		ЭлементыФормы.НадписьТелефоны.Значение	= "Телефоны ответственного";
		
		Телефоны = "";
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Док.Ответственный КАК Ответственный
		|ИЗ
		|	Документ." + мРасшифровка.Документ.Метаданные().Имя + " КАК Док
		|ГДЕ
		|	Док.Ссылка = &Документ";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Запрос.УстановитьПараметр("Ответственный",	Выборка.Ответственный);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КонтактнаяИнформация.Вид КАК ВидТелефона,
			|	КонтактнаяИнформация.Представление КАК Телефон
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|		ПО Пользователи.ФизЛицо = КонтактнаяИнформация.Объект
			|			И (КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
			|ГДЕ
			|	Пользователи.Ссылка = &Ответственный";
			ВыборкаКИ = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаКИ.Следующий() Цикл
				Если Не ПустаяСтрока(Телефоны) Тогда
					Телефоны = Телефоны + ", ";
				КонецЕсли;
				Телефоны = Телефоны + ВыборкаКИ.Телефон;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		Заголовок		= "Расшифровка занятости сотрудника: " + мРасшифровка.Объект;
		Наименование	= мРасшифровка.Объект;
		ОписаниеЗанятости	= "Сотрудник занят с " + мРасшифровка.ДатаНачала + " по " + мРасшифровка.ДатаОкончания;
		
		ЭлементыФормы.НадписьТелефоны.Значение	= "Телефоны сотрудника";
		
		Телефоны = "";
		
		Запрос.УстановитьПараметр("Физлицо",	мРасшифровка.Объект);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Вид КАК ВидТелефона,
		|	КонтактнаяИнформация.Представление КАК Телефон
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &Физлицо
		|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)";
		ВыборкаКИ = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаКИ.Следующий() Цикл
			Если Не ПустаяСтрока(Телефоны) Тогда
				Телефоны = Телефоны + ", ";
			КонецЕсли;
			Телефоны = Телефоны + ВыборкаКИ.Телефон;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ОбновитьЗанятость();
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	ОбновитьЗанятость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыОткрытьДокумент(Кнопка)
	
	мРасшифровка.Документ.ПолучитьФорму().Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ