////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция УстановитьЗакладкуПоТипуВопроса(Направление = 1)
	
	// установим страницу по типу значения
	Если Вопрос.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета Тогда
		ИмяЗакладки = "НесколькоВариантовОтвета";
	ИначеЕсли Вопрос.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
		ИмяЗакладки = "Табличный";
	КонецЕсли;
	
	ЭлементыФормы.ПанельТиповВопросов.ТекущаяСтраница = ЭлементыФормы.ПанельТиповВопросов.Страницы[ИмяЗакладки];
	
	ФормулировкаВопроса = Вопрос.ПолнаяФормулировка;
	
	// Заполним реквизиты в зависимости от закладки
	Если ИмяЗакладки = "НесколькоВариантовОтвета" Тогда
		НесколькоВариантовОтветов.Очистить();
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВариантыОтветовОпросов.Ссылка
		|ИЗ
		|	Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
		|ГДЕ
		|	ВариантыОтветовОпросов.Владелец = &Вопрос";
		
		Запрос.УстановитьПараметр("Вопрос",Вопрос);
		
		Варианты = Запрос.Выполнить().Выбрать();
		
		НомерСтрокиТаблицы = 0;
		Пока Варианты.Следующий() Цикл
			НомерСтрокиТаблицы = НомерСтрокиТаблицы + 1;
			
			НоваяСтрока = НесколькоВариантовОтветов.Добавить();
			НоваяСтрока.НомерСтроки		= НомерСтрокиТаблицы;
			НоваяСтрока.ВариантОтвета	= Варианты.Ссылка;
		КонецЦикла;
		
		СтруктураПоиска = Новый Структура("ВопросВладелец");
		СтруктураПоиска.Вставить("ВопросВладелец", Вопрос); 
		
		СтрокиПоиска = СоставнойОтвет.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаПоиска из СтрокиПоиска Цикл
			СтрокаОтвета = НесколькоВариантовОтветов.Найти(СтрокаПоиска.ТиповойОтвет);
			Если СтрокаОтвета <> Неопределено Тогда
				СтрокаОтвета.Выбор = Истина;
				СтрокаОтвета.Ответ = СтрокаПоиска.Ответ;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяЗакладки = "Табличный" Тогда
		ЭлементыФормы.ТабличноеПолеСоставногоОтвета.ИзменятьПорядокСтрок 	= Ложь;
		ЭлементыФормы.ТабличноеПолеСоставногоОтвета.ИзменятьСоставСтрок 	= Ложь;
		
		КолонкиТабличногоВопроса = Вопрос.КолонкиТаблицы.Выгрузить();
		
		ТаблицаСоставногоОтвета.Колонки.Очистить();
		
		ТаблицаСоставногоОтвета.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"), "№");
		Для Каждого КолонкаТабличногоВопроса Из КолонкиТабличногоВопроса Цикл
			ТаблицаСоставногоОтвета.Колонки.Добавить("Колонка"+КолонкаТабличногоВопроса.НомерСтроки, КолонкаТабличногоВопроса.КолонкаТаблицы.ТипЗначения, КолонкаТабличногоВопроса.КолонкаТаблицы.ПолнаяФормулировка, 10);
		КонецЦикла;
		ЭлементыФормы.ТабличноеПолеСоставногоОтвета.СоздатьКолонки();
		
		ИндексКолонки = 0;
		Для Каждого КолонкаТаблицы Из ЭлементыФормы.ТабличноеПолеСоставногоОтвета.Колонки Цикл
			Если КолонкаТаблицы.Имя = "НомерСтроки" Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(КолонкиТабличногоВопроса[ИндексКолонки].КолонкаТаблицы.ТипЗначения) = Тип("ПланВидовХарактеристикСсылка.ВопросыДляАнкетирования") Тогда
				КолонкаТаблицы.ЭлементУправления.ВыборПоВладельцу = КолонкиТабличногоВопроса[ИндексКолонки].КолонкаТаблицы;
			КонецЕсли;
			
			ИндексКолонки = ИндексКолонки + 1;
		КонецЦикла;
		
		Для ИндексНомераСтроки = 1 по Вопрос.КоличествоСтрокТаблицы Цикл
			
			ПараметрыПоиска = Новый Структура("ВопросВладелец, НомерСтрокиВТаблице");
			ПараметрыПоиска.ВопросВладелец		= Вопрос;
			ПараметрыПоиска.НомерСтрокиВТаблице	= ИндексНомераСтроки;
			
			ОтветНаТабличныйВопрос = СоставнойОтвет.НайтиСтроки(ПараметрыПоиска);
			
			СтрокаОтвет = ТаблицаСоставногоОтвета.Добавить();
			СтрокаОтвет.НомерСтроки = ИндексНомераСтроки;
			
			Для Каждого КолонкаОтвет Из ОтветНаТабличныйВопрос Цикл
				СтрокаОтвет[КолонкиТабличногоВопроса.Найти(КолонкаОтвет.Вопрос).НомерСтроки] = КолонкаОтвет.ТиповойОтвет;
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура АктуализироватьТЧСоставногоОтвета(Элемент,Данные)
	
	Если Данные.Количество() = 0 тогда
		
		СтруктураПоиска = Новый Структура("ВопросВладелец");
		СтруктураПоиска.Вставить("ВопросВладелец", Вопрос); 
		
		СтрокиКУдалению = СоставнойОтвет.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаДляУдаления из СтрокиКУдалению Цикл
			СоставнойОтвет.Удалить(СтрокаДляУдаления);
		КонецЦикла;
		
	Иначе
		
		// здесь при редактировании составного ответа удаляем все строки соответствуюшие вопросу владельцу
		// и заново их добавляем
		// для табличного вопроса удаляем все ответы соответствующие данной строке таблицы и заново их добавляем
		// связано с тем, что у табличного вопроса строки фиксированы, а у составного их может быть в общем случае
		// столько, сколько у него вариантов ответа
		
		СтруктураПоиска = Новый Структура("ВопросВладелец");
		СтруктураПоиска.Вставить("ВопросВладелец",Вопрос); 
		
		СтрокиКУдалению = СоставнойОтвет.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаДляУдаления из СтрокиКУдалению Цикл
			СоставнойОтвет.Удалить(СтрокаДляУдаления);
		КонецЦикла;
		
		Если Вопрос.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета тогда
			
			НомерСтрокиОтвета = 0;
			
			Для Каждого СтрокаОтвета Из Данные Цикл
				
				Если НЕ СтрокаОтвета.Выбор Тогда Продолжить; КонецЕсли;
				
				НомерСтрокиОтвета = НомерСтрокиОтвета + 1;
				
				НоваяСтрокаОтвета						= СоставнойОтвет.Добавить();
				НоваяСтрокаОтвета.Вопрос				= Вопрос;
				НоваяСтрокаОтвета.ВопросВладелец		= Вопрос;
				НоваяСтрокаОтвета.НомерСтрокиВТаблице	= НомерСтрокиОтвета;
				НоваяСтрокаОтвета.ТиповойОтвет			= СтрокаОтвета.ВариантОтвета;
				НоваяСтрокаОтвета.Ответ					= СтрокаОтвета.Ответ;
			КонецЦикла;
			
		Иначе
			Для Каждого СтрокаОтвета Из Данные Цикл
				Для Каждого КолонкаОтвета Из Вопрос.КолонкиТаблицы Цикл
					НоваяСтрокаОтвета						= СоставнойОтвет.Добавить();
					НоваяСтрокаОтвета.Вопрос				= КолонкаОтвета.КолонкаТаблицы;
					НоваяСтрокаОтвета.ВопросВладелец		= Вопрос;
					НоваяСтрокаОтвета.НомерСтрокиВТаблице	= СтрокаОтвета.НомерСтроки;
					НоваяСтрокаОтвета.ТиповойОтвет			= СтрокаОтвета["Колонка"+КолонкаОтвета.НомерСтроки];
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочестьТиповойОтвет()
	
	ИмяТекущейЗакладки = ЭлементыФормы.ПанельТиповВопросов.ТекущаяСтраница.Имя;
	
	Если ИмяТекущейЗакладки = "НесколькоВариантовОтвета" Тогда
		АктуализироватьТЧСоставногоОтвета(ЭлементыФормы.НесколькоВариантовОтветов,НесколькоВариантовОтветов);
		
	ИначеЕсли ИмяТекущейЗакладки = "Табличный" Тогда
		АктуализироватьТЧСоставногоОтвета(ЭлементыФормы.ТабличноеПолеСоставногоОтвета,ТаблицаСоставногоОтвета);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Вопрос) Тогда
		
		Если Вопросы.Количество() = 0 Тогда
			Отказ = Истина;
			Возврат;
		Иначе
			Вопрос = Вопросы[0].Вопрос;
		КонецЕсли;
		
	КонецЕсли;
	
	Отказ = НЕ УстановитьЗакладкуПоТипуВопроса();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КоманднаяПанельФормыОК(Кнопка)
	
	СтрокаДокумента = Вопросы.Найти(Вопрос);
	
	Если СтрокаДокумента <> Неопределено Тогда
		ПрочестьТиповойОтвет();
		СтрокаДокумента.Ответ			= "";
		Если Вопрос.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета Тогда
			Для Каждого ОтветНаВопрос Из НесколькоВариантовОтветов Цикл
				Если Не ОтветНаВопрос.Выбор Тогда
					Продолжить;
				КонецЕсли;
				Если Не ПустаяСтрока(СтрокаДокумента.Ответ) Тогда
					СтрокаДокумента.Ответ = СтрокаДокумента.Ответ + "; ";
				КонецЕсли;
				СтрокаДокумента.Ответ = СтрокаДокумента.Ответ + ОтветНаВопрос.ВариантОтвета;
			КонецЦикла;
		ИначеЕсли Вопрос.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
			Если Вопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТЗнаниеЯзыков Тогда
				Ответ = "";
				Для Каждого ОтветНаВопрос Из ТаблицаСоставногоОтвета Цикл
					ОтветСтроки = "";
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка1) Тогда // Язык
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка1;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка2) Тогда // Степень знания языка
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка2;
					КонецЕсли;
					
					Если Не ПустаяСтрока(ОтветСтроки) Тогда
						Если Не ПустаяСтрока(Ответ) Тогда
							Ответ = Ответ + "; ";
						КонецЕсли;
						Ответ = Ответ + ОтветСтроки;
					КонецЕсли;
				КонецЦикла;
				СтрокаДокумента.Ответ = Ответ;
				
			ИначеЕсли Вопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТОбразование Тогда
				Ответ = "";
				Для Каждого ОтветНаВопрос Из ТаблицаСоставногоОтвета Цикл
					ОтветСтроки = "";
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка2) Тогда // Учебное заведение
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка2;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка5) Тогда // Год окончания
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка5;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка1) Тогда // Образование
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка1;
					КонецЕсли;
					
					Если Не ПустаяСтрока(ОтветСтроки) Тогда
						Если Не ПустаяСтрока(Ответ) Тогда
							Ответ = Ответ + "; ";
						КонецЕсли;
						Ответ = Ответ + ОтветСтроки;
					КонецЕсли;
				КонецЦикла;
				СтрокаДокумента.Ответ = Ответ;
				
			ИначеЕсли Вопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТСоставСемьи Тогда
				Ответ = "";
				Для Каждого ОтветНаВопрос Из ТаблицаСоставногоОтвета Цикл
					ОтветСтроки = "";
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка1) Тогда // Степень родства
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка1;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка2) Тогда // Имя
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка2;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка3) Тогда // Год рождения
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка3;
					КонецЕсли;
					
					Если Не ПустаяСтрока(ОтветСтроки) Тогда
						Если Не ПустаяСтрока(Ответ) Тогда
							Ответ = Ответ + "; ";
						КонецЕсли;
						Ответ = Ответ + ОтветСтроки;
					КонецЕсли;
				КонецЦикла;
				СтрокаДокумента.Ответ = Ответ;
				
			ИначеЕсли Вопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТТрудоваяДеятельность Тогда
				Ответ = "";
				Для Каждого ОтветНаВопрос Из ТаблицаСоставногоОтвета Цикл
					ОтветСтроки = "";
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка1) Тогда // Организация
						ОтветСтроки = ОтветСтроки + ОтветНаВопрос.Колонка1;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОтветНаВопрос.Колонка2) И ЗначениеЗаполнено(ОтветНаВопрос.Колонка3) Тогда // Период
						Если Не ПустаяСтрока(ОтветСтроки) Тогда
							ОтветСтроки = ОтветСтроки + ", ";
						КонецЕсли;
						ОтветСтроки = ОтветСтроки + Формат(ОтветНаВопрос.Колонка2, "ДЛФ=D") + " - " + Формат(ОтветНаВопрос.Колонка3, "ДЛФ=D");
					КонецЕсли;
					
					Если Не ПустаяСтрока(ОтветСтроки) Тогда
						Если Не ПустаяСтрока(Ответ) Тогда
							Ответ = Ответ + "; ";
						КонецЕсли;
						Ответ = Ответ + ОтветСтроки;
					КонецЕсли;
				КонецЦикла;
				СтрокаДокумента.Ответ = Ответ;
				
			Иначе
				КоличествоЗаполненныхСтрок = 0;
				Для Каждого ОтветНаВопрос Из ТаблицаСоставногоОтвета Цикл
					СтрокаЗаполнена = Ложь;
					Для Каждого Колонка Из Вопрос.КолонкиТаблицы Цикл
						Если ЗначениеЗаполнено(ОтветНаВопрос["Колонка" + Колонка.НомерСтроки]) Тогда
							СтрокаЗаполнена = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если СтрокаЗаполнена Тогда
						КоличествоЗаполненныхСтрок = КоличествоЗаполненныхСтрок + 1;
					КонецЕсли;
				КонецЦикла;
				Если КоличествоЗаполненныхСтрок > 0 Тогда
					СтрокаДокумента.Ответ = ОбщегоНазначения.ФормаМножественногоЧисла("Заполнена ","Заполнено ","Заполнено ",КоличествоЗаполненныхСтрок) + КоличествоЗаполненныхСтрок + " " + ОбщегоНазначения.ФормаМножественногоЧисла("строка","строки","строк",КоличествоЗаполненныхСтрок);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		Модифицированность				= Ложь;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура НесколькоВариантовОтветовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элемент.Колонки.Ответ.ЭлементУправления.Доступность = Элемент.ТекущиеДанные.ВариантОтвета.ТребуетРазвернутыйОтвет
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ