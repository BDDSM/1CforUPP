Перем СохраненныеПометки;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УправлениеДоступностью ()
	
	Если ЭлементыФормы.СписокДокументов.ТекущаяСтрока = Неопределено Тогда	
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСдвинутьВверх.Доступность = Ложь;
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСдвинутьВниз.Доступность = Ложь;
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСнятьПометки.Доступность = Ложь;
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеУстановитьПометки.Доступность = Ложь;
	Иначе
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСнятьПометки.Доступность = Истина;
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеУстановитьПометки.Доступность = Истина;
		
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСдвинутьВверх.Доступность = СписокДокументов.Индекс(ЭлементыФормы.СписокДокументов.ТекущаяСтрока) > 0;
		ЭлементыФормы.ДействияФормы.Кнопки.ДействиеСдвинутьВниз.Доступность = СписокДокументов.Индекс(ЭлементыФормы.СписокДокументов.ТекущаяСтрока) < СписокДокументов.Количество() - 1;
	КонецЕсли; 
	
КонецПроцедуры // УправлениеДоступностью()

Процедура Пометить (Пометка)

	СписокДокументов.ЗаполнитьПометки(Пометка);	

КонецПроцедуры // Пометить()

Процедура Сдвинуть (Смещение)

	Если ЭлементыФормы.СписокДокументов.ТекущаяСтрока <> Неопределено Тогда
		НовыйИндекс = СписокДокументов.Индекс(ЭлементыФормы.СписокДокументов.ТекущаяСтрока) + Смещение;
		Если НовыйИндекс < 0 
			ИЛИ НовыйИндекс > СписокДокументов.Количество() - 1 Тогда
			
			Возврат;
		КонецЕсли; 
		СписокДокументов.Сдвинуть(ЭлементыФормы.СписокДокументов.ТекущаяСтрока, Смещение);
		
		УправлениеДоступностью();
	КонецЕсли; 

КонецПроцедуры // Сдвинуть()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НачальноеЗначениеВыбора = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокДокументов = НачальноеЗначениеВыбора.СписокДокументов.Скопировать();
	
	СохраненныеПометки = ВосстановитьЗначение("ФормаНастройкиРежимаЗаполнитьИПровести_Пометки");
	
	СписокДокументов.ЗаполнитьПометки(Истина);
	Если СохраненныеПометки <> Неопределено Тогда
		Для каждого СтрокаСписка Из СохраненныеПометки Цикл
			СтрокаСпискаФормы = СписокДокументов.НайтиПоЗначению(СтрокаСписка.Значение); 
			Если СтрокаСпискаФормы <> Неопределено Тогда
				СтрокаСпискаФормы.Пометка = СтрокаСписка.Пометка;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УправлениеДоступностью();
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	Если СохраненныеПометки = Неопределено
		ИЛИ ТипЗнч(СохраненныеПометки) <> Тип("СписокЗначений") Тогда
		СохраненныеПометки = СписокДокументов.Скопировать();
	Иначе
		Для каждого СтрокаСписка Из СписокДокументов Цикл
			ЭлементСохраненныеПометки = СохраненныеПометки.НайтиПоЗначению(СтрокаСписка.Значение); 
			Если ЭлементСохраненныеПометки = Неопределено Тогда
				ЭлементСохраненныеПометки = СохраненныеПометки.Добавить();
			КонецЕсли;
			ЭлементСохраненныеПометки.Пометка = СтрокаСписка.Пометка;
		КонецЦикла; 
	КонецЕсли; 
	
	СохранитьЗначение("ФормаНастройкиРежимаЗаполнитьИПровести_Пометки", СохраненныеПометки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыДействиеЗаполнитьИПровести(Кнопка)
	
	Для каждого СтрокаСписка Из СписокДокументов Цикл
	
		Если СтрокаСписка.Пометка Тогда
			СписокВыбранныхДокументов.Добавить(СтрокаСписка.Значение);
		КонецЕсли;
	
	КонецЦикла; 
	
	Закрыть();
	
КонецПроцедуры

Процедура ДействияФормыДействиеУстановитьПометки(Кнопка)
	
	Пометить (Истина);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСнятьПометки(Кнопка)
	
	Пометить (Ложь);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСдвинутьВверх(Кнопка)
	
	Сдвинуть(-1);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСдвинутьВниз(Кнопка)
	
	Сдвинуть(1);
	
КонецПроцедуры

Процедура СписокДокументовПриАктивизацииСтроки(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры