Перем мТекущаяДатаДокумента; // Хранит последнюю установленную дату документа - для проверки перехода документа в другой период

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.Продукция,ЭлементыФормы.КоманднаяПанельПродукция);
	СоответствиеТЧ.Вставить(ЭлементыФормы.ИсходныеКомплектующие,ЭлементыФормы.КоманднаяПанельКомплектующие);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры // УстановитьКнопкиПодменюЗаполненияТЧ()

Процедура ОбновитьНадписьВалютаЦены()
	ВалютаЦены = "";
	Если ЗначениеЗаполнено(ТипЦен.ВалютаЦены) Тогда
		ВалютаЦены = "(" + ТипЦен.ВалютаЦены + ")";
	КонецЕсли;
	
	ЭлементыФормы.Продукция.Колонки.Себестоимость.ТекстШапки = "Себестоимость " + ВалютаЦены;
	ЭлементыФормы.ИсходныеКомплектующие.Колонки.Цена.ТекстШапки = "Цена " + ВалютаЦены;
	ЭлементыФормы.ИсходныеКомплектующие.Колонки.Себестоимость.ТекстШапки = "Сумма " + ВалютаЦены;
	ЭлементыФормы.НадписьВалютаЦены.Заголовок = ВалютаЦены;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()
	
	МакетСКД = ПолучитьМакет("НастройкаЗаполненияПродукции");
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(МакетСКД));
	ТекНастройкиКомпоновщика = МакетСКД.НастройкиПоУмолчанию;
	
	Если ЭтоНовый() Тогда
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, , ПараметрОбъектКопирования);
		Если ПараметрОбъектКопирования <> Неопределено Тогда
			ТекНастройкиКомпоновщика = ПараметрОбъектКопирования.НастройкиКомпоновщика.Получить();
		КонецЕсли;
	Иначе
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
		ТекНастройкиКомпоновщика = НастройкиКомпоновщика.Получить();
	КонецЕсли;
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(ТекНастройкиКомпоновщика);
	
	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", ЭтотОбъект, ЭтаФорма);

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;

	// Установить видимость колонок "ХарактеристикаНоменклатуры"
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(ЭлементыФормы.Продукция.Колонки);
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(ЭлементыФормы.ИсходныеКомплектующие.Колонки, Новый Структура("ХарактеристикаНоменклатуры, ХарактеристикаПродукции"));
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
	ОбновитьНадписьВалютаЦены();
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

Процедура ПослеЗаписи()
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	НастройкиКомпоновщика = Новый ХранилищеЗначения(КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки());
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	
КонецПроцедуры // ДатаПриИзменении()

Процедура ТипЦенПриИзменении(Элемент)
	ОбновитьНадписьВалютаЦены();
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ Продукция

Процедура ПродукцияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	КолонкиПродукция = ЭлементыФормы.Продукция.Колонки;
	РаботаСДиалогами.ПоказатьКодАртикул(КолонкиПродукция, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Продукция".
//
Процедура ПродукцияНоменклатураПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Продукция.ТекущиеДанные;
	
	// Выполнить общие действия для всех документов при изменении номенклатуры.
	ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	СтрокаТабличнойЧасти.Спецификация 			= УправлениеПроизводством.ОпределитьСпецификациюПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, Дата);
	ЗаполнитьЕдиницуИКоличествоПродукции(ЭлементыФормы.Продукция.ТекущиеДанные);
КонецПроцедуры // ПродукцияНоменклатураПриИзменении()

// Процедура - обработчик события "НачалоВыбора" поля ввода "Спецификация"
Процедура ПродукцияСпецификацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	РаботаСДиалогами.НачалоВыбораЗначенияСпецификации(ЭлементыФормы.Продукция.ТекущиеДанные.Номенклатура, Элемент, СтандартнаяОбработка);
КонецПроцедуры

Процедура ПродукцияСпецификацияПриИзменении(Элемент)
	ЗаполнитьЕдиницуИКоличествоПродукции(ЭлементыФормы.Продукция.ТекущиеДанные);
КонецПроцедуры

// Процедура - обработчик события "ПриИзменени" поля ввода "ХарактеристикаНоменклатуры"
Процедура ПродукцияХарактеристикаНоменклатурыПриИзменении(Элемент)
	СтрокаТабличнойЧасти = ЭлементыФормы.Продукция.ТекущиеДанные;
	СтрокаТабличнойЧасти.Спецификация = УправлениеПроизводством.ОпределитьСпецификациюПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, Дата);
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ИсходныеКомплектующие

Процедура ИсходныеКомплектующиеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	КолонкиКомплектующие = ЭлементыФормы.ИсходныеКомплектующие.Колонки;
	РаботаСДиалогами.ПоказатьКодАртикул(КолонкиКомплектующие, ОформлениеСтроки.Ячейки, ДанныеСтроки.Продукция);
КонецПроцедуры

Процедура ИсходныеКомплектующиеСпецификацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные.Продукция) Тогда
		РаботаСДиалогами.НачалоВыбораЗначенияСпецификации(ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные.Продукция, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

Процедура ИсходныеКомплектующиеНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;

	// Выполнить общие действия для всех документов при изменении номенклатуры.
	ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.Номенклатура.ЕдиницаХраненияОстатков;
КонецПроцедуры

Процедура ИсходныеКомплектующиеЦенаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	СтрокаТабличнойЧасти.Себестоимость = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
КонецПроцедуры

Процедура ИсходныеКомплектующиеКоличествоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	СтрокаТабличнойЧасти.Себестоимость = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
КонецПроцедуры

Процедура ИсходныеКомплектующиеСебестоимостьПриИзменении(Элемент)
	СтрокаТабличнойЧасти = ЭлементыФормы.ИсходныеКомплектующие.ТекущиеДанные;
	Если СтрокаТабличнойЧасти.Количество = 0 Тогда
		Возврат;
	КонецЕсли;
	СтрокаТабличнойЧасти.Цена = СТрокаТабличнойЧасти.Себестоимость / СтрокаТабличнойЧасти.Количество;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КнопкаЗаполнитьНажатие(Элемент)
	Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
		Предупреждение("Не указан тип цен для расчета себестоимости исходных комплектующих");
		Возврат;
	КонецЕсли;
	ЗаполнятьТабличныеЧасти = Истина;
	Если ИсходныеКомплектующие.Количество() > 0 ИЛИ Продукция.Количество() > 0 Тогда
		ТекстВопроса = "Перед расчетом будут очищены данные " + Символы.ПС +
			"на закладках ""Себестоимость"", ""Состав затрат""" + Символы.ПС +
			"Рассчитать?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Расчет плановой себестоимости продукции");
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Продукция.Очистить();
	ИсходныеКомплектующие.Очистить();
	ЗаполнитьПродукцию(КомпоновщикНастроекКомпоновкиДанных);
	ЗаполнитьИсходныеКомплектующиеПоСпецификации();
	ЗаполнитьЦеныИсходныхКомплектующих();
	РассчитатьСебестоимостьПродукции();
	ОбщегоНазначения.Сообщение("Расчет завершен");
КонецПроцедуры